c =====================Begin Identity Section===================================================
c
c Mnemonic input method for Sebatbeit script on US-QWERTY
c keyboards for Keyman version 7.1, compliant with Unicode 4.1 and later.
c
c License       :  http://creativecommons.org/licenses/by/3.0/
c Info          :  http://keyboards.ethiopic.org/
c Keyman        :  http://www.tavultesoft.com/
c Bugs          :  keyboards@ethiopic.org
c
c Specification : http://keyboards.ethiopic.org/specification/
c
c Updates       :  ftp://ftp.ethiopic.org/pub/keyboards/Keyman/7.0/
c Last Update   :  2008-12-05
c

store(&Version) "7.0"
store(&Name) "Sebatbeit"
store(&Bitmap) "../Bitmaps/SewtSe.bmp"
store(&MnemonicLayout) "1"
store(&CapsAlwaysOff) "1"
store(&Copyright) "Creative Commons Attribution 3.0"
store(&Message) "This is a Sebatbeit language mnemonic input method for Ethiopic script that requires Unicode 4.1 support."
store(&EthnologueCode) "sgw"
store(&VisualKeyboard) 'gff-sgw-7.kvk'
HOTKEY "^%S"
c
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
store(gIz)  'ለመረሰሸቀⷀበተቸነኘአከⷈኸⷐወዘዠየደጀገⷘጠጨጰፀፈፐ'
store(kaIb) 'ሉሙሩሱሹቁⷁቡቱቹኑኙኡኩⷉኹⷑዉዙዡዩዱጁጉⷙጡጩጱፁፉፑ'
store(sals) 'ሊሚሪሲሺቂⷂቢቲቺኒኚኢኪⷊኺⷒዊዚዢዪዲጂጊⷚጢጪጲፂፊፒ'
store(rabI) 'ላማራሳሻቃⷃባታቻናኛኣካⷋኻⷓዋዛዣያዳጃጋⷛጣጫጳፃፋፓ'
store(hams) 'ሌሜሬሴሼቄⷄቤቴቼኔኜኤኬⷌኼⷔዌዜዤዬዴጄጌⷜጤጬጴፄፌፔ'
store(sads) 'ልምርስሽቅⷅብትችንኝእክⷍኽⷕውዝዥይድጅግⷝጥጭጵፅፍፕ'
store(sabI) 'ሎሞሮሶሾቆⷆቦቶቾኖኞኦኮⷎኾⷖዎዞዦዮዶጆጎⷞጦጮጶፆፎፖ'

store(keys) 'lmrsxq_btcnNek_h_wzZydjg_TCPSfp'
c the following is Sebatbeit specific
store(extraKeys) 'HLMRXBVvQKWYDJGF'
store(extraSads) 'ኽልምርሽብብብቅክውይድጅግፍ'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Begin Maps of Sebatbeit Forms-----------------------------------------------
store(potentialSebatbeit) 'ኽቅክግ'
store(sebatbeit)  'ⷕⷅⷍⷝ'
store(sebatbeitY) 'yY'
c ---------------------End Maps of Sebatbeit Forms-----------------------------------------------

c ---------------------Maps for Vowels----------------------------------------------------------
store(loneVowelKeys)  'aAuUiIeEoO'
store(loneVowelEthio) 'አአኡኡኢኢእእኦኦ'
store(composableVowel) 'አኡኢእኦኧ'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
store(zemedeKeys)     'ልምርስሽቅብቭትችኅንኝክኽዝዥድጅግጥጭጵፍፕ'
store(zemedeKeysKaIb) 'ሉሙሩሱሹቁቡቩቱቹኁኑኙኩኹዙዡዱጁጉጡጩጱፉፑ'
store(zemedeEthio)    'ሏሟሯሷሿቋቧቯቷቿኋኗኟኳዃዟዧዷጇጓጧጯጷፏፗ'

c 'hmbqkKgfp'
store(zemedeKeysKaIbExtended) 'ሙቡቁኩኹጉፉፑ'
store(zemedeGeez)  'ᎀᎄቈኰዀጐᎈᎌ'
store(zemedeKaIb)  'ᎃᎇቍኵዅጕᎉᎍ'
store(zemedeSalis) 'ᎁᎅቊኲዂጒᎋᎏ'
store(zemedeRabI)  'ሟቧቋኳዃጓፏፗ'
store(zemedeHamis) 'ᎂᎆቌኴዄጔᎊᎎ'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
store(ArabOnes)    '23456789'
store(ones)        '፪፫፬፭፮፯፰፱'
store(tens)        '፳፴፵፶፷፸፹፺'
store(arabNumbers) '123456789'
store(ethNumbers)  '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
store(arabNumbersWithZero) '0123456789'
store(ColonOrComma) ':,'
store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
store(gIz-key)  'eEäÄéÉèÈêÊëË'
store(kaIb-key) 'uUùÙúÚûÛüÜ'
store(sals-key) 'iIìÌíÍîÎïÏ'
store(rabI-key) 'aAàÀáÁâÂãÃåÅ'
store(hams-key) 'æÆ'
store(sabI-key) 'oOòÒóÓôÔõÕöÖøØ'
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c
begin Unicode > use(main)
group(main) using keys

c ---------------------Input of Special Symbols-------------------------------------------------
c
c The input section here is for rules that must precede other rules
c
  + '_' > U+135F c gemination
  U+135F + '_' > '_'

  + any(extraKeys) > index(extraSads,1)
  + any(keys)      > index(sads,1)
c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Input of Double Strike Letters-------------------------------------------
c None for Sebatbeit
c ---------------------End Input of Double Strike Letters---------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c
c Alef-A Vowels as Vowels
  + any(loneVowelKeys) > index(loneVowelEthio,1)

c Alef-A Vowels as Regular Syllables
  'አ' + any(gIz-key)  > 'ኧ'
  'አ' + any(kaIb-key) > 'ኡ'
  'አ' + any(sals-key) > 'ኢ'
  'አ' + any(rabI-key) > 'ኣ'
  'ኢ' + any(gIz-key)  > 'ኤ'
  'አ' + any(sabI-key) > 'ኦ'
  'ኧ' + any(gIz-key)  > 'እ'

  any(composableVowel) + "'" > index(composableVowel,1) deadkey(1)
c
c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(sads) + any(gIz-key)  > index(gIz,1)
  any(sads) + any(kaIb-key) > index(kaIb,1)
  any(sads) + any(sals-key) > index(sals,1)
  any(sads) + any(rabI-key) > index(rabI,1)
  any(sads) + any(hams-key) > index(hams,1)
  any(sals) + any(gIz-key)  > index(hams,1)
  any(sads) + any(sabI-key) > index(sabI,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Sebatbeit Syllables-------------------------------------------------
  any(potentialSebatbeit) + any(sebatbeitY) > index(sebatbeit,1)
c ---------------------End Input of Sebatbeit Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(zemedeKeysKaIb)         + any(rabI-key) > index(zemedeEthio,1)
  any(zemedeKeysKaIbExtended) + any(gIz-key)  > index(zemedeGeez,1)
  any(zemedeKeysKaIbExtended) + any(kaIb-key) > index(zemedeKaIb,1)
  any(zemedeKeysKaIbExtended) + any(sals-key) > index(zemedeSalis,1)
  any(zemedeSalis)            + any(gIz-key)  > index(zemedeHamis,1)
c ---------------------End Input of Labiovelars-------------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c
c The only dead key for entry of special characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c
  any(sads)  + "'" > index(sads,1) deadkey(1)
  deadkey(1) + "'" > "'"

  + ':'     > '፡'
  + [SHIFT ' '] > '፡'
  '፡' + ':' > '።'
  '።' + ':' > ':'
  ':' + ':' > '፡'  c makes circular
      + ',' > '፣'
  '፣' + ',' > '፥'
  '፥' + ',' > ','
  ',' + ',' > '፣'  c makes circular

      + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፤'  c makes circular

  '፡' + '-' > '፦'

      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
  store(euro) 'ÇçÐðÝýÿßÑñ'
  store(euroMap) 'ጭችድድይይይጽኝኝ'
  + any(euro) > index(euroMap,1)

  'ጽ' + 'ß' > 'ፅ'
  + '¨' > U+135F

  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c this mapping has an Sebatbeit bias
  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Arabic Numerals
c
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  "'" + '1' > '፩'
  "'" + any(ArabOnes) > index(ones,2)

c special cases for multiples of one
  '፩'   + '0' > '፲'
  '፲'   + '0' > '፻'
  '፻'   + '0' > '፲፻'
  '፲፻'  + '0' > '፼'
  '፼'   + '0' > '፲፼'
  '፲፼'  + '0' > '፻፼'
  '፻፼'  + '0' > '፲፻፼'
  '፲፻፼' + '0' > '፼፼'
  '፼፼'  + '0' > context beep  c do not go any higher, we could beep here

c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1) '፻'  c Hundreds
  any(ones)  '፻ '+ '0' > index(tens,1) '፻'  c Thousands
  any(tens)  '፻' + '0' > index(ones,1) '፼'  c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1) '፼'  c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions

c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)
c ---------------------End Input of Numbers-----------------------------------------------------

c =====================End Functional Section===================================================
