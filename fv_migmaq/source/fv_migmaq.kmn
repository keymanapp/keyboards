store(&VERSION) "9.0"
store(&KEYBOARDVERSION) "9.0"
store(&TARGETS) "windows macosx"
store(&ETHNOLOGUECODE) "mic"
store(&COPYRIGHT) "© 2015 FirstVoices, Tavultesoft. Portions © 2006 Chris Harvey."
store(&NAME) "Mi'gmawi'simg / Mi'kmawi'simk"
store(&BITMAP) 'fv_migmaq.bmp'
store(&VISUALKEYBOARD) 'fv_migmaq.kvk'

begin Unicode > use(main)

group(main) using keys   

c constraints

store(overaccent)  U+0301 
store(overaccentkey) ";"

any(overaccent) + any(overaccentkey) > context beep

store(NumbersAndPunctuation) '1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?‘’“”‹›«»' "'"  c preventing accents on non-letters
any(NumbersAndPunctuation) + any(overaccentkey) > context beep


c original punctuation
+ [RALT K_PERIOD] > '>'
+ [RALT K_COMMA] > '<'
+ [SHIFT RALT K_BKQUOTE] > '~'
+ [RALT K_BKQUOTE] > '`'
+ [SHIFT RALT K_COLON] > ':'
+ [RALT K_COLON] > ';'
+ [RALT K_SLASH] > '/'
+ [RALT K_QUOTE] > U+0027
+ [RALT K_BKSLASH] > '\'
+ [SHIFT RALT K_BKSLASH] > '|'
+ [SHIFT RALT K_RBRKT] > '}'
+ [SHIFT RALT K_LBRKT] > '{'
+ [RALT K_RBRKT] > ']'
+ [RALT K_LBRKT] > '['
+ [RALT shift K_EQUAL] > '+'
+ [RALT K_HYPHEN] > '_'          
+ [RALT K_EQUAL] > '='

c Guillemets
+ [SHIFT RALT K_0] > '›'
+ [SHIFT RALT K_9] > '‹'
+ [RALT K_0] > '»'
+ [RALT K_9] > '«'

c Quotes
+ [SHIFT K_RBRKT] > "”"
+ [SHIFT K_LBRKT] > "“"
+ [K_BKQUOTE] > "‘"
+ [SHIFT K_6] > "’"

+ [caps shift k_equal] > "ɨ" 
+ [ncaps shift k_equal] > "Ɨ"
+ [caps k_equal] > "Ɨ"
+ [ncaps k_equal] > "ɨ" 
                                             
+ ";" > U+0301 


nomatch > use(x)                            

group(x) using keys

store(allkeys) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"
+ any(allkeys) > index(allkeys, 1)
