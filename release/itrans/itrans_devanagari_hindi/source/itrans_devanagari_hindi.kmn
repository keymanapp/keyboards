c Version 1.1.0 Streamline Code
c 20190524 Remap based on feedback from Ken P
c Version 1.0.0 Initial Version
c 20190517 - Based on itrans_devanagari_sanskrit_vedic
store(&VERSION) "10.0" 
store(&NAME) 'Hindi Devanagari Phonetic (ITRANS)'
store(&TARGETS) 'windows macosx linux web'
store(&COPYRIGHT) '©2019 sanskritdocuments.org'
store(&MESSAGE) 'A Phonetic Devanagari (ITRANS) keyboard for Hindi.'
store(&KMW_HELPTEXT) 'Uses Unshifted, Shifted layouts.'
store(&KEYBOARDVERSION) '1.1.0'
store(&BITMAP) 'itrans_devanagari_hindi.ico'
store(&VISUALKEYBOARD) 'itrans_devanagari_hindi.kvks'

begin Unicode > use(main)
group(main) using keys

+	"_"	>	U+200C	c ZERO-WIDTH NON-JOINER
+	"^"	>	"्"	c VIRAMA

c	Punctuation
store(Punctuation) "!" "'" "(" ")" "+" "-" "/" ":" ";" "<" ">" "?" "@" "`" "{" "}" "~" '"' "," "." "=" "\"
+ any(Punctuation)	>	index(Punctuation,1)

","	+	","	>	U+0970	c	DEVANAGARI ABBREVIATION SIGN

"."	+	"."	>	"।"	c twice for DANDA
"।" + "."	> "॥"	c thrice for DOUBLE DANDA
"॥"	+	"."	>	U+2026	c four dots for a HORIZONTAL ELLIPSIS

"-"	+	"-"	>	U+2013	c hyphen twice for EN DASH
U+2013	+	"-"	>	U+2014	c three times for EM DASH

c	numbers - Devanagari DigitsU
store(Digits)	"0"		"1"		"2"		"3"		"4"		"5"		"6"		"7"		"8"		"9"
store(DigitsU)	"०"		"१"		"२"		"३"		"४"		"५"		"६"		"७"		"८"		"९"

+ any(Digits)	>	index(DigitsU,1)
any(DigitsU)	+	[K_BKSP]	>	index(Digits,1)

c standalone vowel signs with @
"@"	+	"A"	>	"ा"
"@"	+	"i"	>	"ि"
"@"	+	"I"	>	"ी"
"@"	+	"u"	>	"ु"
"@"	+	"U"	>	"ू"
"@"	+	"e"	>	"े"
"@"	+	"o"	>	"ो"
"@"	+	"R"	>	"ृ"
"@"	+	"E"	>	"ॅ"
"@"	+	"O"	>	"ॉ"
"@"	+	"a"	>	"@a"
"@a"	+	"a"	>	"ा"
"@a"	+	"e"	>	"ॅ"
"@a"	+	"i"	>	"ै"
"@a"	+	"u"	>	"ौ"
"@a"	+	"w"	>	"ॉ"
"ि"	+	"i"	>	"ी"
"ु"	+	"u"	>	"ू"
"ृ"	+	"R"	>	"ॄ"
"ृ"	+	"^"	>	"ॄ"
"े"	+	"e"	>	"ी"
"े"	+	"i"	>	"ै"
"ो"	+	"o"	>	"ू"
"ो"	+	"u"	>	"ौ"

c Independent Vowels
+	"a"	>	"अ"
"अ"	+	"a"	>	"आ"
+	"A"	>	"आ"
+	"i"	>	"इ"
"इ"	+	"i"	>	"ई"
"ए"	+	"e"	>	"ई"
+	"I"	>	"ई"
+	"u"	>	"उ"
"उ"	+	"u"	>	"ऊ"
"ओ"	+	"o"	>	"ऊ"
+	"U"	>	"ऊ"
+	"R"	>	"ऋ"
"ऋ"	+	"^"	>	"ॠ"
"ऋ"	+	"R"	>	"ॠ"
+	"["	>	"ऎ"
+	"e"	>	"ए"
+	"E"	>	"ऍ"
"अ"	+	"e"	>	"ऍ"
"अ"	+	"i"	>	"ऐ"
"ए"	+	"i"	>	"ऐ"
+	"]"	>	"ऒ"
+	"o"	>	"ओ"
+	"O"	>	"ऑ"
"अ"	+	"w"	>	"ऑ"
"अ"	+	"u"	>	"औ"
"ओ"	+	"u"	>	"औ"

c Vowel Modifiers
+	"H"	>	"ः" c VISARGA
+	"M"	>	"ं" c ANUSVARA
"।" + "n" > "ं" c ANUSVARA
"।" + "m" > "ं" c ANUSVARA
"ं" + "M" > "ँ"	c CHANDRABINDU

c Consonants
+	"b"	>	"ब्"
+	"B"	>	"भ्"
+	"c"	>	"च्"
+	"C"	>	"छ्"
+	"D"	>	"ड्"
+	"d"	>	"द्"
+	"g"	>	"ग्"
+	"G"	>	"घ्"
+	"h"	>	"ह्"
+	"j"	>	"ज्"
+	"J"	>	"झ्"
+	"k"	>	"क्"
+	"K"	>	"ख्"
+	"l"	>	"ल्"
+	"L"	>	"ळ्"
+	"m"	>	"म्"
+	"N"	>	"ण्"
+	"n"	>	"न्"
+	"p"	>	"प्"
+	"P"	>	"फ्"
+	"r"	>	"र्"
+	"S"	>	"ष्"
+	"s"	>	"स्"
+	"T"	>	"ट्"
+	"t"	>	"त्"
+	"v"	>	"व्"
+	"w"	>	"व्"
+	"x"	>	"क्ष्"
+	"Y"	>	"ञ्"
+	"y"	>	"य्"

c consonants with Nukta
+	"q"	> "क़्"
+	"Q"	> "ख़्"
+	"F"	> "ग़्"
+	"z"	> "ज़्"
+	"Z"	> "झ़्"
+	"W"	> "ड़्"
+	"X"	> "ढ़्"
+	"f"	>	"फ़्"

c Nukta with preceding dot
"।" + "k" > "क़्"
"क़्" + "h" > "ख़्"
"।" + "g" > "ग़्"
"।" + "j" > "ज़्"
"ज़्" + "h" > "झ़्"
"।" + "D" > "ड़्"
"ड़्" + "h" > "ढ़्"

c alternate mapping with ~
"~"	+	"n"	>	"ञ्"
"~"	+	"N"	>	"ङ्"

c add h to consonants
"क्"	+	"h"	>	"ख्"
"ग्"	+	"h"	>	"घ्"
"च्"	+	"h"	>	"च्"
"छ्"	+	"h"	>	"छ्"
"ज्"	+	"h"	>	"झ्"
"ट्"	+	"h"	>	"ठ्"
"ड्"	+	"h"	>	"ढ्"
"त्"	+	"h"	>	"थ्"
"द्"	+	"h"	>	"ध्"
"प्"	+	"h"	>	"फ्"
"ब्"	+	"h"	>	"भ्"
"ष्"	+	"h"	>	"ष्"
"स्"	+	"h"	>	"श्"

c special combinations
"घ्"	+	"Y"	>	"ज्ञ्"	c	GY
"श्"	+	"r"	>	"श्र्"	c	shr
"ण्"	+	"g"	>	"ङ्"	c	Ng

store(Vyanjana) 'कक्षखगघङचछजज्ञझञटठडढणतथदधनपफबभमयरलळवश्रषसह'
store(NuktaVyanjana) 'कखगजझडढफ'
store(ViramaMaatraa) 'eiouAEIORU[]|'
store(ViramaMaatraaU) "े" "ि" "ो" "ु" "ा" "ॅ" "ी" "ॉ" "ृ" "ू" "ॆ" "ॊ" "ॢ"
store(aPlusMaatraa) 'aeiuw'
store(aPlusMaatraaU) "ा" "ॅ" "ै" "ौ" "ॉ"

any(Vyanjana)"्" + "a" > index(Vyanjana,1)
any(Vyanjana) + any(aPlusMaatraa)  > context index(aPlusMaatraaU,2) 
any(Vyanjana)"्" + any(ViramaMaatraa)  > index(Vyanjana,1) index(ViramaMaatraaU,3) 
any(Vyanjana)"ि" + "i" >  index(Vyanjana,1)"ी"	 c ii
any(Vyanjana)"ु" + "u" >  index(Vyanjana,1)"ू"	 c uu
any(Vyanjana)"े" + "e" >  index(Vyanjana,1)"ी"	 c ee
any(Vyanjana)"े" + "i" > index(Vyanjana,1)"ै"	 c ei
any(Vyanjana)"ो" + "o" >  index(Vyanjana,1)"ू"	 c oo
any(Vyanjana)"ो" + "u" > index(Vyanjana,1)"ौ"	 c ou

any(NuktaVyanjana)"़""्" + "a" > index(NuktaVyanjana,1)"़"
any(NuktaVyanjana)"़" + any(aPlusMaatraa)  > context index(aPlusMaatraaU,3) 
any(NuktaVyanjana)"़""्" + any(ViramaMaatraa)  > index(NuktaVyanjana,1)"़" index(ViramaMaatraaU,4) 
any(NuktaVyanjana)"़""ि" + "i" >  index(NuktaVyanjana,1)"़""ी"	 c ii
any(NuktaVyanjana)"़""ु" + "u" >  index(NuktaVyanjana,1)"़""ू"	 c uu
any(NuktaVyanjana)"़""े" + "e" >  index(NuktaVyanjana,1)"़""ी"	 c ee
any(NuktaVyanjana)"़""े" + "i" > index(NuktaVyanjana,1)"़""ै"	 c ei
any(NuktaVyanjana)"़""ो" + "o" >  index(NuktaVyanjana,1)"़""ू"	 c oo
any(NuktaVyanjana)"़""ो" + "u" > index(NuktaVyanjana,1)"़""ौ"	 c ou


c	Special	Symbols

+	"#"	>	"ॐ"
+	"$"	>	"₹"
+	"%"	>	U+0FD5	c ࿕ RIGHT-FACING SVASTI SIGN
+	"*"	>	"❀"
+	"&"	>	"☸"
+	"V"	>	"ऽ"	c AVAGRAHA

c EOF
