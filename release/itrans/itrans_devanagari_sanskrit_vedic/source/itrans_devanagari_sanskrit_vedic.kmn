c References for existing keyboards/IME
c ITRANS - https://www.aczoom.com/itrans/
c Vaidika - http://svayambhava.blogspot.com/p/siddhanta-devanagariunicode-open-type.html
c Vidyut - http://www.mywhatever.com/sanskrit/vidyut/
c Swanalekha - https://swanalekha.smc.org.in/
c sil_devanagari_phonetic - https://keyman.com/keyboards/sil_devanagari_phonetic
c sil_ipa - https://github.com/keymanapp/keyboards/tree/master/release/sil/sil_ipa
c Version 1.0.0
c 20190510 - Created Initial Version
c 20190515 - Initial submission to Github
c 20190516 - Fix comments in kmn, improve welcome.htm
c 20190517 - Fixes based on PR feedback, Remove Q for English output
c Version 1.1.0 
c 20190522 - Changes based on user feedback
c Increase minor version because of mapping change
c Change {/ऍ , }/ऑ  to ae/E/ऍ ,   aw/O/ऑ
c Add alternate mapping for Samaveda marks on Right Ctrl layout
c Add Hindi as a supported language
c Give error for unused keys with beep
c Display punctuation marks on OSK
c Display ZWNJ on OSK for _
c Version 1.2.0
c 20190526
c Streamline code with use of `store` and `any`
c Add support for Marathi eyelash ra R - र्‍ 
c Add support for ZERO WIDTH JOINER +
c Add option for implicit akaara deletion for Hindi mode 
c Remap and add keys
c RR, RR^, LL, LL^, MM
c .k .kh .g .j .jh .D .Dh .f
c 20190527
c L maps to ळ् as well as ॢ 
c That combination is NOT used in Sanskrit, rendering will be incorrect if both used together.
c Add nukta chars - .n .r .L .y 
c Add  X ज्ञ् and F ङ् 
c Add W } ऄ ॵ ॆ ॏ 
c Add q Q  ऺ ऻ कऺ कऻ
c Add z Z  ॖ   ॗ  कॖ कॗ
c Add { for U+A8FE Aand U+A8FF (AY) not yet supported in fonts
c Remove some Samaveda marks from RALT
c Add some Samaveda marks to RCTRL
c Regenerate keyboard graphics with ZWJ & ZWNJ
c Edit manually Shift layout graphics to add AY and svasti
c Limit Samaveda Cantillation marks 1-7
c Change 8 - "꣢꣯" 9 - "꣢꣫" 0 - "꣣꣬"
c 20190529
c Changes based on PR feedback
c Fix double virama bug
c implicit a for word-ending NuktaVyanjana in Hindi mode
c Version 1.2.1
c 20190530 
c Add OM for ॐ
c Add examples with Vedic accents to welcome and help
c Add missing Samaveda marks for  key + EQUAL (similar to RCTRL) 
c 20190531
c Add _ _ for   ॒
store(&VERSION) "10.0" 
store(&NAME) 'Vedic Sanskrit Devanagari Phonetic (ITRANS)'
store(&TARGETS) 'web desktop'
store(&COPYRIGHT) '© sanskritdocuments.org'
store(&MESSAGE) 'A Phonetic Devanagari (ITRANS) keyboard with support for Vedic Sanskrit (Devanagari Extended and Vedic Extensions Unicode ranges).'
store(&KMW_HELPTEXT) 'Uses Unshifted, Shifted, Right Alt and Right Ctrl layouts.'
store(&KEYBOARDVERSION) '1.2.2'
store(&BITMAP) 'itrans_devanagari_sanskrit_vedic.ico'
store(&VISUALKEYBOARD) 'itrans_devanagari_sanskrit_vedic.kvks'

begin Unicode	>	use(main)
group(main) using keys

c	Reset Hindi mode by default
store(opt1) '0'
+ [RCTRL RALT K_1]	>	set(opt1='hindi') 
+ [RCTRL RALT K_0]	>	reset(opt1)

c	Special Symbols

+ "#"	>	"ॐ"
+ "$"	>	"₹"
+ "%"	>	U+0FD5	c ࿕ RIGHT-FACING SVASTI SIGN
+ "*"	>	"❀"
+ "&"	>	"☸"
+ "V"	>	"ऽ"	c AVAGRAHA
+ "^"	>	"्"	c VIRAMA
+ "_"	>	U+200C	c ZERO-WIDTH NON-JOINER
+ "+"	>	U+200D	c ZERO-WIDTH JOINER

c	Punctuation
store(Punctuation) "!" "'" "(" ")" "-" "/" ":" ";" "" "?" "`" '"' "," "." "=" "\" "<" ">" "|"
+ any(Punctuation)	>	index(Punctuation,1)

+ "~"	>	"~" c Breaks Hindi mode rules if included in punctuation
+ "@"	>	"@" 

"," + ","	>	U+0970	c	DEVANAGARI ABBREVIATION SIGN
"." + "."	>	"।"	c twice for DANDA
"।" + "."	>	"॥"	c thrice for DOUBLE DANDA
"॥" + "."	>	U+2026	c four dots for a HORIZONTAL ELLIPSIS
"-" + "-"	>	U+2013	c hyphen twice for EN DASH
U+2013 + "-"	>	U+2014	c three times for EM DASH

c	COMMON VEDIC MARKS
"'" + "'"	>	U+0951	c DEVANAGARI STRESS SIGN DATTA - SVARITA - ''
U+0951 + "'"	>	U+1CDA	c VEDIC TONE DOUBLE SVARITA - '''
U+1CDA + "'"	>	U+1CDB	c VEDIC TONE TRIPLE SVARITA - ''''
"`" + "`"	>	U+0952	c ` twice for ANUDATTA
U+200C + "_" > U+0952	c _ twice for ANUDATTA
"ँ" + "^"	>	"ꣳ"	c CANDRABINDU VIRAMA - MM^
"=" + "="	>	"ᳶ"	c VEDIC SIGN UPADHMANIYA - ==

c	numbers - Devanagari DigitsU
store(Digits)	"0"		"1"		"2"		"3"		"4"		"5"		"6"		"7"		"8"		"9"
store(DigitsU)	"०"		"१"		"२"		"३"		"४"		"५"		"६"		"७"		"८"		"९"
+ any(Digits)	>	index(DigitsU,1)
any(DigitsU) + [K_BKSP]	>	index(Digits,1)

c standalone vowel signs with @
"@" + "A"	>	"ा"
"@" + "i"	>	"ि"
"@" + "I"	>	"ी"
"@" + "u"	>	"ु"
"@" + "U"	>	"ू"
"@" + "e"	>	"े"
"@" + "o"	>	"ो"
"@" + "E"	>	"ॅ"
"@" + "O"	>	"ॉ"
"@" + "["	>	'ॆ'
"@" + "]"	>	'ॊ'
"@" + "W"	>	'ॆ'
"@" + "{" > U+A8FF c DEVANAGARI VOWEL SIGN AY
"@" + "}"	>	'ॏ'
"@" + "a"	>	"@a"
"@a" + "a"	>	"ा"
"@a" + "e"	>	"ॅ"
"@a" + "i"	>	"ै"
"@a" + "u"	>	"ौ"
"@a" + "w"	>	"ॉ"
"ि" + "i"	>	"ी"
"ु" + "u"	>	"ू"
"े" + "e"	>	"ी"
"े" + "i"	>	"ै"
"ो" + "o"	>	"ू"
"ो" + "u"	>	"ौ"
"@" + "R"	>	"ृ"
"ृ" + "R"	>	"ृ" c @RR
"ृ" + "^"	>	"ॄ" c @RR^
"@" + "L"	>	"ॢ"
"ॢ" + "L"	>	"ॢ" c @LL
"ॢ" + "^"	>	"ॣ" c @LL^
"@" + "q"	>	'ऺ'
"@" + "Q"	>	'ऻ'
"@" + "z"	>	'ॖ'
"@" + "Z"	>	'ॗ'

c Independent Vowels
+ "a"	>	"अ"
"अ" + "a"	>	"आ"
+ "A"	>	"आ"
+ "i"	>	"इ"
"इ" + "i"	>	"ई"
"ए" + "e"	>	"ई"
+ "I"	>	"ई"
+ "u"	>	"उ"
"उ" + "u"	>	"ऊ"
"ओ" + "o"	>	"ऊ"
+ "U"	>	"ऊ"
+ "["	>	"ऎ"
+ "e"	>	"ए"
+ "E"	>	"ऍ"
"अ" + "e"	>	"ऍ"
"अ" + "i"	>	"ऐ"
"ए" + "i"	>	"ऐ"
+ "]"	>	"ऒ"
+ "o"	>	"ओ"
+ "O"	>	"ऑ"
"अ" + "w"	>	"ऑ"
"अ" + "u"	>	"औ"
"ओ" + "u"	>	"औ"
"र्‍" + "R"	>	"ऋ" c RR
"ऋ" + "^"	>	"ॠ" c RR^
"ळ्" + "L"	>	"ऌ" c LL
"ऌ" + "^"	>	"ॡ" c LL^
+ 'W' > 'ऄ'
+ "{" > U+A8FE c DEVANAGARI LETTER AY
+ '}' > 'ॵ'
+ "Z" > 'ॷ'
+ "Q" > 'ॴ'
+ "z" > 'ॶ'
+ "q" > 'ॳ'

c Vowel Modifiers
+ "H"	>	"ः" c VISARGA - H
+ "M"	>	"ं" c ANUSVARA - M
"ं" + "M"	>	"ँ"	c CHANDRABINDU - MM

c Consonants
+ "b"	>	"ब्"
+ "B"	>	"भ्"
+ "c"	>	"च्"
+ "C"	>	"छ्"
+ "d"	>	"द्"
+ "D"	>	"ड्"
+ "g"	>	"ग्"
+ "G"	>	"घ्"
+ "h"	>	"ह्"
+ "j"	>	"ज्"
+ "J"	>	"झ्"
+ "k"	>	"क्"
+ "K"	>	"ख्"
+ "l"	>	"ल्"
+ "L"	>	"ळ्"
+ "m"	>	"म्"
+ "n"	>	"न्"
+ "N"	>	"ण्"
+ "p"	>	"प्"
+ "P"	>	"फ्"
+ "r"	>	"र्"
+ "R"	>	"र्‍" c Marathi eyelash ra
+ "s"	>	"स्"
+ "S"	>	"ष्"
+ "t"	>	"त्"
+ "T"	>	"ट्"
+ "v"	>	"व्"
+ "w"	>	"व्"
+ "x"	>	"क्ष्"
+ "y"	>	"य्"
+ "f"	>	"फ्"

c duplicate mappings for ease of typing consonants
+ "F"	>	"ङ्"
+ "X"	>	"ज्ञ्"
+ "Y"	>	"ञ्"

c Nukta with preceding dot
"." + "k"	>	"क़्" c .k
"क़्" + "h"	>	"ख़्" c .kh
"." + "g"	>	"ग़्" c .g
"." + "j"	>	"ज़्" c .j
"ज़्" + "h"	>	"झ़्" c .jh
"." + "D"	>	"ड़्" c .D
"ड़्" + "h"	>	"ढ़्" c .Dh
"." + "f"	>	"फ़्" c .f
"." + "n"	>	"ऩ्" c .n
"." + "r"	>	"ऱ्" c .r
"." + "L"	>	"ऴ्" c .L
"." + "y"	>	"य़्" c .y

c alternate mapping with ~
"~" + "n"	>	"ञ्" c ~n
"~" + "N"	>	"ङ्" c ~N

c add h to consonants
"क्" + "h"	>	"ख्"
"ग्" + "h"	>	"घ्"
"च्" + "h"	>	"च्" c same c=ch
"छ्" + "h"	>	"छ्" c same C=Ch
"ज्" + "h"	>	"झ्"
"ट्" + "h"	>	"ठ्"
"ड्" + "h"	>	"ढ्"
"त्" + "h"	>	"थ्"
"द्" + "h"	>	"ध्"
"प्" + "h"	>	"फ्" 
"ब्" + "h"	>	"भ्"
"ष्" + "h"	>	"ष्" c same S=Sh
"स्" + "h"	>	"श्"

c special combinations
$keyman: if(opt1  = '0') "न्" + "k"	>	"ङ्क्"
$keyman: if(opt1  = '0') "न्" + "g"	>	"ङ्ग्"
$keyman: if(opt1  = '0') "न्" + "c"	>	"nc"
$keyman: if(opt1  = '0') "nc" + "h"	>	"ञ्च्"	c nch
$keyman: if(opt1  = '0') "न्" + "j"	>	"ञ्ज्"
"घ्" + "Y"	>	"ज्ञ्"	c	GY
"श्" + "r"	>	"श्र्"	c	shr
"ण्" + "g"	>	"ङ्"	c	Ng
"ऑ" + "M" > "ॐ"	c	OM

c Rules for Consonants + Vowel Signs

store(Vyanjana) 'कक्षखगघङचछजज्ञझञटठडढणतथदधनपफबभमयरलळवश्रषसह'
store(NuktaVyanjana) 'कखगजझडढफनरळय'
store(ViramaMaatraa) 'eiouAEIORU[]LW{}qQzZ'
store(ViramaMaatraaU) "े" "ि" "ो" "ु" "ा" "ॅ" "ी" "ॉ" "ृ" "ू" "ॆ" "ॊ" "ॢ" "ॆ" 'ꣿ' 'ॏ' 'ऺ' 'ऻ'  'ॖ' 'ॗ'
store(aPlusMaatraa) 'aeiuw'
store(aPlusMaatraaU) "ा" "ॅ" "ै" "ौ" "ॉ"

c	implicit akaara deletion for hindi mode 
store(opt1endings) outs(Punctuation) ' '
$keyman: if(opt1  = 'hindi') any(Vyanjana)"्" + any(opt1endings)	>	index(Vyanjana,2) index(opt1endings,4)

any(Vyanjana)"्" + "^"	>	index(Vyanjana,1)"्"
any(Vyanjana)"्" + "a"	>	index(Vyanjana,1)
any(Vyanjana) + any(aPlusMaatraa) 	>	context index(aPlusMaatraaU,2) 
any(Vyanjana)"्" + any(ViramaMaatraa) 	>	index(Vyanjana,1) index(ViramaMaatraaU,3) 
any(Vyanjana)"ि" + "i"	>	 index(Vyanjana,1)"ी"	 c ii
any(Vyanjana)"ु" + "u"	>	 index(Vyanjana,1)"ू"	 c uu
any(Vyanjana)"े" + "e"	>	 index(Vyanjana,1)"ी"	 c ee
any(Vyanjana)"े" + "i"	>	index(Vyanjana,1)"ै"	 c ei
any(Vyanjana)"ो" + "o"	>	 index(Vyanjana,1)"ू"	 c oo
any(Vyanjana)"ो" + "u"	>	index(Vyanjana,1)"ौ"	 c ou

$keyman: if(opt1  = 'hindi') any(NuktaVyanjana)"़" "्" + any(opt1endings)	>	index(NuktaVyanjana,2)"़" index(opt1endings,5)

any(NuktaVyanjana)"़""्" + "^"	>	index(NuktaVyanjana,1)"़""्"
any(NuktaVyanjana)"़""्" + "a"	>	index(NuktaVyanjana,1)"़"
any(NuktaVyanjana)"़" + any(aPlusMaatraa) 	>	context index(aPlusMaatraaU,3) 
any(NuktaVyanjana)"़""्" + any(ViramaMaatraa) 	>	index(NuktaVyanjana,1)"़" index(ViramaMaatraaU,4) 
any(NuktaVyanjana)"़""ि" + "i"	>	 index(NuktaVyanjana,1)"़""ी"	 c ii
any(NuktaVyanjana)"़""ु" + "u"	>	 index(NuktaVyanjana,1)"़""ू"	 c uu
any(NuktaVyanjana)"़""े" + "e"	>	 index(NuktaVyanjana,1)"़""ी"	 c ee
any(NuktaVyanjana)"़""े" + "i"	>	index(NuktaVyanjana,1)"़""ै"	 c ei
any(NuktaVyanjana)"़""ो" + "o"	>	 index(NuktaVyanjana,1)"़""ू"	 c oo
any(NuktaVyanjana)"़""ो" + "u"	>	index(NuktaVyanjana,1)"़""ौ"	 c ou

c VEDIC MARKS 

c Samaveda Number Combining Marks
"१" + "="	>"꣡"		
"२" + "="	>"꣢"		 
"३" + "="	>"꣣"		
"४" + "="	>"꣤"		
"५" + "="	>"꣥"		
"६" + "="	>"꣦"		
"७" + "="	>"꣧"		
"८" + "="	>"꣢꣯"		c	8=
"९" + "="	>"꣢꣫"	c	9=
"०" + "="	>"꣣꣬"		c	0=

c	Samaveda Combining vowels
"अ" + "="	>"꣪"
"उ" + "="	>"꣫"

c	Other Samaveda Combining characters
"क्" + "="	>"꣬"	c	k=
"न्" + "="	>"꣭"	c	n=
"प्" + "="	>"꣮"	c	p=
"र्" + "="	>"꣯"	c	r=
"व्" + "="	>"꣰"	c	v=
"स्" + "="	>"꣱"	c	s=
"ॶ" + "=" 	>  '᳸'	c	z=
"क्ष्" + "=" 	>  '᳹'	c	x=
"च्" + "=" 	>  '᳒'	c	c=
"/" + "=" 	>	'᳓'	c	/=
"." + "=" 	>	'᳑'	c	.=
"," + "=" 	>	'᳐'	c	,=
"\" + "=" 	> '꣼'	c	\=
"ऎ" + "=" 	> '꣹'	c	]=
"ऒ" + "=" 	> '꣸'	c	[=
"'" + "=" 	> 'ꣻ'	c	'=
";" + "=" 	> '꣺'	c	;=

c	Vedic Extensions Unicode Range - Right Alt 
+ [RALT	K_1]	>	'᳑'
+ [RALT	K_2]	>	'᳒'
+ [RALT	K_3]	>	'᳓'
+ [RALT	K_4]	>	'᳔'
+ [RALT	K_5]	>	'᳕'
+ [RALT	K_6]	>	'᳖'
+ [RALT	K_7]	>	'᳗'
+ [RALT	K_8]	>	'᳘'
+ [RALT	K_9]	>	'᳙'
+ [RALT	K_A]	>	'᳧'
+ [RALT	K_BKQUOTE]	>	'᳐'
+ [RALT	K_D]	>	'ᳱ'
+ [RALT	K_E]	>	'᳥'
+ [RALT	K_G]	>	'ᳩ'
+ [RALT	K_H]	>	'ᳬ'
+ [RALT	K_I]	>	'ᳮ'
+ [RALT	K_O]	>	'ᳯ'
+ [RALT	K_P]	>	'ᳰ'
+ [RALT	K_Q]	>	'᳣'
+ [RALT	K_R]	>	'᳦'
+ [RALT	K_S]	>	'᳨'
+ [RALT	K_T]	>	'ᳪ'
+ [RALT	K_U]	>	'᳴'
+ [RALT	K_W]	>	'᳤'
+ [RALT	K_X]	>	'ᳵ'
+ [RALT	K_Y]	>	'ᳫ'
+ [RALT	K_Z]	>	'᳢'
+ [RALT	K_B]	>	'᳹'
+ [RALT	K_C]	>	U+1CDD	c VEDIC TONE DOT BELOW
"᳝" + [RALT	K_C]	>	U+1CDE	c	VEDIC TONE TWO DOTS BELOW
"᳞" + [RALT	K_C]	>	U+1CDF	c	VEDIC TONE THREE DOTS BELOW
+ [RALT	K_F]	>	'᳜'
+ [RALT	K_M]	>	'᳢'
+ [RALT	K_N]	>	'᳠'
+ [RALT	K_V]	>	'᳸'

c Unmapped RALT Keys
c + [RALT K_BKQUOTE]	>	'꣼'
+ [RALT K_HYPHEN]	>	'ꣻ'
+ [RALT K_EQUAL]	>	'ꣽ'
+ [RALT K_BKSLASH]	>	'꣺'
+ [RALT K_RBRKT]	>	'꣹'
+ [RALT K_LBRKT]	>	'꣸'
+ [RALT K_L]	>	'ꣷ'
+ [RALT K_QUOTE]	>	'ꣶ'
+ [RALT K_COLON]	>	'ꣵ'
+ [RALT K_SLASH]	>	'ꣴ'
+ [RALT K_PERIOD]	>	'ꣳ'
+ [RALT K_COMMA]	>	'ꣲ'

+ [RALT	K_0]	>	beep
c + [RALT	K_BKSLASH]	>	beep
c + [RALT	K_COLON]	>	beep
c + [RALT	K_COMMA]	>	beep
c + [RALT	K_EQUAL]	>	beep
c + [RALT	K_HYPHEN]	>	beep
c + [RALT	K_LBRKT]	>	beep
c + [RALT	K_L]	>	beep
c + [RALT	K_PERIOD]	>	beep
c + [RALT	K_QUOTE]	>	beep
c + [RALT	K_RBRKT]	>	beep
c + [RALT	K_SLASH]	>	beep
+ [RALT K_J]	>	beep
+ [RALT K_K]	>	beep

c Alternate - Right Ctrl for Samaveda Marks
+ [RCTRL K_1]	>	'꣡'
+ [RCTRL K_2]	>	'꣢'
+ [RCTRL K_3]	>	'꣣'
+ [RCTRL K_4]	>	'꣤'
+ [RCTRL K_5]	>	'꣥'
+ [RCTRL K_6]	>	'꣦'
+ [RCTRL K_7]	>	'꣧'
+ [RCTRL K_8]	>	"꣢꣯"
+ [RCTRL K_9]	>	"꣢꣫"
+ [RCTRL K_0]	>	"꣣꣬"
+ [RCTRL K_A]	>	'꣪'
+ [RCTRL K_K]	>	'꣬'
+ [RCTRL K_N]	>	'꣭'
+ [RCTRL K_P]	>	'꣮'
+ [RCTRL K_R]	>	'꣯'
+ [RCTRL K_S]	>	'꣱'
+ [RCTRL K_U]	>	'꣫'
+ [RCTRL K_V]	>	'꣰'

+ [RCTRL K_SLASH]	>	'᳓'
+ [RCTRL K_PERIOD]	>	'᳑'
+ [RCTRL K_COMMA]	>	'᳐'
+ [RCTRL K_BKSLASH] > '꣼'
+ [RCTRL K_RBRKT] > '꣹'
+ [RCTRL K_LBRKT] > '꣸'
+ [RCTRL K_QUOTE] > 'ꣻ'
+ [RCTRL K_COLON] > '꣺'
+ [RCTRL K_Z] >  '᳸'
+ [RCTRL K_X] >  '᳹'
+ [RCTRL K_C] >  '᳒'

c Unmapped RCTRL Keys
+ [RCTRL K_B]	>	beep
+ [RCTRL K_D]	>	beep
+ [RCTRL K_EQUAL] > beep
+ [RCTRL K_E] > beep
+ [RCTRL K_F]	>	beep
+ [RCTRL K_G]	>	beep
+ [RCTRL K_HYPHEN]	>	beep
+ [RCTRL K_H]	>	beep
+ [RCTRL K_I]	>	beep
+ [RCTRL K_J]	>	beep
+ [RCTRL K_O]	>	beep
+ [RCTRL K_Q] > beep
+ [RCTRL K_T]	>	beep
+ [RCTRL K_W] >beep
+ [RCTRL K_Y]	>	beep
c + [RCTRL K_M]	>	beep c hangs while importing in OSK

c EOF
