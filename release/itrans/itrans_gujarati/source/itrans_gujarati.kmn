c  Version 1.0.0 Initial Version
c  20190518 - Based on itrans_devanagari_hindi
c  20190522 - Update based on PR feedback
c  Version 1.2.0
c  20190528 
c Sync with new code from sanskrit_vedic
c Add Gujarati specific modifications eg. ZHA
c Add ZWJ and ZWNJ to OSK
c 20190530
c Make changes based on PR feedback
c Fix documentation for Nuktas
c Add info regarding ZWJ in welcome.htm
c 20190601
c Reduce nukta letters - only for Avestan now
c Add Avestan written in Gujarati as language
c Update regression tests
c
store(&VERSION) "10.0" 
store(&NAME) 'Gujarati Phonetic (ITRANS)'
store(&TARGETS) 'web desktop'
store(©RIGHT) '©2019 sanskritdocuments.org'
store(&MESSAGE) 'A Phonetic (ITRANS) keyboard for Gujarati.'
store(&KMW_HELPTEXT) 'Uses Unshifted, Shifted layouts.'
store(&KEYBOARDVERSION) '1.2.0'
store(&BITMAP) 'itrans_gujarati.ico'
store(&VISUALKEYBOARD) 'itrans_gujarati.kvks'

begin Unicode > use(main)
group(main) using keys

c  Set Hindi mode by default (implict word ending `a`)

store(opt1) 'hindi'
+ [RCTRL RALT K_0]	>	set(opt1='0') 
+ [RCTRL RALT K_1]	>	reset(opt1)

c 	Special Symbols
"ઑ" + "M" > "ૐ" 
+ "#"	>	"ૐ"
+ "$"	>	"₹"
+ "%"	>	"રૂ૰"
+ "*"	>	"❀"
+ "&"	>	"☸"
+ "V"	>	"ઽ"	c AVAGRAHA

c 	Common VEDIC MARKS
"'" + "'"	>	U+0951	c DEVANAGARI STRESS SIGN DATTA - SVARITA
U+0951 + "'"	>	U+1CDA	c VEDIC TONE DOUBLE SVARITA
U+1CDA + "'"	>	U+1CDB	c VEDIC TONE TRIPLE SVARITA
"`" + "`"	>	U+0952	c DEVANAGARI STRESS SIGN ANUDATTA
"ઁ" + "^"	>	"ꣳ"	c CANDRABINDU VIRAMA - MM^
"=" + "="	>	"ᳶ"	c VEDIC SIGN UPADHMANIYA     

c Special Marks
+ "_"	>	U+200C	c ZERO-WIDTH NON-JOINER
+ "+"	>	U+200D	c ZERO-WIDTH JOINER
+ "^"	>	"્"	c VIRAMA

c 	Punctuation
store(Punctuation) "!" "'" "(" ")" "-" "/" ":" ";" "" "?" "`" "{" "}" '"' "," "." "=" "\" "<" ">" "|" "[" "]"
+ any(Punctuation)	>	index(Punctuation,1)

+ "~"	>	"~" c Breaks Hindi mode rules if included in punctuation
+ "@"	>	"@" 

"," + ","	>	"૰"	c	GUJARATI ABBREVIATION SIGN
"." + "."	>	"।"	c twice for DANDA
"।" + "."	>	"॥"	c thrice for DOUBLE DANDA
"॥" + "."	>	U+2026	c four dots for a HORIZONTAL ELLIPSIS

"-" + "-"	>	U+2013	c hyphen twice for EN DASH
U+2013 + "-"	>	U+2014	c three times for EM DASH

c 	numbers - Gujarati DigitsU
store(Digits)	"0"	"1"	"2"	"3"	"4"	"5"	"6"	"7"	"8"	"9"
store(DigitsU)	"૦"	"૧"	"૨"	"૩"	"૪"	"૫"	"૬"	"૭"	"૮"	"૯"
+ any(Digits)	>	index(DigitsU,1)
any(DigitsU) + [K_BKSP]	>	index(Digits,1)

c  standalone vowel signs with @
"@" + "A"	>	"ા"
"@" + "i"	>	"િ"
"@" + "I"	>	"ી"
"@" + "u"	>	"ુ"
"@" + "U"	>	"ૂ"
"@" + "e"	>	"ે"
"@" + "o"	>	"ો"
"@" + "E"	>	"ૅ"
"@" + "O"	>	"ૉ"
"@" + "a"	>	"@a"
"@a" + "a"	>	"ા"
"@a" + "e"	>	"ૅ"
"@a" + "i"	>	"ૈ"
"@a" + "u"	>	"ૌ"
"@a" + "w"	>	"ૉ"
"િ" + "i"	>	"ી"
"ુ" + "u"	>	"ૂ"
"ે" + "e"	>	"ી"
"ે" + "i"	>	"ૈ"
"ો" + "o"	>	"ૂ"
"ો" + "u"	>	"ૌ"
"@" + "R"	>	"ૃ"
"ૃ" + "R"	>	"ૃ" c @RR
"ૃ" + "^"	>	"ૄ" c @RR^
"@" + "L"	>	"ૢ"
"ૢ" + "L"	>	"ૢ" c @LL
"ૢ" + "^"	>	"ૣ" c @LL^

c  Independent Vowels
+ "a"	>	"અ"
"અ" + "a"	>	"આ"
+ "A"	>	"આ"
+ "i"	>	"ઇ"
"ઇ" + "i"	>	"ઈ"
"એ" + "e"	>	"ઈ"
+ "I"	>	"ઈ"
+ "u"	>	"ઉ"
"ઉ" + "u"	>	"ઊ"
"ઓ" + "o"	>	"ઊ"
+ "U"	>	"ઊ"
+ "e"	>	"એ"
+ "E"	>	"ઍ"
"અ" + "e"	>	"ઍ"
"અ" + "i"	>	"ઐ"
"એ" + "i"	>	"ઐ"
+ "o"	>	"ઓ"
+ "O"	>	"ઑ"
"અ" + "w"	>	"ઑ"
"અ" + "u"	>	"ઔ"
"ઓ" + "u"	>	"ઔ"
"ઋ" + "R"	>	"ઋ" c RR
"ઋ" + "^"	>	"ૠ" c RR^
"ળ્" + "L"	>	"ઌ" c LL
"ઌ" + "^"	>	"ૡ" c LL^

c  Vowel Modifiers
+ "H"	>	"ઃ" c VISARGA - H
+ "M"	>	"ં" c ANUSVARA - M
"ં" + "M"	>	"ઁ"	c CHANDRABINDU - MM

c  Consonants
+ "b"	>	"બ્"
+ "B"	>	"ભ્"
+ "c"	>	"ચ્"
+ "C"	>	"છ્"
+ "d"	>	"દ્"
+ "D"	>	"ડ્"
+ "g"	>	"ગ્"
+ "G"	>	"ઘ્"
+ "h"	>	"હ્"
+ "j"	>	"જ્"
+ "J"	>	"ઝ્"
+ "k"	>	"ક્"
+ "K"	>	"ખ્"
+ "l"	>	"લ્"
+ "L"	>	"ળ્"
+ "m"	>	"મ્"
+ "n"	>	"ન્"
+ "N"	>	"ણ્"
+ "p"	>	"પ્"
+ "P"	>	"ફ્"
+ "r"	>	"ર્"
+ "R"	>	"ઋ" c also RR
+ "s"	>	"સ્"
+ "S"	>	"ષ્"
+ "t"	>	"ત્"
+ "T"	>	"ટ્"
+ "v"	>	"વ્"
+ "w"	>	"વ્"
+ "x"	>	"ક્ષ્"
+ "y"	>	"ય્"
+ "Y"	>	"ઞ્"
+ "f"	>	"ફ્"

c  GUJARATI LETTER ZHA - not supported in most fonts
+ "z"	>	"ૹ્"

c  ununsed - beep
+ "F"	>	beep
+ "Q"	>	beep
+ "W"	>	beep
+ "X"	>	beep
+ "Z"	>	beep
+ "q"	>	beep

c  alternate mapping with ~
"~" + "n"	>	"ઞ્"
"~" + "N"	>	"ઙ્"

c  add h to consonants
"ક્" + "h"	>	"ખ્"
"ગ્" + "h"	>	"ઘ્"
"ચ્" + "h"	>	"ચ્" c same c=ch
"છ્" + "h"	>	"છ્" c same C=Ch
"જ્" + "h"	>	"ઝ્"
"ટ્" + "h"	>	"ઠ્"
"ડ્" + "h"	>	"ઢ્"
"ત્" + "h"	>	"થ્"
"દ્" + "h"	>	"ધ્"
"પ્" + "h"	>	"ફ્"
"બ્" + "h"	>	"ભ્"
"ષ્" + "h"	>	"ષ્" c same S=Sh
"સ્" + "h"	>	"શ્"

c  special combinations
$keymanonly: if(opt1 = '0') "ન્" + "k"	>	"ઙ્ક્"
$keymanonly: if(opt1 = '0') "ન્" + "g"	>	"ઙ્ગ્"
$keymanonly: if(opt1 = '0') "ન્" + "c"	>	"nc"
$keymanonly: if(opt1 = '0') "nc" + "h"	>	"ઞ્ચ્"	c nch
$keymanonly: if(opt1 = '0') "ન્" + "j"	>	"ઞ્જ્"
"ઘ્" + "Y"	>	"જ્ઞ્"	c	GY
"શ્" + "r"	>	"શ્ર્"	c	shr
"ણ્" + "g"	>	"ઙ્"	c	Ng

c  Nukta - https://r12a.github.io/scripts/gujarati/#extensions
"." + "k"	>	"ક઼્" c .k
"." + "g"	>	"ગ઼્" c .g
"ક઼્" + "h"	>	"ખ઼્" c .kh
"ગ઼્" + "h"	>	"ઘ઼્" c .gh
"." + "j"	>	"જ઼્" c .j
"." + "t"	>	"ત઼્" c .t

c  Rules for Consonants + Vowel Signs
store(Vyanjana) 'કક્ષખગઘઙચછજજ્ઞઝઞટઠડઢણતથદધનપફબભમયરલળવશ્રષસહૹ'
store(NuktaVyanjana) 'તઘજખકગ'
store(ViramaMaatraa) 'eiouAEIORUL'
store(ViramaMaatraaU) "ે" "િ" "ો" "ુ" "ા" "ૅ" "ી" "ૉ" "ૃ" "ૂ" "ૢ"
store(aPlusMaatraa) 'aeiuw'
store(aPlusMaatraaU) "ા" "ૅ" "ૈ" "ૌ" "ૉ"

c 	implicit akaara deletion for Hindi mode 
store(opt1endings) outs(Punctuation) ' '

$keymanonly: if(opt1 = 'hindi') any(Vyanjana)"્" + any(opt1endings)	> index(Vyanjana,2) index(opt1endings,4)
any(Vyanjana) + "^"	>	index(Vyanjana,1)"્"U+200C
any(Vyanjana)"્" + "^"	>	index(Vyanjana,1)"્" U+200C
any(Vyanjana)"્" + "a"	>	index(Vyanjana,1)
any(Vyanjana) + any(aPlusMaatraa) >	context index(aPlusMaatraaU,2) 
any(Vyanjana)"્" + any(ViramaMaatraa) >	index(Vyanjana,1) index(ViramaMaatraaU,3) 
any(Vyanjana)"િ" + "i"	>	index(Vyanjana,1)"ી"	c ii
any(Vyanjana)"ુ" + "u"	>	index(Vyanjana,1)"ૂ"	c uu
any(Vyanjana)"ે" + "e"	>	index(Vyanjana,1)"ી"	c ee
any(Vyanjana)"ે" + "i"	>	index(Vyanjana,1)"ૈ"	c ei
any(Vyanjana)"ો" + "o"	>	index(Vyanjana,1)"ૂ"	c oo
any(Vyanjana)"ો" + "u"	>	index(Vyanjana,1)"ૌ"	c ou

$keymanonly: if(opt1 = 'hindi') any(NuktaVyanjana)"઼" "્" + any(opt1endings)	>	index(NuktaVyanjana,2)"઼" index(opt1endings,5)
any(NuktaVyanjana)"઼" "્" + "^"	>	index(NuktaVyanjana,1)"઼""્"U+200C
any(NuktaVyanjana)"઼" "્" + "a"	>	index(NuktaVyanjana,1)"઼"
any(NuktaVyanjana)"઼" + any(aPlusMaatraa) >	 context index(aPlusMaatraaU,3) 
any(NuktaVyanjana)"઼" "્" + any(ViramaMaatraa) > index(NuktaVyanjana,1)"઼" index(ViramaMaatraaU,4) 
any(NuktaVyanjana)"઼" "િ" + "i"	>	index(NuktaVyanjana,1)"઼""ી"	c ii
any(NuktaVyanjana)"઼" "ુ" + "u"	>	index(NuktaVyanjana,1)"઼""ૂ"	c uu
any(NuktaVyanjana)"઼" "ે" + "e"	>	index(NuktaVyanjana,1)"઼""ી"	c ee
any(NuktaVyanjana)"઼" "ે" + "i"	>	index(NuktaVyanjana,1)"઼""ૈ"	c ei
any(NuktaVyanjana)"઼" "ો" + "o"	>	index(NuktaVyanjana,1)"઼""ૂ"	c oo
any(NuktaVyanjana)"઼" "ો" + "u"	>	index(NuktaVyanjana,1)"઼""ૌ"	c ou

c  EOF
