c dazaga_gourane_karra generated from template at 2023-11-28 21:03:39
c with name "dazaga_gourane_karra"
store(&VERSION) '10.0'
store(&NAME) 'dazaga_gourane_karra'
store(&COPYRIGHT) '© Zacharias van Stek & Henri Schellberg'
store(&KEYBOARDVERSION) '1.0.1'
store(&MnemonicLayout) "1"
store(&TARGETS) 'any'
store(&LAYOUTFILE) 'dazaga_gourane_karra.keyman-touch-layout'
store(&MESSAGE) 'keyboard for Dazaga Gourane Karra'
store(&KMW_HELPTEXT) 'keyboard for Dazaga Gourane Karra'
store(vowels) 'aeiouAEIOUáéíóúÁÉÍÓÚàèìòùÀÈÌÒÙäëöÄËÖ'


begin Unicode > use(main)

group(main) using keys

c acute and gravis for vowels

store(plainvowels) "aeiouAEIOU"
store(acutevowels) "áéíóúÁÉÍÓÚ"
store(gravevowels) "àèìòùÀÈÌÒÙ"
+ "'" > dk(apostrophe)
+ "´" > dk(acute)
+ "`" > dk(grave)
dk(apostrophe) + any(plainvowels) > index(acutevowels,2)
dk(acute) + any(plainvowels) > index(acutevowels,2)
dk(grave) + any(plainvowels) > index(gravevowels,2)
dk(apostrophe) + "'" > "'"
dk(acute) + "´" > "´"
dk(grave) + "`" > "`"

c misc lower case letters
";" + "a" > "ä"
";" + "e" > "ë"
";" + "o" > "ö"
";" + "g" > "ŋ"
";" + "y" > "ɲ"
";" + "n" > "ṅ"
";" + "s" > "ṡ"
";" + "r" > "ṙ"
c misc upper case letters
";" + "E" > "Ë"
";" + "A" > "Ä"
";" + "O" > "Ö"
";" + "G" > "Ŋ"
";" + "Y" > "Ɲ"
";" + "N" > "Ṅ"
";" + "S" > "Ṡ"
";" + "R" > "Ṙ"

c tonal diacritics
  c circ
  any(vowels) + '^' > index(vowels, 1) U+0302

  c macron
  any(vowels) + '|' > index(vowels, 1) U+0304

  c tilde
  any(vowels) + "~" > index(vowels, 1) U+0303
  



