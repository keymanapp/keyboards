c Gondi (Telugu) Keyman keyboard for the Adilabad Gondi (wsg) language spoken in Central India.
c Adapted from NLCI's Telugu Unicode keyboard.
store(&VERSION) '9.0'
store(&TARGETS) 'any'
store(&NAME) 'Gondi (Telugu)'
store(&COPYRIGHT) '© SIL International'
store(&KMW_HELPTEXT) 'Gondi in Telugu Script keyboard'
store(&KEYBOARDVERSION) '1.0.2'
store(&VISUALKEYBOARD) 'gondi_tel.kvks'
store(&LAYOUTFILE) 'gondi_tel.keyman-touch-layout'
store(&BITMAP) 'gondi_tel.ico'
begin Unicode > use(main)

group(main) using keys                                      

platform('hardware') + '=' > deadkey(1)
+ 'x' > deadkey(2)
+ 'X' > deadkey(3)
platform('hardware') + '`' > deadkey(4)
 
store (vowel)  'a' 'i' 'u' 'R' '|'     'E'     'O'\
               'A' 'I' 'U' '^' '_' 'e' 'Y' 'o' 'V' 
store (vowelU) U+0C05 U+0C07 U+0C09 U+0C0B U+0C0C        U+0C0F        U+0C13 \
               U+0C06 U+0C08 U+0C0A U+0C60 U+0C61 U+0C0E U+0C10 U+0C12 U+0C14

store (consonant) 'k' 'g' 'W' 'c' 'j' 'w' 't' 'd' 'N' 'q' '[' 'n' 'p' 'b' 'm' 'y' 'l' 'v' 'z' 's' \
                  'K' 'G'     'C' 'J'     'T' 'D'     'Q' '{'     'P' 'B'         'L'     'Z' 'h' 'F'
store (consonantU) U+0C15 U+0C17 U+0C19 U+0C1A  U+0C1C U+0C1E U+0C1F U+0C21 U+0C23 U+0C24 U+0C26 U+0C28 U+0C2A U+0C2C U+0C2E U+0C2F U+0C32 U+0C35 U+0C36 U+0C38 \
                   U+0C16 U+0C18        U+0C1B  U+0C1D        U+0C20 U+0C22        U+0C25 U+0C27        U+0C2B U+0C2D               U+0C33        U+0C37 U+0C39 U+0C34

store (specialcons)  'r'
                          
store (specialconsU) U+0C31

store (specialcase2) 'r'
store (specialcase2U) U+0C30

store (diacriticalmod)  'M' 'H' 'S'
store (diacriticalmodU) U+0C02 U+0C03 U+0C01

store (number)  '0' '1' '2' '3' '4' '5' '6' '7' '8' '9'
store (Telugu) U+0C66 U+0C67 U+0C68 U+0C69 U+0C6A U+0C6B U+0C6C U+0C6D U+0C6E U+0C6F

+ 'f' > context U+0C3D c TELUGU_SIGN_AVAGRAHA (newly added by MP, along with 'F' = U+0C34)

+ any(vowel) > index(vowelU,1)
+ any(consonant) > index(consonantU,1) 
dk(1) + any(specialcons) > index(specialconsU,2)
+ any(diacriticalmod) > index(diacriticalmodU,1) 
+ any(specialcase2) >  index(specialcase2U,1)
dk(4) + any(number) > index(Telugu,2)

any (consonantU) + 'a' > context U+200D  
any (consonantU) + 'A' > context U+0C3E  
any (consonantU) + 'i' > context U+0C3F
any (consonantU) + 'I' > context U+0C40
any (consonantU) + 'u' > context U+0C41
any (consonantU) + 'U' > context U+0C42
any (consonantU) + 'R' > context U+0C43
any (consonantU) + '^' > context U+0C44
any (consonantU) + 'e' > context U+0C46
any (consonantU) + 'E' > context U+0C47
any (consonantU) + 'Y' > context U+0C48
any (consonantU) + 'o' > context U+0C4A
any (consonantU) + 'O' > context U+0C4B
any (consonantU) + 'V' > context U+0C4C


any (consonantU) + any(diacriticalmod) > context index(diacriticalmodU,2)

any (consonantU) + 'x'  > context U+0C4D 
any (consonantU) U+0C4D + 'x' > context U+200C 

any (consonantU) 'A' + any(diacriticalmod) > context index(diacriticalmodU,3) 
any (consonantU) 'i' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (consonantU) 'I' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (consonantU) 'u' + any(diacriticalmod) > context index(diacriticalmodU,3) 
any (consonantU) 'U' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (consonantU) 'R' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (consonantU) '^' + any(diacriticalmod) > context index(diacriticalmodU,3)
any (consonantU) 'e' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (consonantU) 'E' + any(diacriticalmod) > context index(diacriticalmodU,3)
any (consonantU) 'Y' + any(diacriticalmod) > context index(diacriticalmodU,3)
any (consonantU) 'o' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (consonantU) 'O' + any(diacriticalmod) > context index(diacriticalmodU,3) 
any (consonantU) 'V' + any(diacriticalmod) > context index(diacriticalmodU,3) 

any (specialconsU) + 'x' > context U+0C4D 
any (specialconsU) U+0C4D + 'x' > index( specialconsU,1 ) U+0C4D U+200C

any (specialconsU) + 'a' > context U+200D  
any (specialconsU) + 'A' > context U+0C3E  
any (specialconsU) + 'i' > context U+0C3F
any (specialconsU) + 'I' > context U+0C40
any (specialconsU) + 'u' > context U+0C41
any (specialconsU) + 'U' > context U+0C42
any (specialconsU) + 'R' > context U+0C43
any (specialconsU) + '^' > context U+0C44
any (specialconsU) + 'e' > context U+0C46
any (specialconsU) + 'E' > context U+0C47
any (specialconsU) + 'Y' > context U+0C48
any (specialconsU) + 'o' > context U+0C4A
any (specialconsU) + 'O' > context U+0C4B
any (specialconsU) + 'V' > context U+0C4C

any (specialconsU) + any(diacriticalmod) > context index(diacriticalmodU,2)

any (specialcase2U) + 'a' > context U+200D  
any (specialcase2U) + 'A' > context U+0C3E  
any (specialcase2U) + 'i' > context U+0C3F
any (specialcase2U) + 'I' > context U+0C40
any (specialcase2U) + 'u' > context U+0C41
any (specialcase2U) + 'U' > context U+0C42
any (specialcase2U) + 'R' > context U+0C43
any (specialcase2U) + '^' > context U+0C44
any (specialcase2U) + 'e' > context U+0C46
any (specialcase2U) + 'E' > context U+0C47
any (specialcase2U) + 'Y' > context U+0C48
any (specialcase2U) + 'o' > context U+0C4A
any (specialcase2U) + 'O' > context U+0C4B
any (specialcase2U) + 'V' > context U+0C4C

any (specialcase2U) + any(diacriticalmod) > context index(diacriticalmodU,2)

any (specialcase2U) + 'x'  > context U+0C4D 
any (specialcase2U) U+0C4D  + 'x' > context U+200C

any (specialcase2U) 'A' + any(diacriticalmod) > context index(diacriticalmodU,3) 
any (specialcase2U) 'i' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (specialcase2U) 'I' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (specialcase2U) 'u' + any(diacriticalmod) > context index(diacriticalmodU,3) 
any (specialcase2U) 'U' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (specialcase2U) 'R' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (specialcase2U) '^' + any(diacriticalmod) > context index(diacriticalmodU,3)
any (specialcase2U) 'e' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (specialcase2U) 'E' + any(diacriticalmod) > context index(diacriticalmodU,3)
any (specialcase2U) 'Y' + any(diacriticalmod) > context index(diacriticalmodU,3)
any (specialcase2U) 'o' + any(diacriticalmod) > context index(diacriticalmodU,3)  
any (specialcase2U) 'O' + any(diacriticalmod) > context index(diacriticalmodU,3) 
any (specialcase2U) 'V' + any(diacriticalmod) > context index(diacriticalmodU,3) 

c Indic Vowel Signs (if ever needed in isolation)

dk(1) + [K_SPACE] > dk(1)" " 

dk(1)" " + [SHIFT K_a] > U+0C3E
dk(1)" " + [K_i]       > U+0C3F  
dk(1)" " + [SHIFT K_i] > U+0C40
dk(1)" " + [K_u]       > U+0C41
dk(1)" " + [SHIFT K_u] > U+0C42
dk(1)" " + [SHIFT K_r] > U+0C43
dk(1)" " + [SHIFT K_6] > U+0C44
dk(1)" " + [SHIFT K_e] > U+0C46
dk(1)" " + [K_e]       > U+0C47
dk(1)" " + [SHIFT K_y] > U+0C48
dk(1)" " + [SHIFT K_o] > U+0C4A     
dk(1)" " + [K_o]       > U+0C4B
dk(1)" " + [SHIFT K_v] > U+0C4C
dk(1)" " + [SHIFT K_m] > U+0C02
dk(1)" " + [SHIFT K_h] > U+0C03
dk(1)" " + [SHIFT K_s] > U+0C01
dk(1)" " + [K_x]       > dk(1)"  " 
dk(1)"  " + [K_x]      > U+0C4D 

c Punctuation marks
c "'" + "'" > U+201D    c Closing double quotes
c '"' + '"' > U+201C    c Opening double quotes
c "-" + "-" > U+005F U+005F               c Drawing line
c U+005F U+005F + '-' > context U+005F    c Drawing line 
"-" + "-"   > "_" c This has been added in order to make it possible to type an underscore

c Consonant Cluster Handling (with virama and ZWJ or ZWNJ forms)
any ( consonantU ) + 'X'  > context U+0C4D U+200D
any ( specialconsU ) + 'X'  > context U+0C4D U+200D
any ( specialcase2U )  + 'X'  > context U+0C4D U+200D

any ( consonantU ) U+0C4D U+200C + 'X' > index(consonantU,1) U+0C4D U+200D  
any ( specialconsU ) U+0C4D U+200C + 'X' > index(specialconsU,1) U+0C4D U+200D  
any ( specialcase2U ) U+0C4D U+200C + 'X' > index(specialcase2U,1) U+0C4D U+200D  

any ( consonantU ) U+0C4D U+200C + 'x' > index(consonantU,1) U+0C4D U+200D  
any ( specialconsU ) U+0C4D U+200C + 'x' > index(specialconsU,1) U+0C4D U+200D  
any ( specialcase2U ) U+0C4D U+200C + 'x' > index(specialcase2U,1) U+0C4D U+200D  

any (consonantU) U+0C4D U+0C30 + 'X' > index(consonantU,1) U+0C4D U+0C30 U+0C4D 
any (specialcase2U) U+0C4D U+0C30  + 'X' > index(specialcase2U,1) U+0C4D U+0C30 U+0C4D 

any (consonantU) U+0C4D U+0C30 U+0C4D U+200C + 'x' > index(consonantU,1) U+0C4D U+0C30 U+0C4D 
any (specialcase2U) U+0C4D U+0C30 U+0C4D U+200C + 'x' > index(specialcase2U,1) U+0C4D U+0C30 U+0C4D 
                                             
c Short-cuts for commonly used word endings/suffixes
+ [T_NG]     > U+0C02 U+0C17 U+0C4D         
+ [T_TANG]   > U+0C24 U+0C02 U+0C17 U+0C4D 
+ [T_NANG]   > U+0C28 U+0C02 U+0C17 U+0C4D 
+ [T_KUN]    > U+0C15 U+0C41 U+0C28 U+0C4D 
+ [T_TTAL]   > U+0C1F U+0C32 U+0C4D  
+ [T_GATAL]  > U+0C17 U+0C1F U+0C32 U+0C4D  
+ [T_DDUN]   > U+0C21 U+0C41 U+0C28 U+0C4D 
+ [T_TOR]    > U+0C24 U+0C4B U+0C30 U+0C4D 
+ [T_TAL]    > U+0C24 U+0C32 U+0C4D  
+ [T_DAL]    > U+0C26 U+0C32 U+0C4D  
+ [T_NAL]    > U+0C28 U+0C32 U+0C4D 
+ [T_VAL]    > U+0C35 U+0C32 U+0C4D 
+ [T_JEKE]   > U+0C1C U+0C46 U+0C15 U+0C46 
+ [T_TEKE]   > U+0C24 U+0C46 U+0C15 U+0C46 
+ [T_NEKE]   > U+0C28 U+0C46 U+0C15 U+0C46 
+ [T_SEKE]   > U+0C38 U+0C46 U+0C15 U+0C46 
               
c Frequently used short words in Gondi
+ [T_ANI]    > U+0C05 U+0C28 U+0C3F                              c and
+ [T_ASKE]   > U+0C05 U+0C38 U+0C4D U+0C15 U+0C46                c then
+ [T_AD]     > U+0C05 U+0C26 U+0C4D                              c she / it
+ [T_AV]     > U+0C05 U+0C35 U+0C4D                              c they / those
+ [T_AATA]   > U+0C06 U+0C24                                     c she became / it happened (nm)
+ [T_AATOR]  > U+0C06 U+0C24 U+0C4A U+0C30 U+0C4D                c that kind of man / he became / happened (m)
+ [T_ITTEKE] > U+0C07 U+0C24 U+0C4D U+0C24 U+0C46 U+0C15 U+0C46  c if saying
+ [T_INKE]   > U+0C07 U+0C02 U+0C15 U+0C46                       c now
+ [T_INJER]  > U+0C07 U+0C02 U+0C1C U+0C46 U+0C30 U+0C4D         c so saying
+ [T_IITAL]  > U+0C08 U+0C24 U+0C32 U+0C4D                       c this kind of
+ [T_IITANG] > U+0C08 U+0C24 U+0C02 U+0C17 U+0C4D                c these kinds of
+ [T_IITUR]  > U+0C08 U+0C24 U+0C42 U+0C41 U+0C30 U+0C4D         c these kinds of people (m)
+ [T_UNDI]   > U+0C09 U+0C02 U+0C26 U+0C3F                       c one
+ [T_UNDDE]  > U+0C09 U+0C02 U+0C21 U+0C46                       c furthermore / also
+ [T_UCCI]   > U+0C09 U+0C1A U+0C4D U+0C1A U+0C3F                c having sat
+ [T_ULTTA]  > U+0C09 U+0C32 U+0C4D U+0C1F                       c opposed
+ [T_SUDDSI] > U+0C38 U+0C42 U+0C21 U+0C4D U+0C38 U+0C3F         c having seen
+ [T_VOR]    > U+0C35 U+0C4B U+0C30 U+0C4D                       c he
+ [T_VON]    > U+0C35 U+0C4B U+0C28 U+0C4D                       c him
+ [T_BATI]   > U+0C2C U+0C24 U+0C3F                              c but

c Short-cuts for most commonly used question words (And they all conveniently start with b!)
+ [T_BATAL]  > U+0C2C U+0C24 U+0C32 U+0C4D                       c what
+ [T_BAGA]   > U+0C2C U+0C17                                     c where
+ [T_BASKE]  > U+0C2C U+0C38 U+0C4D U+0C15 U+0C46                c when
+ [T_BARI]   > U+0C2C U+0C3E U+0C30 U+0C3F                       c why
+ [T_BACOR]  > U+0C2C U+0C1A U+0C4A U+0C30 U+0C4D                c how much
+ [T_BAHAN]  > U+0C2C U+0C39 U+0C28 U+0C4D                       c how
+ [T_BAD]    > U+0C2C U+0C26 U+0C4D                              c who / which (nm.sg)
+ [T_BAV]    > U+0C2C U+0C35 U+0C4D                              c who / which (nm.pl)
+ [T_BOR]    > U+0C2C U+0C4B U+0C30 U+0C4D                       c who (m.sg)
+ [T_BUR]    > U+0C2C U+0C42 U+0C30 U+0C4D                       c who (m.pl)

c Gondi Consonant clusters (Just the top 100 of 250 possible clusters based on freq count from NT)
c Note that this is generated code:
+ [T_KA_TA]  > U+0C15 U+0C4D U+0C24   c count=3126
+ [T_KA_NA]  > U+0C15 U+0C4D U+0C28   c count= 938
+ [T_KA_RA]  > U+0C15 U+0C4D U+0C30   c count= 615
+ [T_KA_LA]  > U+0C15 U+0C4D U+0C32   c count= 226
+ [T_KA_SA]  > U+0C15 U+0C4D U+0C38   c count=2378
+ [T_GA_TA]  > U+0C17 U+0C4D U+0C24   c count= 392
+ [T_GA_DA]  > U+0C17 U+0C4D U+0C26   c count= 296
+ [T_GA_NA]  > U+0C17 U+0C4D U+0C28   c count= 320
+ [T_GA_RA]  > U+0C17 U+0C4D U+0C30   c count=1012
+ [T_GA_LA]  > U+0C17 U+0C4D U+0C32   c count= 290
+ [T_CA_CA]  > U+0C1A U+0C4D U+0C1A   c count=1175
+ [T_JA_KA]  > U+0C1C U+0C4D U+0C15   c count= 599
+ [T_JA_TA]  > U+0C1C U+0C4D U+0C24   c count= 399
+ [T_JA_NA]  > U+0C1C U+0C4D U+0C28   c count= 568
+ [T_JA_RA]  > U+0C1C U+0C4D U+0C30   c count=1110
+ [T_TTA_KA] > U+0C1F U+0C4D U+0C15   c count= 786
+ [T_TTA_TA] > U+0C1F U+0C4D U+0C24   c count= 556
+ [T_TTA_NA] > U+0C1F U+0C4D U+0C28   c count=1554
+ [T_TTA_VA] > U+0C1F U+0C4D U+0C35   c count= 329
+ [T_TTA_SA] > U+0C1F U+0C4D U+0C38   c count= 493
+ [T_DDA_KA] > U+0C21 U+0C4D U+0C15   c count=2838
+ [T_DDA_DDA] > U+0C21 U+0C4D U+0C21  c count=1437
+ [T_DDA_TA] > U+0C21 U+0C4D U+0C24   c count=1636
+ [T_DDA_MA] > U+0C21 U+0C4D U+0C2E   c count= 606
+ [T_DDA_VA] > U+0C21 U+0C4D U+0C35   c count= 788
+ [T_DDA_SA] > U+0C21 U+0C4D U+0C38   c count=2403
+ [T_TA_KA]  > U+0C24 U+0C4D U+0C15   c count= 628
+ [T_TA_TA]  > U+0C24 U+0C4D U+0C24   c count=9942
+ [T_TA_NA]  > U+0C24 U+0C4D U+0C28   c count=1504
+ [T_TA_MA]  > U+0C24 U+0C4D U+0C2E   c count=1063
+ [T_TA_RA]  > U+0C24 U+0C4D U+0C30   c count=1314
+ [T_DA_KA]  > U+0C26 U+0C4D U+0C15   c count= 407
+ [T_DA_NA]  > U+0C26 U+0C4D U+0C28   c count= 771
+ [T_DA_YA]  > U+0C26 U+0C4D U+0C2F   c count= 231
+ [T_DA_RA]  > U+0C26 U+0C4D U+0C30   c count= 297
+ [T_DA_LA]  > U+0C26 U+0C4D U+0C32   c count= 275
+ [T_NA_KA]  > U+0C28 U+0C4D U+0C15   c count=1551
+ [T_NA_GA]  > U+0C28 U+0C4D U+0C17   c count= 863
+ [T_NA_TA]  > U+0C28 U+0C4D U+0C24   c count=2968
+ [T_NA_DA]  > U+0C28 U+0C4D U+0C26   c count= 998
+ [T_NA_LA]  > U+0C28 U+0C4D U+0C32   c count= 500
+ [T_NA_VA]  > U+0C28 U+0C4D U+0C35  c count=12739
+ [T_PA_KA]  > U+0C2A U+0C4D U+0C15   c count= 599
+ [T_PA_TTA] > U+0C2A U+0C4D U+0C1F   c count= 436
+ [T_PA_TA]  > U+0C2A U+0C4D U+0C24   c count= 780
+ [T_PA_NA]  > U+0C2A U+0C4D U+0C28   c count= 752
+ [T_PA_RA]  > U+0C2A U+0C4D U+0C30   c count= 389
+ [T_PA_VA]  > U+0C2A U+0C4D U+0C35   c count= 497
+ [T_BA_KA]  > U+0C2C U+0C4D U+0C15   c count= 279
+ [T_BA_NA]  > U+0C2C U+0C4D U+0C28   c count= 416
+ [T_BA_RA]  > U+0C2C U+0C4D U+0C30   c count= 880
+ [T_MA_KA]  > U+0C2E U+0C4D U+0C15   c count=1439
+ [T_MA_TTA] > U+0C2E U+0C4D U+0C1F   c count= 943
+ [T_MA_DA]  > U+0C2E U+0C4D U+0C26   c count=4518
+ [T_MA_NA]  > U+0C2E U+0C4D U+0C28   c count=1420
+ [T_MA_VA]  > U+0C2E U+0C4D U+0C35   c count=1075
+ [T_YA_TA]  > U+0C2F U+0C4D U+0C24   c count=1310
+ [T_YA_DA]  > U+0C2F U+0C4D U+0C26   c count=1120
+ [T_YA_NA]  > U+0C2F U+0C4D U+0C28   c count=2219
+ [T_YA_MA]  > U+0C2F U+0C4D U+0C2E   c count=1976
+ [T_YA_LA]  > U+0C2F U+0C4D U+0C32   c count=2643
+ [T_YA_VA]  > U+0C2F U+0C4D U+0C35   c count=9175
+ [T_YA_SA]  > U+0C2F U+0C4D U+0C38   c count=2664
+ [T_RA_KA]  > U+0C30 U+0C4D U+0C15   c count=5724
+ [T_RA_GA]  > U+0C30 U+0C4D U+0C17   c count=2779
+ [T_RA_JA]  > U+0C30 U+0C4D U+0C1C   c count=2297
+ [T_RA_TA]  > U+0C30 U+0C4D U+0C24   c count=6049
+ [T_RA_DA]  > U+0C30 U+0C4D U+0C26   c count=1405
+ [T_RA_NA]  > U+0C30 U+0C4D U+0C28   c count= 909
+ [T_RA_PA]  > U+0C30 U+0C4D U+0C2A   c count=1023
+ [T_RA_BA]  > U+0C30 U+0C4D U+0C2C   c count=1605
+ [T_RA_MA]  > U+0C30 U+0C4D U+0C2E   c count=2668
+ [T_RA_VA]  > U+0C30 U+0C4D U+0C35   c count=1443
+ [T_RA_SA]  > U+0C30 U+0C4D U+0C38   c count=2029
+ [T_LA_KA]  > U+0C32 U+0C4D U+0C15   c count=4615
+ [T_LA_TA]  > U+0C32 U+0C4D U+0C24   c count=1308
+ [T_LA_MA]  > U+0C32 U+0C4D U+0C2E   c count= 575
+ [T_LA_VA]  > U+0C32 U+0C4D U+0C35   c count= 810
+ [T_LA_SA]  > U+0C32 U+0C4D U+0C38   c count= 963
+ [T_VA_KA]  > U+0C35 U+0C4D U+0C15   c count= 866
+ [T_VA_TA]  > U+0C35 U+0C4D U+0C24   c count= 264
+ [T_VA_RA]  > U+0C35 U+0C4D U+0C30   c count= 375
+ [T_VA_LA]  > U+0C35 U+0C4D U+0C32   c count=1194
+ [T_SA_KA]  > U+0C38 U+0C4D U+0C15   c count=5697
+ [T_SA_TTA] > U+0C38 U+0C4D U+0C1F   c count=2729
+ [T_SA_TA]  > U+0C38 U+0C4D U+0C24   c count=4345
+ [T_SA_NA]  > U+0C38 U+0C4D U+0C28   c count= 846
+ [T_SA_YA]  > U+0C38 U+0C4D U+0C2F   c count= 838
+ [T_SA_RA]  > U+0C38 U+0C4D U+0C30   c count=1259
+ [T_SA_VA]  > U+0C38 U+0C4D U+0C35   c count=2828
+ [T_HA_KA]  > U+0C39 U+0C4D U+0C15   c count=1050
+ [T_HA_GA]  > U+0C39 U+0C4D U+0C17   c count=1111
+ [T_HA_CA]  > U+0C39 U+0C4D U+0C1A   c count=1551
+ [T_HA_TA]  > U+0C39 U+0C4D U+0C24   c count=4575
+ [T_HA_NA]  > U+0C39 U+0C4D U+0C28   c count= 686
+ [T_HA_RA]  > U+0C39 U+0C4D U+0C30   c count= 649
+ [T_HA_VA]  > U+0C39 U+0C4D U+0C35   c count=1226

c Some common Telugu Clusters (but not used in Gondi)
+ [T_KA_SSA] > U+0C15 U+0C4D U+0C37
+ [T_SSA_RA] > U+0C36 U+0C4D U+0C30
+ [T_JA_NYA] > U+0C1C U+0C4D U+0C1E

c Some other fun things to have
+ [T_GMAIL] > '@gmail.com'
