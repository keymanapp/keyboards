store(&VERSION) '10.0'
store(&NAME) 'Ge' U+0027 'ez Berhan'
store(&COPYRIGHT) 'Copyright © 2021 Fr. Berhanemeskel M. Embaye OFM Cap.'
store(&KEYBOARDVERSION) '1.0'
store(&BITMAP) 'GeezBrhan.ico'
store(&MESSAGE) 'The Ge' U+0027 'ez Berhan is intended to be the fast, smooth and a shortcut to edit Eritrean/Ethiopian alphabets.'
store(&TARGETS) 'desktop'


c =====================Begin Data Section=======================================================

  store(ግዕዝ)  'ሀለሐመሠረሰሸቀቐበቨተቸኀነአኘከⷈኸⷐወዐዘዠየደዸጀገጘጠጨጰጸፀፈፐⶠⷀⶨⶰⷘⶸꬠꬨ' 
  store(ካዕብ)  'ሁሉሑሙሡሩሱሹቁቑቡቩቱቹኁኑኡኙኩⷉኹⷑዉዑዙዡዩዱዹጁጉጙጡጩጱጹፁፉፑⶡⷁⶩⶱⷙⶹꬡꬩ'
  store(ሣልስ)  'ሂሊሒሚሢሪሲሺቂቒቢቪቲቺኂኒኢኚኪⷊኺⷒዊዒዚዢዪዲዺጂጊጚጢጪጲጺፂፊፒⶢⷂⶪⶲⷚⶺꬢꬪ'
  store(ራብዕ)  'ሃላሓማሣራሳሻቃቓባቫታቻኃናኣኛካⷋኻⷓዋዓዛዣያዳዻጃጋጛጣጫጳጻፃፋፓⶣⷃⶫⶳⷛⶻꬣꬫ'
  store(ኃምስ)  'ሄሌሔሜሤሬሴሼቄቔቤቬቴቼኄኔኤኜኬⷌኼⷔዌዔዜዤዬዴዼጄጌጜጤጬጴጼፄፌፔⶤⷄⶬⶴⷜⶼꬤꬬ'
  store(ሳድስ)  'ህልሕምሥርስሽቅቕብቭትችኅንእኝክⷍኽⷕውዕዝዥይድዽጅግጝጥጭጵጽፅፍፕⶥⷅⶭⶵⷝⶽꬥꬭ'
  store(ሳብዕ)  'ሆሎሖሞሦሮሶሾቆቖቦቮቶቾኆኖኦኞኮⷎኾⷖዎዖዞዦዮዶዾጆጎጞጦጮጶጾፆፎፖⶦⷆⶮⶶⷞⶾꬦꬮ'
	
  store(ካዕብ-x) 'ꬁꬑꬉ'
  store(ሣልስ-x) 'ꬂꬒꬊ'
  store(ራብዕ-x) 'ꬃꬓꬋ'
  store(ኃምስ-x) 'ꬄꬔꬌ'
  store(ሳድስ-x) 'ꬅꬕꬍ'
  store(ሳብዕ-x) 'ꬆꬖꬎ'

c QWERTY keymappings
  store(a-keys)  'hlmrsqbtnpkwzdcxfgy'
  store(a-ሳድስ)   'ህልምርስቅብትንእክውዝድጥጽፍግይ'

  store(ab-ሳድስ)   'ህልስꬅርቅቍብትንእክኵⷍውዝⶵይድጥጽፍግምᎃᎋⶽ' 
  store(ab-j-ሳድስ) 'ሕኅሽⶥሥቕቝቭችኝዕኽዅⷕፅዥꬕዽጅጭጵፕጕጝⶖᎏꬥ'  c "j" following the above

  store(መዕን-ሳድስ) 'ህልምርስሽቅብትችኅንኝእክውፅዝይድዽጅግጥጭጵፕ'
  store(መዕን)     'ሇⶀⶁⶂⶃⶄቇⶅⶆⶇኇⶈⶉⶊኯዏፇⶋዯⶌⶍⶎጏⶏⶐⶑⶒ' c "O" following the above"

  store(ዘመደ፡ራብዕ-key) 'ሕልምርሥስሽቭትችንኝዝዥድዽጅጥጭጽጵ'
  store(ዘመደ፡ራብዕ-out) 'ሗሏሟሯሧሷሿቯቷቿኗኟዟዧዷዿጇጧጯጿጷ' c "A" following the above

  store(ዘመደ፡ያ-key) 'ምርእፍ'
  store(ዘመደ፡ያ)     'ፙፘኧፚ'  c "I" follwing the above

  store(ዘመደ፡ግዕዝ) 'ᎀቈቘᎄኈኰዀጐⶓᎈᎌ'
  store(ዘመደ፡ሣልስ) 'ᎁቊቚᎅኊኲዂጒⶔᎉᎍ'
  store(ዘመደ፡ራብዕ) 'ሟቋቛቧኋኳዃጓጟፏፗ'
  store(ዘመደ፡ኃምስ) 'ᎂቌቜᎆኌኴዄጔⶕᎊᎎ'
  store(ዘመደ፡ሳድስ) 'ᎃቍቝᎇኍኵዅጕⶖᎋᎏ'

  store(a-upper-keys) 'SBNPWZMGCXFQK'
  store(a-upper-ሳድስ)  'ꬅᎇⷍእⷅⶵᎃⷝⶽꬭᎋቍኵ'

  store(extra-keys) 'DHPR'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(LatinOnes) '1234567890-'
  store(GeezOnes)  '፩፪፫፬፭፮፯፰፱ቯ-'
  store(ዜማ)        '᎐᎖᎕᎘᎔᎑᎓᎒᎙ርስ'

 c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
  store(ግዕዝ-key) 'e'
  store(ካዕብ-key) 'u'
  store(ሣልስ-key) 'i'
  store(ራብዕ-key) 'a'
  store(ኃምስ-key) 'v'
  store(ሳብዕ-key) 'o'

  store(switch-key) 'j'
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 

  begin Unicode > use(main)
  group(main) using keys

c ---------------------Input of Special Symbols-------------------------------------------------f
  + any(a-keys)       > index(a-ሳድስ,1)
  + any(a-upper-keys) > index(a-upper-ሳድስ,1)

  any(መዕን-ሳድስ)     + 'O' > index(መዕን,1)
  any(ዘመደ፡ያ-key)   + 'I' > index(ዘመደ፡ያ,1)
  any(ዘመደ፡ራብዕ-key) + 'A' > index(ዘመደ፡ራብዕ-out,1)

  any(ab-ሳድስ) + any(switch-key) > index(ab-j-ሳድስ,1)
c ---------------------End Input of Special Symbols---------------------------------------------


c ---------------------Input of Basic Syllables-------------------------------------------------
  any(ሳድስ) + any(ግዕዝ-key) > index(ግዕዝ,1)
  any(ሳድስ) + any(ካዕብ-key) > index(ካዕብ,1)
  any(ሳድስ) + any(ሣልስ-key) > index(ሣልስ,1)
  any(ሳድስ) + any(ራብዕ-key) > index(ራብዕ,1)
  any(ሳድስ) + any(ኃምስ-key) > index(ኃምስ,1) 
  any(ሳድስ) + any(ሳብዕ-key) > index(ሳብዕ,1)

  any(ዘመደ፡ሳድስ) + any(ግዕዝ-key) > index(ዘመደ፡ግዕዝ,1)
  any(ዘመደ፡ሳድስ) + any(ሣልስ-key) > index(ዘመደ፡ሣልስ,1)
  any(ዘመደ፡ሳድስ) + any(ራብዕ-key) > index(ዘመደ፡ራብዕ,1)
  any(ዘመደ፡ሳድስ) + any(ኃምስ-key) > index(ዘመደ፡ኃምስ,1)


  any(ሳድስ-x) + any(ካዕብ-key) > index(ካዕብ-x,1)
  any(ሳድስ-x) + any(ሣልስ-key) > index(ሣልስ-x,1)
  any(ሳድስ-x) + any(ራብዕ-key) > index(ራብዕ-x,1)
  any(ሳድስ-x) + any(ኃምስ-key) > index(ኃምስ-x,1) 
  any(ሳድስ-x) + any(ሳብዕ-key) > index(ሳብዕ-x,1)

  + 'L' > deadkey(1)
  + 'T' > deadkey(2)
  + 'Y' > deadkey(3)

  deadkey(1) + any(switch-key) > 'ኍ'
  deadkey(2) + any(switch-key) > 'ⶭ'
  deadkey(3) + any(switch-key) > 'ꬍ'


  + any(ግዕዝ-key) > beep
  + any(ካዕብ-key) > beep
  + any(ሣልስ-key) > beep
  + any(ራብዕ-key) > beep
  + any(ኃምስ-key) > beep
  + any(ሳብዕ-key) > beep

  + any(extra-keys) > beep
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Punctuation -----------------------------------------------------
c
+ ',' > '፡'
+ '.' > '።'
'/' + any(switch-key) > '፧'
'።' + any(switch-key) > '.'
'፡' + any(switch-key) > ','

+ ';' > '፤'
+ ':' > '፦'
'፤' + any(switch-key) > ';'
'፦' + any(switch-key) > ':'

+ "'" > '፣'
+ '"' > '፥'
'፣' + any(switch-key) > "'"
'፥' + any(switch-key) > '"'

  + any(switch-key) > beep
c ---------------------End Input Punctuation ---------------------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------
c
c Ge'ez Numerals
any(LatinOnes) + 'u' > index(GeezOnes,1)

 '10' + 'u' > '፲'
 '20' + 'u' > '፳'
 '30' + 'u' > '፴'
 '40' + 'u' > '፵'
 '50' + 'u' > '፶'
 '60' + 'u' > '፷'
 '70' + 'u' > '፸'
 '80' + 'u' > '፹'
 '90' + 'u' > '፺'
 '100' + 'u' > '፻'
 '1000' + 'u' > '፲፻'
 '10000' + 'u' > '፼'

c Zema Musical Notes
any(LatinOnes) + 'o' > index(ዜማ,1)

c ---------------------End Input of Numbers-----------------------------------------------------

c =====================End  Functional Section==================================================
