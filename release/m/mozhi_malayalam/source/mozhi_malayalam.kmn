c Malayalam Keyman Keyboard for Mozhi Keyboard Layout
c Original work done by Raj Nair, but the source is not available
c This keyboard was created by Junaid P. V.
c This keyboard closely follows Mozhi Spec (https://sites.google.com/site/cibu/mozhi/mozhi2)
store(&VERSION) '9.0'
store(&NAME) 'Malayalam Mozhi'
store(&KEYBOARDVERSION) '2.0.1'
store(&BITMAP) 'mozhi_malayalam.ico'
store(&COPYRIGHT) '© Junaid P. V.'
store(&TARGETS) 'windows macosx linux web'
store(&ETHNOLOGUECODE) 'mal'
store(&WINDOWSLANGUAGES) 'x044C'
store(&LANGUAGE) 'x044C'
begin Unicode > use(main)


group(main) using keys

store(consonants) "കഖഗഘങചഛജഝഞടഠഡഢണതഥദധനപഫബഭമയരലവശഷസഹളഴറ"
store(vowels) "അആഇഈഉഊഋൠഌൡഎഏഐഒഓഔ"
store(vowelsigns) "ാിീുൂൃൄൢൣെേൈൊോൗ"

c special for m
any(consonants) + 'm' > index(consonants,1) 'ം'
any(vowels) + 'm' > index(vowels,1) 'ം'
any(consonants) any(vowels) + 'm' > index(consonants,1)  index(vowels,2) 'ം'

store(chills) 'ൺൻർൽൾൿം'
store(chillSource) 'NnrlLcm'
store(chillBases) "ണനരലളകമ"

c produce chill
+ any(chillSource) > index(chills,1)
any(chills) U+200C + any(chillSource) > index(chills,1) index(chills,3)

c chill breakers
any(chills) + '~' > index(chillBases,1) '്'
any(chills) + 'a' > index(chillBases,1)
any(chills) + 'A' > index(chillBases,1) 'ാ'
any(chills) + 'i' > index(chillBases,1) 'ി'
any(chills) + 'I' > index(chillBases,1) 'ീ'
any(chills) + 'u' > index(chillBases,1) 'ു'
any(chills) + 'U' > index(chillBases,1) 'ൂ'
any(chills) + 'R' > index(chillBases,1) 'ൃ'
any(chills) + 'e' > index(chillBases,1) 'െ'
any(chills) + 'E' > index(chillBases,1) 'േ'
any(chills) + 'E' > index(chillBases,1) 'േ'
any(chills) + 'o' > index(chillBases,1) 'ൊ'
any(chills) + 'O' > index(chillBases,1) 'ോ'
any(chills) + 'y' > index(chillBases,1) '്യ്'
any(chills) + 'R' > index(chillBases,1) 'ൃ'            
'ർ' + 'r' > 'റ്' c Ben's addition
'ന്ര്' + 'r' > 'ൻറ്' c Ben's addition
any(chills) + 'r' > index(chillBases,1) '്ര്' c Ben's addition

c Archaic Forms
'അ' + '\' > 'ഽ'    
'ഈ' + '\' > 'ൟ'
'ക്' + '\' > 'ൿ'
'റ്റ്' + '\' > 'ഺ്'
'റ്റ' + '\' > 'ഺ'
'ൻ' + '\' > 'ഩ്'
'ന' + '\' > 'ഩ'
c TODO: n@erra	 ൻെറ	 CHILLU-N, SIGN E, RRA	 എൻെറ	 en@erra
c TODO: n@errarra	 ൻെററ	 CHILLU-N, SIGN E, RRA, RRA	 എൻെററ	 en@errarra
c TODO: 'ൾ' '@' + 'ക്' > 'ൾ്ക്'
'നു' + '\' > '൹' c Malayalam Date Mark
' ം' + '\' > U+0D00
U+0D00 + '\' >  U+0D01
'ർ' + '\' > 'ൎ'
+ '$' > '₹' 
'₹' + '\' > '₨'
'₨' + '\' > '$' c Ben's additions
'പറ' + '\' > U+0D4F
'ൾ' + '\' > 'ഌ'
'‌ള്' + '\' > 'ൢ'
'ള്ള്' + '\' > 'ൡ'
'ഌ' + '\' > 'ൡ' c Ben's additions
'ള്ള്' + '\' > 'ൣ'
'ൢ' + '\' > 'ൣ' c Ben's additions
'ൗ' + '\' > 'ൌ'
'്' + '\' > U+0D3C
U+0D3C + '\' > U+0D3B
any(consonants) '്ല്' + '\' > index(consonants, 1) U+0D62
c Sequence Chills
any(chills) '\' + '\' > index(chillBases,1) '്' U+200D
'ഩ്' + '\' > 'ന' '്' U+200D

c The following rule is an experimental rule to prevent chillu r formation 
c (instead it will form vattu ra form) if it follows a chandrakkala. By Ben
'്' + 'r' > '്ര്'                                                                                            

c produce vowels
store(vDirectSmallValues) "അഎഇഒഉ"
store(vDirectSmallKeys) "aeiou"
store(vDirectCapsValues) "ആഏഐഓഋഊ"
store(vDirectCapsKeys) "AEIORU"

c always remove none joiner before vowels
U+200C + any(vDirectSmallKeys) > index(vDirectSmallValues,2)
U+200C + any(vDirectCapsKeys) > index(vDirectCapsValues,2)

+ any(vDirectSmallKeys) > index(vDirectSmallValues,1)
U+200C + any(vDirectSmallKeys) > index(vDirectSmallValues,2)
+ any(vDirectCapsKeys) > index(vDirectCapsValues,1)
U+200C + any(vDirectCapsKeys) > index(vDirectCapsValues,2)

'അ' + 'a' > 'ആ'
'അ' + 'i' > 'ഐ'
'അ' + 'u' > 'ഔ'
'ഇ' + 'i' > 'ഈ'
'ഉ' + 'u' > 'ഊ'
'ഊ' + 'o' > 'ഊഊ'
'ഋ' + 'R' > 'ൠ'
'ഋ' + 'a' > 'റ'
'ഋ' + 'A' > 'റാ'
'ഋ' + 'i' > 'റി'
'ഋ' + 'I' > 'റീ'
'ഋ' + 'u' > 'റു'
'ഋ' + 'U' > 'റൂ'
'ഋ' + 'e' > 'റെ'
'ഋ' + 'E' > 'റേ'
'ഋ' + 'o' > 'റൊ'
'ഋ' + 'O' > 'റോ'
'ഋ' + '~' > 'റ്'
'ഌ' + 'l' > 'ൡ'
'എ' + 'e' > 'ഈ'
'ഒ' + 'o' > 'ഊ'
'ാ' + 'a' > 'ാാ'
'ീ' + 'i' > 'ീീ'
'ൂ' + 'u' > 'ൂൂ'
'ൂ' + 'o' > 'ൂൂ'
'ൄ' + 'R' > 'ൄൄ'
'േ' + 'E' > 'േേ'
'ോ' + 'O' > 'ോോ'
'ൗ' + 'u' > 'ൗൗ'

c vowel signs
'മ്' + '\' > 'ം'
'ആ' + '\' > 'ാ'
'ഇ' + '\' > 'ി'
'ഈ' + '\' > 'ീ'
'ഉ' + '\' > 'ു'
'ഊ' + '\' > 'ൂ'
'ഋ' + '\' > 'ൃ'
'ൠ' + '\' > 'ൄ'
'എ' + '\' > 'െ'
'ഏ' + '\' > 'േ'
'ഐ' + '\' > 'ൈ'
'ഒ' + '\' > 'ൊ'
'ഓ' + '\' > 'ോ'
'ഔ' + '\' > 'ൗ'
'ൗ' + '\' > 'ൌ'
'ഌ' + '\' > 'ൢ'
'ൡ' + '\' > 'ൣ'

store(thinConsonants) "കഗചജടഡതദപബസശ"
store(thickConsonants) "ഖഘഛഝഠഢഥധഫഭഷഴ"

store(cDirectSmallValues)  'ബദഫഗഹജകമപസവവയശ'
store(cDirectSmallKeys) 'bdfghjkmpsvwyz'
store(cDirectCapsValues) 'ഡശട'
store(cDirectCapsKeys) 'DST'

any(thinConsonants) '്' + 'h' > index(thickConsonants,1) '്' 
any(consonants) '്' + 'a' > index(consonants,1)
any(consonants) '്' + 'A' > index(consonants,1) 'ാ'
any(consonants) + 'a' > index(consonants,1) 'ാ'
any(consonants) '്' + 'i' > index(consonants,1) 'ി'
any(consonants) 'ി' + 'i' > index(consonants,1) 'ീ'
any(consonants) '്' + 'I' > index(consonants,1) 'ീ'
any(consonants) 'െ' + 'e' > index(consonants,1) 'ീ'
any(consonants) '്' + 'u' > index(consonants,1) 'ു'
any(consonants) 'ൊ' + 'o' > index(consonants,1) 'ൂ'
any(consonants) 'ു' + 'u' > index(consonants,1) 'ൂ'
any(consonants) '്' + 'U' > index(consonants,1) 'ൂ'
any(consonants)  '്' + 'R' > index(consonants,1) 'ൃ'
any(consonants)  'ൃ' + 'R' > index(consonants,1) 'ൄ'
any(consonants)  '്' + 'e' > index(consonants,1) 'െ'
any(consonants)  '്' + 'E' > index(consonants,1) 'േ'
any(consonants) + 'i' > index(consonants,1) 'ൈ'
any(consonants) '്' + 'o' > index(consonants,1) 'ൊ'
any(consonants) '്' + 'O' > index(consonants,1) 'ോ'
any(consonants) + 'u' > index(consonants,1) 'ൗ'

+ any(cDirectSmallKeys) > index(cDirectSmallValues,1) '്'
any(chills) U+200C + any(cDirectSmallKeys) > index(chills,1) index(cDirectSmallValues,3) '്'
+ any(cDirectCapsKeys) > index(cDirectCapsValues,1)  '്'
any(chills) U+200C + any(cDirectCapsKeys) > index(chills,1) index(cDirectCapsValues,3) '്' 

store(nonSa) "കഖഗഘങചഛജഝഞടഠഡഢണതഥദധനപഫബഭമയരലവശഷഹളഴറ"

'ൻ' + 'g' > 'ങ്'
'ൿ' + 'h' > 'ച്'

'ം' + 'm' > 'മ്മ്'
'ം' + 'l' > 'മ്ല്'
any(nonSa) 'ം' + 'r' > index(nonSa,1) 'മ്ര്'
'ം' + 'p' > 'മ്പ്'
any(nonSa) 'ം' + 'l' > index(nonSa,1) 'മ്ല്'
any(nonSa) 'ം' + 'L' > index(nonSa,1) 'മ്ല്' c deadkey(capl) c dk commented out by ben
any(nonSa) 'ം' + 'n' > index(nonSa,1) 'മ്ന്'
any(nonSa) 'ം' + 'y' > index(nonSa,1) 'മ്യ്'
any(vowelsigns) 'ം' + 'y' > index(vowelsigns,1) 'മ്യ്'
'സം' + 'r' > 'സംർ'
'സം' + 'y' > 'സംയ്'

c ['ൻൿh','nc','ഞ്ച്'],
'ൻൿ' + 'h' > 'ഞ്ച്'
c ['ൿh','c', 'ച്'],
c ['ക്ക്h','cc', 'ച്ച്'],
'ക്ക്' deadkey(doublec) + 'h' > 'ച്ച്'
c ['ല്പ്p','lp','ൽപ്പ്'],
'ല്പ്' + 'p' > 'ൽപ്പ്'
c ['([ക-ഹ])്ല്l', 'L', '$2\u0d62'],
c any(consonants) '്ല്' deadkey(capl) + 'l' > index(consonants,1) U+0D62

store(escapable) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ\>_~'
any(escapable) '\' + '\' > index(escapable,1)

'ന്ന്' + 'j' > 'ഞ്ഞ്'
'ന്ന്' + 'g' > 'ങ്ങ്'
'റ്റ്' + 'h' > 'ത്'
'റ്റ്' + 't' > 'ട്ട്'
'ന്റ്' + 'h' > 'ന്ത്'
any(consonants) U+0D62 + 'l' > index(consonants,1) U+0D63

c ['([അ-ഊഌഎ-ഐഒ-ഹ])([ഽ-ൂെ-ൌൗ])+R', '', '$2$3റ്'],
any(consonants) 'ൃ' + 'a' > index(consonants,1) '്ര'
any(consonants) 'ൃ' + 'A' > index(consonants,1) '്രാ'
any(consonants) 'ൃ' + 'i' > index(consonants,1) '്രി'
any(consonants) 'ൃ' + 'I' > index(consonants,1) '്രീ'
any(consonants) 'ൃ' + 'u' > index(consonants,1) '്രു'
any(consonants) 'ൃ' + 'U' > index(consonants,1) '്രൂ'
any(consonants) 'ൃ' + 'e' > index(consonants,1) '്രെ'
any(consonants) 'ൃ' + 'E' > index(consonants,1) '്രേ'
any(consonants) 'ൃ' + 'o' > index(consonants,1) '്രൊ'
any(consonants) 'ൃ' + 'O' > index(consonants,1) '്രോ'
any(consonants) '്' + 'l' > index(consonants,1) '്ല്' c deadkey(capl) c commented out by Ben
any(consonants) '്' + 'L' > index(consonants,1) '്ള്'  c deadkey(capl) c commented out by Ben

'ൺ' + 'N' > 'ണ്ണ്'
'ൺ' + 'T' > 'ണ്ട്'
'ൺ' + 'D' > 'ണ്ഡ്'
'ൺ' + 'v' > 'ണ്വ്'
'ൺ' + 'm' > 'ണ്മ്'

'ൻ' + 'r' > 'ന്ര്'
'ൻ' + 'k' > 'ങ്ക്'
'ൻ' + 'j' > 'ഞ്'
'ൻ' + 'n' > 'ന്ന്'
'ൻ' + 'd' > 'ന്ദ്'
'ൻ' + 't' > 'ന്റ്'
'ൻ' + 'm' > 'ന്മ്'
'ൻ' + 'L' > 'ന്ല്' c deadkey(capl) c commented out by Ben
'ൻ' + 'v' > 'ന്വ്'

'ർ' + 'r' > 'റ്'
'ർ' + '#' > 'ൎ'

'ൽ' + 'l' > 'ല്ല്'
'ൽ' + 'L' > 'ല്ല്' c deadkey(capl)
'ൽ' + 'm' > 'ല്മ്'
'ൽ' + 'p' > 'ല്പ്'
'ൽ' + 'v' > 'ല്വ്'

'ൾ' + 'L' > 'ള്ള്' c deadkey(capl)
'ൾ' + 'l' > 'ഌ'            

c Normally it was not required in Keymagic. Following does not happen
c automatically in Keyman

'ൿ' + 'l' > 'ക്ല്'
'ൿ' + 'L' > 'ക്ല്' c deadkey(capl)
'ൿ' + 'v' > 'ക്വ്'
'ൿ' + 'k' > 'ക്ക്'
'ൿ' + 'c' > 'ക്ക്' c deadkey(doublec)

c digits
'\' + '0' > '൦'
'\' + '1' > '൧'
'\' + '2' > '൨'
'\' + '3' > '൩'
'\' + '4' > '൪'
'\' + '5' > '൫'
'\' + '6' > '൬'
'\' + '7' > '൭'
'\' + '8' > '൮'
'\' + '9' > '൯'

+ 'B' > 'ബ്ബ്'
any(chills) U+200C + 'B' > index(chills,1) 'ബ്ബ്'
+ 'C' > 'ച്ച്'
any(chills) U+200C + 'C' > index(chills,1) 'ച്ച്'
+ 'G' > 'ഗ്ഗ്'
any(chills) U+200C + 'G' > index(chills,1) 'ഗ്ഗ്'
+ 'H' > 'ഃ'
any(chills) U+200C + 'H' > index(chills,1) 'ഃ'
+ 'J' > 'ജ്ജ്'
any(chills) U+200C + 'J' > index(chills,1) 'ജ്ജ്'
+ 'K' > 'ക്ക്'
any(chills) U+200C + 'K' > index(chills,1) 'ക്ക്'
+ 'M' > 'മ്മ്'
any(chills) U+200C + 'M' > index(chills,1) 'മ്മ്'
+ 'P' > 'പ്പ്'
any(chills) U+200C + 'P' > index(chills,1) 'പ്പ്'
+ 'Q' > 'ക്യ്'
any(chills) U+200C + 'Q' > index(chills,1) 'ക്യ്'
+ 'V' > 'വ്വ്'
any(chills) U+200C + 'V' > index(chills,1) 'വ്വ്'
+ 'W' > 'വ്വ്'
any(chills) U+200C + 'W' > index(chills,1) 'വ്വ്'
+ 'X' > 'ക്ഷ്'
any(chills) U+200C + 'X' > index(chills,1) 'ക്ഷ്'
+ 'Y' > 'യ്യ്'
any(chills) U+200C + 'Y' > index(chills,1) 'യ്യ്'
+ 'Z' > 'ശ്ശ്'
any(chills) U+200C + 'Z' > index(chills,1) 'ശ്ശ്'
+ 'q' > 'ക്ക്'
any(chills) U+200C + 'q' > index(chills,1) 'ക്ക്'
+ 't' > 'റ്റ്'
any(chills) U+200C + 't' > index(chills,1) 'റ്റ്'
+ 'x' > 'ക്ഷ്'
any(chills) U+200C + 'x' > index(chills,1) 'ക്ഷ്'
+ '~' > '്'
any(chills) U+200C + '~' > index(chills,1) '്' 

'ട്ട്' + 'h' > 'ത്ത്'
'ട്' + 'H' > 'ത്ത്'
'ൺ' + 'G' > 'ങ്ങ്'
'ൺ' + 'J' > 'ഞ്ഞ്'

U+200C + '_' > U+200C
+ '_' > U+200C

'/' + '/' > 'ऽ'
'1/4' + '\' > '൳'
'1/2' + '\' > '൴'
'3/4' + '\' > '൵'
'10' + '\' > U+0D70
'100' + '\' > U+0D71
'1000' + '\' > U+0D72
c em dash
'---' + '\' > '—'
c en dash
'--' + '\' > '–'
'\' + '-' > '−'
'\' + '*' > '×'
'\' + '/' > '÷'
