store(&VERSION) '9.0'
c Malayalam Keyman Keyboard for Mozhi Keyboard Layout
c Original work done by Raj Nair, but the source is not available
c This keyboard was created by Junaid P. V.
c This keyboard closely follows Mozhi Spec (https://sites.google.com/site/cibu/mozhi/mozhi2)
store(&NAME) 'Malayalam Mozhi'
store(&KEYBOARDVERSION) '2.0.8.4'
store(&BITMAP) 'mozhi_malayalam.ico'
store(&COPYRIGHT) '© Junaid P. V.'
store(&TARGETS) 'windows macosx linux web'
store(&ETHNOLOGUECODE) 'mal'
store(&WINDOWSLANGUAGES) 'x044C'
store(&LANGUAGE) 'x044C'
store(&VISUALKEYBOARD) 'mozhi_malayalam.kvks'
begin Unicode > use(main)

group(main) using keys

store(consonants) "കഖഗഘങചഛജഝഞടഠഡഢണതഥദധനപഫബഭമയരലവശഷസഹളഴറ"
store(vowels) "അആഇഈഉഊഋൠഌൡഎഏഐഒഓഔ"
store(vowelsigns) "ാിീുൂൃൄൢൣെേൈൊോൗ"

c Consonants
store(cDirectSmallKeys) 'kqQgjdpfbyvwzsh'
store(cDirectSmallValues)  'കഖഖഗജദപഫബയവവശസഹ'
store(cDirectCapsKeys) 'DSTF'
store(cDirectCapsValues) 'ഡശടഫ'

c produce vowels
store(vDirectSmallValues) "അഎഇഒഉ"
store(vDirectSmallKeys) "aeiou"
store(vDirectCapsValues) "ആഏഈഓഋഊ"
store(vDirectCapsKeys) "AEIORU"

c Chills
store(chills) 'ൺൻർൽൾൿ'
store(chillSource) 'NnrlLc'
store(chillBases) "ണനരലളക"

c Treat anusvaram as chillu (Solve Issue: https://github.com/beniza/keyboards/issues/1)
store(chills_anu) 'ൺൻർൽൾൿം'
store(chill_anu_Bases) "ണനരലളകമ"       

c always remove zwnj before a word final chars
store(WordFinalChars) ' ?.!)]}"”’'

c Store Universal KeyStroke stores
store(Keystrokes) outs(chillSource) outs(vDirectSmallKeys) outs(vDirectCapsKeys) outs(WordFinalChars) outs(cDirectSmallKeys) outs(cDirectCapsKeys)

c digits
store(digits) '0123456789'
store(malDigits) '൦൧൨൩൪൫൬൭൮൯'
any(malDigits) + any(digits) > index(malDigits,1) index(malDigits,2)
any(digits) + '\' > index(malDigits, 1)
'10' + '\' > U+0d70
'100' + '\' > U+0d71
'1000' + '\' > U+0d72
'1/4' + '\' > U+0d73
'1/2' + '\' > U+0d74
'3/4' + '\' > U+0d75
'1/16' + '\' > U+0d76
'1/8' + '\' > U+0d77
'3/16' + '\' > U+0d78

c Joiners (This is a new addition to the spec)
+ '+' > U+200D dk(zwj)  c Explicit zero-width-joiner
U+200D dk(zwj) + '+' > '+' c To enable the keyboarding of '+' sign  
'+' + '+' > '++' c Repeated plus signs
+ '|' > U+200C dk(zwnj) c Exlplicit zero-width-non-joiner
U+200C dk(zwnj) + '|' > U+200B dk(zwsp) c Explicit zero-width-space
U+200B dk(zwsp) + '|' > '|' c To enable the keyboarding of '|' sign
'|' + '|' > '||' c Repeated pipes      

c Explicit Virma
c Virama added explicitely (using ~) should be preserved 
U+0D4D + '~' > U+0D4D dk(vir)
c U+0D4D dk(vir) + any(consonants) > U+0D4D U+200D index(consonants, 3)
any(chills_anu) + '~' > index(chill_anu_bases, 1) '്' dk(vir)
dk(vir) + '_' > Nul c cancels the explicit virama property  
any(consonants) U+0D4D dk(vir) + any(cDirectSmallKeys) > index(consonants, 1) U+0D4D U+200C index(cDirectSmallValues, 4) U+0D4D
any(consonants) U+0D4D dk(vir) + any(cDirectCapsKeys) > index(consonants, 1) U+0D4D U+200C index(cDirectCapsValues, 4) U+0D4D
any(consonants) U+0D4D dk(vir) + any(chillSource) > index(consonants, 1) U+0D4D U+200C index(chills, 4)
any(consonants) U+0D4D dk(vir) + 'm' > index(consonants, 1) U+0D4D U+200C U+0D2E U+0D4D
any(consonants) U+0D4D dk(vir) + 't' > index(consonants, 1) U+0D4D U+200C U+0D31 U+0D4D U+0D31 U+0D4D

c Preventing Contextual Substitutions
c Modify the behaviour of '_' as a contextual substitutions blocker
+ '_' > '_' dk(noconjuncts)
'_' dk(noconjuncts) + '_' > '__'
'_' + '_' > '__'
'_' dk(noconjuncts) + '~' > U+0D4D
'_' dk(noconjuncts) + any(cDirectSmallKeys) > index(cDirectSmallValues, 3) U+0D4D
'_' dk(noconjuncts) + any(cDirectCapsKeys) > index(cDirectCapsValues, 3) U+0D4D
'_' dk(noconjuncts) + any(chillSource) > index(chills, 3)
'_' dk(noconjuncts) + 'm' > U+0D2E U+0D4D
'_' dk(noconjuncts) + 't' > U+0D31 U+0D4D U+0D31 U+0D4D

c To solve issue 22: m റ്റൈപ്പ് ചെയ്യുമ്പോൾ അനുസ്വാരത്തിനു പകരം മ് ആണ് വരുന്നത്
+ 'm' > 'ം'
c 'ം' + '~' > 'മ്'  c <= this is now handled directly by Explicit Virama rule above

c To solve issue 13: നുക്ത
'്' + 'F' > '़്'
c other special cases for m
any(consonants) + 'm' > index(consonants,1) 'ം'
any(vowels) + 'm' > index(vowels,1) 'ം'
any(vowelsigns) + 'm' > index(vowelsigns,1) 'ം'  
any(consonants) any(vowels) + 'm' > index(consonants,1)  index(vowels,2) 'ം'

c produce chill
+ any(chillSource) > index(chills,1)
any(chills) U+200C + any(chillSource) > index(chills,1) index(chills,3)                

c legacy chillu support (Solve Issue: https://github.com/beniza/keyboards/issues/5)
any(chills) + '^' > index(chillBases, 1) '്' U+200D
any(chillBases) '്' U+200D + '^' > index(chills, 1) c Not part of the spec, but I'm adding this to cycle between atomic and sequence chillu

c chill breakers
any(chills_anu) + '_' > index(chills_anu, 1) '_' dk(noconjuncts)
any(chills_anu) + '~' > index(chill_anu_Bases,1) '്'
any(chills_anu) + 'a' > index(chill_anu_Bases,1)
any(chills_anu) + 'A' > index(chill_anu_Bases,1) 'ാ'
any(chills_anu) + 'i' > index(chill_anu_Bases,1) 'ി'
any(chills_anu) + 'I' > index(chill_anu_Bases,1) 'ീ'
any(chills_anu) + 'u' > index(chill_anu_Bases,1) 'ു'
any(chills_anu) + 'U' > index(chill_anu_Bases,1) 'ൂ'
any(chills_anu) + 'R' > index(chill_anu_Bases,1) 'ൃ'
any(chills_anu) + 'e' > index(chill_anu_Bases,1) 'െ'
any(chills_anu) + 'E' > index(chill_anu_Bases,1) 'േ'
any(chills_anu) + 'E' > index(chill_anu_Bases,1) 'േ'
any(chills_anu) + 'o' > index(chill_anu_Bases,1) 'ൊ'
any(chills_anu) + 'O' > index(chill_anu_Bases,1) 'ോ'
any(chills_anu) + 'y' > index(chill_anu_Bases,1) '്യ്'
any(chills_anu) + 'R' > index(chill_anu_Bases,1) 'ൃ'            
'ർ' + 'r' > 'റ്' c Ben's addition
'ന്ര്' + 'r' > 'ൻറ്' c Ben's addition
any(chills_anu) + 'r' > index(chill_anu_bases,1) '്ര്' c Ben's addition

c Archaic Forms
'.' + '\' >  '।'
'..' + '\' > '॥'
'അ' + '\' > 'ഽ'    
'ഈ' + '\' > 'ൟ'
'ീ' + '\' > 'ൟ'
'ക്' + '\' > 'ൿ'
'റ്റ്' + '\' > 'ഺ്'
'റ്റ' + '\' > 'ഺ'
'ൻ' + '\' > 'ഩ്'
'ന' + '\' > 'ഩ'
'ം' + '\' > 'ൔ'
'ൔ' + '\' > 'ഀ'
'ഀ' + '\' > 'ഁ'
'ൻ' + '@' > context dk(at)
dk(at) + 'e' > 'െ'
'ൾ' + '@' > context '്' c dk(at)
'നു' + '\' > '൹' c Malayalam Date Mark
'ർ' + '\' > 'ൎ'
+ '$' > '₹' 
'₹' + '\' > '₨'
'₨' + '\' > '$' c Ben's additions
'പറ' + '\' > U+0D4F
'ൾ' + '\' > 'ഌ'
'്ള്' + '\' > 'ൢ'
'ള്ള്' + '\' > 'ൡ'
'ഌ' + '\' > 'ൡ' c Ben's additions
'്ള്ള്' + '\' > 'ൣ'
'ൢ' + '\' > 'ൣ' c Ben's additions
'ൗ' + '\' > 'ൌ'
'്' + '\' > U+0D3C
U+0D3C + '\' > U+0D3B
any(consonants) '്ല്' + '\' > index(consonants, 1) U+0D62

c always remove none joiner before vowels
U+200C + any(vDirectSmallKeys) > index(vDirectSmallValues,2)
U+200C + any(vDirectCapsKeys) > index(vDirectCapsValues,2)

+ any(vDirectSmallKeys) > index(vDirectSmallValues,1)
U+200C + any(vDirectSmallKeys) > index(vDirectSmallValues,2)
+ any(vDirectCapsKeys) > index(vDirectCapsValues,1)
U+200C + any(vDirectCapsKeys) > index(vDirectCapsValues,2)

c always remove zwnj before a word final chars
c U+200C + any(WordFinalChars) > index(WordFinalChars, 2)
U+200C + "'" > beep "'"    
U+200C + " " > beep " "
c always remove zwnj before a word final chars
c store(WordFinalChars) ' ?.!)]}"”’'
c Fix #50: Extra elongation for vowel signs
'അ' + 'a' > 'ആ'
'അ' + 'i' > 'ഐ'
'അ' + 'u' > 'ഔ'
'ഇ' + 'e' > 'ഈ'
'ഇ' + 'i' > 'ഈ'
'ഉ' + 'u' > 'ഊ'
'ഊ' + 'o' > 'ഊൗ'
'ഋ' + 'R' > 'ൠ'
'ഋ' + 'a' > 'റ'
'ഋ' + 'A' > 'റാ'
'ഋ' + 'i' > 'റി'
'ഋ' + 'I' > 'റീ'
'ഋ' + 'u' > 'റു'
'ഋ' + 'U' > 'റൂ'
'ഋ' + 'e' > 'റെ'
'ഋ' + 'E' > 'റേ'
'ഋ' + 'o' > 'റൊ'
'ഋ' + 'O' > 'റോ'
'ഋ' + '~' > 'റ്'
'ഌ' + 'l' > 'ൡ'
'എ' + 'a' > 'ഈ'
'എ' + 'e' > 'ഈ'
'എ' + 'i' > 'ഐ'
'ഒ' + 'o' > 'ഊ'
'ഒ' + 'u' > 'ഔ'  
'ി' + 'e' > 'ീ'
'ി' + 'i' > 'ൈ'
'െ' + 'a' > 'ീ'
'െ' + 'e' > 'ീ'
'ൊ' + 'o' > 'ോ'
'ൊ' + 'u' > 'ൗ'


c Stand-alone vowel signs after chandrakkala (Solve Issue: https://github.com/beniza/keyboards/issues/8)
'്' + 'A' > 'ാ'
'്' + 'a' > dk(2)
dk(2) + 'a' > 'ാ'
dk(2) + 'i' > 'ൈ'
dk(2) + 'u' > 'ൗ'
'്' + 'i' > 'ി'
'്' + 'I' > 'ീ' 
'ി' + 'i' > 'ീ'
'്' + 'u' > 'ു'
'്' + 'U' > 'ൂ'
'ു' + 'u' > 'ൂ'   
'്' + 'R' > 'ൃ'
'ൃ' + 'R' > 'ൄ'   
'്' + 'e' > 'െ'
'്' + 'E' > 'േ'
'െ' + 'e' > 'ീ'
'്' + 'o' > 'ൊ'   
'്' + 'O' > 'ോ'   
'്' + 'o' > 'ൂ'   

c To solve issue-20 ചന്ദ്രക്കലയ്ക്ക് ശേഷം ~ റ്റൈപ്പ് ചെയ്താൽ രണ്ടാമത്തെ ചന്ദ്രക്കല വരുന്നു
'്' + '~' > '്'   

c Consonants
+ any(cDirectSmallKeys) > index(cDirectSmallValues,1) '്'
+ any(cDirectCapsKeys) > index(cDirectCapsValues,1)  '്'

c To cleanup any unwanted zwnj between Chillu and a consonant
c any(chills) U+200C + any(cDirectSmallKeys) > index(chills,1) index(cDirectSmallValues,3) '്'
c any(chills) U+200C + any(cDirectCapsKeys) > index(chills,1) index(cDirectCapsValues,3) '്' 

c Aspiration and certain fluids are obtained by typing an 'h' after the unaspirated counterparts and സ, ശ
store(thinConsonants) "കഗചജടഡതദപബസശ"
store(thickConsonants) "ഖഘഛഝഠഢഥധഫഭഷഴ"
any(thinConsonants) '്' + 'h' > index(thickConsonants,1) '്' 

any(consonants) '്' + 'a' > index(consonants,1)
any(consonants) '്' + 'A' > index(consonants,1) 'ാ'
any(consonants) + 'a' > index(consonants,1) 'ാ'
any(consonants) '്' + 'i' > index(consonants,1) 'ി'
any(consonants) 'ി' + 'i' > index(consonants,1) 'ീ'
any(consonants) '്' + 'I' > index(consonants,1) 'ീ'
any(consonants) 'െ' + 'e' > index(consonants,1) 'ീ'
any(consonants) '്' + 'u' > index(consonants,1) 'ു'
any(consonants) 'ൊ' + 'o' > index(consonants,1) 'ൂ'
any(consonants) 'ു' + 'u' > index(consonants,1) 'ൂ'
any(consonants) '്' + 'U' > index(consonants,1) 'ൂ'
any(consonants)  '്' + 'R' > index(consonants,1) 'ൃ'
any(consonants)  'ൃ' + 'R' > index(consonants,1) 'ൄ'
any(consonants)  '്' + 'e' > index(consonants,1) 'െ'
any(consonants)  '്' + 'E' > index(consonants,1) 'േ'
any(consonants) + 'i' > index(consonants,1) 'ൈ'
any(consonants) '്' + 'o' > index(consonants,1) 'ൊ'
any(consonants) '്' + 'O' > index(consonants,1) 'ോ'
any(consonants) + 'u' > index(consonants,1) 'ൗ'
any(consonants)  '്' + 'r' > index(consonants,1) '്ര്'

store(nonSa) "കഖഗഘങചഛജഝഞടഠഡഢണതഥദധനപഫബഭമയരലവശഷഹളഴറ"

'ൻ' + 'g' > 'ങ്'
'ൿ' + 'h' > 'ച്'

'ം' + 'm' > 'മ്മ്'
'ം' + 'l' > 'മ്ല്'
c any(nonSa) 'ം' + 'r' > index(nonSa,1) 'മ്ര്'
'ം' + 'p' > 'മ്പ്'
any(nonSa) 'ം' + 'l' > index(nonSa,1) 'മ്ല്'
any(nonSa) 'ം' + 'L' > index(nonSa,1) 'മ്ല്' c deadkey(capl) c dk commented out by ben
any(nonSa) 'ം' + 'n' > index(nonSa,1) 'മ്ന്'
any(nonSa) 'ം' + 'y' > index(nonSa,1) 'മ്യ്'
any(vowelsigns) 'ം' + 'y' > index(vowelsigns,1) 'മ്യ്'
any(vowelsigns) 'ം' + 'r' > index(vowelsigns,1) 'മ്ര്'
'സം' + 'r' > 'സംർ'
'സം' + 'y' > 'സംയ്'

c Bypassing transliteration (Solve Issue: https://github.com/beniza/keyboards/issues/2)
+ '=' > dk(1)
dk(1) + '=' > '=' c to provide a way to keyin an actual '='                                                        

c Experimental: the sign chars at the end are added for enabling special strings such as web addresses or email ids. 
c Warning: treating = as an escapable can create inadvertent consequences as there may be cases to treat is as the dk. 
store(escapable) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ\>~@.=' 
dk(1) + any(escapable) > index(escapable, 2)     
any(escapable) + any(escapable) > index(escapable, 1) index(escapable, 2)

any(consonants) U+0D62 + 'l' > index(consonants,1) U+0D63

c The sequence 'nj' produces 'ഞ്ഞ്', iff it is typed after a vowel, vowelsign or a consonant   To Fix #39
any(consonants) 'ൻ' + 'j' > index(consonants, 1) 'ഞ്ഞ്'
any(vowels) 'ൻ' + 'j' > index(vowels, 1) 'ഞ്ഞ്'
any(vowelsigns) 'ൻ' + 'j' > index(vowelsigns, 1) 'ഞ്ഞ്'

c ['([അ-ഊഌഎ-ഐഒ-ഹ])([ഽ-ൂെ-ൌൗ])+R', '', '$2$3റ്'],
any(vowels) + 'R' > context 'ർ'
any(vowelSigns) + 'R' > context 'ർ'

any(consonants) 'ൃ' + 'a' > index(consonants,1) '്ര'
any(consonants) 'ൃ' + 'A' > index(consonants,1) '്രാ'
any(consonants) 'ൃ' + 'i' > index(consonants,1) '്രി'
any(consonants) 'ൃ' + 'I' > index(consonants,1) '്രീ'
any(consonants) 'ൃ' + 'u' > index(consonants,1) '്രു'
any(consonants) 'ൃ' + 'U' > index(consonants,1) '്രൂ'
any(consonants) 'ൃ' + 'e' > index(consonants,1) '്രെ'
any(consonants) 'ൃ' + 'E' > index(consonants,1) '്രേ'
any(consonants) 'ൃ' + 'o' > index(consonants,1) '്രൊ'
any(consonants) 'ൃ' + 'O' > index(consonants,1) '്രോ'
any(consonants) '്' + 'l' > index(consonants,1) '്ല്' c deadkey(capl) c commented out by Ben
any(consonants) '്' + 'L' > index(consonants,1) '്ള്'  c deadkey(capl) c commented out by Ben

'ൺ' + 'N' > 'ണ്ണ്'
'ൺ' + 'T' > 'ണ്ട്'
'ൺ' + 'D' > 'ണ്ഡ്'
'ൺ' + 'v' > 'ണ്വ്'
'ൺ' + 'm' > 'ണ്മ്'

'ൻ' + 'r' > 'ന്ര്'
'ന്ര്' + 'r' > 'ന്റ്'
'ൻ' + 'k' > 'ങ്ക്'
'ൻ' + 'j' > 'ഞ്'
'ൻ' + 'n' > 'ന്ന്'
'ൻ' + 'd' > 'ന്ദ്'
'ൻ' + 't' >  'ൻ്റ്' c To solve issue 35: the encoding of ൻ്റ
'ൻ' + 'm' > 'ന്മ്'
'ൻ' + 'L' > 'ന്ല്' c deadkey(capl) c commented out by Ben
'ൻ' + 'v' > 'ന്വ്'

'ർ' + 'r' > 'റ്'
'ർ' + '#' > 'ൎ'

'ൽ' + 'l' > 'ല്ല്'
'ൽ' + 'L' > 'ല്ല്' c deadkey(capl)
'ൽ' + 'm' > 'ല്മ്'
'ൽ' + 'p' > 'ല്പ്'
'ൽ' + 'v' > 'ല്വ്'

'ൾ' + 'L' > 'ള്ള്' c deadkey(capl)
'ൾ' + 'l' > 'ഌ'            

c Normally it was not required in Keymagic. Following does not happen
c automatically in Keyman

'ൿ' + 'l' > 'ക്ല്'
'ൿ' + 'L' > 'ക്ല്' c deadkey(capl)
'ൿ' + 'v' > 'ക്വ്'
'ൿ' + 'k' > 'ക്ക്'
'ൿ' + 'c' > 'ക്ക്' dk(3) c deadkey(doublec)
'ക്ക്' dk(3) + 'h' > 'ച്ച്'

c digits
'\' + '0' > '൦'
'\' + '1' > '൧'
'\' + '2' > '൨'
'\' + '3' > '൩'
'\' + '4' > '൪'
'\' + '5' > '൫'
'\' + '6' > '൬'
'\' + '7' > '൭'
'\' + '8' > '൮'
'\' + '9' > '൯'

+ 'B' > 'ബ്ബ്'
+ 'C' > 'ച്ച്'
+ 'G' > 'ഗ്ഗ്'
+ 'H' > 'ഃ'
+ 'J' > 'ജ്ജ്'
+ 'K' > 'ക്ക്'
+ 'M' > 'മ്മ്'
+ 'P' > 'പ്പ്'
+ 'Q' > 'ക്യു'
+ 'V' > 'വ്വ്'
+ 'W' > 'വ്വ്'
+ 'X' > 'ക്സ്'
+ 'Y' > 'യ്യ്'
+ 'Z' > 'ഴ്'
+ 'q' > 'ക്ക്'
+ 't' > 'റ്റ്'
+ 'x' > 'ക്ഷ്'
+ '~' > '്'
                  
c Error Correction in Conjuncts
'ട്ട്' + 'h' > 'ത്ത്'
'ട്' + 'H' > 'ത്ത്'
'ൺ' + 'G' > 'ങ്ങ്'
'ൺ' + 'J' > 'ഞ്ഞ്'
'ട്' + 't' > 'ട്ട്'
'റ്റ്' + 'T' > 'ട്ട്'
'ൻ' + 'T' > 'ണ്ട്'
'ൺ' + 't' > 'ണ്ട്'
'ന്ന്' + 'j' > 'ഞ്ഞ്'
'ന്ന്' + 'g' > 'ങ്ങ്'
'റ്റ്' + 'h' > 'ത്'
'റ്റ്' + 't' > 'ട്ട്' 
'ൻ്റ്' + 'h' > 'ന്ത്' 
'ന്റ്' + 'h' >  'ന്ത്'
'ന്‍റ്' + 'h' >  'ന്ത്'
'ൻൿ' + 'h' > 'ഞ്ച്'
'ക്ക്' deadkey(doublec) + 'h' > 'ച്ച്'
'ല്പ്' + 'p' > 'ൽപ്പ്'

c Issue 50: Extra elongation for vowel sign
'ആ' + 'a' > 'ആാ'
'ഈ' + 'i' > 'ഈൗ'
'ഈ' + 'e' > 'ഈൗ'
'ഊ' + 'o' > 'ഊൗ' 
'ൗ' + 'i' > 'ൗൗ'
'ഓ' + 'O' > 'ഓാ'
'ാ' + 'O' > 'ാാ'
'ൗ' + 'o' > 'ൗൗ'
'ഊ' + 'u' > 'ഊൗ' 
'ൗ' + 'u' > 'ൗൗ' 
'ഔ' + 'u' > 'ഔൗ' 
'ാ' + 'a' > 'ാാ'
'ീ' + 'e' > 'ീീ'
'ീ' + 'E' > 'ീീ'
'ീ' + 'i' > 'ീീ'
'ൂ' + 'u' > 'ൂൂ'
'ൂ' + 'o' > 'ൂൂ'
'ൂ' + 'O' > 'ൂൂ'
'ൄ' + 'R' > 'ൄൄ'
'േ' + 'E' > 'ീീ'
'ോ' + 'O' > 'ൂൂ'
'ൗ' + 'u' > 'ൗൗ'

c Important! The behaviour of '_' has been updated in the new mozhi spec. 
c According to the new spec, '_' will just block the execution contextual substitution
c of characters. for eg. nn => ന്ന് wherein n_n => ൻൻ.  So the following rules are 
c now depricated and hence I'm commenting them out. In a future release we can delete them from the source 
c '_' will output zwnj only after a chandrakkala
c '്' + '_' > '്' U+200C    
c prevent the typing of repeated zw(n)j
c U+200C + '_' > context                   
c U+200D + '_' > context
c '_' + 'm' > 'മ്' c fix issue 43

c Fix issue 54, Legacy encoding of ൻ്റ 
'ൻ്റ്' + '^' > 'ന്റ്'
'ന്റ്' + '^' > 'ന്' U+200D 'റ്'
'ൻ്റ' + '^' > 'ന്റ'
'ന്റ' + '^' > 'ന്' U+200D 'റ'  
'ൻ്റ' any(vowelSigns) + '^' > 'ന്റ' index(vowelSigns, 4)
'ന്റ' any(vowelSigns) + '^' > 'ന്' U+200D 'റ' index(vowelSigns, 4)

c Fix issue 56, Backspace  (the list below may not be comprehensive)
"ന്ന്" + [K_BKSP] > 'ൻ'  
"ന്ന" + [K_BKSP] > 'ൻ'  
'ൻ്റ്' + [K_BKSP] > 'ൻ'
'ൻ്റ' + [K_BKSP] > 'ൻ'
'ന്റ്' + [K_BKSP] > 'ൻ'
'ന്റ' + [K_BKSP] > 'ൻ'
'ന്‍റ്' + [K_BKSP] > 'ൻ'
'ന്‍റ' + [K_BKSP] > 'ൻ'
'ഞ്ച്' + [K_BKSP] > 'ൻ'
'ഞ്ച' + [K_BKSP] > 'ൻ'
'ന്മ്' + [K_BKSP] > 'ൻ'
'ന്മ' + [K_BKSP] > 'ൻ'
'ങ്ക്' + [K_BKSP] > 'ൻ'
'ങ്ക' + [K_BKSP] > 'ൻ'
'ണ്ട്' + [K_BKSP] > 'ൺ'
'ണ്ട' + [K_BKSP] > 'ൺ'
'ണ്ഡ്' + [K_BKSP] > 'ൺ'
'ണ്ഡ' + [K_BKSP] > 'ൺ'
'ണ്ഠ്' + [K_BKSP] > 'ൺ'
'ണ്ഠ' + [K_BKSP] > 'ൺ'
'മ്പ്'  + [K_BKSP] > 'ം'
'മ്പ'  + [K_BKSP] > 'ം'
'മ്മ്' + [K_BKSP] > 'ം'
'മ്മ' + [K_BKSP] > 'ം'
'ഞ്ഞ്' + [K_BKSP] > 'ൻ'
'ഞ്ഞ' + [K_BKSP] > 'ൻ' 
'ത്ത്' +  [K_BKSP] > 'ത്' 
'ത്ത' +  [K_BKSP] > 'ത്' 
c ''  +  [K_BKSP] > ''  
'ൢ'  +  [K_BKSP] > '്' 
'ൣ'  +  [K_BKSP] > '്' 
'റ്റ്' + [K_BKSP] > nul
'റ്റ' + [K_BKSP] > nul

c Convenience Mapping and Emoji
'---' + '\' > '—'
'--' + '\' > '–'
'\' + '-' > '−'
'\' + '*' > '×'
'\' + '/' > '÷'
':' + ')' > '😊'
':' + '(' > '☹'
':' + 'D' > '😄'
':' + 'x' > '😘'


c Smart single quotes
+"'" > U+2018
U+2018 + "'" > U+2019
U+2019 + "'" > "'"

c Smart double quotes
+ '"' > U+201C
U+201C + '"' > U+201D
U+201D + '"' > '"'
