store(&VERSION) '10.0'
store(&NAME) 'Mon Anonta'
store(&COPYRIGHT) 'Â© 2021-2022 Anonta Mon'
store(&KEYBOARDVERSION) '1.0.3'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'mon_anonta.kvks'
store(&LAYOUTFILE) 'mon_anonta.keyman-touch-layout'
store(&BITMAP) 'mon_anonta.ico'

begin Unicode > use(Main)

 store(baseK)   'qwert' \
		        'yuiop' \
		        "[zxc" \
		        "vbnQ" \
		        "OP{AKL" \
		        "ZXCVNM" \
		        '`!#%' \
		        '&' \
		        '=_\]' \
		        '~*' \
		        'WEUI' \
		        'B?/,.]'
                
 store(baseU)  u+1006 u+1010 u+1014 u+1019 u+1021 \
		       u+1015 u+1000 u+105A u+101E u+1005 \
		       u+101F u+1016 u+1011 u+1001 \
		       u+101C u+1018 u+100A u+105B \
		       u+103F u+100F u+1028 u+1017 u+1012 u+1013 \
		       u+1007 u+100C u+1003 u+1020 u+1009 U+00F7 \
		       U+105D u+100D u+100B '%' \
		       u+101B \
		       U+003D u+00D7 U+1051 u+1029 \
		       u+100E U+1002 \
		       u+101D u+1023 u+1025 U+104E  \
		       U+1050 U+104A U+104B U+101A u+105C u+1029 
               
                 
store(addK) ',.]'
store(addU) U+101A u+105C u+1029
             
store(numK)     '1234567890' 
store(numU)     U+1041 U+1042 U+1043 U+1044 U+1045 \
                U+1046 U+1047 U+1048 U+1049 U+1040                
                
store(aftereK)  'gmh;'
store(aftereU)  U+102B U+102C U+1034 U+1038

store (aaK)     'gm'
store (aaU)     U+102B U+102C  

store (lDotK)   'h'
store (lDotU)   U+1034    

store (visargaK) ';'
store (visargaU) U+1038
                      
store(udia1K)    'dDT^'
store(udia1U)    U+102D U+102E U+1033 U+1035

store(udia2K)   'JH'     
store(udia2U)   U+1032 U+1036
                  
store(ldiaK)    'kl'
store(ldiaU)    U+102F U+1030

store(asatK)    'f'
store(asatU)    U+103A 


store(consU)    u+1000 u+1001 u+1002 u+1003 u+105A u+1005 u+1006 u+1007 \
		  	   	u+105B u+1009 u+100A u+100B u+100C u+100D u+100E u+100F \
		  	   	u+1010 u+1011 u+1012 u+1013 u+1014 u+1015 u+1016 u+1017  \
		  	   	u+1018 u+1019 u+101A u+101B u+101C u+101D u+101E u+101F  \
		  	   	u+1020 u+105C u+1021 u+105D u+103F u+1050 u+1051 u+1029

store(mediaK)   'sjSGRY<' 
store(mediaU)   u+103B u+103C u+103E u+103D U+105F U+1060 U+105E

store(yDiaK)    's'
store(yDiaU)    U+103B

store(rDiaK)    'j'
store(rDiaU)    U+103C
   
store(wDiaK)    'G'
store(wDiaU)    U+103D 

store(hDiaK)    'S' 
store(hDiaU)    U+103E

store (mDiaK)   'R'
store (mDiaU)   U+105F

c store (lDia1K)   'Y'
c store (lDia1U)   U+1060

store(nDiaK)    '<'
store(nDiaU)    U+105E
                
store(filler)   U+200B 
  

store(vowelEK)  'a'
store(vowelEU)  U+1031  

store(allDiaU)  U+102F U+1030 U+102D U+102E U+1032 U+1036 u+1033 u+1034 u+1035
store(firstDiaU)  U+102D U+102F U+1032
store(secondDiaU)  U+102D U+1036 U+1037 U+102F    

store(stackK)   'F'
store(stackU)   U+1039  
store(abbK)     '@'
store(accK)     '|'
c store(addK)     '}' Handled in l. 180


group(Main) using keys 
+ any(vowelEK) > outs(filler) outs(vowelEU)
+ any(stackK) > outs(stackU)
any(filler) U+1031 + any(baseK) > index(baseU, 3) U+1031
U+1031 U+1039 + any(baseK) > U+1039 index(baseU,3) U+1031
U+1031 + any(mediaK) > index(mediaU, 2) U+1031
c U+1007 U+1031 + any(yDiaK) > U+105B U+1031 
c U+1007 + 's' > U+105B
U+1025 + 'f' > U+1009 U+103A
U+1025 + 'F' > U+1009 U+1039 
U+1025 + 'm' > U+1009 U+102C

any(filler) U+1031 + any(accK) > U+100B U+1039 U+100C U+1031
any(filler) U+1031 + any(abbK) > U+100F U+1039 U+100D  U+1031

+ any(addK)> index(addU,1)
+ any(numK) > index(numU, 1)
+ any(baseK) > index(baseU, 1) 
+ any(aftereK) > index(aftereU, 1) 
+ any(asatK) > outs(asatU)         

U+1025 + 'D' > U+1026 

any(udia2U)+ any(aaK) > index(aaU, 2) context  
any(ldiaU)+ any(udia1K) > index(udia1U, 2) context 
any(udia2U)+ any(ldiaK)> index(ldiaU, 2) context
any(udia2U)+ any(udia1K)> index(udia1U, 2) context
U+1036 + 'J'> U+1032 U+1036
U+1038 + any(lDotK) > index(lDotU, 2) context 
c Mon
any(consU) U+103A + 'S' > index(consU, 1) U+103E U+103A
any(consU) U+103E U+1031 + 'f' > index(consU, 1) U+103E U+103A U+1031
any(consU) U+103A U+1031 + 'S' > index(consU, 1) U+103E U+103A U+1031
U+102C U+103A + 'S' > U+102C U+103E U+103A

U+1031 + any(asatK)> index (asatU, 2) U+1031
+ any(ldiaK) > index(ldiaU, 1)
+ any(udia1K) > index(udia1U, 1)  
+ any(udia2K) > index(udia2U, 1)
 
 
any(wDiaU) + any(yDiaK) > index(yDiaU, 2) context
any(hDiaU) + any(yDiaK) > index(yDiaU, 2) context 
any(wDiaU) + any(rDiaK) > index(rDiaU, 2) context
any(hDiaU) + any(rDiaK) > index(rDiaU, 2) context 
any(hDiaU) + any(wDiaK) > index(wDiaU, 2) context   

any(wDiaU) any(hDiaU) + any(yDiaK) > index(yDiaU, 3) context
any(wDiaU) any(hDiaU) + any(rDiaK) > index(rDiaU, 3) context  

any(wDiaU) U+1031 + any(yDiaK) > index(yDiaU, 3) context
any(hDiaU) U+1031 + any(yDiaK) > index(yDiaU, 3) context
any(wDiaU) U+1031 + any(rDiaK) > index(rDiaU, 3) context
any(hDiaU) U+1031 + any(rDiaK) > index(rDiaU, 3) context  
any(hDiaU) U+1031 + any(wDiaK) > index(wDiaU, 3) context 

any(wDiaU) any(hDiaU) U+1031 + any(yDiaK) > index(yDiaU, 4) context
any(wDiaU) any(hDiaU) U+1031 + any(rDiaK) > index(rDiaU, 4) context   

+ any(mediaK) > index(mediaU, 1) 
+'@' > U+100F U+1039 U+100D  
+'|' > U+100B U+1039 U+100C
+'}' > U+1021 U+1034 
+'$' > U+102D U+1032 
+'*' > U+1002 
+ '>' > nul
         
U+200B U+1031 + [K_BKSP] > nul
any(baseU) U+1031 + [K_BKSP] > outs(filler) U+1031 
any(mediaU) U+1031 + [K_BKSP]> U+1031

U+1039 any(consU) U+1031 + [K_BKSP] > U+1039 outs(filler) U+1031
U+1039 any(consU) + [K_BKSP] > nul     
