c es: Consulte el mapa de teclas con caracteres, y el guía de usuario.
c en: Refer to the key map with characters, and the user's guide.

c Keyman keyboard generated by ImportKeyboard
c Imported: 2024-03-11 14:40:31
c
c Source Keyboard File: KBDLA.DLL
c Source KeyboardID: 0000080a

store(&VERSION) '17.0'
store(&NAME) 'Winchus'
store(&COPYRIGHT) '© SIL Global'
store(&KEYBOARDVERSION) '1.1'
store(&TARGETS) 'any'
store(&BITMAP) 'winchus.ico'
store(&VISUALKEYBOARD) 'winchus.kvks'
store(&LAYOUTFILE) 'winchus.keyman-touch-layout'
store(&MESSAGE) 'Un teclado para los idiomas Quechua'

begin Unicode > use(main)

c vea un mapa del teclado en https://help.keyman.com/developer/language/guide/virtual-keys
c Lo que aparece en [corchetes] es el nombre de la misma tecla física en el teclado. Su nombre por lo general se basa en la letra o carácter que tiene en el teclado inglés. 
c Los códigos que empiezan con U+ y que son seguidas por un código alfanumérico de 4 dígitos son los códigos Unicode, que especifican un carácter. 
c En este idioma de programación, la letra c empieza un comentario. Al final de cada Línea he puesto "c | " y después el carácter en sí

c ~~~ English: ~~~
c see a keyboard map at https://help.keyman.com/developer/language/guide/virtual-keys
c What appears in [square brackets] is the name of the same physical key on the keyboard. Its name is usually based on the letter or character it has on the English keyboard. 
c Codes that begin with U+ and are followed by a 4-digit alphanumeric code are Unicode codes, which specify a character. 
c In this programming language, the letter c begins a comment. At the end of each line I have put "c | " and then the character itself.

group(main) using keys

+ [K_SPACE] > U+0020         c |    (hace un espacio)
 
+ [K_0] > U+0030             c | 0
+ [SHIFT K_0] > U+003d       c | =
 
+ [K_1] > U+0031             c | 1
+ [SHIFT K_1] > U+0021       c | !
 
+ [K_2] > U+0032             c | 2
+ [SHIFT K_2] > U+0022       c | "
 
+ [K_3] > U+0033             c | 3
+ [SHIFT K_3] > U+0023       c | #
 
+ [K_4] > U+0034             c | 4
+ [SHIFT K_4] > U+0024       c | $
 
+ [K_5] > U+0035             c | 5
+ [SHIFT K_5] > U+0025       c | %
 
+ [K_6] > U+0036             c | 6
+ [SHIFT K_6] > U+0026       c | &
 
+ [K_7] > U+0037             c | 7
+ [SHIFT K_7] > U+002f       c | /
 
+ [K_8] > U+0038             c | 8
+ [SHIFT K_8] > U+0028       c | (
 
+ [K_9] > U+0039             c | 9
+ [SHIFT K_9] > U+0029       c | )
 
+ [NCAPS K_A] > U+0061       c | a | cuando el usuario teclea la tecla de A, pone una "a"
+ [CAPS K_A] > U+0041        c | A | cuando el usuario teclea la tecla de A y el Bloq Mayúscula está activado, pone una "A"
+ [NCAPS SHIFT K_A] > U+0041 c | A | cuando el usuario teclea la tecla Mayúscula y después la tecla de A, pone una "A"
+ [CAPS SHIFT K_A] > U+0061  c | a | cuando el usuario teclea la tecla Mayúscula y después la tecla de A, y el Bloq Mayúscula está activado, pone una "a"
                             c |     ~~~ English: ~~~
c + [NCAPS K_A] > + U+0061       a | when the user types the A key, it puts an "a" 
c + [NCAPS K_A] > U+0061         a | when the user types the A key, it puts an "A
c + [CAPS K_A] > U+0041          A | when the user types the A key and Caps Lock is on, it puts an "A".
c + [NCAPS SHIFT K_A] > U+0041   A | when the user types the Shift key and then the A key, it puts an "A".
c + [CAPS SHIFT K_A] > U+0061    a | when the user types the Shift key and then the A key, and Caps Lock is activated, it puts an "a".
 
+ [NCAPS K_B] > U+0062       c | b
+ [CAPS K_B] > U+0042        c | B
+ [NCAPS SHIFT K_B] > U+0042 c | B
+ [CAPS SHIFT K_B] > U+0062  c | b
 
+ [NCAPS K_C] > U+0063       c | c
+ [CAPS K_C] > U+0043        c | C
+ [NCAPS SHIFT K_C] > U+0043 c | C
+ [CAPS SHIFT K_C] > U+0063  c | c
 
+ [NCAPS K_D] > U+0064       c | d
+ [CAPS K_D] > U+0044        c | D
+ [NCAPS SHIFT K_D] > U+0044 c | D
+ [CAPS SHIFT K_D] > U+0064  c | d
 
+ [NCAPS K_E] > U+0065       c | e
+ [CAPS K_E] > U+0045        c | E
+ [NCAPS SHIFT K_E] > U+0045 c | E
+ [CAPS SHIFT K_E] > U+0065  c | e
 
+ [NCAPS K_F] > U+0066       c | f
+ [CAPS K_F] > U+0046        c | F
+ [NCAPS SHIFT K_F] > U+0046 c | F
+ [CAPS SHIFT K_F] > U+0066  c | f
 
+ [NCAPS K_G] > U+0067       c | g
+ [CAPS K_G] > U+0047        c | G
+ [NCAPS SHIFT K_G] > U+0047 c | G
+ [CAPS SHIFT K_G] > U+0067  c | g
 
+ [NCAPS K_H] > U+0068       c | h
+ [CAPS K_H] > U+0048        c | H
+ [NCAPS SHIFT K_H] > U+0048 c | H
+ [CAPS SHIFT K_H] > U+0068  c | h
 
+ [NCAPS K_I] > U+0069       c | i
+ [CAPS K_I] > U+0049        c | I
+ [NCAPS SHIFT K_I] > U+0049 c | I
+ [CAPS SHIFT K_I] > U+0069  c | i
 
+ [NCAPS K_J] > U+006a       c | j
+ [CAPS K_J] > U+004a        c | J
+ [NCAPS SHIFT K_J] > U+004a c | J
+ [CAPS SHIFT K_J] > U+006a  c | j
 
+ [NCAPS K_K] > U+006b       c | k
+ [CAPS K_K] > U+004b        c | K
+ [NCAPS SHIFT K_K] > U+004b c | K
+ [CAPS SHIFT K_K] > U+006b  c | k
 
+ [NCAPS K_L] > U+006c       c | l
+ [CAPS K_L] > U+004c        c | L
+ [NCAPS SHIFT K_L] > U+004c c | L
+ [CAPS SHIFT K_L] > U+006c  c | l
 
+ [NCAPS K_M] > U+006d       c | m
+ [CAPS K_M] > U+004d        c | M
+ [NCAPS SHIFT K_M] > U+004d c | M
+ [CAPS SHIFT K_M] > U+006d  c | m
 
+ [NCAPS K_N] > U+006e       c | n
+ [CAPS K_N] > U+004e        c | N
+ [NCAPS SHIFT K_N] > U+004e c | N
+ [CAPS SHIFT K_N] > U+006e  c | n
 
+ [NCAPS K_O] > U+006f       c | o
+ [CAPS K_O] > U+004f        c | O
+ [NCAPS SHIFT K_O] > U+004f c | O
+ [CAPS SHIFT K_O] > U+006f  c | o
 
+ [NCAPS K_P] > U+0070       c | p
+ [CAPS K_P] > U+0050        c | P
+ [NCAPS SHIFT K_P] > U+0050 c | P
+ [CAPS SHIFT K_P] > U+0070  c | p
 
+ [NCAPS K_Q] > U+0071       c | q
+ [CAPS K_Q] > U+0051        c | Q
+ [NCAPS SHIFT K_Q] > U+0051 c | Q
+ [CAPS SHIFT K_Q] > U+0071  c | q
+ [NCAPS RALT K_Q] > U+0040  c | @
 
+ [NCAPS K_R] > U+0072       c | r
+ [CAPS K_R] > U+0052        c | R
+ [NCAPS SHIFT K_R] > U+0052 c | R
+ [CAPS SHIFT K_R] > U+0072  c | r
 
+ [NCAPS K_S] > U+0073       c | s
+ [CAPS K_S] > U+0053        c | S
+ [NCAPS SHIFT K_S] > U+0053 c | S
+ [CAPS SHIFT K_S] > U+0073  c | s
 
+ [NCAPS K_T] > U+0074       c | t
+ [CAPS K_T] > U+0054        c | T
+ [NCAPS SHIFT K_T] > U+0054 c | T
+ [CAPS SHIFT K_T] > U+0074  c | t
 
+ [NCAPS K_U] > U+0075       c | u
+ [CAPS K_U] > U+0055        c | U
+ [NCAPS SHIFT K_U] > U+0055 c | U
+ [CAPS SHIFT K_U] > U+0075  c | u
 
+ [NCAPS K_V] > U+0076       c | v
+ [CAPS K_V] > U+0056        c | V
+ [NCAPS SHIFT K_V] > U+0056 c | V
+ [CAPS SHIFT K_V] > U+0076  c | v
 
+ [NCAPS K_W] > U+0077       c | w
+ [CAPS K_W] > U+0057        c | W
+ [NCAPS SHIFT K_W] > U+0057 c | W
+ [CAPS SHIFT K_W] > U+0077  c | w
 
+ [NCAPS K_X] > U+0078       c | x
+ [CAPS K_X] > U+0058        c | X
+ [NCAPS SHIFT K_X] > U+0058 c | X
+ [CAPS SHIFT K_X] > U+0078  c | x
 
+ [NCAPS K_Y] > U+0079       c | y
+ [CAPS K_Y] > U+0059        c | Y
+ [NCAPS SHIFT K_Y] > U+0059 c | Y
+ [CAPS SHIFT K_Y] > U+0079  c | y
 
+ [NCAPS K_Z] > U+007a       c | z
+ [CAPS K_Z] > U+005a        c | Z
+ [NCAPS SHIFT K_Z] > U+005a c | Z
+ [CAPS SHIFT K_Z] > U+007a  c | z

+ [K_LBRKT] > dk(00b4)       c | Esta tecla pone los acentos encima de los vocales en español. ( " ´ " ). Es una tecla muerta. 
+ [SHIFT K_LBRKT] > dk(00a8) c | Esta tecla (con Mayúscula) pone los umlauts encima de los vocales en español. ( " ¨ " ). Es una tecla muerta. 
c                             ~~~ English: ~~~
c + [K_LBRKT] > dk(00b4)       | This key places accents on top of vowels in Spanish ( " ´ " ). It is a deadkey. 
c + [SHIFT K_LBRKT] > dk(00a8) | This key (with Shift) places the umlauts on top of the vowels in Spanish ( " ¨ " ). It is a deadkey. 
 
+ [K_RBRKT] > U+002b         c | +
+ [SHIFT K_RBRKT] > U+002a   c | *
+ [RALT K_RBRKT] > U+007e    c | ~
 
+ [K_COMMA] > U+002c         c | ,
+ [SHIFT K_COMMA] > U+003b   c | ;
 
+ [K_SLASH] > U+002d         c | -
+ [SHIFT K_SLASH] > U+005f   c | _
 
+ [K_PERIOD] > U+002e        c | .
+ [SHIFT K_PERIOD] > U+003a  c | :
 
+ [K_BKSLASH] > U+007d           c | }
+ [SHIFT K_BKSLASH] > U+005d     c | ]
+ [RALT K_BKSLASH] > dk(0060)    c | es: Alt Derecho más "}" hace una "tecla muerta virtual" que pondrá un  "`" encima del vocal siguiente. Por ejemplo: ALT-DERECHO + "}" + e > ê
c                                    en: Right Alt plus "}" makes a "virtual deadkey" that will put a "`" on top of the following vowel. For example: ALT-RIGHT + "}" + e > ê
 
+ [NCAPS K_COLON] > U+00f1       c | ñ
+ [CAPS K_COLON] > U+00d1        c | Ñ
+ [NCAPS SHIFT K_COLON] > U+00d1 c | Ñ
+ [CAPS SHIFT K_COLON] > U+00f1  c | ñ
 
+ [K_HYPHEN] > U+0027        c | '
+ [SHIFT K_HYPHEN] > U+003f  c | ?
+ [RALT K_HYPHEN] > U+005c   c | \
 
+ [K_BKQUOTE] > U+007c       c | |
+ [SHIFT K_BKQUOTE] > U+00b0 c | °
+ [RALT K_BKQUOTE] > U+00ac  c | ¬
 
+ [K_EQUAL] > U+00bf         c | ¿
+ [SHIFT K_EQUAL] > U+00a1   c | ¡
 
+ [K_QUOTE] > U+007b         c | {
+ [SHIFT K_QUOTE] > U+005b   c | [
+ [RALT K_QUOTE] > dk(005e)  c | Alt Derecho más "{" hace una "tecla muerta virtual" que pondrá un  "^" encima del vocal siguiente. 
c |                              Por ejemplo: ALT-DERECHO + "{" + e > ê. También, para conseguir el "^" solo, solo hay que teclear
c |                              Alt-DERECHO + "{" + ESPACIO > "^"
c                             ~~~ English: ~~~
c |                              Right Alt plus "{" makes a "virtual deadkey" that will put a "^" on top of the following vowel. 
c |                              For example: RIGHT-ALT + "{" + e > ê. Also, to get the "^" by itself, just type
c |                              RIGHT-ALT + "{" + SPACE > "^".

+ [K_oE2] > U+003c           c | <
+ [SHIFT K_oE2] > U+003e     c | >

c | es: Inicio de modificaciones para el teclado Winchus en group(main)
c | en: Beginning of modifications for Winchus keyboard in group(main)

c | == es: Habilitar menor-que y mayor-que en teclados norteamericanos 
c | == en: Enable less-than and greater-than on North American keyboards 
+ [RALT K_COMMA]  > U+003C c | Alt derecho + "," = "<"
+ [RALT K_PERIOD] > U+003E c | Alt derecho + "." = ">" 

c | == Habilitar teclear el símbolo AFI para alargamiento 
c | + [RALT K_SLASH] > U+02D0 alargamiento Alt Derecho + "-" = "ː" ...Pensé en asignar la ">" a la "-" 
c |                           y la "ː" a la "." (":"), pero decidí hacerlo como está porque considero 
c |                           que la ">" se usa mucho más, y los norteamericanos ya están acostumbrados 
c |                           a que esté en la tecla de ".".
c                           ~~~ English: ~~~
c == Enable typing the IPA symbol for lengthening 
c | + [RALT K_SLASH] > U+02D0 lengthening Right Alt + "-" = "ː" ...I thought about assigning the ">" to the "-" 
c |                           and the "ː" to the "." (":"), but I decided to do it as is because I consider 
c |                           that the ">" is used a lot more, and Americans are already used to it being on the
c |                           "." key.


c | == es: Habilitar teclas para los dispositivos móviles ==
c | == en: Enable keys for mobile devices ==
+ [T_0061_A78C] > U+0061 U+0027 c | a'
+ [T_0063_A78C] > U+0063 U+0027 c | c'
+ [T_0068_A78C] > U+0068 U+0027 c | h'
+ [T_006B_A78C] > U+006B U+0027 c | k'
+ [T_006C_A78C] > U+006C U+0027 c | l'
+ [T_006E_A78C] > U+006E U+0027 c | n'
+ [T_0070_A78C] > U+0070 U+0027 c | p'
+ [T_0071_A78C] > U+0071 U+0027 c | q'
+ [T_0074_A78C] > U+0074 U+0027 c | t'
+ [T_0079_A78C] > U+0079 U+0027 c | y'
+ [U_00DF] > U+00DF c | ß
+ [U_00E0] > U+00E0 c | à
+ [U_00E1] > U+00E1 c | á
+ [U_00E2] > U+00E2 c | â
+ [U_00E3] > U+00E3 c | ã
+ [U_00E4] > U+00E4 c | ä
+ [U_00E6] > U+00E6 c | æ
+ [U_00E7] > U+00E7 c | ç
+ [U_00E8] > U+00E8 c | è
+ [U_00E9] > U+00E9 c | é
+ [U_00EA] > U+00EA c | ê
+ [U_00EB] > U+00EB c | ë
+ [U_00EC] > U+00EC c | ì
+ [U_00ED] > U+00ED c | í
+ [U_00EE] > U+00EE c | î
+ [U_00EF] > U+00EF c | ï
+ [U_00F0] > U+00F0 c | ý
+ [U_00F2] > U+00F2 c | ò
+ [U_00F3] > U+00F3 c | ó
+ [U_00F4] > U+00F4 c | ô
+ [U_00F5] > U+00F5 c | õ
+ [U_00F6] > U+00F6 c | ö
+ [U_00F8] > U+00F8 c | ø
+ [U_00F9] > U+00F9 c | ù
+ [U_00FA] > U+00FA c | ú
+ [U_00FB] > U+00FB c | û
+ [U_00FC] > U+00FC c | ü
+ [U_00FD] > U+00FD c | ý
+ [U_00FE] > U+00FE c | þ
+ [U_0107] > U+0107 c | ć
+ [U_0109] > U+0109 c | ĉ
+ [U_010D] > U+010D c | č
+ [U_0155] > U+0155 c | ŕ
+ [U_015B] > U+015B c | ś
+ [U_0161] > U+0161 c | š
+ [U_0251] > U+0251 c | ɑ
+ [U_0254] > U+0254 c | ɔ
+ [U_0259] > U+0259 c | ə
+ [U_025B] > U+025B c | ɛ
+ [U_0262] > U+0262 c | ɢ
+ [U_0263] > U+0263 c | ɣ
+ [U_026A] > U+026A c | ɪ
+ [U_0272] > U+0272 c | ɲ
+ [U_0278] > U+0278 c | ɸ
+ [U_0283] > U+0283 c | ʃ
+ [U_028A] > U+028A c | ʊ
+ [U_028E] > U+028E c | ʎ
+ [U_02B0] > U+02B0 c | ʰ
+ [U_03B2] > U+03B2 c | β
+ [U_03B8] > U+03B8 c | θ
+ [U_03C7] > U+03C7 c | χ

+ [T_0041_A78C] > U+0041 U+0027 c | A'
+ [T_0043_A78C] > U+0043 U+0027 c | C'
+ [T_0048_A78C] > U+0048 U+0027 c | H'
+ [T_0049_A78C] > U+0049 U+0027 c | I'
+ [T_004B_A78C] > U+004B U+0027 c | K'
+ [T_004C_A78C] > U+004C U+0027 c | L'
+ [T_004E_A78C] > U+004E U+0027 c | N'
+ [T_0050_A78C] > U+0050 U+0027 c | P'
+ [T_0051_A78C] > U+0051 U+0027 c | Q'
+ [T_0054_A78C] > U+0054 U+0027 c | T'
+ [T_0055_A78C] > U+0055 U+0027 c | U'
+ [T_0059_A78C] > U+0059 U+0027 c | Y'
+ [U_00C0] > U+00C0 c | À
+ [U_00C1] > U+00C1 c | Á
+ [U_00C2] > U+00C2 c | Â
+ [U_00C3] > U+00C3 c | Ã
+ [U_00C4] > U+00C4 c | Ä
+ [U_00C6] > U+00C6 c | Æ
+ [U_00C7] > U+00C7 c | Ç
+ [U_00C8] > U+00C8 c | È
+ [U_00C9] > U+00C9 c | É
+ [U_00CA] > U+00CA c | Ê
+ [U_00CB] > U+00CB c | Ë
+ [U_00CC] > U+00CC c | Ì
+ [U_00CD] > U+00CD c | Í
+ [U_00CE] > U+00CE c | Î
+ [U_00CF] > U+00CF c | Ï
+ [U_00D2] > U+00D2 c | Ò
+ [U_00D3] > U+00D3 c | Ó
+ [U_00D4] > U+00D4 c | Ô
+ [U_00D5] > U+00D5 c | Õ
+ [U_00D6] > U+00D6 c | Ö
+ [U_00D8] > U+00D8 c | Ø
+ [U_00D9] > U+00D9 c | Ù
+ [U_00DA] > U+00DA c | Ú
+ [U_00DB] > U+00DB c | Û
+ [U_00DC] > U+00DC c | Ü
+ [U_00DD] > U+00DD c | Ý
+ [U_0106] > U+0106 c | Ć
+ [U_0108] > U+0108 c | Ĉ
+ [U_010C] > U+010C c | Č
+ [U_0154] > U+0154 c | Ŕ
+ [U_015A] > U+015A c | Ś
+ [U_0160] > U+0160 c | Š
+ [U_0021] > U+0021 c | !
+ [U_0022] > U+0022 c | \"
+ [U_0023] > U+0023 c | #
+ [U_0024] > U+0024 c | $
+ [U_0025] > U+0025 c | %
+ [U_0026] > U+0026 c | &
+ [U_0027] > U+0027 c | '
+ [U_0028] > U+0028 c | (
+ [U_0029] > U+0029 c | )
+ [U_002A] > U+002A c | *
+ [U_002B] > U+002B c | +
+ [U_002C] > U+002C c | ,
+ [U_003A] > U+003A c | :
+ [U_003B] > U+003B c | ;
+ [U_003C] > U+003C c | <
+ [U_003E] > U+003E c | >
+ [U_003F] > U+003F c | ?
+ [U_0040] > U+0040 c | @
+ [U_005B] > U+005B c | [
+ [U_005C] > U+005C c | \
+ [U_005D] > U+005D c | ]
+ [U_005E] > U+005E c | ^
+ [U_005F] > U+005F c | _
+ [U_0060] > U+0060 c | `
+ [U_007B] > U+007B c | {
+ [U_007C] > U+007C c | |
+ [U_007D] > U+007D c | }
+ [U_007E] > U+007E c | ~
+ [U_00A1] > U+00A1 c | ¡
+ [U_00A3] > U+00A3 c | £
+ [U_00A5] > U+00A5 c | ¥
+ [U_00A6] > U+00A6 c | ¦
+ [U_00A8] > U+00A8 c | ¨
+ [U_00A9] > U+00A9 c | ©
+ [U_00AA] > U+00AA c | ª
+ [U_00AB] > U+00AB c | «
+ [U_00AE] > U+00AE c | ®
+ [U_00AF] > U+00AF c | ¯
+ [U_00B0] > U+00B0 c | °
+ [U_00B1] > U+00B1 c | ±
+ [U_00B4] > U+00B4 c | ´
+ [U_00B7] > U+00B7 c | ·
+ [U_00BA] > U+00BA c | º
+ [U_00BB] > U+00BB c | »
+ [U_00BF] > U+00BF c | ¿
+ [U_00D7] > U+00D7 c | ×
+ [U_00F7] > U+00F7 c | ÷
+ [U_0294] > U+0294 c | ʔ
+ [U_02BC] > U+02BC c | ʼ
+ [U_02D0] > U+02D0 c | ː
+ [U_2022] > U+2022 c | •
+ [U_2039] > U+2039 c | ‹
+ [U_203A] > U+203A c | ›
+ [U_20AC] > U+20AC c | €
+ [U_A78C] > U+A78C c | ꞌ

match > use(deadkeys)

group(deadkeys)


c | "store(vocales) A E I O U" quiere decir, "recuerda esta lista de caracteres que nombro 'vocales'
c | "store(vocales_con_acentos) Á E Í Ó U" quiere decir, "recuerda esta lista de caracteres que nombro 'vocales_con_acentos'
c | "dk(tecla_muerta_de_acento) any(vocales) > index(vocales_con_acentos, 2)" quiere decir, "Cuando tecleo la tecla muerta de acento 
c |     y después un carácter en la lista "vocales", pone el carácter de la lista "vocales_con_acentos" que aparece en la misma posición 
c |     del carácter en la lista de "vocales". Por ejemplo, "´" + "e" > "é". 
c ~~~ English: ~~~
c | "store(vocales) A E I O U" means, "remember this list of characters that I name 'vocales'.
c | "store(vocales_con_acentos) Á E Í Ó U" means, "remember this list of characters that I name 'vocales_con_acentos'.
c | "dk(tecla_muerta_de_acento) any(vocales) > index(vocales_con_acentos, 2)" means, "When I type the accent dead key and then a character 
c |     in the "vocales" list, it puts the character from the "vocales_con_acentos" list that appears in the same position as the character 
c |     in the "vocales" list. For example, "´" + "e" > "é". 


store(dkf00b4) U+0020 U+0061 U+0041 U+0065 U+0045 U+0069 U+0049 U+006f U+004f U+0075 U+0055 U+0079 U+0059 c |   a A e E i I o O u U y Y
store(dkt00b4) U+00b4 U+00e1 U+00c1 U+00e9 U+00c9 U+00ed U+00cd U+00f3 U+00d3 U+00fa U+00da U+00fd U+00dd c | ´ á Á é É í Í ó Ó ú Ú ý Ý
dk(00b4) any(dkf00b4) > index(dkt00b4, 2)

store(dkf00a8) U+0020 U+0061 U+0041 U+0065 U+0045 U+0069 U+0049 U+006f U+004f U+0075 U+0055 U+0079 c |   a A e E i I o O u U y
store(dkt00a8) U+00a8 U+00e4 U+00c4 U+00eb U+00cb U+00ef U+00cf U+00f6 U+00d6 U+00fc U+00dc U+00ff c | ¨ ä Ä ë Ë ï Ï ö Ö ü Ü ÿ
dk(00a8) any(dkf00a8) > index(dkt00a8, 2)

store(dkf0060) U+0020 U+0061 U+0041 U+0065 U+0045 U+0069 U+0049 U+006f U+004f U+0075 U+0055 c |   a A e E i I o O u U
store(dkt0060) U+0060 U+00e0 U+00c0 U+00e8 U+00c8 U+00ec U+00cc U+00f2 U+00d2 U+00f9 U+00d9 c | ` à À è È ì Ì ò Ò ù Ù
dk(0060) any(dkf0060) > index(dkt0060, 2)

store(dkf005e) U+0020 U+0061 U+0041 U+0065 U+0045 U+0069 U+0049 U+006f U+004f U+0075 U+0055 c |   a A e E i I o O u U
store(dkt005e) U+005e U+00e2 U+00c2 U+00ea U+00ca U+00ee U+00ce U+00f4 U+00d4 U+00fb U+00db c | ^ â Â ê Ê î Î ô Ô û Û
dk(005e) any(dkf005e) > index(dkt005e, 2)

c | es: Inicio de modificaciones para el teclado Winchus en group(deadkeys)
c | en: Start of modifications for Winchus keyboard in group(deadkeys)

c | == es: Sección de consonantes ==
c | == en: Consonants section ==


c | == es: Habilitar consonantes con acento ==
c | == en: Enable accented consonants ==
store(consonantes_sin_tilde) U+0063 U+0043 U+0072 U+0052 U+0073 U+0053 c | c C r R s S
store(consonantes_con_tilde) U+0107 U+0106 U+0155 U+0154 U+015B U+015A c | ć Ć ŕ Ŕ ś Ś
dk(00b4) any(consonantes_sin_tilde) > index(consonantes_con_tilde, 2) c | es: usa la tecla "´" para poner el acento, igual como los vocales
c |                                                                       en: use the "´" key to place the accent, just like for the vowels
U+007c any(consonantes_sin_tilde) > index(consonantes_con_tilde, 2) c | es: usa la tecla "|" ("`" en teclados norteamericanos) para lograr lo mismo que "'" (método heredero)
c                                                                       en: use the "|" key ("`" on North American keyboards) to achieve the same as "'" (legacy method)

c | == es: Habilitar consonantes con acento compuestos ==
c | == en: Enable compound accented consonants ==

c | es: No existe un solo carácter h́, así que pone la h más el acento, como un carácter compuesto (U+0068 U+0301)
c | en: There is no single h́ character, so put the h plus the accent, as a compound character (U+0068 U+0301)
store(decompuestos) U+0068 U+0048 c | h H
dk(00b4) any(decompuestos) > index(decompuestos,2) U+0301 c | h́ H́

c | == es: Habilitar consonantes con circunflejo ==
c | == en: Enable consonants with circumflex ==
store(c)                 U+0063 U+0043 U+0073 U+0053 c | c C s S
store(c_con_circunflejo) U+0109 U+0108 U+015D U+015C c | ĉ Ĉ ŝ Ŝ
dk(005e) any(c) > index(c_con_circunflejo, 2) c | es: AltDerecho + "{" + "c" > "ĉ"
c |                                               en: RightAlt + "{" + "c" > "ĉ"
U+0026 any(c) > index(c_con_circunflejo, 2) c | es: Mayús + "6" + c" > "ĉ"
c |                                             es: Shift + "6" + c" > "ĉ"

c | == es: Habilitar consonantes con carón ==
c | == en: Enable consonants with caron ==
store(consonantes_sin_caron) U+0063 U+0043 U+0073 U+0053 c | c C s S
store(consonantes_con_caron) U+010D U+010C U+0161 U+0160 c | č Č š Š
U+0025 any(consonantes_sin_caron) > index(consonantes_con_caron, 2) c | es: Mayús + "5" + c > "č"
c |                                                                     en: Shift + "5" + c > "č"

c | == es: Sección de puntuación ==
c | == en: Punctuation section ==

c | == es: Habilitar las comillas angulares simples y dobles ==
c | == en: Enable single and double guille­mets ==
U+003c U+003c > U+00AB c | "<" + "<" = "«"
U+00AB U+003c > U+2039 c | "«" + "<" = "‹"
U+003e U+003e > U+00BB c | ">" + ">" = "»"
U+00BB U+003e > U+203A c | "»" + ">" = "›"

dk(00b4) U+0027 > U+A78C c | ꞌ para teclear el saltillo (glotal), teclea la tecla de acento 
c |                            (a la derecha de la "P") y después la tecla de comilla 
c |                            (a la derecha de la "0"). En breve "´" + "'" > "ꞌ"
c |                      ~~~ English: ~~~
c |                          ꞌ to type the saltillo (glottal), type the accent key (to the right
c |                            of the "P") and then the quote key (to the right of the 
c |                            "0"). In short "´" + "'" > "ꞌ"
dk(00b4) U+003f > U+A78B c | es: Ꞌ   ´ + Mayús + ' > Ꞌ (El saltillo mayúscula)
c |                          en: Ꞌ   ´ + Shift + ' > Ꞌ (The capital saltillo)

c | == es: Sección de AFI == 
c | == en: IPA section == 

c | basado en el SIL IPA Keyboard (https://help.keyman.com/keyboard/sil_ipa/1.8.6/sil_ipa),
c | pero considerando la configuración del teclado latinoamericano.
c | Desactivado porque solían activar algunos caracteres cuando no se deseaban. Véase también la sección "Habilitar teclear el símbolo AFI para alargamiento" más arriba. 
c | Esta funcionalidad podría habilitarse mediante la creación de un nuevo teclado ampliado para lingüistas. También habría que añadir otros caracteres utilizados en la
c | lexicografía. Véase, por ejemplo, el diccionario SIL de Quechua Huallaga, compilado por David Weber et. al. 
c ~~~ English: ~~~
c | based on the SIL IPA Keyboard (https://help.keyman.com/keyboard/sil_ipa/1.8.6/sil_ipa),
c | but taking the configuration of the Latin American Keyboard into mind.
c | Disabled because many of these characters would get activated when they were not desired. See also the section "Enable typing the IPA symbol for lengthening" above. 
c | This functionality could be enabled through the creation of a new, expanded keyboard for linguists. Additional characters used in lexicography should also be added. 
c | See, for example, the SIL Huallaga Quechua dictionary compiled by David Weber et. al. 

c | U+005D U+005D   > U+02BC c | ʼ  "]" + "]" es: eyectiva
c |                                           en: ejective
c | U+003F U+003d   > U+0294 c | ʔ	"?" + "=" es: glotal  
c |                                           en: glottal
c | U+0061 U+003d   > U+0251 c | ɑ 	"a" + "=" 
c | U+0061 U+005b   > U+00E6 c | æ	"a" + "[" 
c | U+006f U+005b   > U+0254 c | ɔ	"o" + "[" 
c | U+0065 U+003d   > U+0259 c | ə	"e" + "=" 
c | U+0065 U+005b   > U+025B c | ɛ	"e" + "["
c | U+0047 U+003d   > U+0262 c | ɢ	"G" + "="
c | U+0067 U+003d   > U+0263 c | ɣ	"g" + "="
c | U+0068 U+0026   > U+02B0 c | ʰ	"h" + "&"	
c | U+0069 U+003d   > U+026A c | ɪ	"i" + "="	
c | U+006e U+003d   > U+0272 c | ɲ	"n" + "="	
c | U+0066 U+003d   > U+0278 c | ɸ	"f" + "="	
c | U+0073 U+003d   > U+0283 c | ʃ	"s" + "="	
c | U+0075 U+005b   > U+028A c | ʊ	"u" + "["	
c | U+004c U+005b   > U+028E c | ʎ	"L" + "["	
c | U+0062 U+003d   > U+03B2 c | β	"b" + "="	
c | U+0074 U+003d   > U+03B8 c | θ	"t" + "="	
c | U+0078 U+003d   > U+03C7 c | χ	"x" + "="


c | es: Nombre del teclado: ¿"Winchus"? ¡Sin grafemas contenciosos! ¡Comunica las ideas de belleza, elegancia, velocidad!
c | en: Keyboard name: "Winchus"? No contentious graphemes! Communicate the ideas of beauty, elegance, speed!
