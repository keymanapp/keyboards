store(&NAME) 'NLCI Devanagari WinScript'
store(&COPYRIGHT) '© NLCI 2000-2018'
begin Unicode > use(main)

group(main) using keys

store(vowelKeys) 'AiIuUReEYoOVMH'
store(vowels) 'आइईउऊऋएऐऐओऔऔंः'
store(vowelMatras) 'ािीुूृेैैोौौंः'
store(consKeys) 'kKgGW' \
    	   	   	'cCjJw' \
    	   	   	'tTdDN' \
    	   	   	'qQ[{n' \
    	   	   	'pPbBm' \
    	   	   	'yrlvzZshL'

store(cons) 'कखगघङ' \
    	   	'चछजझञ' \
    	   	'टठडढण' \
    	   	'तथदधन' \
    	   	'पफबभम' \
    	   	'यरलवशषसहळ'

store(consonants) outs(cons) 'ऱऴ'
store(allKeys) outs(consKeys) outs(vowelKeys) 'a'

c Adjust the behavior of 'a'
+ 'a' > 'अ'   
'अ' + 'a' > 'आ'               
any(consonants) + 'a' > context dk(a)
dk(a) + 'a' > 'ा' dk(aa)
'ा' dk(aa) + 'a' > 'अ'

c Adjust the behavior of 'e'
'ए' + 'e' > 'ऐ'      
'े' + 'e' > 'ै'  

c Adjust the behavior of 'i'
'इ' + 'i' > 'ई' 
'ि' + 'i' > 'ी'    

c Adjust the behavior of l
'ल' + 'l' > 'ळ'
'ळ' + 'l' > 'ऴ'      

c Adjust the behavior of n
'न' + 'n' > 'ऩ'     

c Adjust the behavior of o
'ओ' + 'o' > 'औ'    
'ो' + 'o' > 'ौ'   

c Adjust the behavior of r
 'र' + 'r' > 'ऱ'       
 
c Adjust the behavior of R
'ऋ' + 'R' > 'ॠ'   
'ृ' + 'R' > 'ॄ'     
   
c Adjust the behavior of S
+ 'S' > nul  

c Adjust the behavior of u
'उ' + 'u' > 'ऊ'    
'ु' + 'u' > 'ू' 

c Adjust the behavior of s
'स' + 's'> 'ष'

c Adjust the behavior of X
any(consonants) + 'X' > context '्' U+200C
any(consonants) '्' + 'X' > context U+200C
U+200C + 'X' > context beep
store(WordFinalPunct) ' ",.>!]})' 
U+200C + any(WordFinalPunct) > index(WordFinalPunct, 2)

                     
c Add =
+ '=' > '=' dk(eq)
'=' dk(eq)+ 'r' > 'ऱ'
'=' dk(eq)+ 'z' > 'ऴ'    

c backtick to change into english
+ '`' > '`' dk(bt)
'`' dk(bt) + any(allKeys) > index(allKeys, 3)
any(allKeys) + any(allKeys) > context index(allKeys, 2)

c numbers
store(numberKeys) '1234567890'
store(devanagariNumbers) '१२३४५६७८९०'
'`' dk(bt) + any(numberKeys) > index(devanagariNumbers, 3)
any(devanagariNumbers) + any(numberKeys) > context index(devanagariNumbers, 2)

 
+ any(vowelKeys) > index(vowels, 1)
+ any(consKeys) > index(cons, 1)
any(consonants) + any(vowelKeys) > context index(vowelMatras, 2)

c cons + virama
any (consonants) + 'x' > context '्'          
any(vowels) + 'x' > context '्'   

c vowels + virama
any(vowels) + 'x' > context '्' 
'अ' + 'x' > context '्'
any(vowelMatras) + 'x' > context '्'
