store(&VERSION) '10.0'
store(&NAME) 'Oriya WinScript(NLCI)'
store(&COPYRIGHT) '© 2002-2019 NLCI Bangalore'
store(&KEYBOARDVERSION) '2.0'
store(&TARGETS) 'windows macosx linux web desktop'
store(&BITMAP) 'nlci_oriya_winscript.ico'
store(&VISUALKEYBOARD) 'nlci_oriya_winscript.kvks'
begin Unicode > use(main)

group(main) using keys

store(vowelKeys) 'AiIuUReYoVMH'
store(vowels) 'ଆଇଈଉଊଋଏଐଓଔଂଃ'
store(vowelMatras) 'ାିୀୁୂୃେୈୋୌଂଃ'
store(consKeys) 'kKgGW' \
    	   	   	'cCjJw' \
    	   	   	'tTdDN' \
    	   	   	'qQ[{n' \
    	   	   	'pPbBm' \
    	   	   	'yrlvzZshL'

store(cons) 'କଖଗଘଙ' \
    	   	'ଚଛଜଝଞ' \
    	   	'ଟଠଡଢଣ' \
    	   	'ତଥଦଧନ' \
    	   	'ପଫବଭମ' \
    	   	'ଯରଲୱଶଷସହଳ'

store(consonants) outs(cons) 'ୠଢ଼ଡ଼ୟ'
store(allKeys) outs(consKeys) outs(vowelKeys) 'azZ'  


c Adjust the behavior of 'a'
+ 'a' > 'ଅ'   
'ଅ' + 'a' > 'ଆ'               
any(consonants) + 'a' > context dk(a)
dk(a) + 'a' > 'ା' dk(aa)
'ା' dk(aa) + 'a' > 'ଅ'

c Adjust the behavior of 'i'
'ଇ' + 'i' > 'ଈ' 
'ି' + 'i' > 'ୀ'    

c Adjust the behavior of l
'ଲ' + 'l' > 'ଳ'
  
c Adjust the behavior of R
'ଋ' + 'R' > 'ୠ'   
'ୃ' + 'R' > 'ୄ'     
   
c Adjust the behavior of S
+ 'S' > 'ଁ'    
'`' dk(bt) + 'S' > 'S'

c Adjust the behavior of u
'ଉ' + 'u' > 'ଊ'    
'ୁ' + 'u' > 'ୂ' 

c Adjust the behavior of s
'ସ' + 's'> 'ଷ'

c Adjust the behavior of E
 + 'E' > nul  
 '`' dk(bt) + 'E' > 'E'          
 
c Adjust the behavior of O
 + 'O' > nul    
 '`' dk(bt) + 'O' > 'O'
 
 c Adjust the behavior of F
 + 'F' > nul    
 '`' dk(bt) + 'F' > 'F'

c Adjust the behavior of y
'ଯ' + 'y' > 'ୟ'   

c Adjust the behavior of f
 +'f' > '଼'  
 '`' dk(bt) + 'f' > 'f'  
 
c Adjust the behavior of D and d
'ଢ' + 'D' > 'ଢ଼'
'ଡ' + 'd' > 'ଡ଼'   

c Adjust the behavior of x and X
'`' dk(bt) + 'x' > 'x'
'`' dk(bt) + 'X' > 'X'

c Adjust the behavior of X
any(consonants) + 'X' > context '୍' U+200C
any(consonants) '୍' + 'X' > context U+200C
U+200C + 'X' > context beep
store(WordFinalPunct) ' ",;:.>!]}-/;\~@#$%^&*(_+|:<?)' 
U+200C + any(WordFinalPunct) > index(WordFinalPunct, 2)

c = is a modifier key that alters the default behavior of some keystrokes
+ '=' > '=' dk(eq)
'=' dk(eq)+ 'r' > 'ଡ଼'

c Restrict the ZW characters 
U+200C + 'x' > context beep
U+200D + 'X' > context beep
U+200C + 'X' > context beep
U+200D + 'x' > context beep             

c Smart quotes
+ '"' > U+201C
U+201C + '"' > U+201D
U+201D + '"' > '"'

+ "'" > "'"
"'" + "'" > U+2018
U+2018 + "'" > U+2019
U+2019 + "'" > "'"
c Dashes
'--' + '`' > U+2013 
U+2013 + '`' > '--' 
U+2013 + [K_BKSP] > '--'
'---' + '`' > U+2014
U+2014 + '`' > '---'
U+2014 + [K_BKSP] > '---'   

c full stop
   + '.' > '।'
'।' + '.' > '.' 

c backtick to change into English
+ '`' > '`' dk(bt)
'`' dk(bt) + any(allKeys) > index(allKeys, 3)
any(allKeys) + any(allKeys) > context index(allKeys, 2)

c numbers
store(numberKeys) '1234567890'
store(oriyaNumbers) '୧୨୩୪୫୬୭୮୯୦'
'`' dk(bt) + any(numberKeys) > index(oriyaNumbers, 3)
any(oriyaNumbers) + any(numberKeys) > context index(oriyaNumbers, 2)


+ any(vowelKeys) > index(vowels, 1)
+ any(consKeys) > index(cons, 1)
any(consonants) + any(vowelKeys) > context index(vowelMatras, 2)  
any(consonants) '଼' + any(vowelKeys) > context '଼' index(vowelMatras,3)


c cons + virama
any (consonants) + 'x' > context '୍'          
any(vowels) + 'x' > context '୍'   

c vowels + virama
any(vowels) + 'x' > context '୍' 
'ଅ' + 'x' > context '୍'
any(vowelMatras) + 'x' > context '୍'      

c Vowel sign elongation
'ୀ' + 'i' > context 'ୀ' 
'ୂ' + 'u' > context 'ୂ'
'ା' + 'a' > context 'ା'

