store(&NAME) 'NLCI Malayalam WinScript'
store(&COPYRIGHT) '© NLCI 2000-2018'
begin Unicode > use(main)

group(main) using keys

store(vowelKeys) 'AiIuUReEYoOVMH'
store(vowels) 'ആഇഈഉഊഋഎഏഐഒഓഔംഃ'
store(vowelMatras) 'ാിീുൂൃെേൈൊോൌംഃ'
store(consKeys) 'kKgGw' \
    	   	   	'cCjJW' \
    	   	   	'tTdDN' \
    	   	   	'qQ[{n' \
    	   	   	'pPbBm' \
    	   	   	'yrlvzZshL'

store(cons) 'കഖഗഘങ' \
    	   	'ചഛജഝഞ' \
    	   	'ടഠഡഢണ' \
    	   	'തഥദധന' \
    	   	'പഫബഭമ' \
    	   	'യരലവശഷസഹള'

store(consonants) outs(cons) 'റഴഩഺ'

c Adjust the behavior of 'a'
+ 'a' > 'അ'   
'അ' + 'a' > 'ആ'               
any(consonants) + 'a' > context dk(a)
dk(a) + 'a' > 'ാ' dk(aa)
'ാ' dk(aa) + 'a' > 'അ'

c Adjust the behavior of 'e'
'എ' + 'e' > 'ഏ'      
'െ' + 'e' > 'േ'  

c Adjust the behavior of 'i'
'ഇ' + 'i' > 'ഈ' 
'ി' + 'i' > 'ീ'    

c Adjust the behavior of l
'ല' + 'l' > 'ള'
'ള' + 'l' > 'ഴ'      

c Adjust the behavior of n
'ന' + 'n' > 'ഩ'     

c Adjust the behavior of o
'ഒ' + 'o' > 'ഓ'    
'ൊ' + 'o' > 'ോ'   

c Adjust the behavior of r
 'ര' + 'r' > 'റ'       
 
c Adjust the behavior of R
'ഋ' + 'R' > 'ൠ'   
'ൃ' + 'R' > 'ൄ'     

c Adjust the behavior of S
+ 'S' > nul


c Add praslesham
+ 'F' > 'ഽ'

+ any(vowelKeys) > index(vowels, 1)
+ any(consKeys) > index(cons, 1)
any(consonants) + any(vowelKeys) > context index(vowelMatras, 2)
c cons + chandrakala
any (consonants) + 'x' > context '്'          
any(vowels) + 'x' > context '്'
