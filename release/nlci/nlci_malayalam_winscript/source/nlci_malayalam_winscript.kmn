store(&NAME) 'NLCI Malayalam WinScript'
store(&COPYRIGHT) '© NLCI 2000-2018'
begin Unicode > use(main)

group(main) using keys

store(vowelKeys) 'AiIuUReEYoOVMH'
store(vowels) 'ആഇഈഉഊഋഎഏഐഒഓഔംഃ'
store(vowelMatras) 'ാിീുൂൃെേൈൊോൗംഃ'
store(consKeys) 'kKgGW' \
    	   	   	'cCjJw' \
    	   	   	'tTdDN' \
    	   	   	'qQ[{n' \
    	   	   	'pPbBm' \
    	   	   	'yrlvzZshL'

store(cons) 'കഖഗഘങ' \
    	   	'ചഛജഝഞ' \
    	   	'ടഠഡഢണ' \
    	   	'തഥദധന' \
    	   	'പഫബഭമ' \
    	   	'യരലവശഷസഹള'

store(consonants) outs(cons) 'റഴഩഺ'
store(chillu_base) 'ണനരലളകയമഴ'
store(chillu) 'ൺൻർൽൾൿൔൕൖ'
store(allKeys) outs(consKeys) outs(vowelKeys) 'a'

c Adjust the behavior of 'a'
+ 'a' > 'അ'   
'അ' + 'a' > 'ആ'               
any(consonants) + 'a' > context dk(a)
dk(a) + 'a' > 'ാ' dk(aa)
'ാ' dk(aa) + 'a' > 'അ'

c Adjust the behavior of 'e'
'എ' + 'e' > 'ഏ'      
'െ' + 'e' > 'േ'  

c Adjust the behavior of 'i'
'ഇ' + 'i' > 'ഈ' 
'ി' + 'i' > 'ീ'    

c Adjust the behavior of l
'ല' + 'l' > 'ള'
'ള' + 'l' > 'ഴ'      

c Adjust the behavior of n
'ന' + 'n' > 'ഩ'     

c Adjust the behavior of o
'ഒ' + 'o' > 'ഓ'    
'ൊ' + 'o' > 'ോ'   

c Adjust the behavior of r
 'ര' + 'r' > 'റ'       
 
c Adjust the behavior of R
'ഋ' + 'R' > 'ൠ'   
'ൃ' + 'R' > 'ൄ'     
   
c Adjust the behavior of S
+ 'S' > nul  

c Adjust the behavior of u
'ഉ' + 'u' > 'ഊ'    
'ു' + 'u' > 'ൂ' 

c Adjust the behavior of s
'സ' + 's'> 'ഷ'

c Adjust the behavior of X
any(consonants) + 'X' > context '്' U+200C
any(consonants) '്' + 'X' > context U+200C
U+200C + 'X' > context beep
store(WordFinalPunct) ' ",.>!]})' 
U+200C + any(WordFinalPunct) > index(WordFinalPunct, 2)

c Add Malayalam Chillu
any(chillu_base) '്' + 'x' > index(chillu, 1)
any(chillu) + 'x' > context beep 
c Add praslesham
+ 'F' > 'ഽ'    

c Add ^
'്ല' + '^' > 'ൢ'
'ൢ' + '^' > 'ൣ'                                              

c Add =
+ '=' > '=' dk(eq)
'=' dk(eq)+ 'r' > 'റ'
'=' dk(eq)+ 'z' > 'ഴ'

c numbers
store(numberKeys) '1234567890'
store(malayalamNumbers) '൧൨൩൪൫൬൭൮൯൦'
'=' dk(eq) + any(numberKeys) > index(malayalamNumbers, 3)
any(malayalamNumbers) + any(numberKeys) > context index(malayalamNumbers, 2)

'=' dk(eq) + any(allKeys) > index(allKeys, 3)
any(allKeys) + any(allKeys) > context index(allKeys, 2)

c nta
'ന്' + 't' > 'ൻ്റ'       

+ any(vowelKeys) > index(vowels, 1)
+ any(consKeys) > index(cons, 1)
any(consonants) + any(vowelKeys) > context index(vowelMatras, 2)

c cons + chandrakala
any (consonants) + 'x' > context '്'          
any(vowels) + 'x' > context '്'   

c vowels + chandrakala
any(vowels) + 'x' > context '്' 
'അ' + 'x' > context '്'
any(vowelMatras) + 'x' > context '്'
