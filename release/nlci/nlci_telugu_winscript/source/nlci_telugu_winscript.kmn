store(&VERSION) '10.0'
store(&NAME) 'Telugu WinScript Keyboard[NLCI]'
store(&COPYRIGHT) '© 2002-2019 NLCI,Bangalore '
store(&KEYBOARDVERSION) '2.0'
store(&TARGETS) 'web desktop'
store(&BITMAP) 'nlci_telugu_winscript.ico'
store(&VISUALKEYBOARD) 'nlci_telugu_winscript.kvks'
begin Unicode > use(main)

group(main) using keys

store(vowelKeys) 'AiIuUReEYoOVMH'
store(vowels) 'ఆఇఈఉఊఋఎఏఐఒఓఔంః'
store(vowelMatras) 'ాిీుూృెేైొోౌంః'
store(consKeys) 'kKgGW' \
    	   	   	'cCjJw' \
    	   	   	'tTdDN' \
    	   	   	'qQ[{n' \
    	   	   	'pPbBm' \
    	   	   	'yrlvzZshL'

store(cons) 'కఖగఘఙ' \
    	   	'చఛజఝఞ' \
    	   	'టఠడఢణ' \
    	   	'తథదధన' \
    	   	'పఫబభమ' \
    	   	'యరలవశషసహళ'

store(consonants) outs(cons) 'ఱఴ'
store(allKeys) outs(consKeys) outs(vowelKeys) 'azZS'

c Adjust the behavior of 'a'
+ 'a' > 'అ'   
'అ' + 'a' > 'ఆ'               
any(consonants) + 'a' > context dk(a)
dk(a) + 'a' > 'ా' dk(aa)
'ా' dk(aa) + 'a' > 'అ'

c Adjust the behavior of 'e'
'ఎ' + 'e' > 'ఏ'      
'ె' + 'e' > 'ే'  

c Adjust the behavior of 'i'
'ఇ' + 'i' > 'ఈ' 
'ి' + 'i' > 'ీ'    

c Adjust the behavior of l
'ల' + 'l' > 'ళ'
'ళ' + 'l' > 'ఴ'      

c Adjust the behavior of o
'ఒ' + 'o' > 'ఓ'    
'ొ' + 'o' > 'ో'   

c Adjust the behavior of r
 'ర' + 'r' > 'ఱ'       
 
c Adjust the behavior of R
'ఋ' + 'R' > 'ౠ'   
'ృ' + 'R' > 'ౄ'     
   
c Adjust the behavior of S
+ 'S' > 'ఁ'  
'`' dk(bt) + 'S' > 'S'

c Adjust the behavior of u
'ఉ' + 'u' > 'ఊ'    
'ు' + 'u' > 'ూ' 

c Adjust the behavior of s
'స' + 's'> 'ష'      

c Adjust the behavior of F
 + 'F' > nul  
 '`' dk(bt) + 'F' > 'F'
 + 'f' > nul   
 '`' dk(bt) + 'f' > 'f'

c Adjust the behavior of X
any(consonants) + 'X' > context '్' U+200C
any(consonants) '్' + 'X' > context U+200C
U+200C + 'X' > context beep
store(WordFinalPunct) ' ",;:.>!]}-\;/~@#$%^&*(_+|:<?)' 
U+200C + any(WordFinalPunct) > index(WordFinalPunct, 2)

c = is a modifier key that alters the default behavior of some keystrokes
+ '=' > '=' dk(eq)
'=' dk(eq)+ 'r' > 'ఱ'
'=' dk(eq)+ 'z' > 'ఴ'     

c Restrict the ZW characters 
U+200C + 'x' > context beep
U+200D + 'X' > context beep
U+200C + 'X' > context beep
U+200D + 'x' > context beep             

c Smart quotes
+ '"' > U+201C
U+201C + '"' > U+201D
U+201D + '"' > '"'

+ "'" > U+2018
U+2018 + "'" > U+2019
U+2019 + "'" > "'"

c Dashes
'--' + '`' > U+2013 
U+2013 + '`' > '--' 
U+2013 + [K_BKSP] > '--'
'---' + '`' > U+2014
U+2014 + '`' > '---'
U+2014 + [K_BKSP] > '---'    

c backtick to change into English
+ '`' > '`' dk(bt)
'`' dk(bt) + any(allKeys) > index(allKeys, 3)
any(allKeys) + any(allKeys) > context index(allKeys, 2)

c numbers
store(numberKeys) '1234567890'
store(teluguNumbers) '౧౨౩౪౫౬౭౮౯౦'
'`' dk(bt) + any(numberKeys) > index(teluguNumbers, 3)
any(teluguNumbers) + any(numberKeys) > context index(teluguNumbers, 2)


+ any(vowelKeys) > index(vowels, 1)
+ any(consKeys) > index(cons, 1)
any(consonants) + any(vowelKeys) > context index(vowelMatras, 2)

c cons + virama
any (consonants) + 'x' > context '్'          
any(vowels) + 'x' > context '్'   

c vowels + virama
any(vowels) + 'x' > context '్' 
'అ' + 'x' > context '్'
any(vowelMatras) + 'x' > context '్'      

c Vowel sign elongation
'ీ' + 'i' > context 'ీ' 
'ూ' + 'u' > context 'ూ'
'ా' + 'a' > context 'ా'

