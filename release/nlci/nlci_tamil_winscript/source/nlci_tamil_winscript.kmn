store(&VERSION)'10.0'
store(&NAME) 'Tamil WinScript [NLCI]'
store(&COPYRIGHT) '© 2000-2019 NLCI Bangalore'
store(&BITMAP) 'nlci_tamil_winscript.ico'
store(&VISUALKEYBOARD) 'nlci_tamil_winscript.kvks'
store(&TARGETS) 'web desktop'
store(&KEYBOARDVERSION) '2.0'
begin Unicode > use(main)

group(main) using keys

store(vowelKeys) 'AiIuUeEYoOVH'
store(vowels) 'ஆஇஈஉஊஎஏஐஒஓஔஃ'
store(vowelMatras) 'ாிீுூெேைொோௌஃ'   

store(consKeys) 'kKgGW' \
    	   	   	'cCjJw' \
    	   	   	'tTdDN' \
    	   	   	'qQn' \
    	   	   	'pPbBm' \
    	   	   	'yrlvzZshL'

store(cons) 'ககககங' \
    	   	'சசஜஜஞ' \
    	   	'டடடடண' \
    	   	'ததந' \
    	   	'பபபபம' \
    	   	'யரலவஷஷஸஹள'

store(consonants) outs(cons) 'றழன'
store(allKeys) outs(consKeys) outs(vowelKeys) 'aM'

c Adjust the behavior of 'a'
+ 'a' > 'அ'   
'அ' + 'a' > 'ஆ'               
any(consonants) + 'a' > context dk(a)
dk(a) + 'a' > 'ா' dk(aa)
'ா' dk(aa) + 'a' > 'அ'

c Adjust the behavior of 'e'
'எ' + 'e' > 'ஏ'      
'ெ' + 'e' > 'ே'  

c Adjust the behavior of 'i'
'இ' + 'i' > 'ஈ' 
'ி' + 'i' > 'ீ'    

c Adjust the behavior of l
'ல' + 'l' > 'ள'
'ள' + 'l' > 'ழ' 
'ழ' + 'l' > 'ல'     

c Adjust the behavior of L
'ள' + 'L' > 'ழ'

c Adjust the behavior of n 
'ந'  + 'n' >  'ண'   
'ண' + 'n' >  'ன'   
'ன'  + 'n' > 'ந'

c Adjust the behavior of o
'ஒ' + 'o' > 'ஓ'    
'ொ' + 'o' > 'ோ'   

c Adjust the behavior of r
 'ர' + 'r' > 'ற'       
 
c Adjust the behavior of S  
+ 'S' > nul           
'`' dk(bt) + 'S' > 'S'

c Adjust the behavior of F
 + 'F' > nul
 + 'f' > nul
 '`' dk(bt) + 'F' > 'F' 
 '`' dk(bt) + 'f' > 'f'
c Adjust the behavior of u
'உ' + 'u' > 'ஊ'    
'ு' + 'u' > 'ூ' 

c Adjust the behavior of s
'ஸ' + 's'> 'ஷ'    

c Adjust the behavior of M
 + 'M' > 'ம்'   

c Adjust the behavior of R
 + 'R' > nul 
 '`' dk(bt) + 'R' > 'R'

c Adjust the behavior of X
any(consonants) + 'X' > context '்' U+200C
any(consonants) '்' + 'X' > context U+200C
U+200C + 'X' > context beep
store(WordFinalPunct) ' ",.>!]}-/;\~@#$%^&*(_+|:<?)' 
U+200C + any(WordFinalPunct) > index(WordFinalPunct, 2)

c Add =
+ '=' > '=' dk(eq)
'=' dk(eq)+ 'r' > 'ர'
'=' dk(eq)+ 'z' > 'ழ'     

c Add \
 + '\' > '\'
 '\' + 'n' > 'ன'
 '\' + 'L' > 'ழ'
 '\' + 'r' > 'ற'  
 
 c Restrict the ZW characters 
U+200C + 'x' > context beep
U+200D + 'X' > context beep
U+200C + 'X' > context beep
U+200D + 'x' > context beep             

c Smart quotes
+ '"' > U+201C
U+201C + '"' > U+201D
U+201D + '"' > '"'

+ "'" > U+2018
U+2018 + "'" > U+2019
U+2019 + "'" > "'"

c Dashes
'--' + '`' > U+2013 
U+2013 + '`' > '--' 
U+2013 + [K_BKSP] > '--'
'---' + '`' > U+2014
U+2014 + '`' > '---'
U+2014 + [K_BKSP] > '---'    

c backtick for change into English

+ '`' > '`' dk(bt)
'`' dk(bt) + any(allKeys) > index(allKeys, 3)
any(allKeys) + any(allKeys) > context index(allKeys, 2)
    
c numbers
store(numberKeys) '1234567890'
store(TamilNumbers) '௧௨௩௪௫௬௭௮௯௦'
'`' dk(bt) + any(numberKeys) > index(TamilNumbers, 3)
any(TamilNumbers) + any(numberKeys) > context index(TamilNumbers, 2)

+ any(vowelKeys) > index(vowels, 1)
+ any(consKeys) > index(cons, 1)
any(consonants) + any(vowelKeys) > context index(vowelMatras, 2)

c cons + virama
any (consonants) + 'x' > context '்'          
any(vowels) + 'x' > context '்'   

c vowels + virama
any(vowels) + 'x' > context '்' 
'அ' + 'x' > context '்'
any(vowelMatras) + 'x' > context '்'
