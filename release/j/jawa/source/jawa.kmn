c Keyboard: Nulisa Aksara Jawa
c 
c Provided by: Benny Lin
c 
c Fonts: Noto Sans Javanese, or Tuladha Jejeg

VERSION 9.0
store(&NAME) 'Jawa'
store(&ETHNOLOGUECODE) 'jav'
store(&MESSAGE) 'Based on is.gd/nulisa Transliterator'
store(&KEYBOARDVERSION) '1.0'
store(&VISUALKEYBOARD) 'jawa.kvks'
store(&BITMAP) 'jawa.bmp'
store(&COPYRIGHT) '© Benny Lin - 2019-01-24'  

store(isA) 'Aaâaæ'
store(isE) 'EeÈèÉé'
store(isI) 'Iii'
store(isO) 'Ooôo'
store(isU) 'Uuu'
store(isX) 'XxÊêEeEe'
store(isVowel) outs(isA) outs(isE) outs(isI) outs(isO) outs(isU) outs(isX)
store(isConsonant) 'BCDfGHJKLMNPRSTVWYZbcdfghjklmnpqrstvwxyz?'  
store(isSandhangan) U+A9B4 U+A9B6 U+A9B8 U+A9BA U+A9BC \
                    U+A9B3 U+A9B5 U+A9B7 U+A9B9 U+A9BB \
                    c a, i, u, e, x, cecak telu, tolong, ii, uu, ai
store(isNglegena) U+A98F U+A992 U+A994 U+A995 U+A997 U+A99A U+A99B U+A99D U+A9A0 U+A9A2 \
                  U+A9A4 U+A9A5 U+A9A7 U+A9A9 U+A9AA U+A9AB U+A9AD U+A9AE U+A9B1 U+A9B2 \
                  c 20 aksara sorted by Unicode (ka-ga-nga)
store(isNglegena2) outs(isNglegena) U+A9B3 
store(isAll) outs(isNglegena) outs(isSandhangan)
store(isNglegenaConsonant) 'kgqcjxfvtdnpbmyrlwsh'
store(isSesigeg) U+A981 U+A982 U+A983 c Cecak, Layar, Wignyan
store(isWyanjana) U+A9BD U+A9BE U+A9BF c Keret, Pengkal, Cakra 
store(isAll2) outs(isNglegena) outs(isWyanjana)

c constants
store(Cecak) U+A981
store(Layar) U+A982
store(Wignyan) U+A983
store(Keret) U+A9BD
store(Pengkal) U+A9BE
store(Cakra) U+A9BF
store(Pangkon) U+A9C0
store(ZWJ) U+200C
store(opt1) '1'       

begin Unicode > use(Umain)

group (Umain) using keys   
      
+ [CTRL K_2] > set(opt1='2')
+ [CTRL K_1] > set(opt1='1')   

if(opt1='1') + 'a' > context use(Javanese)
if(opt1='2') + 'a' > context use(Latin)

group (Latin) using keys   

group (Javanese) using keys   

c c c c c c c c c
c Spacing rules
c c c c c c c c c

+ ' ' > nul
+ '`' > $ZWJ c zero width joiner ZWJ
c avoid double pasangan, insert ZWJ; except when typing 'jny' -> nya murda and 'ngg'+'[ry]'
U+A997 $Pangkon U+A9A4 $Pangkon + 'y' > U+A998 $Pangkon c nya murda  
c every cakra and pengkal at the end will have pangkon, for example "nggr" before it gets vowel
c   because to accomodate long vowels, especially long 'aa', otherwise "nggra" already have tarung
c   with pengkal, the first time one types the vowel "nggra" it deletes the pangkon, and the second
c   time it "nggraa" it adds the tarung.
U+A994 $Pangkon U+A992 $Pangkon + 'r' > context(1) context(2) context(3) $Cakra $Pangkon
U+A994 $Pangkon U+A992 $Pangkon + 'y' > context(1) context(2) context(3) $Pengkal $Pangkon
c U+A994 $Pangkon U+A992 $Pangkon + 'w' > context(1) context(2) $ZWJ context(3) context(4) U+A9AE $Pangkon 
c U+A994 $Pangkon U+A992 $Pangkon + 'l' > context(1) context(2) $ZWJ context(3) context(4) U+A9AD $Pangkon
c TOFIX 
c any(isNglegena) $Pangkon any(isNglegena) $Pangkon + any(isNglegenaConsonant) > \
c   context(1) context(2) $ZWJ context(3) context(4) index(isNglegenaConsonant,1) $Pangkon

c c c c c c c c c
c Impossible rules
c c c c c c c c c

U+A9C0 + 'q' > context beep                     

c c c c c c c c c
c Vowel rules
c c c c c c c c c

+ 'a' > U+A9B2
$Pangkon + any(isA) > nul
any(isNglegena) + any(isA) > context U+A9B4        c aa
any(isNglegena) + any(isI) > context U+A9BB        c ai
any(isNglegena) + any(isU) > context U+A9BB U+A9B4 c au
c no 'ae'
c no 'ax'
c no 'ao'
$Cecak    + any(isA) > U+A994                      c cxcak + a = nga
$Layar    + any(isA) > U+A9AB                      c layar + a = ra
$Wignyan  + any(isA) > U+A9B2                      c wignyan + a = ha
any(isAll2) + any(isA) > context  U+A9B4           c wyanjana + a = wyanjana
+ 'A' > U+A984

+ 'i' > U+A9B2 U+A9B6
$Pangkon  + any(isI) > U+A9B6
U+A9B6 + any(isA) > context U+A9AA                c ia -> iya
U+A9B6 + any(isI) > U+A9B7                        c ii
U+A9B6 + any(isU) > context U+A9B8                c iu -> iyu
U+A9B6 + any(isE) > context U+A9BA                c ie -> iye
U+A9B6 + any(isX) > context U+A9BC                c ix -> iyx
U+A9B6 + any(isO) > context U+A9BA U+A9B4         c io -> iyo
$Cecak    + any(isI) > U+A994 U+A9B6              c cxcak + i = ngi
$Layar    + any(isI) > U+A9AB U+A9B6              c layar + i = ri
$Wignyan  + any(isI) > U+A9B2 U+A9B6              c wignyan + i = hi
any(isWyanjana) + any(isI) > context U+A9B6       c wyanjana + i
+ 'I' > U+A986

+ 'u' > U+A9B2 U+A9B8
$Pangkon  + any(isU) > U+A9B8
U+A9B8 + any(isA) > context U+A9AE                c ua -> uwa
U+A9B8 + any(isI) > context U+A9AE U+A9B7         c ui -> uwi
U+A9B8 + any(isU) > U+A9B9                        c uu
U+A9B8 + any(isE) > context U+A9AE U+A9BA         c ue -> uwe
U+A9B8 + any(isX) > context U+A9AE U+A9BC         c ux -> uwx
U+A9B8 + any(isO) > context U+A9AE U+A9BA U+A9B4  c uo -> uwo
$Cecak    + any(isU) > U+A994 U+A9B8              c cxcak + u = ngu
$Layar    + any(isU) > U+A9AB U+A9B8              c layar + u = ru
$Wignyan  + any(isU) > U+A9B2 U+A9B8              c wignyan + u = hu
any(isWyanjana) + any(isU) > context U+A9B8       c wyanjana + u
+ 'U' > U+A988

+ 'e' > U+A9B2 U+A9BA
$Pangkon  + any(isE) > U+A9BA
U+A9BA + any(isA) > context U+A9AA                c ea -> eya
U+A9BA + any(isI) > context U+A9AA U+A9B6         c ei -> eyi
U+A9BA + any(isU) > context U+A9AA U+A9B8         c eu -> eyu
c no 'ee'
c no 'ex'
U+A9BA + any(isO) > context U+A9AA U+A9BA U+A9B4  c eo -> eyo
$Cecak    + any(isE) > U+A994 U+A9BA              c cxcak + e = nge
$Layar    + any(isE) > U+A9AB U+A9BA              c layar + e = re
$Wignyan  + any(isE) > U+A9B2 U+A9BA              c wignyan + e = he
any(isWyanjana) + any(isE) > context U+A9BA       c wyanjana + e
+ 'E' > U+A98C

+ 'x' > U+A9B2 U+A9BC
U+A9AB $Pangkon + any(isX) > U+A989               c r + x = pa cxrxk
$Layar + any(isX) > U+A989                        c layar + x = pa cxrxk
U+A9AD $Pangkon + any(isX) > U+A98A               c l + x = nga lxlxt
$Pangkon  + any(isX) > U+A9BC
$Cecak    + any(isX) > U+A994 U+A9BC              c cxcak + x = ngx
$Layar    + any(isX) > U+A9AB U+A9BC              c layar + x = rx
$Wignyan  + any(isX) > U+A9B2 U+A9BC              c wignyan + x = hx
$Cakra    + any(isX) > $Keret                     c cakra + x = cakra keret
any(isWyanjana) + any(isX) > context U+A9BC       c wyanjana + x
+ 'X' > U+A984 U+A9BC

+ 'o' > U+A9B2 U+A9BA U+A9B4
$Pangkon  + any(isO) > U+A9BA U+A9B4
U+A9BA U+A9B4 + any(isA) > context U+A9AE         c oa -> owa
U+A9BA U+A9B4 + any(isI) > context U+A9AE U+A9B7  c oi -> owi
U+A9BA U+A9B4 + any(isU) > context U+A9AE U+A9B8  c ou -> owu
U+A9BA U+A9B4 + any(isE) > context U+A9AE U+A9BA  c oe -> owe
U+A9BA U+A9B4 + any(isX) > context U+A9AE U+A9BC  c ox -> owx
c no 'oo'
$Cecak    + any(isO) > U+A994 U+A9BA U+A9B4        c cecak + o = ngo
$Layar    + any(isO) > U+A9AB U+A9BA U+A9B4        c layar + o = ro
$Wignyan  + any(isO) > U+A9B2 U+A9BA U+A9B4        c wignyan + o = ho
any(isWyanjana) + any(isO) > context U+A9BA U+A9B4 c wyanjana + o
+ 'O' > U+A98E

c c c c c c c c c
c Consonant rules
c c c c c c c c c       
                         + 'k' > U+A98F $Pangkon
                         + 'g' > U+A992 $Pangkon           
         U+A9A4 $Pangkon + 'g' > $Cecak                          c ng -> cecak 
  $Cecak U+A9A4 $Pangkon + 'g' > $Cecak U+A994 $Pangkon          c no double cecak
  $Layar U+A9A4 $Pangkon + 'g' > $Layar U+A994 $Pangkon          c no double sesigeg
$Wignyan U+A9A4 $Pangkon + 'g' > $Wignyan U+A994 $Pangkon        c no double sesigeg
       any(isAll) $Cecak + 'g' > context U+A992 $Pangkon             
                  $Cecak + 'g' > U+A994 $Pangkon U+A992 $Pangkon c ngg -> cecak
                         + 'c' > U+A995 $Pangkon
         U+A9A4 $Pangkon + 'c' > U+A99A $Pangkon U+A995 $Pangkon c nc -> nyc
                         + 'j' > U+A997 $Pangkon
         U+A9A4 $Pangkon + 'j' > U+A99A $Pangkon U+A997 $Pangkon c nj -> nyj
                         + 'z' > U+A997 U+A9B3 $Pangkon
                         + 't' > U+A9A0 $Pangkon                            
                         + 'd' > U+A9A2 $Pangkon
                         + 'n' > U+A9A4 $Pangkon
                         + 'p' > U+A9A5 $Pangkon
                         + 'f' > U+A9A5 U+A9B3 $Pangkon
                         + 'b' > U+A9A7 $Pangkon
                         + 'm' > U+A9A9 $Pangkon
                         + 'y' > U+A9AA $Pangkon                  
         U+A9A4 $Pangkon + 'y' > U+A99A $Pangkon               c ny   
any(isNglegena2) $Pangkon + 'y' > context(1) $Pengkal           c pengkal            
                $Wignyan + 'y' > U+A9B2 $Pengkal               c wignyan + y -> ha + pengkal 
                         + 'r' > $Layar                      c r -> layar 
                  $Cecak + 'r' > $Cecak U+A9AB $Pangkon        c no double sesigeg 
                  $Layar + 'r' > $Layar U+A9AB $Pangkon      c no double layar
                $Wignyan + 'r' > U+A9B2 $Cakra $Pangkon      c wignyan + r -> ha + cakra       
     any(isAll) $Wignyan + 'r' > context U+A9AB $Pangkon  
any(isNglegena2) $Pangkon + 'r' > context(1) $Cakra $Pangkon c cakra
                         + 'l' > U+A9AD $Pangkon
       any(isAll) $Cecak + 'l' > context U+A9AD $Pangkon
                  $Cecak + 'l' > U+A994 $Pangkon U+A9AD $Pangkon c cxcak + l = panjingan la
       any(isAll) $Layar + 'l' > context U+A9AD $Pangkon
                  $Layar + 'l' > U+A9AB $Pangkon U+A9AD $Pangkon c layar + l = panjingan la
     any(isAll) $Wignyan + 'l' > context U+A9AD $Pangkon
                $Wignyan + 'l' > U+A9B2 $Pangkon U+A9AD $Pangkon c wignyan + l = panjingan la
                         + 'w' > U+A9AE $Pangkon    
       any(isAll) $Cecak + 'w' > context U+A9AE $Pangkon 
                  $Cecak + 'w' > U+A994 $Pangkon U+A9AE $Pangkon c cxcak + w = panjingan wa
       any(isAll) $Layar + 'w' > context U+A9AE $Pangkon
                  $Layar + 'w' > U+A9AB $Pangkon U+A9AE $Pangkon c layar + w = panjingan wa 
     any(isAll) $Wignyan + 'w' > context U+A9AE $Pangkon   
                $Wignyan + 'w' > U+A9B2 $Pangkon U+A9AE $Pangkon c wignyan + w = panjingan wa
                         + 'v' > U+A9AE U+A9B3 $Pangkon
                         + 's' > U+A9B1 $Pangkon
                         + 'h' > $Wignyan                      c h -> wignyan 
                  $Cecak + 'h' > $Cecak U+A9B2 $Pangkon        c no double sesigeg
                  $Layar + 'h' > $Layar U+A9B2 $Pangkon        c no double sesigeg
                $Wignyan + 'h' > $Wignyan U+A9B2 $pangkon      c no double wignyan
         U+A9A0 $Pangkon + 'h' > U+A99B $Pangkon               c t + h = th 
         U+A9A2 $Pangkon + 'h' > U+A99D $Pangkon               c d + h = dh
                         + 'q' > $Pangkon                      c virama

c c c c c c c c c
c Number & other rules
c c c c c c c c c

c + '`' > U+200C c this is already defined above
+ '1' > U+A9D1
+ '2' > U+A9D2
+ '3' > U+A9D3
+ '4' > U+A9D4
+ '5' > U+A9D5
+ '6' > U+A9D6
+ '7' > U+A9D7
+ '8' > U+A9D8
+ '9' > U+A9D9
+ '0' > U+A9D0
+ '-' > U+A980 c candrabindu (om sign)
+ '=' > U+A9CF c pangrangkep
+ '[' > U+A9CC c pada piseleh
+ ']' > U+A9CD c pada piseleh walik
+ '\' > U+A9CA c pada adxg
+ ';' > U+A9C3 c pada andhap
+ "'" > U+A9C5 c pada madya
+ ',' > U+A9C8 c pada lingsa
+ '.' > U+A9C9 c pada lungsi
+ '/' > U+A9C5 c pada luhur

c if non-number followed by number > :number
c if number followed by non-number > number:

c c c c c c c c c
c Shift rules
c c c c c c c c c

+ 'K' > U+A991 $Pangkon c ka murda
+ 'G' > U+A993 $Pangkon c ga murda
+ 'C' > U+A996 $Pangkon c ca murda
+ 'J' > U+A999 $Pangkon c ja mahaprana
+ 'Z' > U+A9B0 $Pangkon c sa mahaprana
+ 'T' > U+A9A1 $Pangkon c ta murda
+ 'D' > U+A9A3 $Pangkon c da murda
+ 'N' > U+A99F $Pangkon c na murda
+ 'P' > U+A9A6 $Pangkon c pa murda
+ 'F' > nul           c empty
+ 'B' > U+A9A8 $Pangkon c ba murda
+ 'M' > nul           c empty
+ 'Y' > $Cecak        c cxcak
+ 'R' > $Layar        c layar
+ 'L' > nul           c empty
+ 'W' > nul           c empty
+ 'V' > nul           c empty
+ 'S' > U+A9AF $Pangkon c sa murda
+ 'H' > $Wignyan        c wignyan
+ 'Q' > U+A990 $Pangkon c ka sasak      

U+A98F $Pangkon + 'h' > U+A991 $Pangkon c ka murda
U+A993 $Pangkon + 'h' > U+A993 $Pangkon c ga murda
U+A996 $Pangkon + 'h' > U+A996 $Pangkon c ca murda
U+A999 $Pangkon + 'h' > U+A999 $Pangkon c ja mahaprana
U+A9B0 $Pangkon + 'h' > U+A9B0 $Pangkon c sa mahaprana 
U+A9A1 $Pangkon + 'h' > U+A99C $Pangkon c tta mahaprana
U+A9A3 $Pangkon + 'h' > U+A99E $Pangkon c dda mahaprana
U+A99F $Pangkon + 'h' > U+A99F $Pangkon c na murda
U+A9A6 $Pangkon + 'h' > U+A9A6 $Pangkon c pa murda
U+A9A8 $Pangkon + 'h' > U+A9A8 $Pangkon c ba murda
U+A9AF $Pangkon + 'h' > U+A9AF $Pangkon c sa murda

+ '~' > nul c empty
+ '!' > U+A989 c shift 1 rx
+ '@' > U+A98A c shift 2 lx
+ '#' > U+A994 c shift 3 nga
+ '$' > U+A99A c shift 4 nya
+ '%' > U+A99B c shift 5 tta (tha)
+ '^' > U+A99D c shift 6 dda (dha)
+ '&' > $Keret c shift 7 kxrxt
+ '*' > $Pengkal c shift 8 pxngkal
+ '(' > $Cakra c shift 9 cakra
+ ')' > U+A998 c shift 0 nya murda
+ '_' > U+A99C c shift - tta mahaprana
+ '+' > U+A998 c shift = dda mahaprana
+ '|' > U+A9CB c shift \ pada adxg-adxg
+ '{' > U+A9C1 c shift [ rxrxnggan kiwa
+ '}' > U+A9C2 c shift ] rxrxnggan txngxn
+ ':' > U+A9C7 c shift ; pada pangkat
+ '"' > U+A9C6 c shift ' pada windu
+ '<' > U+A9DE c shift , pada tirta tumxtxs
+ '>' > U+A9DF c shift . pada isen-isen
+ '?' > nul c empty

c c c c c c c c c
c Alt rules
c c c c c c c c c

+ [ALT K_BKQUOTE] > nul c empty
+ [ALT K_1] > U+A9B3 c alt 1 cxcak txlu
+ [ALT K_2] > U+A9B4 c alt 2 tarung
+ [ALT K_3] > U+A9B5 c alt 3 tolong
+ [ALT K_4] > U+A9B7 c alt 4 wulu mxlik
+ [ALT K_5] > U+A9B9 c alt 5 suku mxndut
+ [ALT K_6] > U+A9BB c alt 6 dirga mure
+ [ALT K_7] > $Keret c alt 7 kxrxt
+ [ALT K_8] > $Pengkal c alt 8 pxngkal
+ [ALT K_9] > $Cakra c alt 9 cakra
+ [ALT K_0] > nul c empty
+ [ALT K_HYPHEN] > nul c empty
+ [ALT K_EQUAL] > nul c empty
+ [ALT K_LBRKT] > nul c empty
+ [ALT K_RBRKT] > nul c empty
+ [ALT K_BKSLASH] > nul c empty
+ [ALT K_COLON] > nul c empty
+ [ALT K_QUOTE] > nul c empty
+ [ALT K_COMMA] > nul c empty
+ [ALT K_PERIOD] > nul c empty
+ [ALT K_SLASH] > nul c empty

c store(lowercase) "aeiou"
c store(uppercase) "XEIOU"

c + any(lowercase) > index(uppercase, 1)    c translate vowels to upper case
