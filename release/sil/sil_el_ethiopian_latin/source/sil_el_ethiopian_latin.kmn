store(&VERSION) '9.0'
store(&NAME) 'SIL EL - Ethiopian Latin'
store(&TARGETS) 'web desktop'
store(&KEYBOARDVERSION) '1.4.1'
store(&BITMAP) 'sil_el_ethiopian_latin.ico'
store(&COPYRIGHT) '© SIL Ethiopia'
store(&MESSAGE) 'The SIL EL - Ethiopian Latin keyboard enables typing in many Ethiopian Latin based languages.'
c store(&LAYOUT) 'default'

begin Unicode > use(main)

group(main) using keys

c ------------------------------------------------------------------------------
c arrays
c ------------------------------------------------------------------------------
store(plainvowels)      "aeiouAEIOU"

store(acutevowels)       U+00E1 U+00E9 U+00ED U+00F3 U+00FA U+00C1 U+00C9 U+00CD U+00D3 U+00DA

store(gravevowels)       U+00E0 U+00E8 U+00EC U+00F2 U+00F9 U+00C0 U+00C8 U+00CC U+00D2 U+00D9

store(circumflexvowels)  U+00E2 U+00EA U+00EE U+00F4 U+00FB U+00C2 U+00CA U+00CE U+00D4 U+00DB

store(caronvowels)       U+01CE U+011B U+01D0 U+01D2 U+01D4 U+01CD U+011A U+01CF U+01D1 U+01D3

c store(tildevowels)     U+00E3 U+1EBD U+0129 U+00F5 U+0169 U+00C3 U+1EBC U+0128 U+00D5 U+0168

store(doublegravevowels) U+0201 U+0205 U+0209 U+020D U+0215 U+0200 U+0204 U+0208 U+020C U+0214

store(macronvowels)      U+0101 U+0113 U+012B U+014D U+016B U+0100 U+0112 U+012A U+014C U+016A

store(diaeresisvowels)   U+00E4 U+00EB U+00EF U+00F6 U+00FC U+00C4 U+00CB U+00CF U+00D6 U+00DC

c ------------------------------------------------------------------------------
c punctuation / special "word forming" characters
c ------------------------------------------------------------------------------
       + "'"    > U+2018 c ‘
U+2018 + "'"    > U+2019 c ’
U+2019 + "'"    > U+0027 c '
U+0027 + "'"    > U+2018 c ‘ (rota)

       + '"' > U+201C c “
U+201C + '"' > U+201D c ”
U+201D + '"' > U+0022 c "
U+0022 + '"' > U+201C c “ (rota)

"<"    + "'"    > U+2039 c ‹
">"    + "'"    > U+203A c ›
"<"    + '"'    > U+00AB c «
">"    + '"'    > U+00BB c »

";"    + "X"    > U+0294 c ʔ
";"    + "x"    > U+0295 c ʕ

U+005D + U+005D > U+02BC c ʼ
U+02BC + U+005D > U+005D c ] (rota)

";"    + "'"    > U+A78C c ꞌ

"]"    + ":"    > U+A789 c ꞉

c ------------------------------------------------------------------------------
c special vowels (no diacritics)
c ------------------------------------------------------------------------------
"]" + "a" > U+028C c ʌ
"]" + "A" > U+0245 c Ʌ

";" + "e" > U+025B c ɛ
"e" + "<" > U+025B c ɛ
"]" + "e" > U+025B c ɛ
";" + "E" > U+0190 c Ɛ
"E" + "<" > U+0190 c Ɛ
"]" + "E" > U+0190 c Ɛ

":" + "e" > U+0259 c ə
"=" + "e" > U+0259 c ə
":" + "E" > U+018F c Ə
"=" + "E" > U+018F c Ə

";" + "i" > U+0268 c ɨ
"i" + "<" > U+0268 c ɨ
"i" + "=" > U+0268 c ɨ
";" + "I" > U+0197 c Ɨ
"I" + "<" > U+0197 c Ɨ
"I" + "=" > U+0197 c Ɨ

"]" + "i" > U+0269 c ɩ
"]" + "I" > U+0196 c Ɩ

";" + "o" > U+0254 c ɔ
"o" + "<" > U+0254 c ɔ
"]" + "o" > U+0254 c ɔ
";" + "O" > U+0186 c Ɔ
"O" + "<" > U+0186 c Ɔ
"]" + "O" > U+0186 c Ɔ

"=" + "o" > U+00F8 c ø
"=" + "O" > U+00D8 c Ø

";" + "u" > U+0289 c ʉ
"u" + "<" > U+0289 c ʉ
"u" + "=" > U+0289 c ʉ
";" + "U" > U+0244 c Ʉ
"U" + "<" > U+0244 c Ʉ
"U" + "=" > U+0244 c Ʉ

"]" + "u" > U+028B c ʋ
"]" + "U" > U+01B2 c Ʋ

c ------------------------------------------------------------------------------
c special consonants (no diacritics)
c ------------------------------------------------------------------------------
";" + "b" > U+0253 c ɓ
"b" + "<" > U+0253 c ɓ
";" + "B" > U+0181 c Ɓ
"B" + "<" > U+0181 c Ɓ

";" + "c" > U+00E7 c ç
"c" + "<" > U+00E7 c ç
";" + "C" > U+00C7 c Ç
"C" + "<" > U+00C7 c Ç

";" + "d" > U+0257 c ɗ
"d" + "<" > U+0257 c ɗ
";" + "D" > U+018A c Ɗ
"D" + "<" > U+018A c Ɗ

";" + "g" > U+0260 c ɠ
"g" + "<" > U+0260 c ɠ
";" + "G" > U+0193 c Ɠ
"G" + "<" > U+0193 c Ɠ

"]" + "g" > U+0263 c ɣ
"]" + "G" > U+0194 c Ɣ

";" + "h" > U+0127 c ħ
";" + "H" > U+0126 c Ħ

";" + "n" > U+014B c ŋ
"n" + "<" > U+014B c ŋ
"]" + "n" > U+014B c ŋ
";" + "N" > U+014A c Ŋ
"N" + "<" > U+014A c Ŋ
"]" + "N" > U+014A c Ŋ

":" + "n" > U+0272 c ɲ
"=" + "n" > U+0272 c ɲ
":" + "N" > U+019D c Ɲ
"=" + "N" > U+019D c Ɲ

";" + "s" > U+0283 c ʃ
"s" + "<" > U+0283 c ʃ
";" + "S" > U+0286 c ʆ
"S" + "<" > U+0286 c ʆ

c ------------------------------------------------------------------------------
c acute characters: composed else + U+0301
c ------------------------------------------------------------------------------
any(plainvowels) + "1" > index(acutevowels, 1) c áéíóúÁÉÍÓÚ

U+028C + "1" > U+028C U+0301 c ʌ́
U+0245 + "1" > U+0245 U+0301 c Ʌ́

"b"    + "1" > U+0062 U+0301 c b́
"B"    + "1" > U+0042 U+0301 c B́

U+025B + "1" > U+025B U+0301 c ɛ́
U+0190 + "1" > U+0190 U+0301 c Ɛ́

"g"    + "1" > U+0067 U+0301 c ǵ
"G"    + "1" > U+0047 U+0301 c Ǵ

U+0268 + "1" > U+0268 U+0301 c ɨ́
U+0197 + "1" > U+0197 U+0301 c Ɨ́

U+0269 + "1" > U+0269 U+0301 c ɩ́
U+0196 + "1" > U+0196 U+0301 c Ɩ́

"m"    + "1" > U+1E3F        c ḿ
"M"    + "1" > U+1E3E        c Ḿ

"n"    + "1" > U+0144        c ń
"N"    + "1" > U+0143        c Ń

U+014B + "1" > U+014B U+0301 c ŋ́
U+014A + "1" > U+014A U+0301 c Ŋ́

U+0254 + "1" > U+0254 U+0301 c ɔ́
U+0186 + "1" > U+0186 U+0301 c Ɔ́

"t"    + "1" > U+0074 U+0301 c t́
"T"    + "1" > U+0054 U+0301 c T́

U+0289 + "1" > U+0289 U+0301 c ʉ́
U+0244 + "1" > U+0244 U+0301 c Ʉ́

U+028B + "1" > U+028B U+0301 c ʋ́
U+01B2 + "1" > U+01B2 U+0301 c Ʋ́

c ------------------------------------------------------------------------------
c grave characters: composed else + U+0300
c ------------------------------------------------------------------------------
any(plainvowels) + "2" > index(gravevowels, 1) c àèìòùÀÈÌÒÙ

U+028C + "2" > U+028C U+0300 c ʌ̀
U+0245 + "2" > U+0245 U+0300 c Ʌ̀

U+025B + "2" > U+025B U+0300 c ɛ̀
U+0190 + "2" > U+0190 U+0300 c Ɛ̀

U+0268 + "2" > U+0268 U+0300 c ɨ̀
U+0197 + "2" > U+0197 U+0300 c Ɨ̀

U+0269 + "2" > U+0269 U+0300 c ɩ̀
U+0196 + "2" > U+0196 U+0300 c Ɩ̀

"m"    + "2" > U+006D U+0300 c m̀
"M"    + "2" > U+004D U+0300 c M̀

"n"    + "2" > U+006E U+0300 c ǹ
"N"    + "2" > U+004E U+0300 c Ǹ

U+0254 + "2" > U+0254 U+0300 c ɔ̀
U+0186 + "2" > U+0186 U+0300 c Ɔ̀

U+0289 + "2" > U+0289 U+0300 c ʉ̀
U+0244 + "2" > U+0244 U+0300 c Ʉ̀

U+028B + "2" > U+028B U+0300 c ʋ̀
U+01B2 + "2" > U+01B2 U+0300 c Ʋ̀

c ------------------------------------------------------------------------------
c circumflex characters: composed else + U+0302
c ------------------------------------------------------------------------------
any(plainvowels) + "3" > index(circumflexvowels, 1) c âêîôûÂÊÎÔÛ

U+028C + "3" > U+028C U+0302 c ʌ̂
U+0245 + "3" > U+0245 U+0302 c Ʌ̂

U+025B + "3" > U+025B U+0302 c ɛ̂
U+0190 + "3" > U+0190 U+0302 c Ɛ̂

U+0268 + "3" > U+0268 U+0302 c ɨ̂
U+0197 + "3" > U+0197 U+0302 c Ɨ̂

U+0269 + "3" > U+0269 U+0302 c ɩ̂
U+0196 + "3" > U+0196 U+0302 c Ɩ̂

U+0254 + "3" > U+0254 U+0302 c ɔ̂
U+0186 + "3" > U+0186 U+0302 c Ɔ̂

U+0289 + "3" > U+0289 U+0302 c ʉ̂
U+0244 + "3" > U+0244 U+0302 c Ʉ̂

U+028B + "3" > U+028B U+0302 c ʋ̂
U+01B2 + "3" > U+01B2 U+0302 c Ʋ̂

c ------------------------------------------------------------------------------
c caron characters: composed else + U+030C
c ------------------------------------------------------------------------------
any(plainvowels) + "4" > index(caronvowels, 1) c ǎěǐǒǔǍĚǏǑǓ

U+028C + "4" > U+028C U+030C c ʌ̌
U+0245 + "4" > U+0245 U+030C c Ʌ̌

U+025B + "4" > U+025B U+030C c ɛ̌
U+0190 + "4" > U+0190 U+030C c Ɛ̌

U+0268 + "4" > U+0268 U+030C c ɨ̌
U+0197 + "4" > U+0197 U+030C c Ɨ̌

U+0269 + "4" > U+0269 U+030C c ɩ̌
U+0196 + "4" > U+0196 U+030C c Ɩ̌

U+0254 + "4" > U+0254 U+030C c ɔ̌
U+0186 + "4" > U+0186 U+030C c Ɔ̌

U+0289 + "4" > U+0289 U+030C c ʉ̌
U+0244 + "4" > U+0244 U+030C c Ʉ̌

U+028B + "4" > U+028B U+030C c ʋ̌
U+01B2 + "4" > U+01B2 U+030C c Ʋ̌

c ------------------------------------------------------------------------------
c tilde characters: composed else + U+0303
c ------------------------------------------------------------------------------
"n"    + "5" > U+00F1 c ñ
"N"    + "5" > U+00D1 c Ñ

c ------------------------------------------------------------------------------
c combining comma characters: composed else + U+0315
c ------------------------------------------------------------------------------
"c" + "6" > U+0063 U+0315 c c̕
"C" + "6" > U+0043 U+0315 c C̕

"k" + "6" > U+006B U+0315 c k̕
"K" + "6" > U+004B U+0315 c K̕

"s" + "6" > U+0073 U+0315 c s̕
"S" + "6" > U+0053 U+0315 c S̕

"t" + "6" > U+0074 U+0315 c t̕
"T" + "6" > U+0054 U+0315 c T̕

c ------------------------------------------------------------------------------
c double acute characters: composed else + U+030B
c ------------------------------------------------------------------------------
"a"    + "7" > U+0061 U+030B c a̋
"A"    + "7" > U+0041 U+030B c A̋

"e"    + "7" > U+0065 U+030B c e̋
"E"    + "7" > U+0045 U+030B c E̋

"i"    + "7" > U+0069 U+030B c i̋
"I"    + "7" > U+0049 U+030B c I̋

U+0268 + "7" > U+0268 U+030B c ɨ̋
U+0197 + "7" > U+0197 U+030B c Ɨ̋

"o"    + "7" > U+0151        c ő
"O"    + "7" > U+0150        c Ő

"u"    + "7" > U+0171        c ű
"U"    + "7" > U+0170        c Ű

U+0289 + "7" > U+0289 U+030B c ʉ̋
U+0244 + "7" > U+0244 U+030B c Ʉ̋

c ------------------------------------------------------------------------------
c double grave characters: composed else + U+030F
c ------------------------------------------------------------------------------
any(plainvowels) + "8" > index(doublegravevowels, 1) c ȁȅȉȍȕȀȄȈȌȔ

U+0268 + "8" > U+0268 U+030F c ɨ̏
U+0197 + "8" > U+0197 U+030F c Ɨ̏

U+0289 + "8" > U+0289 U+030F c ʉ̏
U+0244 + "8" > U+0244 U+030F c Ʉ̏

c ------------------------------------------------------------------------------
c macron characters: composed else + U+0304
c ------------------------------------------------------------------------------
any(plainvowels) + "9" > index(macronvowels, 1) c āēīōūĀĒĪŌŪ

U+028C + "9" > U+028C U+0304 c ʌ̄
U+0245 + "9" > U+0245 U+0304 c Ʌ̄

U+025B + "9" > U+025B U+0304 c ɛ̄
U+0190 + "9" > U+0190 U+0304 c Ɛ̄

U+0259 + "9" > U+0259 U+0304 c ə̄
U+018F + "9" > U+018F U+0304 c Ə̄

U+0268 + "9" > U+0268 U+0304 c ɨ̄
U+0197 + "9" > U+0197 U+0304 c Ɨ̄

U+0269 + "9" > U+0269 U+0304 c ɩ̄
U+0196 + "9" > U+0196 U+0304 c Ɩ̄

"m"    + "9" > U+006D U+0304 c m̄
"M"    + "9" > U+004D U+0304 c M̄

"n"    + "9" > U+006E U+0304 c n̄
"N"    + "9" > U+004E U+0304 c N̄

U+014B + "9" > U+014B U+0304 c ŋ̄
U+014A + "9" > U+014A U+0304 c Ŋ̄

U+0254 + "9" > U+0254 U+0304 c ɔ̄
U+0186 + "9" > U+0186 U+0304 c Ɔ̄

U+0289 + "9" > U+0289 U+0304 c ʉ̄
U+0244 + "9" > U+0244 U+0304 c Ʉ̄

U+028B + "9" > U+028B U+0304 c ʋ̄
U+01B2 + "9" > U+01B2 U+0304 c Ʋ̄

c ------------------------------------------------------------------------------
c macron below + diaeresis characters: + U+0331 + U+0308
c ------------------------------------------------------------------------------
U+025B U+0331 + "#" > U+025B U+0331 U+0308 c ɛ̱̈
U+025B U+0331 + "[" > U+025B U+0331 U+0308 c ɛ̱̈
U+025B U+0308 + "0" > U+025B U+0331 U+0308 c ɛ̱̈ => keep decomposed order consistent with sudan nuer keyboard

U+0190 U+0331 + "#" > U+0190 U+0331 U+0308 c Ɛ̱̈
U+0190 U+0331 + "[" > U+0190 U+0331 U+0308 c Ɛ̱̈
U+0190 U+0308 + "0" > U+0190 U+0331 U+0308 c Ɛ̱̈ => keep decomposed order consistent with sudan nuer keyboard

c ------------------------------------------------------------------------------
c macron below characters: composed else + U+0331
c ------------------------------------------------------------------------------
"a"    + "0" > U+0061 U+0331 c a̱
"A"    + "0" > U+0041 U+0331 c A̱

U+028C + "0" > U+028C U+0331 c ʌ̱
U+0245 + "0" > U+0245 U+0331 c Ʌ̱

"e"    + "0" > U+0065 U+0331 c e̱
"E"    + "0" > U+0045 U+0331 c E̱

U+025B + "0" > U+025B U+0331 c ɛ̱
U+0190 + "0" > U+0190 U+0331 c Ɛ̱

"i"    + "0" > U+0069 U+0331 c i̱
"I"    + "0" > U+0049 U+0331 c I̱

U+0268 + "0" > U+0268 U+0331 c ɨ̱
U+0197 + "0" > U+0197 U+0331 c Ɨ̱

U+0269 + "0" > U+0269 U+0331 c ɩ̱
U+0196 + "0" > U+0196 U+0331 c Ɩ̱

"o"    + "0" > U+006F U+0331 c o̱
"O"    + "0" > U+004F U+0331 c O̱

U+0254 + "0" > U+0254 U+0331 c ɔ̱
U+0186 + "0" > U+0186 U+0331 c Ɔ̱

"u"    + "0" > U+0075 U+0331 c u̱
"U"    + "0" > U+0055 U+0331 c U̱

U+0289 + "0" > U+0289 U+0331 c ʉ̱
U+0244 + "0" > U+0244 U+0331 c Ʉ̱

U+028B + "0" > U+028B U+0331 c ʋ̱
U+01B2 + "0" > U+01B2 U+0331 c Ʋ̱

c ------------------------------------------------------------------------------
c diaeresis characters: composed else + U+0308
c ------------------------------------------------------------------------------
any(plainvowels) + "#" > index(diaeresisvowels, 1) c äëïöüÄËÏÖÜ
any(plainvowels) + "[" > index(diaeresisvowels, 1) c äëïöüÄËÏÖÜ

U+025B + "#" > U+025B U+0308 c ɛ̈
U+025B + "[" > U+025B U+0308 c ɛ̈
U+0190 + "#" > U+0190 U+0308 c Ɛ̈
U+0190 + "[" > U+0190 U+0308 c Ɛ̈

U+0268 + "#" > U+0268 U+0308 c ɨ̈
U+0268 + "[" > U+0268 U+0308 c ɨ̈
U+0197 + "#" > U+0197 U+0308 c Ɨ̈
U+0197 + "[" > U+0197 U+0308 c Ɨ̈

U+0254 + "#" > U+0254 U+0308 c ɔ̈
U+0254 + "[" > U+0254 U+0308 c ɔ̈
U+0186 + "#" > U+0186 U+0308 c Ɔ̈
U+0186 + "[" > U+0186 U+0308 c Ɔ̈

U+0289 + "#" > U+0289 U+0308 c ʉ̈
U+0289 + "[" > U+0289 U+0308 c ʉ̈
U+0244 + "#" > U+0244 U+0308 c Ʉ̈
U+0244 + "[" > U+0244 U+0308 c Ʉ̈
