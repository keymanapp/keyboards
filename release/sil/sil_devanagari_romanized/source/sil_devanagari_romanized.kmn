store(&VERSION) '10.0'
store(&NAME) 'Devanagari Romanized (SIL)'
store(&COPYRIGHT) '© SIL International'
store(&KEYBOARDVERSION) '1.7.1'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'sil_devanagari_romanized.kvks'
store(&LAYOUTFILE) 'sil_devanagari_romanized.keyman-touch-layout'
store(&BITMAP) 'sil_devanagari_romanized.ico'

begin Unicode > use(main)
c numbers
store(Digits)     "0"    "1"    "2"    "3"    "4"    "5"    "6" \
                  "7"    "8"    "9"
store(DigitsU)    U+0966 U+0967 U+0968 U+0969 U+096A U+096B U+096C \
                  U+096D U+096E U+096F

c consonants (so we can reposition the reph)
store(Consonants)    "k"    "K"    "g"    "G"    "M"    \
                     "c"    "C"    "j"    "J"    "V"    "q"     \
                     "Q"    "z"    "Z"    "N"    "t"    "T"     "d"    "D"    "n"    \
                     "p"    "P"    "b"    "B"    "m"    "y"     \
                     "r"    "l"    "L"    "v"     "x"    "S"    "s"    "h" \
                     "+"    "="

store(ConsonantsU)   U+0915 U+0916 U+0917 U+0918 U+0919 \
                     U+091A U+091B U+091C U+091D U+091E U+091F  \
                     U+0920 U+0921 U+0922 U+0923 U+0924 U+0925 U+0926 U+0927 U+0928 \
                     U+092A U+092B U+092C U+092D U+092E U+092F  \
                     U+0930 U+0932 U+0933 U+0935 U+0936 U+0937 U+0938 U+0939 \
                     U+0960 U+0961

c                    aakar  ikar   iikar  ukar   uukar  ekar   aikar  okar   aukar
store(MatraVsU)      U+093E U+093f U+0940 U+0941 U+0942 U+0947 U+0948 U+094b U+094c

c                           a   aa  e   ai  i   ii  o   au  u   uu
store(MaatraU)        U+0906 U+0905 U+090F U+0910 U+0907 U+0908 U+0913 U+0914 U+0909 U+090a
c                           candra a o e
store(candraU)        U+0972 U+0911  U+090D 
store(RomanCs)       "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ" \
                     "~`;:" c plus some punctuation used in allomorph constraints (the others come thru anyway)

c                       ka क       khha ख   ggha घ  za  ज    dddha ध  rha  र   fa  फ    yya  य   llla ल  rra 
store(LowDotWallahs)    U+0915  U+0916  U+0917  U+091c  U+0921  U+0922  U+092b  U+092f  U+0933  U+0930 U+0928
store(LowDotOutputs)    U+0958  U+0959  U+095a  U+095b  U+095c  U+095d  U+095e  U+095f  U+0934  U+0931 U+0929

group(main) using keys

c Devanagari Independent Vowels 

c Output deadkeys only for the accent keys pressed
+ "q"    > dk(1) c // followed by vowel will produce syllabic form

+ [RALT SHIFT K_A]  > U+0904  c // short a ऄ / used for short e in Awadhi /also used in Devanagari transliterations of some South Indian and Kashmiri languages by a publisher in Lucknow
+ [LCTRL LALT SHIFT K_A]  > U+0904  c // short a ऄ / used for short e in Awadhi /also used in Devanagari transliterations of some South Indian and Kashmiri languages by a publisher in Lucknow
+ "A"     > U+0905 c // Devanagari Letter- A अ
dk(1) + "a"     > U+0906 c // Devanagari Letter- Aa आ
dk(1) + "i"     > U+0907 c // Devanagari Letter- I इ
dk(1) + "I"     > U+0908 c // Devanagari Letter- Ii II ई
dk(1) + "u"     > U+0909 c // Devanagari Letter- U उ 
dk(1) + "U"     > U+090a c // Devanagari Letter- Uu ऊ
dk(1) + [RALT K_Z] > U+090B c // Devanagari Letter Vocalic- R ऋ
dk(1) + [LCTRL LALT K_Z] > U+090B  c // Devanagari Letter Vocalic- R ऋ  fixed by Sam
dk(1) + [RALT K_L] > U+090C c // Devanagari Letter Vocalic- L ऌ /*New Addition*/
dk(1) + [LCTRL LALT K_L] > U+090C c // Devanagari Letter Vocalic- L ऌ /*New Addition*/
dk(1) + [RALT K_W] > U+090D c // Devanagari Letter Candra E ऍ /*New Addition*/
dk(1) + [LCTRL LALT K_W] > U+090D c // Devanagari Letter Candra E ऍ /*New Addition*/
dk(1) + [RALT K_E] > U+090E c // Devanagari Letter Short E  ऎ
dk(1) + [LCTRL LALT K_E] > U+090E c // Devanagari Letter Short E  ऎ fixed by Sam
dk(1) + "e"     > U+090F    c // Devanagari Letter- E ए
dk(1) + "E"     > U+0910    c // Devanagari Letter- Ai ऐ
dk(1) + [RALT K_A] > U+0911 c // Devanagari Letter Candra O ऑ /*New Addition*/
dk(1) + [LCTRL LALT K_A] > U+0911 c // Devanagari Letter Candra O ऑ /*New Addition*/
dk(1) + [RALT K_O] > U+0912 c // Devanagari Letter Short O ऒ /*New Addition*/
dk(1) + [LCTRL LALT K_O] > U+0912 c // Devanagari Letter Short O ऒ /*New Addition*/
dk(1) + "o"     > U+0913    c // Devanagari Letter- O ओ
dk(1) + "O"     >  U+0914   c // Devanagari Letter- Au औ

c Additional Independent Vowels for Sanskrit

dk(1) + [RALT SHIFT K_Z] > U+0960 c // Devanagari Letter Vocalic Rr ॠ /*New Addition*/
dk(1) + [LCTRL LALT SHIFT K_Z] > U+0960 c // Devanagari Letter Vocalic Rr ॠ /*New Addition*/
dk(1) + [RALT SHIFT K_L] > U+0961 c // Devanagari Letter Vocalic- Ll ॡ /*New Addition*/
dk(1) + [LCTRL LALT SHIFT K_L] > U+0961 c // Devanagari Letter Vocalic- Ll ॡ /*New Addition*/

c Devanagari Consonants

+ "k"    > U+0915 c // Devanagari Letter- Ka क
+ "K"    > U+0916 c // Devanagari Letter- Kha ख
+ "g" 	 > U+0917 c // Devanagari Letter- Ga ग 
+ "G" 	 > U+0918 c // Devanagari Letter- Gha घ
+ "M"    > U+0919 c // Devanagari Letter- Nga ङ 
+ "c" 	 > U+091a c // Devanagari Letter- Ca च
+ "C" 	 > U+091b c // Devanagari Letter- Cha छ
+ "j"    > U+091c c // Devanagari Letter- Ja ज
+ "J"    > U+091d c // Devanagari Letter- Jha झ 
+ [RALT K_N] > U+091e c // Devanagari Letter- Nya ञ
+ [LCTRL LALT K_N] > U+091e c // Devanagari Letter- Nya ञ
+ "f" 	 > U+091f c // Devanagari Letter- Tta ट
+ "F" 	 > U+0920 c // Devanagari Letter- Ttha ठ
+ "v"     > U+0921 c // Devanagari Letter- Dda ड
+ "V"     > U+0922 c // Devanagari Letter- Ddha ढ
+ "N"    > U+0923 c // Devanagari Letter- Nna  ण
+ "t"     > U+0924 c // Devanagari Letter- Ta त
+ "T"     > U+0925 c // Devanagari Letter- Tha थ
+ "d" 	 > U+0926 c // Devanagari Letter- Da द
+ "D" 	 > U+0927 c // Devanagari Letter- Dha ध
+ "n"    > U+0928 c // Devanagari Letter- Na न 
+ "p"    > U+092a c // Devanagari Letter- Pa प 
+ "P"    > U+092b c // Devanagari Letter- Pha फ
+ "b" 	 > U+092c c // Devanagari Letter- Ba ब
+ "B" 	 > U+092d c // Devanagari Letter- Bha भ
+ "m"    > U+092e c // Devanagari Letter- Ma म
+ "y"     > U+092f c // Devanagari Letter- Ya य
+ "r"     > U+0930 c // Devanagari Letter- Ra र
+ "l"    > U+0932 c // Devanagari Letter- La ल
+ "L" 	 > U+0933 c // Devanagari Letter- Lla ळ
+ "w"    > U+0935 c // Devanagari Letter- Va व
+ "z"     > U+0936 c // Devanagari Letter- Sha श
+ "S"     > U+0937 c // Devanagari Letter- Ssa ष
+ "s"     > U+0938 c // Devanagari Letter- Sa स
+ "h" 	 > U+0939 c // Devanagari Letter- Ha ह

c Halanta --> Devanagari Sign Virama
c semicolon ";" is keyed between consonants to produce a stack / conjunct

+ ";"           > U+094d   c // use the Devanagari Sign Virama so we get the full conjunct
+ "x"     > U+094d U+200D c // halant (and the zero-width joiner to get half-character) ्‍ + ZWJ
+ "X"     > U+094d U+200C  c // halant (and the zero-width non-joining to get full-char plus halant) ् + ZWNJ

c Devanagari Special Conjuncts

+ "Z"     > U+0915 U+094d U+0937  c // Devanagari Letter- Ksha क्ष

c Devanagari Dependent Vowels

+ "a" > U+093E c // Devanagari Vowel Sign- Aa ा
+ "i" > U+093f c // Devanagari Vowel Sign- I ि
+ "I" > U+0940 c // Devanagari Vowel Sign- Ii ी
+ "u" > U+0941 c // Devanagari Vowel Sign- U ु
+ "U" > U+0942 c // Devanagari Vowel Sign- Uu ू
+ [RALT K_Z] > U+0943  c // Devanagari Vowel Sign Vocalic-R ृ
+ [LCTRL LALT K_Z] > U+0943  c // Devanagari Vowel Sign Vocalic-R ृ
+ [RALT SHIFT K_Z] > U+0944  c // Devanagari Vowel Sign Vocalic-Rr  ॄ
+ [LCTRL LALT SHIFT K_Z] > U+0944  c // Devanagari Vowel Sign Vocalic-Rr  ॄ
+ [RALT K_W] > U+0945 c // Devanagari Vowel Sign Candra-E ॅ
+ [LCTRL LALT K_W] > U+0945 c // Devanagari Vowel Sign Candra-E ॅ
+ [RALT K_E] > U+0946 c // Devanagari Vowel Sign Short E ॆ
+ [LCTRL LALT K_E] > U+0946 c // Devanagari Vowel Sign Short E ॆ
+ "e" > U+0947 c // Devanagari Vowel Sign-E े
+ "E" > U+0948 c // Devanagari Vowel Sign-Ai ै
+ [RALT K_A] > U+0949 c // Devanagari Vowel Sign Candra-O ॉ
+ [LCTRL LALT K_A] > U+0949 c // Devanagari Vowel Sign Candra-O ॉ
+ [RALT K_O] > U+094A c // Devanagari Vowel Sign Short-O ॊ
+ [LCTRL LALT K_O] > U+094A c // Devanagari Vowel Sign Short-O ॊ
+ "o" > U+094b c // Devanagari Vowel Sign-O ो
+ "O" > U+094c c // Devanagari Vowel Sign-Au ौ

c Additional Dependent Vowels for Sanskrit

+ [RALT K_L] > U+0962   c // Devanagari Vowel Sign Vocalic-L ॢ
+ [LCTRL LALT K_L] > U+0962   c // Devanagari Vowel Sign Vocalic-L ॢ
+ [RALT SHIFT K_L] > U+0963  c // Devanagari Vowel Sign Vocalic-Ll ॣ
+ [LCTRL LALT SHIFT K_L] > U+0963  c // Devanagari Vowel Sign Vocalic-Ll ॣ

c Various signs

+ "`" > U+0901 c // Devanagari Sign Candrabindu ँ
+ "~" > U+0902 c // Devanagari Sign Anusvara ं
+ "H" 	 > U+0903 c // Devanagari Sign Visarga ः
+ [RALT K_LBRKT] > U+093c c // Devanagari Sign Nukta (Lower dot) ़
+ [LCTRL LALT K_LBRKT] > U+093c c // Devanagari Sign Nukta (Lower dot) ़
+ [RALT K_H] > U+093d  c // Devanagari Sign Avagraha ऽ
+ [LCTRL LALT K_H] > U+093d  c // Devanagari Sign Avagraha ऽ
+ [RALT K_BKSLASH] > U+00B7 c // Devanagari Sign Mid Dot ·
+ [LCTRL LALT K_BKSLASH] > U+00B7   c // Devanagari Sign Mid Dot ·
+ [RALT SHIFT K_1]  > U+0951  c // Devanagari Stress Sign udatta ॑
+ [LCTRL LALT SHIFT K_1]  > U+0951  c // Devanagari Stress Sign udatta ॑
+ [RALT SHIFT K_2]  > U+0952  c // Devanagari Stress Sign anudatta   ॒
+ [LCTRL LALT SHIFT K_2]  > U+0952  c // Devanagari Stress Sign anudatta   ॒

c Accent marks

c These accent marks were originally intended for Latin
c transliteration of Sanskrit, but 0300 and 0301 should be used
c instead. These two accents should not be used with the
c Devanagari script; they have no Indic shaping properties.

+ [RALT SHIFT K_3]  > U+0953  c // Devanagari Grave accent ॓
+ [LCTRL LALT SHIFT K_3]  > U+0953  c // Devanagari Grave accent ॓
+ [RALT SHIFT K_4]  > U+0954  c // Devanagari Acute accent ॔
+ [LCTRL LALT SHIFT K_4]  > U+0954  c // Devanagari Acute accent ॔

c Additional signs

+ [RALT SHIFT K_5]  > U+0970  c // Devanagari Sign Abbreviation ॰
+ [LCTRL LALT SHIFT K_5]  > U+0970  c // Devanagari Sign Abbreviation ॰
+ [RALT K_RBRKT] > U+0971   c // Devanagari Sign High Spacing Dot ॱ
+ [LCTRL LALT K_RBRKT] > U+0971   c // Devanagari Sign High Spacing Dot ॱ
+ [RALT K_F] > U+0950 c // Devanagari Om ॐ
+ [LCTRL LALT K_F] > U+0950 c // Devanagari Om ॐ

c Devanagari Generic Punctuation Marks

+ "." > U+0964 c // Devanagari Danda ।
+ [SHIFT K_BKSLASH] > U+0965  c // Devanagari Double Danda ॥

c Other Commonly Used Punctuation Marks

+ [RALT K_K] > U+02BC c // modifier apostrophe ʼ
+ [LCTRL LALT K_K] > U+02BC   c // modifier apostrophe ʼ
"-" + "-" > U+2013  c // hit hyphen twice for En Dash –
U+2013 + "-" > U+2014  c // hit hyphen thrice Em Dash —
+ [SHIFT K_COMMA] > U+2018 c // Left Single Quotation Mark ‘
+ [SHIFT K_PERIOD] > U+2019 c // Right Single Quotation Mark ’
+ [RALT K_COLON] > U+201c  c // Left Double Quotation Mark / awataraṇ cinha “
+ [LCTRL LALT K_COLON] > U+201c  c // Left Double Quotation Mark / awataraṇ cinha “
+ [RALT K_QUOTE] > U+201d c // Right Double Quotation Mark /awataraṇ cinha ”
+ [LCTRL LALT K_QUOTE] > U+201d c // Right Double Quotation Mark /awataraṇ cinha ”
+ [RALT SHIFT K_COLON] > U+003B c // Semi-Colon ;
+ [LCTRL LALT SHIFT K_COLON] > U+003B c // Semi-Colon ;
+ [RALT K_PERIOD] > "."   c // Roman period .
+ [LCTRL LALT K_PERIOD] > "."   c // Roman period .

c Devanagari Glottal Stop

+ [RALT K_Q] > U+097D c // Devanagari Letter-Glottal Stop ॽ
+ [LCTRL LALT K_Q] > U+097D c // Devanagari Letter-Glottal Stop ॽ

c Sindhi implosives

+ [RALT K_G] > U+097B c // Devanagari Letter-Gga ॻ
+ [LCTRL LALT K_G] > U+097B c // Devanagari Letter-Gga ॻ 
+ [RALT K_J] > U+097c c // Devanagari Letter-Jja  ॼ
+ [LCTRL LALT K_J] > U+097c c // Devanagari Letter-Jja  ॼ
+ [RALT K_V] > U+097E c // Devanagari Letter Ddda ॾ
+ [LCTRL LALT K_V] > U+097E c // Devanagari Letter Ddda ॾ  
+ [RALT K_B] > U+097F c // Devanagari Letter Bba ॿ
+ [LCTRL LALT K_B] > U+097F c // Devanagari Letter Bba ॿ

c Digits: we get the Arabic wallahs with the numeric key
c and Devanagari with a subsequent backspace or Shift + RALT + sequences 

+ any(Digits)     > index(DigitsU,1) c //Devanagari Digits will be executed
+ [RALT K_0]    >  U+0030 c // digit 0 
+ [RALT K_1]    > U+0031 c // digit 1
+ [RALT K_2]    > U+0032 c // digit 2
+ [RALT K_3]    > U+0033 c // digit 3
+ [RALT K_4]    > U+0034 c // digit 4
+ [RALT K_5]    > U+0035 c // digit 5
+ [RALT K_6]    > U+0036 c // digit 6
+ [RALT K_7]    > U+0037 c // digit 7
+ [RALT K_8]    > U+0038 c // digit 8
+ [RALT K_9]    > U+0039 c // digit 9

c Numbers in English //fixed by Sam

+ [LCTRL LALT K_0] > U+0030 c //digit 0 
+ [LCTRL LALT K_1] > U+0031 c //digit 1
+ [LCTRL LALT K_2] > U+0032 c //digit 2
+ [LCTRL LALT K_3] > U+0033 c //digit 3
+ [LCTRL LALT K_4] > U+0034 c //digit 4
+ [LCTRL LALT K_5] > U+0035 c //digit 5
+ [LCTRL LALT K_6] > U+0036 c //digit 6
+ [LCTRL LALT K_7] > U+0037 c //digit 7
+ [LCTRL LALT K_8] > U+0038 c //digit 8
+ [LCTRL LALT K_9] > U+0039 c //digit 9

c Additional Digits

"1/" + "4"     	 > U+00bc c // 1/4
"1/" + "2"     	 > U+00bd c // 1/2
"3/" + "4"     	 > U+00be c // 3/4

c Open Punctuation Signs

"[" + "["       > U+230A c // Left Floor
"]" + "]"       > U+230B c // Right Floor

c Common Conjuncts

+ [RALT K_R] > U+0930 U+094D  c // ra virama र्
+ [LCTRL LALT K_R] > U+0930 U+094D c // ra virama र्
+ "R"     > U+094d U+0930   c ्/्/् ्+ र  =  ्र
any(ConsonantsU) + "R"  > index(ConsonantsU,1) U+094D U+0930  c consonants +   ्र
+ "W" > U+0926 U+094d U+0935   c // dwa द्व
+ "Y" > U+0930 U+094D U+200D c  // ra virama (joinya) र्‍
+"Q" > U+0924 U+094D U+0924 c // tta त्त

c ZWNJ & ZWJ

+ [RALT K_SLASH]  > U+200C c // zwnj
+ [LCTRL LALT K_SLASH]  > U+200C c // zwnj
+ [RALT SHIFT K_SLASH]  > U+200D  c // zwj
+ [LCTRL LALT SHIFT K_SLASH]  > U+200D  c // zwj
