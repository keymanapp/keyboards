c
c Khmer keyboard for Unicode
c
c Jan 2003
c changed from ABC font encoding to Unicode
c Nov 2003 
c ';' becomes 17d2   ',' becomes modifier key
c adding in (shift-)ctrl-alt: 17a3,a4,b4,b5,d3,d8,dc
c Apr 2004
c changing order of char-rotation on '/' so that '\' comes first. This is for Toolbox!
c April 2006
c adding : (U+00A3) and making a rotation for that key and some deletions of redundant code under "stuff - secondary transformations"                    

store(&VERSION) '10.0'
store(&NAME) 'Khmer (SIL)'
store(&BITMAP) 'sil_khmer.ico'
store(&COPYRIGHT) '(c) 2002 - 2019 SIL International'
store(&MESSAGE) "Created by D. Kanjahn for Unicode. Input method close to former ABC fonts."
c store(&HOTKEY) "^+U"
store(&TARGETS) 'web desktop'
store(&KEYBOARDVERSION) '1.5'
store(&VISUALKEYBOARD) 'sil_khmer.kvks'

begin Unicode> use(main)
               
c ---------------------------------------------definitions

c ----------
c consonants 1780-17a2
c Keys to be pressed
store(cons) "kxKXgcqCQjdzDZNtfTFnbpBPmyrlvSVshLG"
c consonant output
store (cons0) U+1780 U+1781 U+1782 U+1783 U+1784 U+1785 U+1786 U+1787 U+1788 U+1789 U+178A U+178B U+178C U+178D U+178E U+178F U+1790 U+1791 U+1792 U+1793 U+1794 U+1795 U+1796 U+1797 U+1798 U+1799 U+179A U+179B U+179C U+179D U+179E U+179F U+17A0 U+17A1 U+17A2
c leftovers U+17A3 U+17A4

c ------
c vowels 17b6-17c7 apart from C3 C4
store(vow) "aiIwWuUYROoeEAM:"
c output
store (vow0) U+17B6 U+17B7 U+17B8 U+17B9 U+17BA U+17BB U+17BC U+17BD U+17BE U+17BF U+17C0 U+17C1 U+17C2 U+17C5 U+17C6 U+17C7
c Vowels with 2nd variation C3 C4
store(vow1) U+17B6 U+17C1 U+17C4 U+17B5 U+17B4
store(vow2) U+17C4 U+17C3 U+17B5 U+17B4 U+17B6

c -----------------------------
c numbers 17e0-17e9
store(num) "0123456789"
store(num0) U+17E0 U+17E1 U+17E2 U+17E3 U+17E4 U+17E5 U+17E6 U+17E7 U+17E8 U+17E9
store(num1) U+17F0 U+17F1 U+17F2 U+17F3 U+17F4 U+17F5 U+17F6 U+17F7 U+17F8 U+17F9
c store(num1) "0123456789"
                              
c -----------------------------
c Independent vowels 17a5-17b3
store(ind) "}{|\"
store(ind0) U+17A5 U+17AA U+17AC U+17AE 
store(ind1) U+17AF U+17B1 U+17AB U+17AD
store(ind2) U+17B3 U+17B2 U+17A9 U+17B0

c -----------------------------
c diacritics 17c8-17dc except 17d2
store(dia) '"' "'`_+&.>@$< "
store(dia0) U+17C9 U+17CA U+17CB U+17CD U+17CE U+17D0 U+0020 U+17D4 U+17D7 U+17DB U+17DD U+200B
store(dia1) U+17D0 U+17CD U+17DD U+17D5 U+200B U+17D4 U+17CE U+002D U+17DA U+17DB
store(dia2) U+17CC U+17CF U+17D1 U+17DA U+200C U+17D5 U+002B U+005F U+17D4 U+0024
C leftovers: U+17D3 U+17D8 U+17DC
C in MS compatiblity: U+17D9 

c -----------------------------
c misc ()*%$!?.,<><<>>"[]\|}{-_=+~ quotation marks
c " " << >> ( )
store(misc0) U+17CA U+17C9
store(misc1) U+201C U+201D
store(misc2) U+00AB U+00BB
store(misc3) U+2018 U+2019                         
store(misc4) U+17CA U+17C9                         
store(miscA) "()/^"
store(miscA1) "()\;"                         
store(miscB) U+005B U+005D U+002E U+0027
store(miscC) U+007B U+007D U+002C U+0060
store(miscD) U+003C U+003E U+002F U+00B0
store(miscE) U+0028 U+0029 U+005C U+0040
c leftovers: ~

c -----------------------------
c Latin
store(latin) "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"


c -----------------------------
c Mooncalendar
C store(moon) U+19E0 U+19E1 U+19E2 U+19E3 U+19E4 U+19E5 U+19E6 U+19E7 U+19E8 U+19E9 U+19EA U+19EB U+19EC U+19ED U+19EE U+19EF U+19F0 U+19F1 U+19F2 U+19F3 U+19F4 U+19F5 U+19F6 U+19F7 U+19F8 U+19F9 U+19FA U+19FB U+19FC U+19FD U+19FE U+19FF 

c ----------------------------------------------- action

group(main) using keys

c coeng
+ ';' > U+17D2
C ZWNJ
+ [CTRL K_SPACE] > U+200C
c ZWJ
+ [SHIFT K_SPACE] > U+200D
c other spaces
+ [ALT K_SPACE] > U+200A
+ [SHIFT CTRL K_SPACE] > U+2009
+ [ALT CTRL K_SPACE] > U+2005
+ [SHIFT ALT CTRL K_SPACE] > U+2002

c H
+ 'H' > U+17D6    
+ [SHIFT K_J] > nul

c Primary characters                                             
+ any(cons) > index(cons0,1)
+ any(vow) > index(vow0,1)
any(vow1) + ',' > index(vow2,1)

c numbers
+ any(num) > index(num0,1)
any(num0) + ',' > index(num,1)
any(num) + ',' > index(num1,1)
any(num1) + ',' > index(num0,1)

c diacritics
+ any(dia) > index(dia0,1)
any(dia1) + ',' > index(dia2,1)

c misc
any(misc0) + ',' > index(misc1,1)
any(misc1) + ',' > index(misc2,1)
any(misc2) + ',' > index(misc3,1)
any(misc3) + ',' > index(misc4,1)
+ any(miscA) > index(miscA1,1)
any(miscA1) + ',' > index(miscB,1)
any(miscB) + ',' > index(miscC,1)
any(miscC) + ',' > index(miscD,1)
any(miscD) + ',' > index(miscA1,1)

c independent vowels
+ any(ind) > index(ind0,1)
any(ind0) + ',' > index(ind1,1)
any(ind1) + ',' > index(ind2,1)
any(ind2) + ',' > index(ind0,1)
U+17A7 + "'" > U+17A8

c : stuff - secondary transformations
U+17C7 + ',' > U+17C8
U+17C8 + ',' > U+17D6
U+17D6 + ',' > ':'
':' + ',' > U+17C7

c Some compounds
+ ']' > U+1781 U+17D2 U+1789 U+17BB U+17C6
+ '[' > U+17B2 U+17D2 U+1799
U+1781 U+17D2 U+1789 U+17BB U+17C6 + ',' > U+17A6
U+17B2 U+17D2 U+1799 + ',' > U+17A7

c Moon dates                               
U+17E0 U+17E8 U+17D4 + '>' > U+19E0
U+17E1        U+17D4 + '>' > U+19E1
U+17E2        U+17D4 + '>' > U+19E2
U+17E3        U+17D4 + '>' > U+19E3
U+17E4        U+17D4 + '>' > U+19E4
U+17E5        U+17D4 + '>' > U+19E5
U+17E6        U+17D4 + '>' > U+19E6
U+17E7        U+17D4 + '>' > U+19E7
U+17E8        U+17D4 + '>' > U+19E8
U+17E9        U+17D4 + '>' > U+19E9
U+17E1 U+17E0 U+17D4 + '>' > U+19EA
U+17E1 U+17E1 U+17D4 + '>' > U+19EB
U+17E1 U+17E2 U+17D4 + '>' > U+19EC
U+17E1 U+17E3 U+17D4 + '>' > U+19ED
U+17E1 U+17E4 U+17D4 + '>' > U+19EE
U+17E1 U+17E5 U+17D4 + '>' > U+19EF
U+17E8 U+17E8 U+17D4 + '>' > U+19F0
U+17D4 U+17D4        + '1' > U+19F1
U+17D4 U+17D4        + '2' > U+19F2
U+17D4 U+17D4        + '3' > U+19F3
U+17D4 U+17D4        + '4' > U+19F4
U+17D4 U+17D4        + '5' > U+19F5
U+17D4 U+17D4        + '6' > U+19F6
U+17D4 U+17D4        + '7' > U+19F7
U+17D4 U+17D4        + '8' > U+19F8
U+17D4 U+17D4        + '9' > U+19F9
U+17D4 U+17D4 U+17E1 + '1' > U+19FA
U+17D4 U+17D4 U+17E1 + '2' > U+19FB
U+17D4 U+17D4 U+17E1 + '3' > U+19FC
U+17D4 U+17D4 U+17E1 + '4' > U+19FD
U+17D4 U+17D4 U+17E1 + '5' > U+19FE
U+17D4 U+17D4 U+17E1 + '6' > U+19FF
                             
c ctrl-alt - MS compatible! but addition on 'DFGH'
+ [CTRL ALT K_BKQUOTE] > U+17D9
+ [CTRL ALT K_1] > '1'
+ [CTRL ALT K_2] > '2'
+ [CTRL ALT K_3] > '3'
+ [CTRL ALT K_4] > '4'
+ [CTRL ALT K_5] > '5'
+ [CTRL ALT K_6] > '6'
+ [CTRL ALT K_7] > '7'
+ [CTRL ALT K_8] > '8'
+ [CTRL ALT K_9] > '9'
+ [CTRL ALT K_0] > '0'
+ [CTRL ALT K_HYPHEN] > '{'
+ [CTRL ALT K_EQUAL] > '}'
+ [CTRL ALT K_Q] > '*'
+ [CTRL ALT K_E] > U+17AF
+ [CTRL ALT K_R] > U+17AB
+ [CTRL ALT K_T] > U+17A6
+ [CTRL ALT K_U] > U+17A7
+ [CTRL ALT K_I] > U+17A5
+ [CTRL ALT K_O] > U+17B1
+ [CTRL ALT K_P] > U+17B3
+ [CTRL ALT K_LBRKT] > '['
+ [CTRL ALT K_RBRKT] > ']'
+ [CTRL ALT K_BKSLASH] > U+17DA
+ [CTRL ALT K_A] > U+17A9
+ [CTRL ALT K_S] > U+17AA
+ [CTRL ALT K_D] > U+17A3
+ [CTRL ALT K_F] > U+17A4
+ [CTRL ALT K_G] > U+17B4
+ [CTRL ALT K_H] > U+17B5
+ [CTRL ALT K_J] > U+17AE
+ [CTRL ALT K_K] > U+17AD
+ [CTRL ALT K_L] > U+17B0
+ [CTRL ALT K_COLON] > ';'
+ [CTRL ALT K_QUOTE] > U+17DD
+ [CTRL ALT K_Z] > '#'
+ [CTRL ALT K_X] > '@'
+ [CTRL ALT K_C] > '&'
+ [CTRL ALT K_V] > '$'
+ [CTRL ALT K_B] > '%'
+ [CTRL ALT K_N] > '('
+ [CTRL ALT K_M] > ')'
+ [CTRL ALT K_COMMA] > U+2039
+ [CTRL ALT K_PERIOD] > U+203A
+ [CTRL ALT K_SLASH] > U+17D5


C SHIFT CTRL ALT  - MS compatible! but addition on 'NM,'
+ [SHIFT CTRL ALT K_BKQUOTE] > U+19E0
+ [SHIFT CTRL ALT K_1] > U+19E1
+ [SHIFT CTRL ALT K_2] > U+19E2
+ [SHIFT CTRL ALT K_3] > U+19E3
+ [SHIFT CTRL ALT K_4] > U+19E4
+ [SHIFT CTRL ALT K_5] > U+19E5
+ [SHIFT CTRL ALT K_6] > U+19E6
+ [SHIFT CTRL ALT K_7] > U+19E7
+ [SHIFT CTRL ALT K_8] > U+19E8
+ [SHIFT CTRL ALT K_9] > U+19E9
+ [SHIFT CTRL ALT K_0] > U+19EA
+ [SHIFT CTRL ALT K_HYPHEN] > U+19EB
+ [SHIFT CTRL ALT K_EQUAL] > U+19EC
+ [SHIFT CTRL ALT K_Q] > U+19ED
+ [SHIFT CTRL ALT K_W] > U+19EE
+ [SHIFT CTRL ALT K_E] > U+19EF
+ [SHIFT CTRL ALT K_R] > U+17F0
+ [SHIFT CTRL ALT K_T] > U+17F1
+ [SHIFT CTRL ALT K_Y] > U+17F2
+ [SHIFT CTRL ALT K_U] > U+17F3
+ [SHIFT CTRL ALT K_I] > U+17F4
+ [SHIFT CTRL ALT K_O] > U+17F5
+ [SHIFT CTRL ALT K_P] > U+17F6
+ [SHIFT CTRL ALT K_LBRKT] > U+17F7
+ [SHIFT CTRL ALT K_RBRKT] > U+17F8
+ [SHIFT CTRL ALT K_BKSLASH] > U+17F9
+ [SHIFT CTRL ALT K_A] > U+19F0
+ [SHIFT CTRL ALT K_S] > U+19F1
+ [SHIFT CTRL ALT K_D] > U+19F2
+ [SHIFT CTRL ALT K_F] > U+19F3
+ [SHIFT CTRL ALT K_G] > U+19F4
+ [SHIFT CTRL ALT K_H] > U+19F5
+ [SHIFT CTRL ALT K_J] > U+19F6
+ [SHIFT CTRL ALT K_K] > U+19F7
+ [SHIFT CTRL ALT K_L] > U+19F8
+ [SHIFT CTRL ALT K_COLON] > U+19F9
+ [SHIFT CTRL ALT K_QUOTE] > U+19FA
+ [SHIFT CTRL ALT K_Z] > U+19FB
+ [SHIFT CTRL ALT K_X] > U+19FC
+ [SHIFT CTRL ALT K_C] > U+19FD
+ [SHIFT CTRL ALT K_V] > U+19FE
+ [SHIFT CTRL ALT K_B] > U+19FF
+ [SHIFT CTRL ALT K_N] > U+17D3
+ [SHIFT CTRL ALT K_M] > U+17D8
+ [SHIFT CTRL ALT K_COMMA] > U+17DC
+ [SHIFT CTRL ALT K_PERIOD] > U+179D
+ [SHIFT CTRL ALT K_SLASH] > U+179E

c EOF
