store(&VERSION) '9.0'
store(&NAME) 'Bunong ឞូន៝ង (SIL)'
store(&TARGETS) 'any'
store(&COPYRIGHT) '© 2021 - 2023 SIL International'
store(&KEYBOARDVERSION) '1.5'
store(&BITMAP) 'sil_bunong.ico'
store(&VISUALKEYBOARD) 'sil_bunong.kvks'
store(&LAYOUTFILE) 'sil_bunong.keyman-touch-layout'
store(&MESSAGE) 'Bunong ឞូន៝ង (SIL) keyboard is designed for typing Bunong language using Khmer characters.'
store(&KMW_EMBEDCSS) 'sil_bunong.css'
begin Unicode > use(main)

group(main) using keys
+ [SHIFT K_A] > '@'
+ [SHIFT K_8] > '*'
+ [SHIFT K_4] > '៛'
+ [SHIFT K_COMMA] > '<'
+ [SHIFT K_QUOTE] > '"'
+ [SHIFT K_COLON] > 'ៈ'
+ [SHIFT K_HYPHEN] > '_'
+ [K_BKSLASH] > '\'
+ [K_EQUAL] > '='
+ [K_HYPHEN] > '-'
+ [SHIFT K_BKSLASH] > '|'
+ [SHIFT K_M] > nul
+ [SHIFT K_N] > nul
+ [SHIFT K_X] > nul
+ [SHIFT K_Z] > nul
+ [SHIFT K_D] > nul
+ [SHIFT K_F] > 'ឞ'
+ [SHIFT K_Y] > 'ួ'
+ [SHIFT K_L] > nul
+ [SHIFT K_RBRKT] > nul
+ [SHIFT K_P] > nul
+ [SHIFT K_7] > nul
+ [SHIFT K_6] > nul
+ [SHIFT K_R] > nul
+ [SHIFT K_Q] > nul
+ [K_Z] > ','
+ [K_SLASH] > '.'
+ [K_RBRKT] > '៖'
+ [SHIFT K_BKQUOTE] > '»'
+ [SHIFT K_EQUAL] > '+'
+ [SHIFT K_0] > ')'
+ [SHIFT K_9] > '('
+ [SHIFT K_5] > '%'
+ [SHIFT K_3] > '#'
+ [SHIFT K_2] > 'ៗ'
+ [SHIFT K_1] > '!'
+ [SHIFT K_PERIOD] > '>'
+ [SHIFT K_SLASH] > '?'
+ [SHIFT K_H] > nul
+ [SHIFT K_O] > 'ៅ'
+ [SHIFT K_S] > 'ៃ'
+ [SHIFT K_E] > 'ែ'
+ [SHIFT K_LBRKT] > 'ឿ'
+ [SHIFT K_U] > 'ូ'
+ [SHIFT K_W] > 'ឺ'
+ [SHIFT K_I] > 'ី'
+ [SHIFT K_G] > 'អ'
+ [SHIFT K_V] > nul
+ [SHIFT K_B] > 'ព'
+ [SHIFT K_T] > 'ទ'
+ [SHIFT K_J] > 'ញ'
+ [SHIFT K_C] > 'ជ'
+ [K_COMMA] > '៝'
+ [SHIFT K_K] > 'គ'
+ [K_PERIOD] > '។'
+ [K_M] > 'ម'
+ [K_N] > 'ន'
+ [K_B] > 'ប'
+ [K_V] > 'វ'
+ [K_C] > 'ច'
+ [K_X] > 'ខ'
+ [K_BKQUOTE] > '«'
+ [K_1] > '១'
+ [K_2] > '២'
+ [K_3] > '៣'
+ [K_4] > '៤'
+ [K_5] > '៥'
+ [K_6] > '៦'
+ [K_7] > '៧'
+ [K_8] > '៨'
+ [K_9] > '៩'
+ [K_0] > '០'
+ [K_LBRKT] > 'ៀ'
+ [K_P] > 'ផ'
+ [K_O] > 'ោ'
+ [K_I] > 'ិ'
+ [K_U] > 'ុ'
+ [K_Y] > 'យ'
+ [K_T] > 'ត'
+ [K_R] > 'រ'
+ [K_E] > 'េ'
+ [K_W] > 'ឹ'
+ [K_Q] > 'ឆ'
+ [K_A] > 'ា'
+ [K_S] > 'ស'
+ [K_D] > 'ដ'
+ [K_F] > 'ថ'
+ [K_G] > 'ង'
+ [K_H] > 'ហ'
+ [K_J] > '្'
+ [K_QUOTE] > '់'
+ [K_COLON] > 'ើ'
+ [K_L] > 'ល'
+ [K_K] > 'ក'
+ [K_SPACE] > U+200B

U+200B + [K_SPACE] > U+0020
U+0020 + [K_SPACE] > U+200B

+ [SHIFT K_SPACE] > U+0020
+ [SHIFT CTRL K_SPACE] > U+2009

c subscripts for phone layout
+ [T_17D2_1799] > U+17D2 U+1799
+ [T_17D2_179A] > U+17D2 U+179A
+ [T_17D2_179B] > U+17D2 U+179B
+ [T_17D2_179C] > U+17D2 U+179C
+ [T_17D2_17A0] > U+17D2 U+17A0
+ [T_17D2_179F] > U+17D2 U+179F


c Rotas

c ។ > ៕ > ! > ? > ៗ > ។
U+17D4 + [K_PERIOD]             > U+17D5        
U+17D5 + [K_PERIOD]             > U+0021
U+0021 + [K_PERIOD]             > U+003F
U+003F + [K_PERIOD]             > U+17D7
U+17D7 + [K_PERIOD]             > U+17D4        

c " > ' > "
U+0022 + [SHIFT K_QUOTE]        > U+0027        c press Shift + " twice to get a single quote
U+0027 + [SHIFT K_QUOTE]        > U+0022        c press Shift + " again to get a double quote

c « > ‹ > «  and » > › > » -- Mondolkiri font doesn't render these two pairs proportionately (April 6, 2023).
U+00AB + [K_BKQUOTE]            > U+2039
U+2039 + [K_BKQUOTE]            > U+00AB
U+00BB + [SHIFT K_BKQUOTE]      > U+203A
U+203A + [SHIFT K_BKQUOTE]      > U+00BB

c ់ >  ៈ
U+17CB + [K_QUOTE]              > U+17C8
U+17C8 + [K_QUOTE]              > U+17CB

c \ > / > | > \
U+005C + [K_BKSLASH]            > U+002F
U+002F + [K_BKSLASH]            > U+007C
U+007C + [K_BKSLASH]            > U+005C

c ( > [ > { > (
U+0028 + [SHIFT K_9]            > U+005B
U+005B + [SHIFT K_9]            > U+007B
U+007B + [SHIFT K_9]            > U+0028

c ) > ] > } > )
U+0029 + [SHIFT K_0]            > U+005D
U+005D + [SHIFT K_0]            > U+007D
U+007D + [SHIFT K_0]            > U+0029

c U+17B7 > U+17B8 & U+17B9 > U+17BA - above vowel rota
U+17B7 + [K_I]                  > U+17B8
U+17B8 + [K_I]                  > U+17B7

U+17B9 + [K_W]                  > U+17BA
U+17BA + [K_W]                  > U+17B9

c U+17BB > U+17BC, កុ > កូ > កួ > កុ
U+17BB + [K_U]                  > U+17BC
U+17BC + [K_U]                  > U+17BD
U+17BD + [K_U]                  > U+17BB

c U+17C1 > U+17C2 > U+17C3, កេ > កែ > កៃ > កេ
U+17C1 + [K_E]                  > U+17C2
U+17C2 + [K_E]                  > U+17C3
U+17C3 + [K_E]                  > U+17C1

c U+17C0 > U+17BF, កៀ > កឿ > កៀ
U+17C0 + [K_LBRKT]              > U+17BF
U+17BF + [K_LBRKT]              > U+17C0

c U+17C4 > U+17C5, កោ > កៅ > កោ
U+17C4 + [K_O]                  > U+17C5
U+17C5 + [K_O]                  > U+17C4

c  ៖ > :
U+17D6 + [K_RBRKT]              > U+003A
U+003A + [K_RBRKT]              > U+17D6

c - > —  
U+002D + [K_HYPHEN]              > U+2014
U+2014 + [K_HYPHEN]              > U+002D

c EoF
