c sil_akha_act generated from template at 2022-03-16 08:38:08
c with name "Akha ACT"
store(&VERSION) '10.0'
store(&NAME) 'Akha ACT'
store(&COPYRIGHT) '© SIL International'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'sil_akha_act.kvks'
store(&LAYOUTFILE) 'sil_akha_act.keyman-touch-layout'
store(&BITMAP) 'sil_akha_act.ico'
store(&CasedKeys) [K_A] .. [K_Z]
store(&KMW_EMBEDCSS) 'sil_akha_act.css'

begin Unicode > use(main)

group(main) using keys

store(symbolkeys) "`1234567890-=~!@#$%^&*()_+"
store(symbols) "`1234567890-=~!@#$%^&*()_+"
store(othersymbolkeys)  [K_COLON] [K_QUOTE] [K_COMMA] [K_PERIOD] [K_SLASH] \
                        [SHIFT K_COLON] [SHIFT K_QUOTE] [SHIFT K_COMMA] [SHIFT K_PERIOD] [SHIFT K_SLASH]
store(othersymbols) U+003B U+0027 U+002C U+002E U+002F \
                    U+003A U+0022 U+003C U+003E U+003F     
store(akhatonekeys) [K_LBRKT] [K_RBRKT] [K_BKSLASH] [SHIFT K_LBRKT] [SHIFT K_RBRKT] [SHIFT K_BKSLASH]
store(akhatones) U+02EC U+A788 U+02CD U+02C7 U+02C6 U+02C9
store(alphakeys) [K_A] .. [K_Z]
store(alphabet) 'a' .. 'z'
+ any(symbolkeys) > index(symbols,1)
+ any(othersymbolkeys) > index(othersymbols,1)
+ any(akhatonekeys) > index(akhatones,1)
+ any(alphakeys) > index(alphabet, 1)

c vowels with diacritics
store(vowdiackeys) [ALT K_LBRKT] [ALT K_RBRKT] [ALT K_PERIOD] [ALT K_COLON] [ALT K_QUOTE] [ALT K_SLASH] \
                   [SHIFT ALT K_LBRKT] [SHIFT ALT K_RBRKT] [SHIFT ALT K_PERIOD] [SHIFT ALT K_COLON] [SHIFT ALT K_QUOTE] [SHIFT ALT K_SLASH] 
store(vowdiacs) "ôóõòñöÔÓÕÒÑÖ"
+ any(vowdiackeys) > index(vowdiacs,1)

U+02EC + [K_LBRKT] > "[" 
U+02CD  + [K_BKSLASH] > "\"  
U+A788  + [K_RBRKT] > "]"  
U+02C7  + [SHIFT K_LBRKT] > "{"  
U+02C9  + [SHIFT K_BKSLASH] > "|" 
U+02C6  + [SHIFT K_RBRKT] > "}"  


c EOF
