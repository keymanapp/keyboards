c Cameroon QWERTY Keyboard Layout for the
c Standardized Alphabet for Cameroonian Languages
c 
c Version 1.2

store(&version) "9.0"
c store(&hotkey) "%+C"
c %  ????
c ^  ctrl
c + shift
store(&BITMAP) 'sil_cameroon_qwerty.ico'

store(&name) "Cameroon QWERTY"
c Specify the first group of rules to use in processing
store(&ETHNOLOGUECODE) 'abb add ael agq ags aku anv asj ass ato azo baf bag bas bav baw bax baz bbi bbj bbk bbq bbw bbx bby bce bcw bdn bdu beb bec bfd bfj bfm bfp bgf bgj bhs biw bju bkc bkh bkm bko bmo bmv bnz boe bqd bqm bqo bqt bqz bri bse bsi bss btb btc bum bvg bvm bwh bwt bxp bxs byb byv bzv ckx cug cuv dae dbq dek dii dkx dme dmm dmo dow dua dur duz ekm emn eto ewo fak fal fll fmp fub gid gis giz gke gmn gnd gou guv gyi hij hna hya isu iyo jbu jgo jia jim kdz ken kfl kfn kid kkj klc kme kmp knp kot kqx ksf kvj kvm kwu kzr lbi lem leo lfa lmp lmx lns luw maf mbo mcp mcs mct mcu mdd mea meq mfd mff mfh mfi mfj mfk mgg mgo mhk mif mij mkk mlb mlr mlw mmu mnf mpi mqb mru msv mtk muc mug muo muy mxf mxu myg mzd nbv ncp ncr ndb ndu nfu nge ngj ngn ngv ngy nhu njj njy nkc nla nmg nml nmr nnh nnx nnz nsh nwe nza obl oku ost ozm pbi pfe pmm pmn pny sgi sox swq szv tik toz tsh ttf tui twn udl uiv usk vut weh wes wok www xed xmb xmd xmg xmj yam yas yat yav ybb yei yko zhw ziz zuy'
store(&COPYRIGHT) '© 2003-2018 SIL Cameroon'
store(&KEYBOARDVERSION) '1.2'
store(&MESSAGE) 'This keyboard supports the Standardized Alphabet for Cameroonian Languages for a QWERTY (US Keyboard)'
store(&TARGETS) 'windows macosx desktop'
store(&VISUALKEYBOARD) 'sil_cameroon_qwerty.kvks'
store(&WINDOWSLANGUAGES) 'x0409'
store(&LANGUAGE) 'x0409'
begin unicode > use(First)

c base keys and outputs:  1='Cam' (; + key) , 2 = 'Spcl' (: + key)

store(Bku) 'ABCDEFGHIJKLMNOPQRSTUVWXYZ!@#$%^&*()+|<>?'
store(Scu1) U+0190 U+0181 U+00A9 U+018A U+018F U+00C6 U+0261\
U+2C6D U+0197 beep beep beep U+2122 U+014A U+0186 U+00D8 beep\
U+00AE beep U+0152 U+0244 beep U+1E84 beep U+01B3 beep beep beep \
beep U+20AC beep beep beep U+00D7 U+201C U+201D U+00B1 U+00A6 \
U+00AB U+00BB U+0294
store(Scu2) beep beep beep beep beep beep U+0294 beep beep beep beep beep \
beep beep beep beep beep beep beep beep beep beep beep beep beep beep \
beep beep beep beep beep beep beep beep beep U+201E beep beep beep beep \
beep

store(Bkl) 'abcdefghijklmnopqrstuvwxyz1234567890-=\/,.' "'"
store(Scl1) U+025B U+0253 beep U+0257 U+0259 U+00E6 U+02BC U+03B1 \
U+0268 beep beep beep beep U+014B U+0254 U+00F8 beep beep beep U+0153 \
U+0289 beep U+1E85 beep U+01B4 beep U+00BC U+00BD U+00BE beep beep beep \
beep beep U+2018 U+2019 U+002D beep beep U+00F7 U+2039 U+203A U+02C8
store(Scl2) U+0251 beep beep beep beep beep U+02C8 beep beep beep beep \
beep U+00B5 beep U+00B0 beep beep beep beep U+2020 beep beep beep beep beep \
beep beep beep beep beep beep beep beep beep beep beep beep beep beep \
beep beep beep beep

store(Bk) outs(Bku) outs(Bkl)
store(Sc1) outs(Scu1) outs(Scl1)
store(Sc2) outs(Scu2) outs(Scl2)

c tone keys and outputs
store(Tk) "'" '["{_`}'
store(TsM) U+02CB U+02CA U+02C6 U+02C7 U+02C9 U+A78C U+00A8
store(Ts) U+0300 U+0301 U+0302 U+030C U+0304 U+030D U+0308
store(Tc) U+1DC5 U+1DC7 U+1DC6 U+1DC4 beep beep beep
c Unicode 5.0:  store(Tc) U+1DC5 U+1DC7 U+1DC6 U+1DC4 beep beep beep

c nasal keys (tilde under)
store(Nku) '~`'
store(NuM) U+02CD U+02CD
store(Nu) U+0330 U+0330

c nasal keys 'Cam' (cedilla)
store(Nkc) ']'
store(NcM) U+00B8
store(Nc) U+0327

c nasal key 'Cam' (tilde over)
store(Nko) '~'
store(NoM) U+02DC
store(No) U+0303

c nasal characters grouped
store(Nk) outs(Nku) outs(Nkc) outs(Nko)
store(NM) outs(NuM) outs(NcM) outs(NoM)
store(N) outs(Nu) outs(Nc) outs(No)

c nasal characters plus dot under grouped
store(Ndk) outs(Nk) '.'
store(NdM) outs(NM) U+2024
store(Nd) outs(N) U+0323

c groups for modifier-letter versions of diacritics
store(Diacrit) outs(TsM) outs(N)
store(Mod) outs(TsM) outs(NM)

c ---------------- start processing rules---------------------
group(First) using keys

c access underlying diacritic, Cam, & Special keys (3 ways)
any(TsM) + any(Tk) > index(Tk,2)     c double-strike
any(NM) + any(Nk) > index(Nk,2)
';' + any(Tk)  > index(Tk,2)      c 'Cam' or 'Spec' + character
';' + any(Nk)  > index(Nk,2)

c assorted combinations
+ [RALT K_Space] > U+00A0
'1' '/' + '4' > U+00BC
'1' '/' + '2' > U+00BD
'3' '/' + '4' > U+00BE
'..' + '.' > U+2026
'-' + '-' > U+2013
'–' + '-' > U+2014
':' + '.' > U+2024

c nasal + tone + character
any(NdM) any(Ts) ';' + any(Bk) > index(Sc1,4) index(Nd,1) index(Ts,2)
any(NdM) any(Ts) ':' + any(Bk) > index(Sc2,4) index(Nd,1) index(Ts,2)
any(NdM) any(Ts) + any(Bk) > index(Bk,3) index(Nd,1) index(Ts,2)
any(NdM) any(Tc) ';' + any(Bk) > index(Sc1,4) index(Nd,1) index(Tc,2)
any(NdM) any(Tc) ':' + any(Bk) > index(Sc2,4) index(Nd,1) index(Tc,2)
any(NdM) any(Tc) + any(Bk) > index(Bk,3) index(Nd,1) index(Tc,2)

c nasal + tone  (This is for asthetics only, stacking diacritics before character has been entered.)
any(NdM) + any(Tk)> index(NdM,1) index(Ts,2)
any(NdM) ':' + any(Tk)> index(NdM,1) index(Tc,3)

c nasal + character
any(NdM) ';' + any(Bk) > index(Sc1,3) index(Nd,1)
any(NdM) ':' + any(Bk) > index(Sc2,3) index(Nd,1)
any(NdM) + any(Bk) > index(Bk,2) index(Nd,1)

c tone + character
any(TsM) ';' + any(Bk) > index(Sc1,3) index(Ts,1)
any(TsM) ':' + any(Bk) > index(Sc2,3) index(Ts,1)
any(TsM) + any(Bk) > index(Bk,2) index(Ts,1)
'_' any(Tc) ';' + any(Bk) > index(Sc1,4) index(Tc,2)
'_' any(Tc) ':' + any(Bk) > index(Sc2,4) index(Tc,2)
'_' any(Tc) + any(Bk) > index(Bk,3) index(Tc,2)

c set nasal
':' + any(Nku)  > index(NuM,2)
+ any(Nkc) > index(NcM,1)
+ any(Nko) > index(NoM,1)

c set tone
':' + any(Tk)  > '_' index(Tc,2)
+ any(Tk)  > index(TsM,1)

c output special characters
';' + any(Bk) > index(Sc1,2)
':' + any(Bk) > index(Sc2,2)
';' + ' ' > U+00A0 c non-breaking space

c end
