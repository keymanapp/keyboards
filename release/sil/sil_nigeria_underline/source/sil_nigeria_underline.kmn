c Nigeria Unicode Underline Nasal Keyboard
c modified Oct 2006 Nasality Added for Duya [ldb]
c modified Sept 2006 script a (literacy alpha) and g removed. No longer needed because of the Literacy fonts
c below modifications included in Nigeria Unicode Underline.kmn so this keyboard is archived
c modified Nov 2005 to provide underlined alpha and alphabetic turned comma
c created May 2005
c created by Heidi Rosendall, March 2004
c This keyboard is provided along with two other keyboards to covered the expected needs
c of Nigeria languages. The three keyboards are identical except that the treat vowels differently
c This is the keyboard for underlined vowels.

store(&VERSION) '9.0'
store(&NAME) 'Nigeria Underline (SIL)'
store(&BITMAP) 'sil_nigeria_underline.ico'

store(&COPYRIGHT) '2004-2018 SIL International'
store(&ETHNOLOGUECODE) 'ank bys iri kad kaj kcg kdl ldb lnu tiv'
store(&TARGETS) 'windows macosx web'
store(&VISUALKEYBOARD) 'sil_nigeria_underline.kvks'
store(&HOTKEY) '[CTRL ALT K_N]'
store(&KEYBOARDVERSION) '1.5'

begin Unicode		>	use(Main)

store(Let2BMod)     "bcdhkmnyzBCDGKLMNXY$?*"      
store(ModLet)       "ɓçɗɦƙɲŋƴʒƁÇƊɣƘ£ƝŊƎƳ₦ʔ°"      
store(vowel)	 	"aeiouAEIOU"                       
store(vAcute)       "áéíóúÁÉÍÓÚ"                      
store(vMid)         "āēīōūĀĒĪŌŪ"                      
store(vGrave)       "àèìòùÀÈÌÒÙ"                       
store(vCircum)      "âêîôûÂÊÎÔÛ"                      
store(vCaron)       "ǎěǐǒǔǍĚǏǑǓ"                      
store(vNasal)   	"ãẽĩõũÃẼĨÕŨ"                     
store(nasal)        "mnMN"                   
store(nAcute)       "ḿńḾŃ"                         
store(LetN)         "nN"                         
store(nGrave)       "ǹǸ"                            
store(nCaron)       "ňŇ"                          
store(nNasal)		"ñÑ" 


group(main) using keys      


c deadkeys are identified
c single deadkeys
+ ";"	> deadkey(modlet)     c applies to vowels and let2bmod
+ "'"   > deadkey(acute)
+ "-"   > deadkey(mid)
+ "`"   > deadkey(grave) 
+ "^"   > deadkey(circum)
+ "&"   > deadkey(caron) 
+ "!"   > deadkey(downstep) 
+ "~"	> deadkey(nasal)

c most single deadkeys are cleared when typed twice
U+A78C 			+ "'" 	> "'"      c just an acute accent when typed three times
dk(modlet)  	+ ";"  	> ";"      c just a semicolon
dk(mid)   		+ "-"   > "-" 
dk(grave) 		+ "`"   > "`" 
dk(circum)  	+ "^"   > "^" 
dk(caron)   	+ "&"   > "&"     
dk(downstep) 	+ "!"   > "!"
dk(nasal)		+ "~"	> "~"
         
c 1st set (;) combo deadkeys  for tones on underlined vowels (order does not matter) includes underlined nasal
dk(modlet)		+ "'"   >   deadkey(undlAcute)
dk(acute) 		+ ";"   >   deadkey(undlAcute)            
dk(modlet) 		+ "-"   >   deadkey(undlMid)
dk(mid) 		+ ";"   >   deadkey(undlMid)
dk(modlet) 		+ "`"   >   deadkey(undlGrave)
dk(grave) 		+ ";"   >   deadkey(undlGrave)
dk(modlet) 		+ "^"   >   deadkey(undlCircum)
dk(circum) 		+ ";"   >   deadkey(undlCircum)
dk(modlet) 		+ "&"   >   deadkey(undlCaron)
dk(caron) 		+ ";"   >   deadkey(undlCaron)     
dk(modlet) 		+ "!"   >   deadkey(undlDownstep)
dk(downstep) 	+ ";"   >   deadkey(undlDownstep)
dk(modlet) 		+ "~"   >   deadkey(undlNasal)
dk(nasal) 		+ ";"   >   deadkey(undlNasal)

c 2nd set (~) combo deadkeys for nasality and tone on regular vowels (nasal + tone is the only order)
dk(nasal) + "'"	>  deadkey(nasalAcute)
dk(nasal) + "-"	>  deadkey(nasalMid)
dk(nasal) + "`"	>  deadkey(nasalGrave)
dk(nasal) + "^"	>  deadkey(nasalCircum)
dk(nasal) + "&"	>  deadkey(nasalCaron)
dk(nasal) + "!"	>  deadkey(nasalDownstep)


c 3rd set combo deadkeys for nasality and tone on underlined vowels ( underline + nasality + tone is the only order )
dk(undlNasal) + "'"	>  deadkey(undlNasalAcute)
dk(undlNasal) + "-"	>  deadkey(undlNasalMid)
dk(undlNasal) + "`"	>  deadkey(undlNasalGrave)
dk(undlNasal) + "^"	>  deadkey(undlNasalCircum)
dk(undlNasal) + "&"	>  deadkey(undlNasalCaron)
dk(undlNasal) + "!"	>  deadkey(undlNasalDownstep)


c assignments
c plain letters are modified, vowels are underlined
dk(modlet) + any(Let2BMod) 	>  index(ModLet,2)     
dk(modlet) + any(vowel)     >  index(vowel,2) U+0331 
dk(acute)  + "'"   			> U+A78C   c saltillo with two acute accents
 

c plain vowels & nasals are tone marked or nasalized
dk(acute) 	+ any(vowel)	>   index(vAcute,2)    
dk(acute) 	+ any(nasal)    >   index(nAcute,2)       
dk(mid) 	+ any(vowel)    >   index(vMid,2)      
dk(mid) 	+ any(nasal)    >   index(nasal,2) U+0304  
dk(grave) 	+ any(vowel)    >   index(vGrave,2)   
dk(grave) 	+ any(LetN)     >   index(nGrave,2)   
dk(grave) 	+ "m"           >   "m" U+0300  
dk(grave) 	+ "M"           >   "M" U+0300     
dk(circum) 	+ any(vowel)    >   index(vCircum,2)    
dk(circum) 	+ any(nasal)    >   index(nasal,2) U+0302 
dk(caron) 	+ any(vowel)    >   index(vCaron,2)  
dk(caron) 	+ any(LetN)     >   index(nCaron,2)                             
dk(caron) 	+ "m"           >   "m" U+030C   
dk(caron) 	+ "M"           >   "M" U+030C  
dk(downstep) + any(vowel)   >   index(vowel,2) U+030B use(NFC)
dk(downstep) + any(nasal)   >   index(nasal,2) U+030B 
dk(downstep) + "m"          >   "m" U+030B
dk(downstep) + "N"          >   "N" U+030B             
dk(downstep) + "n"          >   "n" U+030B
dk(downstep) + "M"          >   "M" U+030B 
dk(nasal)	+ any(vowel)	> 	index(vNasal,2)
dk(nasal)	+ any(LetN)		> 	index(nNasal,2)
dk(nasal) 	+ "m"			> 	"m" U+0303
dk(nasal) 	+ "M"			> 	"M" U+0303
                                                                
c toned vowels are underlined
dk(undlAcute)  		+ any(vowel)  >    index(vAcute,2) U+0331
dk(undlMid)    		+ any(vowel)  >    index(vMid,2) U+0331
dk(undlGrave)  		+ any(vowel)  >    index(vGrave,2) U+0331
dk(undlCircum) 		+ any(vowel)  >    index(vCircum,2) U+0331
dk(undlCaron) 		+ any(vowel)  >    index(vCaron,2) U+0331       
dk(undlDownstep)	+ any(vowel)  >   index(vowel,2) U+0331 U+030B use(NFC)    

c tonemarked vowels are nasalized
dk(nasalAcute)		+ any(vowel)	> index(vNasal,2) U+0301 use(NFC)
dk(nasalMid)		+ any(vowel)	> index(vNasal,2) U+0304 use(NFC)
dk(nasalGrave)		+ any(vowel)	> index(vNasal,2) U+0300
dk(nasalCircum)		+ any(vowel)	> index(vNasal,2) U+0302
dk(nasalCaron)		+ any(vowel)	> index(vNasal,2) U+030C
dk(nasalDownstep)	+ any(vowel)	> index(vNasal,2) U+030B

c underlined vowels are nasalized
dk(undlNasal)		+ any(vowel)	> index(vNasal,2) U+0331

c Underlined tonemarked vowels are nasalized
dk(undlNasalAcute)		+ any(vowel)	>  index(vowel,2) U+0331 U+0303 U+0301 use(NFC)
dk(undlNasalMid)		+ any(vowel)	>  index(vowel,2) U+0331 U+0303 U+0304 use(NFC)
dk(undlNasalGrave)		+ any(vowel)	>  index(vowel,2) U+0331 U+0303 U+0300 use(NFC)
dk(undlNasalCircum)		+ any(vowel)	>  index(vowel,2) U+0331 U+0303 U+0302 use(NFC)
dk(undlNasalCaron)		+ any(vowel)	>  index(vowel,2) U+0331 U+0303 U+030C use(NFC)
dk(undlNasalDownstep)	+ any(vowel) 	>  index(vowel,2) U+0331 U+0303 U+030B use(NFC) 

c begin NFC group
c ***************************************************

group(NFC)

U+004F U+030B > U+0150 c O double acute
U+006F U+030B > U+0151 c o double acute
U+0055 U+030B > U+0170 c U double acute
U+0075 U+030B > U+0171 c u double acute

U+004F U+0331 U+030B > U+0150 U+0331 c O double acute underline
U+006F U+0331 U+030B > U+0151 U+0331 c o double acute underline
U+0055 U+0331 U+030B > U+0170 U+0331 c U double acute underline
U+0075 U+0331 U+030B > U+0171 U+0331 c u double acute underline

U+00F5 U+0301 > U+1E4D c o tilde acute
U+00D5 U+0301 > U+1E4C c O tilde acute
U+0169 U+0301 > U+1E79 c u tilde acute
U+0168 U+0301 > U+1E78 c U tilde acute

U+00F5 U+0304 > U+022D c o tilde macron
U+00D5 U+0304 > U+022C c O tilde macron

U+006F U+0331 U+0303 U+0301 > U+1E4D U+0331 c o tilde acute underline
U+004F U+0331 U+0303 U+0301 > U+1E4C U+0331 c O tilde acute underline
U+0075 U+0331 U+0303 U+0301 > U+1E79 U+0331 c u tilde acute underline
U+0055 U+0331 U+0303 U+0301 > U+1E78 U+0331 c U tilde acute underline

U+0041 U+0331 U+0303 U+0304 > U+00C3 U+0331 U+0304 c A tilde, underline, macron
U+0061 U+0331 U+0303 U+0304 > U+00E3 U+0331 U+0304 c a tilde, underline, macron
U+0045 U+0331 U+0303 U+0304 > U+1EBC U+0331 U+0304 c E tilde, underline, macron
U+0065 U+0331 U+0303 U+0304 > U+1EBD U+0331 U+0304 c e tilde, underline, macron
U+0049 U+0331 U+0303 U+0304 > U+0128 U+0331 U+0304 c I tilde, underline, macron
U+0069 U+0331 U+0303 U+0304 > U+0129 U+0331 U+0304 c i tilde, underline, macron
U+0055 U+0331 U+0303 U+0304 > U+0168 U+0331 U+0304 c U tilde, underline, macron
U+0075 U+0331 U+0303 U+0304 > U+0169 U+0331 U+0304 c u tilde, underline, macron
U+006F U+0331 U+0303 U+0304 > U+022D U+0331        c o tilde, macron, underline
U+004F U+0331 U+0303 U+0304 > U+022C U+0331        c O tilde, macron, underline

any(vowel) U+0331 U+0303 U+0300 > index(vNasal,1) U+0331 U+0300   
any(vowel) U+0331 U+0303 U+0301 > index(vNasal,1) U+0331 U+0301   
any(vowel) U+0331 U+0303 U+0302 > index(vNasal,1) U+0331 U+0302   
any(vowel) U+0331 U+0303 U+030C > index(vNasal,1) U+0331 U+030C   
any(vowel) U+0331 U+0303 U+030B > index(vNasal,1) U+0331 U+030B   


 
