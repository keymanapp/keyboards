c Keyboard for typing Chechen in Latin script using the 1992 alphabet
c 
c Website: https://vayfond.com/en
store(&VERSION) '15.0'
store(&NAME) 'Chechen Latin'
store(&KEYBOARDVERSION) '1.1'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'chechen_latin.kvks'
store(&LAYOUTFILE) 'chechen_latin.keyman-touch-layout'
store(&BITMAP) 'chechen_latin.ico'
store(&COPYRIGHT) '© Vayfond'

begin Unicode > use(main)
begin NewContext > use(NewContext)
begin PostKeystroke > use(PostKeystroke)

c This tells Keyman which keys should have casing behavior applied
store(&CasedKeys) [K_A] .. [K_Z]

c We'll define some useful stores here
store(key) [K_A] .. [K_Z] [SHIFT K_A] .. [SHIFT K_Z]
store(out) 'a' .. 'z'     'A' .. 'Z'

group(NewContext) readonly
    c Any time we get a new context, by mouse click, tap,
    c cursor movement, new document, etc, we'll try and
    c determine the best layer to use
    nomatch > use(detectStartOfSentence)

group(PostKeystroke) readonly
    c We get here after every keystroke and model action is processed

    c Okay, let's stay on the numeric layer if we are there already
    if(&newLayer = "") if(&layer = 'numerals') > context
    if(&newLayer = "") if(&layer = 'symbols') > context

    c Don't swap off the 'caps lock', 'rightalt-caps' and 'rightalt' layers automatically
    if(&layer = 'caps') > context
    if(&layer = 'rightalt-caps') > context
    if(&layer = 'rightalt') > context    

    c no other changes, so detect sentence or layer change, as long
    c as the user hasn't attempted to change layer themselves.
    if(&newLayer = "") > use(detectStartOfSentence)

group(detectStartOfSentence) readonly
    c We have a shared group for the start of sentence detection now
    c which will set the current layer according to the current context.

    c Some common end-of-sentence punctuation
    store(sentencePunctuation) '.?!'

    c If we are at the start of a text field, we're at the 'start of a
    c sentence', so we'll move to the Shift layer
    nul > layer('shift')

    c Otherwise, we'll move to the Shift layer only if we see end of a
    c sentence followed by one or two spaces.
    any(sentencePunctuation) ' ' > layer('shift')
    any(sentencePunctuation) '  ' > layer('shift')

    c Switch back automatically from 'rightalt-shift' to `rightalt` after keystroke
    if(&layer = 'rightalt-shift') > layer('rightalt')

    c And because we haven't see any of these situations above, we'll
    c move to the default layer. Note that this effectively drops us
    c back to the default layer after every keystroke that isn't blocked
    c by the `PostKeystroke` or early `detectStartOfSentence` rules.    
    nomatch > layer('default')

group(main) using keys
+ any(key) > index(out, 1)
+ [RALT K_EQUAL] > '"'
+ [RALT K_HYPHEN] > U+0027
+ [RALT K_0] > ':'
+ [RALT K_9] > ';'
+ [SHIFT RALT K_COLON] > 'Ç̇'
+ [RALT K_COLON] > 'ç̇'
+ [SHIFT K_QUOTE] > 'Ə'
+ [SHIFT K_COLON] > 'Ç'
+ [K_QUOTE] > 'ə'
+ [K_COLON] > 'ç'
+ [RALT K_Z] > 'ƶ'
+ [SHIFT RALT K_Z] > 'Ƶ'
+ [RALT K_X] > 'ẋ'
+ [SHIFT RALT K_X] > 'Ẋ'
+ [RALT K_U] > 'ü'
+ [SHIFT RALT K_U] > 'Ü'
+ [RALT K_T] > 'ṫ'
+ [SHIFT RALT K_T] > 'Ṫ'
+ [RALT K_S] > 'ş'
+ [SHIFT RALT K_S] > 'Ş'
+ [RALT K_Q] > 'q̇'
+ [SHIFT RALT K_Q] > 'Q̇'
+ [RALT K_P] > 'ṗ'
+ [SHIFT RALT K_P] > 'Ṗ'
+ [RALT K_O] > 'ö'
+ [SHIFT RALT K_O] > 'Ö'
+ [RALT K_N] > 'ŋ'
+ [SHIFT RALT K_N] > 'Ŋ'
+ [RALT K_K] > 'k̇'
+ [SHIFT RALT K_K] > 'K̇'
+ [RALT K_G] > 'ġ'
+ [SHIFT RALT K_G] > 'Ġ'
+ [RALT K_C] > 'ċ'
+ [SHIFT RALT K_C] > 'Ċ'
+ [SHIFT RALT K_A] > 'Ä'
+ [RALT K_A] > 'ä'
