store(&VERSION) '10.0'
c krung generated from template at 2019-03-01 13:05:36
c with name "Krung"
store(&NAME) 'Krung'
store(&COPYRIGHT) '© Keyman'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'krung.kvks'
store(&LAYOUTFILE) 'krung.keyman-touch-layout'
store(&BITMAP) 'krung.ico'

begin Unicode > use(main)

group(main) using keys

c K_F is used in place of /a/ in the presyllable where CC- got mixed up with the consonant cluster, ie​ ម្រ vs ម‍រ

+ [K_F]   > U+200D


c punctuations
store(punct_key)    [K_PERIOD] [SHIFT K_COLON] [SHIFT K_COMMA] [K_2] [SHIFT K_PERIOD] [K_COMMA]
store(punct_out)    U+17D4 U+17D6 U+003C U+002F U+003E U+0020
+ any(punct_key)    > index(punct_out,1)


U+002F + [K_2]      > U+17D7    c ៗ
U+003C + [SHIFT K_COMMA]    > U+00AB    c << > «
U+003E + [SHIFT K_PERIOD]   > U+00BB    c >> > »


c single orthographic consonant mappings
store(sin_con1_key)     [K_B] [K_C] [K_D] [K_H] [K_J] [K_K] [K_Q] [K_S] [K_T]           c bcdhjklnqst      
store(sin_con1_out)     U+1794 U+1785 U+178A U+17A0 U+1786 U+1780 U+17A2 U+179F U+178F  c បចដហឆកឡណអសត
+ any(sin_con1_key)     > index(sin_con1_out,1)

store(sin_con2_key)     [K_G] [K_M] [K_P] [K_R] [K_W] [K_Y] [K_L] [K_N]         c gmprwy
store(sin_con2_out)     U+179D U+1798 U+1796 U+179A U+179C U+1799 U+179B U+1793 c ឝមពរវយ 
+ any(sin_con2_key)     > index(sin_con2_out,1)


c CC onset with ្រ  ្ល  ្ម  ្ន  ្ង
any(sin_con1_out) + [K_R]       > context U+17D2 U+179A     c for ប្រ​ច្រ​ដ្រ​ឆ្រ​ក្រ​ស្រត្រ
any(sin_con2_out) + [K_R]       > context U+17D2 U+179A     c for ឝ្រព្រ
any(sin_con1_out) + [K_L]       > context U+17D2 U+179B     c for ប្ល​ក្ល
any(sin_con2_out) + [K_L]       > context U+17D2 U+179B     c for ព្ល​ឝ្ល
U+17A0 + [K_M]                  > context U+17D2 U+1798     c for ហ្ម
U+17A0 + [K_N]                  > context U+17D2 U+1793     c for ហ្ន
U+17A0 U+17D2 U+1793 + [K_G]    > context(1) U+17D2 U+1784  c for ហ្ង  
U+17A2 + [K_M]                  > context U+17D2 U+1798     c for អ្ម
U+17A2 + [K_N]                  > context U+17D2 U+1793     c for អ្ន
U+17A2 U+17D2 U+1793 + [K_G]    > context(1) U+17D2 U+1784  c for អ្ង
U+17A2 + [K_Y]                  > context(1) U+17D2 U+1799  c for អ្យ


c consonants with two key combination
U+1793 + [K_G]    > U+1784    c ng > ង
U+1793 + [K_H]    > U+1789    c nh > ញ
U+17A2 + [K_B]    > U+179E    c qb > ឞ
U+17A2 + [K_D]    > U+178B    c qd > ឋ
U+1780 + [K_H]    > U+1781    c kh > ខ
U+1796 + [K_H]    > U+1795    c ph > ផ
U+178F + [K_H]    > U+1790    c th > ថ

store(any_two_key_con) U+1784 U+1789 U+179E U+178B U+1781 U+1795 U+1790


c single orthographic final consonant mappings
store(sin_fin_con)      U+1785 U+1780 U+179B U+1798 U+1793 U+1784 U+1789 U+1794 U+1782 U+179A U+179F U+178F U+179C U+1799 c ចកលមនងញបគរសតវយ


c consonants to be modified by Triisap
store(con_triisap)      U+17A0 U+17A2 U+179E U+179F
store(con_bo)           U+1794
store(some_sin_con1)    U+1785 U+178A U+1786 U+1780 U+1781 U+17A1 U+178E U+1795 U+178B U+178F U+1790    
store(all_sin_con1)     outs(con_triisap) outs(some_sin_con1)


c consonant to be modified by Muusikatoan
store(con_muusikatoan)  U+179D U+1798 U+1784 U+1789 U+179A U+179C U+1799
store(some_sin_con2)    U+1787 U+1788 U+1782 U+1783 U+179B U+1793 U+1796 U+1797 U+178D U+1791 U+1792 U+178C
store(all_sin_con2)     outs(con_muusikatoan) outs(some_sin_con2)

store(any_con)  outs(all_sin_con1) outs(all_sin_con2) outs(con_bo)


c +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++MAIN SYLLABLE ONLY+++++++++++++++++++++++++++++++++++++++++++++++++++++++++

c +++++++++++++++++++++++++++++++SINGLE CONSONANT CHANGES SERIES IN HARMONY WITH THE SERIES OF THE SINGLE VOWEL.+++++++++++++++++++++++++++++++
c single orthographic vowel mappings, series 1
store(sin_vow1_key)     [K_O] [K_E] [K_X]       c keys: oex
store(sin_vow1_out)     U+17BB U+17B9 U+17B7    c U+17BB​ឹ​ិ​ in series 1
+ any(sin_vow1_key)     > U+17A2 index(sin_vow1_out,1)
any(all_sin_con1) + any(sin_vow1_key)   > context(1) index(sin_vow1_out,2)
any(con_bo) + any(sin_vow1_key)         > context(1) index(sin_vow1_out,2)


U+179D + any(sin_vow1_key)     > context(1) U+17C9 index(sin_vow1_out,2)
U+1798 + any(sin_vow1_key)     > context(1) U+17C9 index(sin_vow1_out,2)
U+179A + any(sin_vow1_key)     > context(1) U+17C9 index(sin_vow1_out,2)
U+179C + any(sin_vow1_key)     > context(1) U+17C9 index(sin_vow1_out,2)
U+1799 + any(sin_vow1_key)     > context(1) U+17C9 index(sin_vow1_out,2)
U+1784 + any(sin_vow1_key)     > context(1) U+17C9 index(sin_vow1_out,2)
U+1789 + any(sin_vow1_key)     > context(1) U+17C9 index(sin_vow1_out,2)

U+1796 + any(sin_vow1_key)     > U+1794 U+17C9 index(sin_vow1_out,2)
U+179B + any(sin_vow1_key)     > U+17A1 index(sin_vow1_out,2)
U+1793 + any(sin_vow1_key)     > U+178E index(sin_vow1_out,2)

U+1794 + any(sin_vow1_key)     > context(1) index(sin_vow1_out,2)


c single orthographic vowel mappings, series 2
store(sin_vow2_key)     [K_U] [K_V] [K_I]       c keys: uvi
store(sin_vow2_out)     U+17BB U+17B9 U+17B7    c ុ​ឹ​ិ in series 2
+ any(sin_vow2_key)     > U+17A2 U+17CA index(sin_vow2_out,1)
c U+1794 is a special character which can be modified by either U+17CA or U+17C9, thus it occurs in both con_muusikatoan and con_triisap.
U+1794 + any(sin_vow2_key) > context(1) U+17CA index(sin_vow2_out,2) c this rule has to be above the rule below for 'but' to output ប៊ុត instead of បុត.
any(all_sin_con2) + any(sin_vow2_key) > context(1) index(sin_vow2_out,2) c this rule has to be below the above.

U+17A0 + any(sin_vow2_key) > context(1) U+17CA index(sin_vow2_out,2)
U+17A2 + any(sin_vow2_key) > context(1) U+17CA index(sin_vow2_out,2)
U+179F + any(sin_vow2_key) > context(1) U+17CA index(sin_vow2_out,2)
U+179E + any(sin_vow2_key) > context(1) U+17CA index(sin_vow2_out,2)

U+1785 + any(sin_vow2_key) > U+1787 index(sin_vow2_out,2)
U+178A + any(sin_vow2_key) > U+178C index(sin_vow2_out,2)
U+1786 + any(sin_vow2_key) > U+1788 index(sin_vow2_out,2)
U+1780 + any(sin_vow2_key) > U+1782 index(sin_vow2_out,2)
U+178E + any(sin_vow2_key) > U+1793 index(sin_vow2_out,2)
U+178F + any(sin_vow2_key) > U+1791 index(sin_vow2_out,2)
U+178B + any(sin_vow2_key) > U+178D index(sin_vow2_out,2)
U+1781 + any(sin_vow2_key) > U+1783 index(sin_vow2_out,2)
U+1795 + any(sin_vow2_key) > U+1797 index(sin_vow2_out,2)
U+1790 + any(sin_vow2_key) > U+1792 index(sin_vow2_out,2)


c inherent vowel and ា treatments (1) without consonant key fired.
+ [K_A]                                     > U+17A2 U+17B6 U+1782 U+17CB       c a > អាគ់
U+1796 + [K_A]                              > U+1794 U+17C9 U+17B6 U+1782 U+17CB c pa
U+179B + [K_A]                              > U+17A1 U+17B6 U+1782 U+17CB       c la
U+17B6 U+1793 U+17CB + [K_A]                > U+178E U+17B6 U+1782 U+17CB       c danay > ដណៃ
U+1793 + [K_A]                              > U+178E U+17B6 U+1782 U+17CB       c na
U+1794 U+17C9 U+17B6 U+1782 U+17CB + [K_A]  > context(1) context(2) context(3)  c paa
U+17A1 U+17B6 U+1782 U+17CB + [K_A]         > context(1) context(2)             c laa
U+178E U+17B6 U+1782 U+17CB + [K_A]         > context(1) context(2)             c naa
any(con_muusikatoan) + [K_A]                > context U+17C9 U+17B6 U+1782 U+17CB
any(con_muusikatoan) U+17C9 U+17B6 U+1782 U+17CB + [K_A]    > context(1) U+17C9 U+17B6
any(all_sin_con1) + [K_A]                                   > context(1) U+17B6 U+1782 U+17CB   c any C1 a > any C1 ាគ់
any(all_sin_con1) U+17B6 U+1782 U+17CB + [K_A]              > context(1) U+17B6                 c any C1 aa > any C1 ា 
any(con_bo) + [K_A]                                   > context(1) U+17B6 U+1782 U+17CB   c ba > បាគ់
any(con_bo) U+17B6 U+1782 U+17CB + [K_A]              > context(1) U+17B6                 c baa > បា 


+ [K_Z]                       > U+17A2 U+1782 U+17CB        c z > អគ់
U+1796 + [K_Z]                > U+1794 U+17C9 U+1782 U+17CB c pz
U+179B + [K_Z]                > U+17A1 U+1782 U+17CB        c lz
U+1793 + [K_Z]                > U+178E U+1782 U+17CB        c nz
U+1794 U+17C9 U+1782 U+17CB + [K_Z] > context(1) context(2) dk(deadkey_z)   c pzz
U+17A1 U+1782 U+17CB + [K_Z]        > context(1) dk(deadkey_z)              c lzz
U+17B6 U+1793 U+17CB + [K_Z]        > U+178E U+1782 U+17CB                  c danz > ដណគ់
U+178E U+1782 U+17CB + [K_Z]        > context(1) dk(deadkey_z)              c nzz
any(all_sin_con1) + [K_Z]           > context(1) U+1782 U+17CB              c any C1 z > any C1 គ់

c zzp > -ប
U+1782 U+17CB + [K_Z]               > dk(deadkey_z)
dk(deadkey_z) + [K_P]               > U+1794

any(con_bo) + [K_Z]                                 > context(1) U+1782 U+17CB  c bz > បគ់
any(con_bo) U+1782 U+17CB + [K_Z]                   > context(1) dk(deadkey_z)  c bzz > ប 
any(con_muusikatoan) + [K_Z]                        > context U+17C9 U+1782 U+17CB
any(con_muusikatoan) U+17C9 U+1782 U+17CB + [K_Z]   > context(1) U+17C9 dk(deadkey_z)

U+17A2 U+17B6 U+1782 U+17CB + [K_A] > U+17A2 U+17B6 c aa > អា
U+17A2 U+1782 U+17CB + [K_Z]        > U+17A2 dk(deadkey_z)       c zz > អ


c +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ START PRESYLLABLES +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


c Presyllables: CV, CCV, h , q + nasal

U+17B6 any(all_sin_con1) U+17CB + any(sin_vow1_key)     > context(2) index(sin_vow1_out,4)
U+17B6 any(all_sin_con1) U+17CB + [K_A]                 > context(2) U+17B6 U+1782 U+17CB
U+17B6 any(all_sin_con1) U+17CB + [K_Z]                 > context(2) U+1782 U+17CB
U+17B6 any(con_bo) U+17CB + any(sin_vow1_key)           > context(2) index(sin_vow1_out,4)
U+17B6 any(con_bo) U+17CB + [K_A]                       > context(2) U+17B6 U+1782 U+17CB
U+17B6 any(con_bo) U+17CB + [K_Z]                       > context(2) U+1782 U+17CB


U+17B6 any(all_sin_con2) U+17CB + any(sin_vow2_key)     > context(2) index(sin_vow2_out,4)
U+17B6 any(all_sin_con2) U+17CB + [K_A]                 > context(2) U+17B6 U+1782 U+17CB
U+17B6 any(all_sin_con2) U+17CB + [K_Z]                 > context(2) U+1782 U+17CB

c cont. from here


c VV combo, series 1
U+17B7 + [K_E]                      > U+17C0                        c ie > ៀ
U+17C0 + [K_H]                      > context(1) U+17C7             c ieh> ៀះ
U+17B9 + [K_E]                      > U+17BE                        c ee > ើ
U+17A2 U+17B6 U+1782 U+17CB + [K_Y] > context(1) U+17C3             c ay > អៃ
U+17B6 U+1782 U+17CB + [K_Y]        > U+17C3                        c ay > ៃ  
U+17B9 + [K_Y]                      > U+17B8                        c vy > ី 
U+17A2 U+1782 U+17CB + [K_Q]        > U+17A2 U+1782 U+17CB          c zq > អគ់់
U+17A2 U+17B6 U+1782 U+17CB + [K_Q] > U+17A2 U+17B6 U+1782 U+17CB   c aq > អាគ់
U+1782 U+17CB + [K_W]               > U+17BC U+179C                 c zw > ូវ
U+17B7 + [K_H]                      > U+17B7 U+17C7                 c xh > ិះ
U+17B9 + [K_H]                      > U+17B9 U+17C7                 c eh > ឹះ
U+17B7 + [K_Q]                      > U+17B7                        c xq > ិ
U+17B9 + [K_Q]                      > U+17B9                        c eq > ឹ
U+17BB + [K_O]                      > U+17BC                        c oo > ូ 

any(con_triisap) U+17B7 + [K_X]     > context(1) U+17CA U+17C2
any(con_bo) U+17B7 + [K_X]          > context(1) U+17CA U+17C2      c bx > ប៊ិ


c VV combo series 1 with final q
U+17C0 + [K_Q]          > context U+1782    c ieq > ៀគ
U+17BE + [K_Q]          > context U+1782    c eeq > ើគ
U+17BC + [K_Q]          > context U+1782    c ooq > ូគ
U+17C3 + [K_Q]          > context U+1782    c ay > ៃគ


c VV combo, series 2
U+17B7 + [K_X]          > U+17C2            c xx > ែ
U+17B7 + [K_I]          > U+17B8            c ii > ី
U+17B9 + [K_V]          > U+17BA            c vv > ឺ
U+17BA + [K_H]          > U+17BA U+17C7     c vvh > ឺះ
U+17B8 + [K_H]          > U+17B8 U+17C7     c iih > ីះ
U+17BB + [K_H]          > U+17BB U+17C7     c uh > ុះ
U+17BD + [K_H]          > U+17BD U+17C7     c ueh > ួះ
U+17BB + [K_E]          > U+17BD            c ue > ួ
U+17BB + [K_U]          > U+17BC            c uu > ូ


any(sin_con1_out) U+17CA U+17BB + [K_E]     > context(1) U+17BD     c buer > បួរ not ប៊ួរ 
any(con_muusikatoan) U+17C9 U+17B7 + [K_X]  > context(1) U+17C2
any(con_bo) U+17C9 U+17B7 + [K_X]           > U+1796 U+17C2         c pxx > ពែ


c any vowel
store(any_vowel)    outs(sin_vow1_out) outs(sin_vow2_out) U+17C0 U+17BE U+17C3 U+17BC U+17C2 U+17B8 U+17BA U+17BD U+17BC U+17B6 


c change series of single consonants in harmony with the series of the vowel
c first-series consonant + second-series vowel
U+1794 U+17B7 + [K_X]   > U+1796 U+17C2     c pxx > ពែ
U+17A1 U+17B7 + [K_X]   > U+179B U+17C2     c lxx > លែ

U+1785 U+17B7 + [K_X]   > U+1787 U+17C2     c cxx > ជែ
U+178A U+17B7 + [K_X]   > U+178C U+17C2     c dxx > ឌែ
U+1786 U+17B7 + [K_X]   > U+1788 U+17C2     c jxx > ឈែ
U+1780 U+17B7 + [K_X]   > U+1782 U+17C2     c kxx > គែ
U+178E U+17B7 + [K_X]   > U+1793 U+17C2     c nxx > នែ
U+178F U+17B7 + [K_X]   > U+1791 U+17C2     c txx > ទែ
U+178B U+17B7 + [K_X]   > U+178D U+17C2     c qdxx > ឍែ
U+1781 U+17B7 + [K_X]   > U+1783 U+17C2     c khxx > ឃែ
U+1795 U+17B7 + [K_X]   > U+1797 U+17C2     c phxx > ភែ
U+1790 U+17B7 + [K_X]   > U+1792 U+17C2     c thxx > ធែ

c VV combo series 2 with final q
U+17C2 + [K_Q]          > context U+1782    c xxq > ែគ
U+17B8 + [K_Q]          > context U+1782    c iiq > ីគ
U+17BA + [K_Q]          > context U+1782    c vvq > ឺគ
U+17BD + [K_Q]          > context U+1782    c ueq > ួគ
U+17BC + [K_Q]          > context U+1782    c uuq > ូគ




c second-series consonant + first-series vowel "ue" and "ie" > first-series consonant + the first-series vowel
U+1787 U+17BB + [K_E]     > U+1785 U+17BD    c cue > ចួ        
U+178C U+17BB + [K_E]     > U+178A U+17BD    c due > ដួ
U+1788 U+17BB + [K_E]     > U+1786 U+17BD    c jue > ឆួ
U+1782 U+17BB + [K_E]     > U+1780 U+17BD    c kue > កួ
U+1791 U+17BB + [K_E]     > U+178F U+17BD    c tue > តួ
U+178D U+17BB + [K_E]     > U+178B U+17BD    c qdue > ឋួ
U+1783 U+17BB + [K_E]     > U+1781 U+17BD    c khue > ខួ
U+1797 U+17BB + [K_E]     > U+1795 U+17BD    c phue > ផួ

U+1787 U+17B7 + [K_E]     > U+1785 U+17C0    c cie > ចៀ        
U+178C U+17B7 + [K_E]     > U+178A U+17C0    c die > ដៀ
U+1788 U+17B7 + [K_E]     > U+1786 U+17C0    c jie > ឆៀ
U+1782 U+17B7 + [K_E]     > U+1780 U+17C0    c kie > កៀ
U+1791 U+17B7 + [K_E]     > U+178F U+17C0    c tie > តៀ
U+178D U+17B7 + [K_E]     > U+178B U+17C0    c qdie > ឋៀ
U+1783 U+17B7 + [K_E]     > U+1781 U+17C0    c khie > ខៀ
U+1797 U+17B7 + [K_E]     > U+1795 U+17C0    c phie > ផៀ

c second-series consonant + first-series vowel "vy" > first-series consonant + the first-series vowel
U+1787 U+17B9 + [K_Y]     > U+1785 U+17B8    c cvy > ចី        
U+178C U+17B9 + [K_Y]     > U+178A U+17B8    c dvy > ដី
U+1788 U+17B9 + [K_Y]     > U+1786 U+17B8    c jvy > ឆី
U+1782 U+17B9 + [K_Y]     > U+1780 U+17B8    c kvy > កី
U+1791 U+17B9 + [K_Y]     > U+178F U+17B8    c tvy > តី
U+178D U+17B9 + [K_Y]     > U+178B U+17B8    c qdvy > ឋី
U+1783 U+17B9 + [K_Y]     > U+1781 U+17B8    c khvy > ខី
U+1797 U+17B9 + [K_Y]     > U+1795 U+17B8    c phvy > ផី


c +++++++++++++++++++++++++++++++++++CC CLUSTER CHANGES SERIES IN HARMONY WITH THE SERIES OF THE VOWEL+++++++++++++++++++++++++++++++++++

any(all_sin_con1) U+17D2 U+179A + [K_Z]                 > context(1) context(2) context(3) U+1782 U+17CB 
any(all_sin_con1) U+17D2 U+179A U+1782 U+17CB + [K_Z]   > context(1) context(2) context(3)  dk(deadkey_z)
any(con_bo) U+17D2 U+179A + [K_Z]                       > context(1) context(2) context(3) U+1782 U+17CB 
any(con_bo) U+17D2 U+179A U+1782 U+17CB + [K_Z]         > context(1) context(2) context(3)  dk(deadkey_z)


c bl
U+1794 U+17D2 U+179B + any(sin_vow2_key)    > context(1) context(2) context(3) U+17CA index(sin_vow2_out,4)
U+1794 U+17D2 U+179B + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)

U+17CA U+17BB + [K_E]                > U+17BD                c bu + e > បួ
U+17CA U+17B7 + [K_E]                > U+17C0                c bi + e > បៀ
U+17B7 + [K_X]                       > U+17CA U+17C2         c bli + x > ប្ល៊ែ
U+1782 U+17CB + [K_Q]                > context(1) context(2) c ប្លគ់ + q <> ប្លអ់


c br
U+1794 U+17D2 U+179A + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+1794 U+17D2 U+179A + any(sin_vow2_key)    > context(1) context(2) context(3) U+17CA index(sin_vow2_out,4)
U+1794 U+17D2 U+179A U+17B7 + [K_X]         > context(1) context(2) context(3) U+17CA U+17C2 


c cr
U+1785 U+17D2 U+179A + any(sin_vow2_key)    > U+1787 context(2) context(3) index(sin_vow2_out,4)
U+1785 U+17D2 U+179A + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+1787 U+17D2 U+179A U+17B7 + [K_E]         > U+1785 context(2) context(3) U+17C0 c ជ្រិគ > ច្រៀ
U+1787 U+17D2 U+179A U+17BB + [K_E]         > U+1785 context(2) context(3) U+17BD c ជ្រុគ > ច្រួ
U+1785 U+17D2 U+179A U+17B7 + [K_X]         > U+1787 context(2) context(3) U+17C2


c dr
U+178A U+17D2 U+179A + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+178A U+17D2 U+179A + any(sin_vow2_key)    > U+178C context(2) context(3) index(sin_vow2_out,4) 
U+178A U+17D2 U+179A U+17B7 + [K_X]         > U+178C context(2) context(3) U+17C2   c drxx > ឌ្រែ
U+178C U+17D2 U+179A U+17B7 + [K_E]         > U+178A context(2) context(3) U+17C0   c drie > ដ្រៀ
U+178C U+17D2 U+179A U+17BB + [K_E]         > U+178A context(2) context(3) U+17BD   c drue > ដ្រួ


c gl (CLEVERRABBIT DOESN'T DISPLAY 179D CORRECTLY ON THE SCREEN IN MACOS, AND THAT IS HARD TO VERIFY THE OUTPUT.)
U+179D U+17D2 U+179B + any(sin_vow1_key)    > context(1) context(2) context(3) U+17C9 index(sin_vow1_out,4)
U+179D U+17D2 U+179B + any(sin_vow2_key)    > context(1) context(2) context(3) index(sin_vow2_out,4) 
U+179D U+17D2 U+179B U+17BB + [K_E]         > context(1) context(2) context(3) U+17C9 U+17BD   c glue
U+179D U+17D2 U+179B U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C9 U+17C0   c glie
U+179D U+17D2 U+179B U+17C9 U+17B7 + [K_X]  > context(1) context(2) context(3) U+17C2    c glxx

U+179D U+17D2 U+179B + [K_Z]                        > context(1) context(2) context(3) U+17C9 U+1782 U+17CB    c glz
U+179D U+17D2 U+179B U+17C9 U+1782 U+17CB + [K_H]   > context(1) context(2) context(3) U+17C9 U+17C4 U+17C7    c glzh


c gr
U+179D U+17D2 U+179A + any(sin_vow1_key)    > context(1) context(2) context(3) U+17C9 index(sin_vow1_out,4)
U+179D U+17D2 U+179A + any(sin_vow2_key)    > context(1) context(2) context(3) index(sin_vow2_out,4) 
U+179D U+17D2 U+179A U+17BB + [K_E]         > context(1) context(2) context(3) U+17C9 U+17BD   c grue
U+179D U+17D2 U+179A U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C9 U+17C0   c grie
U+179D U+17D2 U+179A U+17C9 U+17B7 + [K_X]  > context(1) context(2) context(3) U+17C2    c grxx


c hm
U+17A0 U+17D2 U+1798 + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+17A0 U+17D2 U+1798 + any(sin_vow2_key)    > context(1) context(2) context(3) U+17CA index(sin_vow2_out,4) 
U+17A0 U+17D2 U+1798 U+17B7 + [K_X]         > context(1) context(2) context(3) U+17C2   c hmxx
U+17A0 U+17D2 U+1798 U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C0   c hmie
U+17A0 U+17D2 U+1798 U+17BB + [K_E]         > context(1) context(2) context(3) U+17BD   c hmue
U+17A0 U+17D2 U+1798 + [K_Z]                > context(1) context(2) context(3) U+1782 U+17CB   c hmz
U+17A0 U+17D2 U+1798 U+1782 U+17CB + [K_Z]  > context(1) context(2) context(3) dk(deadkey_z)   c hmzz


c hn
U+17A0 U+17D2 U+1793 + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+17A0 U+17D2 U+1793 + any(sin_vow2_key)    > context(1) context(2) context(3) U+17CA index(sin_vow2_out,4) 
U+17A0 U+17D2 U+1793 U+17B7 + [K_X]         > context(1) context(2) context(3) U+17C2   c hnxx
U+17A0 U+17D2 U+1793 U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C0   c hnie
U+17A0 U+17D2 U+1793 U+17BB + [K_E]         > context(1) context(2) context(3) U+17BD   c hnue
U+17A0 U+17D2 U+1793 + [K_Z]                > context(1) context(2) context(3) U+1782 U+17CB   c hnz
U+17A0 U+17D2 U+1793 U+1782 U+17CB + [K_Z]  > context(1) context(2) context(3) dk(deadkey_z)   c hnzz


c hng
U+17A0 U+17D2 U+1784 + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+17A0 U+17D2 U+1784 + any(sin_vow2_key)    > context(1) context(2) context(3) U+17CA index(sin_vow2_out,4) 
U+17A0 U+17D2 U+1784 U+17B7 + [K_X]         > context(1) context(2) context(3) U+17C2   c hngxx
U+17A0 U+17D2 U+1784 U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C0   c hngie
U+17A0 U+17D2 U+1784 U+17BB + [K_E]         > context(1) context(2) context(3) U+17BD   c hngue
U+17A0 U+17D2 U+1784 + [K_Z]                > context(1) context(2) context(3) U+1782 U+17CB   c hngz
U+17A0 U+17D2 U+1784 U+1782 U+17CB + [K_Z]  > context(1) context(2) context(3) dk(deadkey_z)   c hngzz


c jr
U+1786 U+17D2 U+179A + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+1786 U+17D2 U+179A + any(sin_vow2_key)    > U+1788 context(2) context(3) index(sin_vow2_out,4) 
U+1788 U+17D2 U+179A U+17BB + [K_E]         > U+1786 context(2) context(3) U+17BD   c jrue
U+1788 U+17D2 U+179A U+17B7 + [K_E]         > U+1786 context(2) context(3) U+17C0   c jrie
U+1786 U+17D2 U+179A U+17B7 + [K_X]         > U+1788 context(2) context(3) U+17C2   c jrxx


c kl
U+1780 U+17D2 U+179B + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+1780 U+17D2 U+179B + any(sin_vow2_key)    > U+1782 context(2) context(3) index(sin_vow2_out,4) 
U+1782 U+17D2 U+179B U+17BB + [K_E]         > U+1780 context(2) context(3) U+17BD   c klue
U+1782 U+17D2 U+179B U+17B7 + [K_E]         > U+1780 context(2) context(3) U+17C0   c klie
U+1780 U+17D2 U+179B U+17B7 + [K_X]         > U+1782 context(2) context(3) U+17C2   c klxx
U+1780 U+17D2 U+179B + [K_Z]                > context(1) context(2) context(3) U+1782 U+17CB   c klz
U+1780 U+17D2 U+179B U+1782 U+17CB + [K_Z]  > context(1) context(2) context(3) dk(deadkey_z)   c klzz


c kr
U+1780 U+17D2 U+179A + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+1780 U+17D2 U+179A + any(sin_vow2_key)    > U+1782 context(2) context(3) index(sin_vow2_out,4) 
U+1782 U+17D2 U+179A U+17BB + [K_E]         > U+1780 context(2) context(3) U+17BD   c krue
U+1782 U+17D2 U+179A U+17B7 + [K_E]         > U+1780 context(2) context(3) U+17C0   c krie
U+1780 U+17D2 U+179A U+17B7 + [K_X]         > U+1782 context(2) context(3) U+17C2   c krxx
U+1780 U+17D2 U+179A + [K_Z]                > context(1) context(2) context(3) U+1782 U+17CB   c krz
U+1780 U+17D2 U+179A U+1782 U+17CB + [K_Z]  > context(1) context(2) context(3) dk(deadkey_z)   c krzz


c pl
U+1796 U+17D2 U+179B + any(sin_vow1_key)    > U+1794 context(2) context(3) U+17C9 index(sin_vow1_out,4)
U+1796 U+17D2 U+179B + any(sin_vow2_key)    > context(1) context(2) context(3) index(sin_vow2_out,4) 
U+1796 U+17D2 U+179B U+17BB + [K_E]         > context(1) context(2) context(3) U+17BD   c plue
U+1796 U+17D2 U+179B U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C0   c plie
U+1794 U+17D2 U+179B U+17C9 U+17B7 + [K_X]  > U+1796 context(2) context(3) U+17C2   c plxx
U+1796 U+17D2 U+179B + [K_Z]                > U+1794 context(2) context(3) U+17C9 U+1782 U+17CB   c plz
U+1794 U+17D2 U+179B U+17C9 U+1782 U+17CB + [K_Z]           > context(1) context(2) context(3) context(4) dk(deadkey_z)  c plzz
U+1796 U+17D2 U+179B + [K_A]                                > U+1794 context(2) context(3) U+17C9 U+17B6 U+1782 U+17CB   c pla
U+1794 U+17D2 U+179B U+17C9 U+17B6 U+1782 U+17CB + [K_A]    > context(1) context(2) context(3) context(4) context(5)     c plaa


c pr
U+1796 U+17D2 U+179A + any(sin_vow1_key)    > U+1794 context(2) context(3) U+17C9 index(sin_vow1_out,4)
U+1796 U+17D2 U+179A + any(sin_vow2_key)    > context(1) context(2) context(3) index(sin_vow2_out,4) 
U+1796 U+17D2 U+179A U+17BB + [K_E]         > context(1) context(2) context(3) U+17BD   c prue
U+1796 U+17D2 U+179A U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C0   c prie
U+1794 U+17D2 U+179A U+17C9 U+17B7 + [K_X]  > U+1796 context(2) context(3) U+17C2   c prxx
U+1796 U+17D2 U+179A + [K_Z]                > U+1794 context(2) context(3) U+17C9 U+1782 U+17CB   c prz
U+1794 U+17D2 U+179A U+17C9 U+1782 U+17CB + [K_Z]           > context(1) context(2) context(3) context(4) dk(deadkey_z)  c przz
U+1796 U+17D2 U+179A + [K_A]                                > U+1794 context(2) context(3) U+17C9 U+17B6 U+1782 U+17CB   c pra
U+1794 U+17D2 U+179A U+17C9 U+17B6 U+1782 U+17CB + [K_A]    > context(1) context(2) context(3) context(4) context(5)     c praa


c sr
U+179F U+17D2 U+179A + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+179F U+17D2 U+179A + any(sin_vow2_key)    > context(1) context(2) context(3) U+17CA index(sin_vow2_out,4) 
U+179F U+17D2 U+179A U+17BB + [K_E]         > context(1) context(2) context(3) U+17BD   c srue
U+179F U+17D2 U+179A U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C0   c srie
U+179F U+17D2 U+179A U+17B7 + [K_X]         > context(1) context(2) context(3) U+17CA U+17C2   c srxx
U+179F U+17D2 U+179A + [K_Z]                > context(1) context(2) context(3) U+1782 U+17CB   c srz
U+179F U+17D2 U+179A U+17C9 U+1782 U+17CB + [K_Z]   > context(1) context(2) context(3) context(4) dk(deadkey_z)    c srzz
U+179F U+17D2 U+179A + [K_A]                        > context(1) context(2) context(3) U+17B6 U+1782 U+17CB        c sra
U+179F U+17D2 U+179A U+17B6 U+1782 U+17CB + [K_A]   > context(1) context(2) context(3) context(4)    c sraa


c tr
U+178F U+17D2 U+179A + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+178F U+17D2 U+179A + any(sin_vow2_key)    > U+1791 context(2) context(3) index(sin_vow2_out,4) 
U+1791 U+17D2 U+179A U+17BB + [K_E]         > U+178F context(2) context(3) U+17BD   c true
U+1791 U+17D2 U+179A U+17B7 + [K_E]         > U+178F context(2) context(3) U+17C0   c trie
U+178F U+17D2 U+179A U+17B7 + [K_X]         > U+1791 context(2) context(3) U+17C2   c trxx
U+178F U+17D2 U+179A + [K_Z]                > context(1) context(2) context(3) U+1782 U+17CB   c trz
U+178F U+17D2 U+179A U+1782 U+17CB + [K_Z]  > context(1) context(2) context(3) dk(deadkey_z)     c trzz
U+178F U+17D2 U+179A + [K_A]                > context(1) context(2) context(3) U+17B6 U+1782 U+17CB   c tra
U+178F U+17D2 U+179A U+17B6 U+1782 U+17CB + [K_A]  > context(1) context(2) context(3) context(4)     c traa


c qy
U+17A2 U+17D2 U+1799 + any(sin_vow1_key)    > context(1) context(2) context(3) index(sin_vow1_out,4)
U+17A2 U+17D2 U+1799 + any(sin_vow2_key)    > context(1) context(2) context(3) U+17CA index(sin_vow2_out,4) 
c U+179F U+17D2 U+179A U+17BB + [K_E]         > context(1) context(2) context(3) U+17BD   c qyue
c U+179F U+17D2 U+179A U+17B7 + [K_E]         > context(1) context(2) context(3) U+17C0   c qyie
U+17A2 U+17D2 U+1799 U+17B7 + [K_X]         > context(1) context(2) context(3) U+17CA U+17C2   c qyxx
U+17A2 U+17D2 U+1799 + [K_Z]                > context(1) context(2) context(3) U+1782 U+17CB   c qyz
U+17A2 U+17D2 U+1799 U+1782 U+17CB + [K_Z]   > context(1) context(2) context(3) dk(deadkey_z)  c qyzz


c qyvv


c any CC with K_A and K_A K_A
any(any_con) U+17D2 any(any_con) + [K_A]        > context(1) context(2) context(3) U+17B6 U+1782 U+17CB
any(any_con) U+17D2 any(any_con) U+17B6 U+1782 U+17CB + [K_A]   > context(1) context(2) context(3) U+17B6

c +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

c change ព > ប in the final position
U+17C9 + [K_P]                          > context(1) U+1794
U+17CA + [K_P]                          > context(1) U+1794
any(any_vowel) + [K_P]                  > context(1) U+1794
any(any_vowel) + [K_P]                  > context(1) U+1794
any(any_vowel) U+1782 U+17CB + [K_P]    > context(1) U+1794 U+17CB


c ហ after គ in the final position
any(any_con) + [K_H]            > U+17C7
U+17B6 U+1782 U+17CB + [K_H]    > U+17C7            c ាហ់ > ះ in the final position
U+1782 U+17CB + [K_H]           > U+17C4 U+17C7     c fix ហ់ 
U+1793 U+17CB + [K_H]           > U+1789 context(2) c nh > ញ


c ង in the final position
U+17B6 U+1793 U+17CB + [K_G]    > U+17B6 U+17C6 U+1784  c ang
U+1793 U+17CB + [K_G]           > U+1784 context(2)     c zng

U+17B6 U+1782 U+17CB + [K_N]    > context(1) U+1793 context(3)  c ន replaced គ in the final position after Bantoc


c អ in the final position becomes គ
any(any_vowel) + [K_Q]          > context
U+17CA + [K_Q]                  > context U+1782
U+17C9 + [K_Q]                  > context U+1782
U+17CA dk(deadkey_z) + [K_Q]                  > context(1) U+1782
U+17C9 dk(deadkey_z) + [K_Q]                  > context(1) U+1782

c wq > វ្គ
U+179C + [K_Q]                  > context U+17D2 U+1782


c any consonant + am > any consonant + ាំ
any(any_con) U+17B6 U+1782 U+17CB + [K_M]   > context(1) context(2) U+17C6

c any consonant + zm > any consonant + ំ
any(any_con) U+1782 U+17CB + [K_M]          > context(1) U+17C6

c any consonant + um > any consonant + ុំ
any(any_con) U+17BB + [K_M]                 > context(1) context(2) U+17C6


c final consonant with deadkey
dk(deadkey_z) + any(sin_con1_key)         > index(sin_con1_out,2)
dk(deadkey_z) + any(sin_con2_key)         > index(sin_con2_out,2)



c +++++++++++++++++ NORMALIZATION +++++++++++++++++
match > use(normalise)

group(normalise)

c inherent vowel and ា treatments (2) with consonant key fired.
any(any_con) U+17B6 U+1782 U+17CB any(sin_con1_out) > context(1) context(2) context(5) context (4)
any(any_con) U+17B6 U+1782 U+17CB any(sin_con2_out) > context(1) context(2) context(5) context (4)

any(any_con) U+1782 U+17CB any(sin_con1_out)        > context(1) context(4) context (3)
any(any_con) U+1782 U+17CB any(sin_con2_out)        > context(1) context(4) context (3)
U+1782 U+17CB any(sin_con1_out)     > context(3) context(2)
U+1782 U+17CB any(sin_con2_out)     > context(3) context(2)


U+1782 U+17CB U+179A                > context(3) context(2)













