c Original keyboard defined on 9 April 1997 by Bryan Russell
c Modified 08/Nov/02 by Beth Bryson to work with Keyman 6.0.
c modified for Keyman 9.0 by Rudolf Schönfeld
c modified 26/Feb/2019 for Keyman 10.0 by S. Vanderwerf
c This file defines a keyboard to easily access all the characters used in Standard Latin Kurmanji

store(&VERSION) '10.0'
store(&NAME) 'Kurmanji'
store(&TARGETS) 'any windows macosx linux desktop'
store(&COPYRIGHT) 'SIL International ©2017-2019'

store(&mnemoniclayout) '1'
store(&MESSAGE) 'Kurmanji Kurdish Latin Keyboard'

begin Unicode > use(main)

c KURMANJI specific characters
store(KKey) 'ceisuCEISU'
store(Kur)  'çêîşûÇÊÎŞÛ'

c for producing German characters on an English keyboard 
c (including some legacy characters for use with legacy applications)
store(Gkey) 'aeigosuxhtzAEIGOSUXHT'
store(Ger)  'äëïðöþüÿœåßÄËÏÐÖÞÜŸŒÅ' 

group(main) using keys

c KURMANJI specific characters
',' + any(KKey) 	> index(Kur,2)		c Standard Kurmanji Keyboard using »'«

c for German characters on an English keyboard
';' + any(GKey) 	> index(Ger,2) 		c German umlauted chars using »"«

c Turkish i
'=' + 'i'		> 'ı'			c undotted lower i
'=' + 'I'		> 'İ'			c dotted uppercase i

c Angle Quotations
',' + '>'   	> U+203A    c "›"
',' + '<'   	> U+2039    c "‹"
'<' + '<'   	> U+00AB    c "«"
'>' + '>'     	> U+00BB    c "»"
