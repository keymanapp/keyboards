store(&VERSION) '9.0'
store(&NAME) 'Obolo Chwerty'
store(&ETHNOLOGUECODE)'ann'
store(&COPYRIGHT) '© 2020 Rogers Katelem Edeh'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'obolo_chwerty.kvks'
store(&LAYOUTFILE) 'obolo_chwerty.keyman-touch-layout'
store(&MESSAGE) 'A simple keyboard layout (chwerty) to help you type very easily in Obolo language.'
store(&KMW_HELPTEXT) 'A simple keyboard layout (chwerty) to help you type very easily in Obolo language.'

begin Unicode > use(main)

store(accentibles) 'aeiọoumnAEIỌOUMN'
store(accents) [T_GRV] [T_ACU] [T_CCM] [T_CRN] [SHIFT T_GRV] [SHIFT T_ACU] [SHIFT T_CCM] [SHIFT T_CRN] [K_BKQUOTE] [SHIFT K_BKQUOTE] [K_BKSLASH] [SHIFT K_BKSLASH]
store(nasals) 'mnMN'
store(nnas) [T_CCM] [T_CRN] [SHIFT T_CCM] [SHIFT T_CRN] [K_BKSLASH] [SHIFT K_BKSLASH]
store(vow) 'aeiouAEIOU'
store(grave) 'àèìòùÀÈÌÒÙ'
store(acute) 'áéíóúÁÉÍÓÚ'
store(circum) 'âêîôûÂÊÎÔÛ'
store(caron) 'ǎěǐǒǔǍĚǏǑǓ'
store(acnasals) 'ḿńḾŃ'
store(spguy) 'mnọMNỌ'
store(spguyop) 'ḿǹộḾǸỘ'
store(erase) [K_BKSP] [SHIFT K_BKSP] [ALT K_BKSP]
store(gravekeys) [T_GRV] [SHIFT T_GRV] [K_BKQUOTE]
store(acutekeys) [T_ACU] [SHIFT T_ACU] [K_BKSLASH]
store(circumflexkeys) [T_CCM] [SHIFT T_CCM] [SHIFT K_BKQUOTE]
store(caronkeys) [T_CRN] [SHIFT T_CRN] [SHIFT K_BKSLASH]

group(main) using keys

+ [T_ENG] > dk(1)
+ [SHIFT T_ENG] > dk(2)

+ [K_Q] > 'ch'
+ [SHIFT K_Q] > 'CH'
+ [SHIFT T_Q] > 'Ch'

+ [K_X] > 'ọ'
+ [SHIFT K_X] > 'Ọ'

+ [K_H] > 'sh'
+ [SHIFT K_H] > 'SH'
+ [SHIFT T_H] > 'Sh'

+ [K_C] > 'n̄'
+ [SHIFT K_C] > 'N̄'

+ [CTRL SHIFT K_C] > 'cC' c for desktop
+ [CTRL SHIFT K_X] > 'xX'
+ [CTRL SHIFT K_H] > 'Sh'
+ [CTRL SHIFT K_Q] > 'Ch'

c deadkey for english equivalent
dk(1) + [K_Q] > 'q'
dk(2) + [SHIFT K_Q] > 'Q'

dk(1) + [K_H] > 'h'
dk(2) + [SHIFT K_H] > 'H'

dk(1) + [K_X] > 'x'
dk(2) + [SHIFT K_X] > 'X'

dk(1) + [K_C] > 'c'
dk(2) + [SHIFT K_C] > 'C'

dk(1) + [K_S] > 'ṣ'
dk(2) + [SHIFT K_S] > 'Ṣ'

c deadkey for tones
dk(1) + [T_GRV] > "`"
dk(2) + [SHIFT T_GRV] > "`"

dk(1) + [T_ACU] > "´"
dk(2) + [SHIFT T_ACU] > "´"

dk(1) + [T_CCM] > "ˆ"
dk(2) + [SHIFT T_CCM] > "ˆ"

dk(1) + [T_CRN] > "ˇ"
dk(2) + [SHIFT T_CRN] > "ˇ"

dk(1) + [K_HYPHEN] > "¯"
dk(2) + [SHIFT K_HYPHEN] > "¯"

c make up for
'm' + any(gravekeys) > 'm̀'
'M' + any(gravekeys) > 'M̀'

'n' + any(gravekeys) > 'ǹ'
'N' + any(gravekeys) > 'Ǹ'

'ọ' + any(gravekeys) > 'ọ̀'
'Ọ' + any(gravekeys) > 'Ọ̀'

'ọ' + any(acutekeys) > 'ọ́'
'Ọ' + any(acutekeys) > 'Ọ́'

'ọ' + any(circumflexkeys) > 'ộ'
'Ọ' + any(circumflexkeys) > 'Ộ'

'ọ' + any(caronkeys) > 'ọ̌'
'Ọ' + any(caronkeys) > 'Ọ̌'

c any and index
any(vow) + any(gravekeys) > index(grave,1)
any(vow) + any(acutekeys) > index(acute,1)
any(vow) + any(circumflexkeys) > index(circum,1)
any(vow) + any(caronkeys) > index(caron,1)
any(nasals) + any(acutekeys) > index(acnasals,1)

'ch' + any(erase) > nul c change backspace behaviour for special diagraphs
'Ch' + any(erase) > nul
'CH' + any(erase) > nul
'sh' + any(erase) > nul
'Sh' + any(erase) > nul
'SH' + any(erase) > nul
'n̄' + any(erase) > nul
'N̄' + any(erase) > nul

c virtual keys
any(grave) + any(erase) > index(vow,1)
any(acute) + any(erase) > index(vow,1)
any(circum) + any(erase) > index(vow,1)
any(caron) + any(erase) > index(vow,1)
any(spguyop) + any(erase) > index(spguy,1)

notany(accentibles) + any(accents) > context beep c prevent diacritics (stacking, on empty space, and plain consonants)
any(nasals) + any(nnas) > context beep c prevent certain accents on nasal consonants
