c bj_cree_woods generated from template at 2020-12-07 13:05:12
c with name "Western Cree (TH-Woods)"
store(&VERSION) '10.0'
store(&NAME) 'Western Cree (TH-Woods)'
store(&COPYRIGHT) '© Bill Jancewicz'
store(&KEYBOARDVERSION) '1.3'
store(&VISUALKEYBOARD) 'bj_cree_woods.kvks'
store(&TARGETS) 'web desktop'

begin Unicode > use(main)

c ================================================================
c STORES
c ================================================================

c Eastern Finals (short)
store(C_ef)  U+1401 U+1403 U+1404 U+1405 U+1406 U+140A U+140B U+142F U+1431 U+1432 U+1433 U+1434 U+1438 U+1439 \
             U+144C U+144E U+144F U+1450 U+1451 U+1455 U+1456 U+146B U+146D U+146E U+146F U+1470 U+1472 U+1473 \
             U+1489 U+148B U+148C U+148D U+148E U+1490 U+1491 U+14A3 U+14A5 U+14A6 U+14A7 U+14A8 U+14AA U+14AB \
             U+14C0 U+14C2 U+14C3 U+14C4 U+14C5 U+14C7 U+14C8 U+14ED U+14EF U+14F0 U+14F1 U+14F2 U+14F4 U+14F5 \
             U+1510 U+1511 U+1512 U+1513 U+1514 U+1515 U+1516 U+1543 U+1546 U+1547 U+1548 U+1549 U+154B U+154C \
             U+14D3 U+14D5 U+14D6 U+14D7 U+14D8 U+14DA U+14DB U+1526 U+1528 U+1529 U+152A U+152B U+152D U+152E \
             U+155A U+1567

c Eastern Finals Composed (short)
store(C_efc) U+140C U+140E U+1410 U+1412 U+1414 U+1417 U+1419 U+143A U+143C U+143E U+1440 U+1442 U+1444 U+1446 \
             U+1457 U+1459 U+145B U+145D U+145F U+1461 U+1463 U+1474 U+1476 U+1478 U+147A U+147C U+147E U+1480 \
             U+1492 U+1494 U+1496 U+1498 U+149A U+149C U+149E U+14AC U+14AE U+14B0 U+14B2 U+14B4 U+14B6 U+14B8 \
             U+14C9 U+18C6 U+18C8 U+18CA U+18CC U+14CB U+14CD U+14F6 U+14F8 U+14FA U+14FC U+14FE U+1500 U+1502 \
             U+1517 U+1519 U+151B U+151D U+151F U+1521 U+1523 U+18CE U+18CF U+18D0 U+18D1 U+18D2 U+18D3 U+154E \
             U+14DC U+14DE U+14E0 U+14E2 U+14E4 U+14E6 U+14E8 U+152F U+1531 U+1533 U+1535 U+1537 U+1539 U+153B \ 
             U+155B U+1568           

c initial consonant keys (The Î is U+1525 which is the sh consonant)
c (The ÿ is U+15AE which is the th consonant that looks like y)

store(ConsKey) "ptkcmnsyf"
store(CapConsKey) "PTKCMNSYF"

c Consonant followed by vowel:
c            p      t      k      c      m      n       s      y       f    
store(Ce)  U+142F U+144C U+146B U+1489 U+14A3 U+14C0  U+14ED U+1526  U+1553 
store(Ci)  U+1431 U+144E U+146D U+148B U+14A5 U+14C2  U+14EF U+1528  U+1555 
store(Cii) U+1432 U+144F U+146E U+148C U+14A6 U+14C3  U+14F0 U+1529  U+1556 
store(Cu)  U+1433 U+1450 U+146F U+148D U+14A7 U+14C4  U+14F1 U+152A  U+1557 
store(Cuu) U+1434 U+1451 U+1470 U+148E U+14A8 U+14C5  U+14F2 U+152B  U+1558 
store(Co)  U+1433 U+1450 U+146F U+148D U+14A7 U+14C4  U+14F1 U+152A  U+1557 
store(Coo) U+1434 U+1451 U+1470 U+148E U+14A8 U+14C5  U+14F2 U+152B  U+1558 
store(Ca)  U+1438 U+1455 U+1472 U+1490 U+14AA U+14C7  U+14F4 U+152D  U+1559 
store(Caa) U+1439 U+1456 U+1473 U+1491 U+14AB U+14C8  U+14F5 U+152E  U+155A 

c Consonant sh and th followed by vowel:
store(Ce_shth)  U+1510 U+15A7
store(Ci_shth)  U+1511 U+15A8
store(Cii_shth) U+1512 U+15A9
store(Cu_shth)  U+1513 U+15AA
store(Cuu_shth) U+1514 U+15AB
store(Co_shth)  U+1513 U+15AA
store(Coo_shth) U+1514 U+15AB
store(Ca_shth)  U+1515 U+15AC
store(Caa_shth) U+1516 U+15AD

c Standalone or trailing consonant, "western finals":
c            p      t      k      c      m      n       s      y       f
store(CC)  U+144A U+141F U+1420 U+1428 U+14BC U+1423  U+1422  U+1429  U+155D

c Standalone or trailing sh and th
store(CC_shth)  U+1525 U+15AE 

c Vowel keys
store(VowelKey) "eiuoa"
store(CapVowelKey) "EIUOA"

c Standalone vowels (also includes doubled):
c (Note that the u triangle (U+1405) is NOT the word-final form.
c           e      i      u      o      a        ii     uu     aa   
store(VV) U+1401 U+1403 U+1405 U+1405 U+140A   U+1404 U+1406 U+140B

c Special keys
store(Hkey) "h"
store(CapHkey) "H"
store(WKey) "wW"

c ================================================================
c GROUP MAIN - convert keycodes
c
c (Conversion to syllables and reordering, etc, takes place in
c the group MakeSyllables.)
c ================================================================

group(Main) using keys

c  Option keys Set composite mode by default
    store(option_key)     ''
 + [RALT K_HYPHEN]	>	set(option_key='nfd') c 
 + [RALT K_EQUAL]	>	reset(option_key)  c 

c Special rules to deal with h: (western finals)
c    s   +    h      > sh (small sh character)
c    s   +    H      > s (small s) and h
c    t   +    h      > th
c    t   +    H      > t (small t) and h
c    c   +    h      > c
c    c   +    H      > c (small c) and h
c  otherwise, h      > standalone h
    U+1422 + any(Hkey)    > U+1525
    U+1422 + any(CapHkey) > U+1422 U+1426
    U+141F + any(Hkey)    > U+15AE
    U+141F + any(CapHkey) > U+141F U+1426
    U+1428 + any(Hkey)    > U+1428
    U+1428 + any(CapHkey) > U+1428 U+1426
         + any(Hkey)      > U+1426
         + any(CapHkey)   > U+1426

c Consonant key to standalone consonant:
    + any(ConsKey)    > index(CC,1)
    + any(CapConsKey) > index(CC,1)

c Alternate consonants: (voiced for voiceless)
    + "b" > U+144A
    + "d" > U+141F
    + "g" > U+1420
    + "j" > U+1428
    + "v" > U+155D
    + "z" > U+1422
c "r" and "l" sigma character (western style)
    + "l" > U+14EC 
    + "r" > U+1552
    + "L" > U+14EC
    + "R" > U+1552

c Vowel key to vowel:
    + any(VowelKey)    > index(VV,1)
    + any(CapVowelKey) > index(VV,1)

c Special rules for w (western)
c    k   +   w       > kw 
c    m   +   w       > mw  
c otherwise, w       > standalone w
    U+1420 + any(wKey) > U+1484
    U+14BC + any(wKey) > U+14BD
         + any(wKey) > U+1424

c Period
    + "." > U+166E

c various symbols and numbers
store(numbers_keys)     '1234567890'
store(numbers_chars)    U+0031 U+0032 U+0033 U+0034 U+0035 U+0036 U+0037 U+0038 U+0039 U+0030
store(symbols_keys)     "`-=[]\;',/"
store(symbols_chars)    U+0060 U+002D U+003D U+005B U+005D U+005C U+003B U+0027 U+002C U+002F     
store(symbols_Skeys)    '~!@#$%^&*()_+{}|:"<>?'
store(symbols_Schars)   U+007E U+0021 U+0040 U+0023 U+0024 U+0025 U+005E U+0026 U+002A U+0028 U+0029 U+202F U+002B U+007B U+007D U+007C U+003A U+0022 U+003C U+003E U+003F

+ any(numbers_keys) > index(numbers_chars,1)
+ any(symbols_keys) > index(symbols_chars,1)
+ any(symbols_Skeys)    > index(symbols_Schars,1)

c unwanted keys: q x Q D G J Z X V B
store(unwanted_keys)    "qxQDGJZXVB"
+ any(unwanted_keys) > nul

c If we did anything, then go look for syllables    
    match   > use(MakeSyllables)


c ================================================================
c GROUP MAKESYLLABLES
c
c This group process no keys, but looks at stuff already in the
c context stack and does things such as make syllables, reorder
c the "w" when in CVw, handle word-final u, etc.
c ================================================================

group(MakeSyllables)
c "w" is the dot+circle character U+1424

c Special rules for w:
c    k   +   w  > kw 
c    m   +   w  > mw  
    U+1420 U+1424 > U+1484
    U+14BC U+1424 > U+14BD

c Re-order the w when the syllable is complete:
c (EASTERN STYLE REORDERING)

c   kw +    vowel  >  w     k vowel 
     U+1484 U+1401 > U+1427 U+146B
     U+1484 U+1403 > U+1427 U+146D
     U+1484 U+1405 > U+1427 U+146F
     U+1484 U+140A > U+1427 U+1472
c   mw +    vowel  >  w     m vowel
     U+14BD U+1401 > U+1427 U+14A3
     U+14BD U+1403 > U+1427 U+14A5
     U+14BD U+1405 > U+1427 U+14A7
     U+14BD U+140A > U+1427 U+14AA
c   Consonant + w + vowel   >  w Consonant vowel
   any(CC)   U+1424  any(VV) > U+1427 index(CC,1) index(VV,3)

   any(CC_shth)   U+1424  any(VV) > U+1427 index(CC_shth,1) index(VV,3)

c Convert consonant followed by vowels into syllable:

    any(CC) U+1401 > index(Ce,1) 
    any(CC) U+1403 > index(Ci,1) 
    any(CC) U+1404 > index(Cii,1) 
    any(CC) U+1405 > index(Cu,1) 
    any(CC) U+1406 > index(Cuu,1) 
c    any(CC) U+1405 > index(Co,1) c duplicate of line 207
c    any(CC) U+1406 > index(Coo,1) c duplicate of line 208
    any(CC) U+140A > index(Ca,1) 
    any(CC) U+140B > index(Caa,1)

c Convert consonant sh and th followed by vowels into syllable:

    any(CC_shth) U+1401 > index(Ce_shth,1) 
    any(CC_shth) U+1403 > index(Ci_shth,1) 
    any(CC_shth) U+1404 > index(Cii_shth,1) 
    any(CC_shth) U+1405 > index(Cu_shth,1) 
    any(CC_shth) U+1406 > index(Cuu_shth,1) 
c    any(CC_shth) U+1405 > index(Co_shth,1) c duplicate of line 219
c    any(CC_shth) U+1406 > index(Coo_shth,1) c duplicate of line 220
    any(CC_shth) U+140A > index(Ca_shth,1) 
    any(CC_shth) U+140B > index(Caa_shth,1)
   

c Vowel followed by duplicate vowel changes to long form:
    U+1403    U+1403 > U+1404  
    U+1405    U+1405 > U+1406  
    U+140A    U+140A > U+140B  
    any(Ci) U+1403 > index(Cii,1)  
    any(Cu) U+1405 > index(Cuu,1)  
    any(Co) U+1405 > index(Coo,1)  
    any(Ca) U+140A > index(Caa,1)  

    any(Ci_shth) U+1403 > index(Cii_shth,1)  
    any(Cu_shth) U+1405 > index(Cuu_shth,1)  
    any(Co_shth) U+1405 > index(Coo_shth,1)  
    any(Ca_shth) U+140A > index(Caa_shth,1)  

c Vowel (other than u, already handled above) followed by u makes u word-final:
    any(Ce)  U+1405 > index(Ce,1)  U+1424
    any(Ci)  U+1405 > index(Ci,1)  U+1424
    any(Cii) U+1405 > index(Cii,1) U+1424
    any(Cuu) U+1405 > index(Cuu,1) U+1424
    any(Coo) U+1405 > index(Coo,1) U+1424
    any(Ca)  U+1405 > index(Ca,1)  U+1424
    any(Caa) U+1405 > index(Caa,1) U+1424

    any(Ce_shth)  U+1405 > index(Ce_shth,1)  U+1424
    any(Ci_shth)  U+1405 > index(Ci_shth,1)  U+1424
    any(Cii_shth) U+1405 > index(Cii_shth,1) U+1424
    any(Cuu_shth) U+1405 > index(Cuu_shth,1) U+1424
    any(Coo_shth) U+1405 > index(Coo_shth,1) U+1424
    any(Ca_shth)  U+1405 > index(Ca_shth,1)  U+1424
    any(Caa_shth) U+1405 > index(Caa_shth,1) U+1424

c Word-final u followed by any letter changes u back to standalone:
    U+1424 any(CC) > U+1427 index(CC,2)
    U+1424 any(VV) > U+1427 index(VV,2)

    U+1424 any(CC_shth) > U+1427 index(CC_shth,2)

c Word-final u+h+any letter changes u back to standalone
    U+1424 U+1426 any(CC) > U+1405 U+1426 index(CC,3)
    U+1424 U+1426 any(VV) > U+1405 U+1426 index(VV,3)
    U+1424  U+1427   > U+1405  U+1427

    U+1424 U+1426 any(CC_shth) > U+1405 U+1426 index(CC_shth,3)

c final hk to output U+157D (ᕽ)
U+1426 U+1420 > U+157D
    
c Special rule for Christ (remember, ch is already c)
c     ch     r      i      s      t    > christ symbol
    U+1428 U+1552 U+1403 U+1422 U+141F > U+166D

c short vowels (doesn't have long vowel)
if(option_key = '') U+1427 any(C_ef) > index(C_efc,3)



c If we did anything, then go back through again
    match > use(MakeSyllables)
