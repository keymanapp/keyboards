store(&VERSION) '10.0'
store(&NAME) 'Seereer Ajami (QWERTY)'
store(&COPYRIGHT) '© SIL Global'
store(&KMW_RTL) '1'
store(&KEYBOARDVERSION) '1.1.1'
store(&TARGETS) 'any'
store(&BITMAP) 'srr_ajami_qwerty.ico'
store(&LAYOUTFILE) 'srr_ajami_qwerty.keyman-touch-layout'
store(&VISUALKEYBOARD) 'srr_ajami_qwerty.kvks'

begin Unicode > use(main)

c DIGITS
store(intl_digits) "0123456789"
store(key_digits)   [RALT K_0] [RALT K_1] [RALT K_2] [RALT K_3] [RALT K_4] [RALT K_5] [RALT K_6] \
                    [RALT K_7] [RALT K_8] [RALT K_9]
store(ajami_digits) U+0660     U+0661     U+0662     U+0663     U+0664     U+0665     U+0666 \
                    U+0667     U+0668     U+0669


store(Kpunct)    [SHIFT K_1]       [SHIFT K_QUOTE] [SHIFT K_3]    [SHIFT K_4] [K_QUOTE]  [SHIFT K_8]     [SHIFT K_HYPHEN] [SHIFT K_6] \
                 [SHIFT K_PERIOD]  [K_HYPHEN]      [K_EQUAL]      [K_LBRKT]   [SHIFT K_2]     [K_RBRKT] [K_BKQUOTE]       \
                 [K_BKSLASH]       [SHIFT K_BKSLASH] \
                 [K_COMMA]         [SHIFT K_COMMA]                [K_PERIOD]  [SHIFT K_SLASH] [SHIFT K_5]     [RALT K_EQUAL] \
                 [SHIFT K_9]       [SHIFT K_0]     [RALT K_SPACE] 
store(Upunct)    U+0021            U+0022          U+0023         U+0024      U+0027     U+002A          U+002D          U+002F \
                 U+003A            U+003C          U+003E         U+005B     U+005C          U+005D  U+0060 \
                 U+00AB            U+00BB \
                 U+060C            U+061B                         U+061E     U+061F          U+066A          U+066D \
                 U+FD3E            U+FD3F          U+0020

store(Kdia)      [K_E]       [K_I]       [K_O]  [K_U] \
                 [SHIFT K_A] [SHIFT K_I]        [SHIFT K_W] [SHIFT K_Q]  [SHIFT K_COLON] 
store(Udia)      U+08F9      U+0650      U+08F7 U+064F \
                 U+064E      U+0650             U+0651      U+0652      U+0670

store(Kbase) [SHIFT K_J] [K_SLASH]       [K_A] \
             [K_B]       [K_T]           [SHIFT K_C] [K_J]       [K_H]        [K_X]       [K_D]       \
             [RALT K_Z]  [K_R]           [K_Z]       [K_S]       [K_C]        [SHIFT K_S] [RALT K_D]  \
             [SHIFT K_D] [SHIFT K_Z]     [K_COLON]       [K_G]   [K_F]        [K_Q]       [SHIFT K_K] \
             [K_L]       [K_M]           [K_N]       [SHIFT K_H] [K_W]        [K_Y] \
             [SHIFT K_T] \
             [K_K]       [SHIFT K_G]       \
             [SHIFT K_X] [K_P]           [SHIFT K_P] [RALT K_S] \
             [SHIFT K_B] [SHIFT K_Y]     [SHIFT K_N] [RALT K_Y]  [RALT K_C]
store(Ubase) U+0621      U+0622          U+0627 \
             U+0628      U+062A          U+08A2      U+062C      U+0647       U+062E      U+062F \
             U+0630      U+0631          U+0632      U+0633      U+0756       U+0635      U+0636 \
             U+0637      U+0638          U+0639      U+06AF      U+0641       U+0642      U+0643 \
             U+0644      U+0645          U+0646      U+062D      U+0648       U+064A \
             U+08A3 \
             U+06A9      U+063A  \
             U+075D      U+0752          U+0755      U+0634 \
             U+08A0      U+0683          U+0767      U+0649      U+062B

store(nul) "vEFLMORUV"

store(K_RTL) [RALT K_Q] [RALT K_W] [RALT K_E] [RALT K_R]
store(U_RTL) U+200C     U+200D     U+200E     U+200F c ZWNJ ZWJ LRM RLM

c Extra punctuation
store(KExtra_punct) [RALT SHIFT K_5]     [RALT SHIFT K_7]     [SHIFT K_LBRKT] [SHIFT K_RBRKT] \
                    [RALT SHIFT K_EQUAL]   [RALT K_COMMA]     [RALT K_PERIOD]  [SHIFT K_EQUAL]       [RALT SHIFT K_2] \
                    [RALT SHIFT K_6]       [RALT SHIFT K_1]   [RALT K_LBRKT]  [RALT K_BKSLASH] [RALT K_RBRKT] \
                    [RALT SHIFT K_BKQUOTE] [RALT SHIFT K_3]   [RALT SHIFT K_8]
store(Extra_punct)  U+0025               U+0026               U+0028           U+0029 \
                    U+002b                 U+002c             U+002e           U+003d                 U+0040 \
                    U+005e                 U+005f             U+007b           U+007c                 U+007d \
                    U+007e                 U+00d7             U+00f7




group(main) using keys

+ any(key_digits) > index(ajami_digits,1)
+ any(intl_digits) > index(intl_digits,1)
+ any(Kpunct) > index(Upunct,1)
+ any(Kdia) > index(Udia,1) use(tanween)
+ any(Kbase) > index(Ubase,1)

+ any(KExtra_punct) > index(Extra_punct,1)

+ any(K_RTL) > index(U_RTL,1)
+ [SHIFT K_BKQUOTE] > U+FDF2 c Allah
+ [SHIFT K_7]       > U+0640 c kashida

U+00AB + [K_BKSLASH] > U+2039
U+00BB + [SHIFT K_BKSLASH]       > U+203A
U+002D + [RALT K_HYPHEN] > U+2013
+ any(nul) > nul

group(tanween)

U+064E U+064E > U+064B
U+064F U+064F > U+064C
U+0650 U+0650 > U+064D

