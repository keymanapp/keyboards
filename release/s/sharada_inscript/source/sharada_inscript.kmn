store(&VERSION) '15.0'
store(&NAME) 'Sharada Inscript'
store(&COPYRIGHT) 'Copyright © SIL International'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any'
store(&BITMAP) 'sharada_inscript.ico'
store(&VISUALKEYBOARD) 'sharada_inscript.kvks'
store(&LAYOUTFILE) 'sharada_inscript.keyman-touch-layout'

begin Unicode > use(main)

c Inscript keys

c             candrabindu anusvara visarga          avagraha              jihvamuliya upadhmaniya      nukta      Altcandrabindu        
store(SignsK) [SHIFT K_X] [K_X]    [SHIFT K_HYPHEN] [RALT SHIFT K_PERIOD] [RALT K_H]  [SHIFT RALT K_H] [K_RBRKT]  [RALT K_X]
store(Signs)  U+11180 .. U+11182                     U+111C1 ..U+111C3                                 U+111CA    U+111CF

c               a          aa          i           ii          u           uu          vr               vrr                 vl                vll              e           ai          o            au
store(IndVowK) [SHIFT K_D] [SHIFT K_E] [SHIFT K_F] [SHIFT K_R] [SHIFT K_G] [SHIFT K_T] [SHIFT K_EQUAL] [RALT SHIFT K_EQUAL] [SHIFT RALT K_F]  [RALT SHIFT K_R] [SHIFT K_S] [SHIFT K_W] [SHIFT K_A] [SHIFT K_Q]
store(IndVow)  U+11183 .. U+11190

c               aa    i     ii    u     uu    vr        vrr            vl         vll        e     ai    o     au   prishE
store(DepVowK) [K_E] [K_F] [K_R] [K_G] [K_T] [K_EQUAL] [RALT K_EQUAL] [RALT K_F] [RALT K_R] [K_S] [K_W] [K_A] [K_Q] [K_Z]
store(DepVow)  U+111B3 .. U+111BF U+111CE

c            ka     kha        ga    gha         nga          ca        cha             ja    jha         nya             tta       ttha            dda       ddha            nna         ta    tha         da    dha         na    pa    pha         ba    bha         ma    ya        ra    la    lla          va    sha         ssa             sa     ha
store(ConsK) [K_K] [SHIFT K_K] [K_I] [SHIFT K_I] [SHIFT K_U] [K_COLON] [SHIFT K_COLON] [K_P] [SHIFT K_P] [SHIFT K_RBRKT] [K_QUOTE] [SHIFT K_QUOTE] [K_LBRKT]  [SHIFT K_RBRKT] [SHIFT K_C] [K_L] [SHIFT K_L] [K_O] [SHIFT K_O] [K_V] [K_H] [SHIFT K_H] [K_Y] [SHIFT K_Y] [K_C] [K_SLASH] [K_J] [K_N] [SHIFT K_N] [K_B] [SHIFT K_M] [SHIFT K_COMMA] [K_M] [K_U]
store(Cons)  U+11191 .. U+111B2


c             danda            doubledanda     abbrev         separator            sutra            ekam       siddham    headstroke continuation
store(PunctK) [SHIFT K_PERIOD] [RALT K_PERIOD] [RALT K_COMMA] [SHIFT RALT K_COMMA] [RALT K_BKQUOTE] [RALT K_U] [RALT K_I] [RALT K_O] [RALT K_P]
store(Punct)  U+111C5 .. U+111C8                                                   U+111CD          U+111DA .. U+111DD

store(DigitsK) "0123456789"
store(Digits)  U+111D0 .. U+111D9

store(LDigitsK) [RALT K_0] [RALT K_1] [RALT K_2] [RALT K_3] [RALT K_4] [RALT K_5] [RALT K_6] [RALT K_7] [RALT K_8] [RALT K_9] 
store(LDigits)  U+0030 .. U+0039


store(nul) "JVB{"
group(main) using keys

+ any(DepVowK)  > index(DepVow,1)
+ any(IndVowK)  > index(IndVow,1)
+ any(SignsK)   > index(Signs,1)
+ any(ConsK)    > index(Cons,1)
+ any(PunctK)   > index(Punct,1)
+ any(DigitsK)  > index(Digits,1)
+ any(LDigitsK) > index(LDigits,1)

+ [K_HYPHEN] > U+002D
+ [K_COMMA] > U+002C
+ [K_PERIOD] > U+002E
+ [K_D] > U+111C0 c virama
+ [RALT SHIFT K_X] > U+1118f U+11180 c om
+ [SHIFT RALT K_1] > U+200d c ZWJ
+ [SHIFT RALT K_2] > U+200c c ZWNJ
+ [K_BKQUOTE]       > U+111DE c section mark-1
+ [SHIFT K_BKQUOTE] > U+111DF c section mark-2
+ [K_BKSLASH]       > U+111CB c vowel modifier
+ [SHIFT K_BKSLASH] > U+111CC c extra short vowel mark
+ [SHIFT K_Z]       > U+111C9 c sandhi mark
+ [RALT K_LBRKT] > U+005B
+ [RALT K_RBRKT] > U+005D
+ [RALT K_BKSLASH] > U+005C
+ [SHIFT RALT K_LBRKT] > U+007B
+ [SHIFT RALT K_RBRKT] > U+007D
+ [SHIFT RALT K_BKSLASH] > U+007C
+ [RALT K_SLASH] > U+002F

 c + [K_HYPHEN] > U+002D c hyphen-minus
 c + [K_EQUAL] > U+2010 c hyphen
 c + [RALT K_EQUAL] > U+002B
 c + [RALT K_PERIOD] > U+2E31
 c + any(nul) > nul
