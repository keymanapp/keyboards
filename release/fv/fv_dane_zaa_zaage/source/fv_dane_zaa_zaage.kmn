store(&VERSION) '10.0'
store(&KEYBOARDVERSION) '9.3'
store(&TARGETS) 'any'
c store(&ETHNOLOGUECODE) "bea"
store(&COPYRIGHT) '© 2015-2023 FirstVoices, First Peoples' U+0027 ' Cultural Foundation, SIL International'
store(&NAME) 'Dane-Z̲aa Z̲áágéʔ'
store(&VISUALKEYBOARD) 'fv_dane_zaa_zaage.kvks'
store(&LAYOUTFILE) 'fv_dane_zaa_zaage.keyman-touch-layout'

begin Unicode > use(main)

group(main) using keys

c Begin mobile layout
+ [SHIFT T_W] > 'W' layer('default')
+ [SHIFT T_E_0] > 'Ę' layer('default')
+ [SHIFT T_E] > 'E' layer('default')
+ [SHIFT T_T_0] > 'T̲s̲' layer('default')
+ [SHIFT T_T_1] > 'T̲S̲' layer('default')
+ [SHIFT T_T] > 'T' layer('default')
+ [SHIFT T_Y] > 'Y' layer('default')
+ [SHIFT T_U] > 'U' layer('default')
+ [SHIFT T_I] > 'I' layer('default')
+ [SHIFT T_O_0] > 'Ǫ' layer('default')
+ [SHIFT T_O] > 'O' layer('default')
+ [SHIFT T_A_0] > 'Ą' layer('default')
+ [SHIFT T_A] > 'A' layer('default')
+ [SHIFT T_S_0] > 'S̲' layer('default')
+ [SHIFT T_S] > 'S' layer('default')
+ [SHIFT T_D_0] > 'D̲z̲' layer('default')
+ [SHIFT T_D_1] > 'D̲Z̲' layer('default')
+ [SHIFT T_D] > 'D' layer('default')
+ [SHIFT T_G] > 'G' layer('default')
+ [SHIFT T_H] > 'H' layer('default')
+ [SHIFT T_J] > 'J' layer('default')
+ [SHIFT T_K] > 'K' layer('default')
+ [SHIFT T_L] > 'L' layer('default')
+ [SHIFT T_APOS_0] > U+0022 layer('default')
+ [SHIFT T_APOS_1] > U+0027 layer('default')
+ [SHIFT T_APOS_2] > '-' layer('default')
+ [SHIFT T_APOS] > '’' layer('default')
+ [SHIFT T_Z_0] > 'Z̲' layer('default')
+ [SHIFT T_Z] > 'Z' layer('default')
+ [SHIFT T_C] > 'C' layer('default')
+ [SHIFT T_GLOT] > 'ʔ' layer('default')
+ [SHIFT T_B] > 'B' layer('default')
+ [SHIFT T_N] > 'N' layer('default')
+ [SHIFT T_M] > 'M' layer('default')
+ [T_W] > 'w'
+ [T_E_0] > 'ę'
+ [T_E] > 'e'
+ [T_T_0] > 't̲s̲'
+ [T_T] > 't'
+ [T_Y] > 'y'
+ [T_U] > 'u'
+ [T_I] > 'i'
+ [T_O_0] > 'ǫ'
+ [T_O] > 'o'
+ [T_A_0] > 'ą'
+ [T_A] > 'a'
+ [T_S_0] > 's̲'
+ [T_S] > 's'
+ [T_D_0] > 'd̲z̲'
+ [T_D] > 'd'
+ [T_G] > 'g'
+ [T_H] > 'h'
+ [T_J] > 'j'
+ [T_K] > 'k'
+ [T_L] > 'l'
+ [T_APOS_0] > U+0022
+ [T_APOS_1] > U+0027
+ [T_APOS_2] > '-'
+ [T_APOS] > '’'
+ [T_Z_0] > 'z̲'
+ [T_Z] > 'z'
+ [T_C] > 'c'
+ [T_GLOT] > 'ʔ'
+ [T_B] > 'b'
+ [T_N] > 'n'
+ [T_M] > 'm'

store(SZ) 'szSZ'

any(SZ) + any(LowLineKeys) > index(SZ,1) U+0332

C Rules Across Both Layouts

c key stores
store(AcuteKeys) [K_SLASH] [T_ACUTE] [SHIFT T_ACUTE]
store(CaronKeys) [K_BKSLASH] [T_CARON] [SHIFT T_CARON]
store(CircKeys) [SHIFT K_BKSLASH] [T_CIRC] [SHIFT T_CIRC]
store(LowLineKeys) [SHIFT K_BKQUOTE] [T_LOWL] [SHIFT T_LOWL]
store(OgoKeys) [K_BKQUOTE] [T_OGO] [SHIFT T_OGO]

c vowel character collections
store(BaseVowels) "aeiouAEIOU"
store(Accents) U+0301 U+030C U+0302
store(AcuteVowels) 'áéíóúÁÉÍÓÚ'
store(CaronVowels) 'ǎěǐǒǔǍĚǏǑǓ'
store(CircumflexVowels) 'âêîôûÂÊÎÔÛ'
store(OgonekVowels) 'ąęįǫųĄĘĮǪŲ'

c accents should join with vowels
c basevowel + accent = combined
any(BaseVowels) + any(AcuteKeys) > index(AcuteVowels,1)
any(BaseVowels) + any(CaronKeys) > index(CaronVowels,1)
any(BaseVowels) + any(CircKeys) > index(CircumflexVowels,1)

c ogonek should join with vowels, and should never double
c basevowel plus ogonek = combined vowelogonek
any(BaseVowels) + any(OgoKeys) > index(OgonekVowels,1)

c vowelogonek plus another ogonek = single vowelogonek (no stacking)
any(Ogonekvowels) + any(OgoKeys) > index(OgonekVowels,1)

c if an accent is present and another is added, it should replace not stack
c already-accented vowel + accent = fresh accented vowel
any(AcuteVowels) + any(AcuteKeys) > index(AcuteVowels,1)
any(CaronVowels) + any(AcuteKeys) > index(AcuteVowels,1)
any(CircumflexVowels) + any(AcuteKeys) > index(AcuteVowels,1)
any(AcuteVowels) + any(CaronKeys) > index(CaronVowels,1)
any(CaronVowels) + any(CaronKeys) > index(CaronVowels,1)
any(CircumflexVowels) + any(CaronKeys) > index(CaronVowels,1)
any(AcuteVowels) + any(CircKeys) > index(CircumflexVowels,1)
any(CaronVowels) + any(CircKeys) > index(CircumflexVowels,1)
any(CircumflexVowels) + any(CircKeys) > index(CircumflexVowels,1)

c prevent lowline and ogonek from stacking
any(OgonekVowels) + any(LowLineKeys) > index(OgonekVowels,1)
U+0332 + any(OgoKeys) > U+0332

c accent + new accent = single new accent
any(accents) + any(AcuteKeys) > U+0301
any(accents) + any(CaronKeys) > U+030C
any(accents) + any(CircKeys) > U+0302

c to add both ogonek and accent on a vowel, ensure the ogonek always joins, and accent is a diacritic
c vowelaccent plus ogonek = vowelogonek plus accent
any(AcuteVowels) + any(OgoKeys) > index(OgonekVowels,1) U+0301
any(CaronVowels) + any(OgoKeys) > index(OgonekVowels,1) U+030C
any(CircumflexVowels) + any(OgoKeys) > index(OgonekVowels,1) U+0302
any(OgonekVowels) + any(AcuteKeys) > index (OgonekVowels,1) U+0301
any(OgonekVowels) + any(CaronKeys) > index(OgonekVowels,1) U+030C
any(OgonekVowels) + any(CircKeys) > index(OgonekVowels,1) U+0302

c if low line combined with t/d, shortcut to underlined ts/dz
'd' + any(LowLineKeys) > 'd̲z̲'
'D' + any(LowLineKeys) > 'D̲z̲'
't' + any(LowLineKeys) > 't̲s̲'
'T' + any(LowLineKeys) > 'T̲s̲'

c Backspacing rules
any(AcuteVowels) + [K_BKSP] > index(BaseVowels,1)
any(CaronVowels) + [K_BKSP] > index(BaseVowels,1)
any(CircumflexVowels) + [K_BKSP] > index(BaseVowels,1)
any(OgonekVowels) + [K_BKSP] > index(BaseVowels,1)


c Desktop layout

c special letter keys also behave correctly with capslock
store(&CasedKeys) [K_F] [K_P] [K_Q] [K_R] [K_V] [K_X] [K_LBRKT] [T_DZ] [T_S] [T_TS] [T_Z]

c LINE 0

c combining ogonek
+ [K_BKQUOTE] > U+0328

c combining lowline
+ [SHIFT K_BKQUOTE] > U+0332
+ [ALT K_BKQUOTE] > '`'
+ [SHIFT ALT K_BKQUOTE] > '~'


c LINE 1

+ [K_Q] > 'ą'
+ [SHIFT K_Q] > 'Ą'
+ [ALT K_Q] > 'q'
+ [SHIFT ALT K_Q] > 'Q'

+ [K_R] > 'ę'
+ [SHIFT K_R] > 'Ę'
+ [ALT K_R] > 'r'
+ [SHIFT ALT K_R] > 'R'

+ [K_P] > 'ǫ'
+ [SHIFT K_P] > 'Ǫ'
+ [ALT K_P] > 'p'
+ [SHIFT ALT K_P] > 'P'

+ [K_LBRKT] > 'z̲'
+ [SHIFT K_LBRKT] > 'Z̲'
+ [ALT K_LBRKT] > '['
+ [SHIFT ALT K_LBRKT] > '{'

+ [K_RBRKT] > '/'
+ [SHIFT K_RBRKT] > '\'
+ [ALT K_RBRKT] > ']'
+ [SHIFT ALT K_RBRKT] > '}'

c caron
+ [K_BKSLASH] > '̌'

c circumflex
+ [SHIFT K_BKSLASH] > '̂'
+ [ALT K_BKSLASH] > '\'
+ [SHIFT ALT K_BKSLASH] > '|'


c LINE 2

+ [K_F] > 't̲'
+ [SHIFT K_F] > 'T̲'
+ [ALT K_F] > 'f'
+ [SHIFT ALT K_F] > 'F'

+ [K_COLON] > 'ʔ'
+ [SHIFT K_COLON] > 'ʔ'

+ [K_QUOTE] > '’'
+ [ALT K_QUOTE] > U+0027


c LINE 3

+ [K_X] > 's̲'
+ [SHIFT K_X] > 'S̲'
+ [ALT K_X] > 'x'
+ [SHIFT ALT K_X] > 'X'

+ [K_V] > 'd̲'
+ [SHIFT K_V] > 'D̲'
+ [ALT K_V] > 'v'
+ [SHIFT ALT K_V] > 'V'

+ [SHIFT K_COMMA] > ';'
+ [ALT K_COMMA] > '<'

+ [SHIFT K_PERIOD] > ':'
+ [ALT K_PERIOD] > '>'

c acute
+ [K_SLASH] > '́'
+ [ALT K_SLASH] > '/'



c don't double other diacritics like ogonek, low line
U+0328 + [K_BKQUOTE] > U+0328
U+0332 + [SHIFT K_BKQUOTE] > U+0332
