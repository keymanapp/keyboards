store(&VERSION) "10.0"
store(&KEYBOARDVERSION) "9.2"
store(&TARGETS) "any"
c store(&ETHNOLOGUECODE) "bea"
store(&COPYRIGHT) "(c) 2015-2018 FirstVoices, SIL International, 2015 First Peoples' Cultural Foundation"
store(&NAME) 'Dane-Z̲aa Z̲áágéʔ'
store(&VISUALKEYBOARD) 'fv_dane_zaa_zaage.kvks'
store(&LAYOUTFILE) 'fv_dane_zaa_zaage.keyman-touch-layout'

begin Unicode > use(main)

group(main) using keys


c special letter keys also behave correctly with capslock
store(&CasedKeys) [K_F] [K_P] [K_Q] [K_R] [K_V] [K_X] [K_LBRKT] [T_DZ] [T_S] [T_TS] [T_Z]

c LINE 0

c combining ogonek
+ [K_BKQUOTE] > U+0328
c combining lowline
+ [SHIFT K_BKQUOTE] > U+0332
+ [ALT K_BKQUOTE] > '`'
+ [SHIFT ALT K_BKQUOTE] > '~'


c LINE 1

+ [K_Q] > 'ą'
+ [SHIFT K_Q] > 'Ą'
+ [ALT K_Q] > 'q'
+ [SHIFT ALT K_Q] > 'Q'

+ [K_R] > 'ę'
+ [SHIFT K_R] > 'Ę'
+ [ALT K_R] > 'r'
+ [SHIFT ALT K_R] > 'R'

+ [K_P] > 'ǫ'
+ [SHIFT K_P] > 'Ǫ'
+ [ALT K_P] > 'p'
+ [SHIFT ALT K_P] > 'P'

+ [K_LBRKT] > 'z̲'
+ [SHIFT K_LBRKT] > 'Z̲'
+ [ALT K_LBRKT] > '['
+ [SHIFT ALT K_LBRKT] > '{'

+ [K_RBRKT] > '/'
+ [SHIFT K_RBRKT] > '\'
+ [ALT K_RBRKT] > ']'
+ [SHIFT ALT K_RBRKT] > '}'

c caron
+ [K_BKSLASH] > '̌'
c circumflex
+ [SHIFT K_BKSLASH] > '̂'
+ [ALT K_BKSLASH] > '\'
+ [SHIFT ALT K_BKSLASH] > '|'


c LINE 2

+ [K_F] > 't̲'
+ [SHIFT K_F] > 'T̲'
+ [ALT K_F] > 'f'
+ [SHIFT ALT K_F] > 'F'

+ [K_COLON] > 'ʔ'
+ [SHIFT K_COLON] > 'ʔ'

+ [K_QUOTE] > '’'
+ [ALT K_QUOTE] > U+0027


c LINE 3

+ [K_X] > 's̲'
+ [SHIFT K_X] > 'S̲'
+ [ALT K_X] > 'x'
+ [SHIFT ALT K_X] > 'X'

+ [K_V] > 'd̲'
+ [SHIFT K_V] > 'D̲'
+ [ALT K_V] > 'v'
+ [SHIFT ALT K_V] > 'V'

+ [SHIFT K_COMMA] > ';'
+ [ALT K_COMMA] > '<'

+ [SHIFT K_PERIOD] > ':'
+ [ALT K_PERIOD] > '>'

c acute
+ [K_SLASH] > '́'
+ [ALT K_SLASH] > '/'


c TOUCH COMBINED KEYS
+ [T_S] > 's̲'
+ [SHIFT T_S] > 'S̲'
+ [T_Z] > 'z̲'
+ [SHIFT T_Z] > 'Z̲'
+ [T_DZ] > 'd̲z̲'
+ [SHIFT T_DZ] > 'D̲Z̲'
+ [T_DZ_MIDSHIFT] > 'D̲z̲'
+ [T_TS] > 't̲s̲'
+ [SHIFT T_TS] > 'T̲S̲'
+ [T_TS_MIDSHIFT] > 'T̲s̲'


c ogonek should join with vowels
store(basesomevowels) "aeoAEO"
store(ogonekvowels) U+0105 U+0119 U+01EB U+0104 U+0118 U+01EA
c basevowel aeo plus ogonek = combined vowelogonek
any(basesomevowels) + [K_BKQUOTE] > index(ogonekvowels,1)

c accents should join with vowels
store(basevowels) "aeiouAEIOU"
store(accents) U+0301 U+030C U+0302
store(acutevowels) U+00E1 U+00E9 U+00ED U+00F3 U+00FA U+00C1 U+00C9 U+00CD U+00D3 U+00DA
store(caronvowels) U+01CE U+011B U+01D0 U+01D2 U+01D4 U+01CD U+011A U+01CF U+01D1 U+01D3
store(circumflexvowels) U+00E2 U+00EA U+00EE U+00F4 U+00FB U+00C2 U+00CA U+00CE U+00D4 U+00DB
c basevowel + accent = combined
any(basevowels) + [K_SLASH] > index(acutevowels,1)
any(basevowels) + [K_BKSLASH]> index(caronvowels,1)
any(basevowels) + [SHIFT K_BKSLASH] > index(circumflexvowels,1)

c if an accent is present and another is added, it should replace not stack
c already-accented vowel + accent = different combined
any(caronvowels) + [K_SLASH] > index(acutevowels,1)
any(circumflexvowels) + [K_SLASH] > index(acutevowels,1)
any(acutevowels) + [K_BKSLASH] > index(caronvowels,1)
any(circumflexvowels) + [K_BKSLASH] > index(caronvowels,1)
any(caronvowels) + [SHIFT K_BKSLASH] > index(circumflexvowels,1)
any(acutevowels) + [SHIFT K_BKSLASH] > index(circumflexvowels,1)
c accent + new accent = new accent
any(accents) + [K_SLASH] > U+0301
any(accents) + [K_BKSLASH] > U+030C
any(accents) + [SHIFT K_BKSLASH] > U+0302

c if accent joined with vowel and ogonek added, switch order so ogonek is joined, not accent
store(acutesomevowels) U+00E1 U+00E9 U+00F3 U+00C1 U+00C9 U+00D3
store(caronsomevowels) U+01CE U+011B U+01D2 U+01CD U+011A U+01D1
store(circumflexsomevowels) U+00E2 U+00EA U+00F4 U+00C2 U+00CA U+00D4
c vowelaccent plus ogonek = combined vowelogonek plus accent
any(acutesomevowels) + [K_BKQUOTE] > index(ogonekvowels,1) U+0301
any(caronsomevowels) + [K_BKSLASH] > index(ogonekvowels,1) U+030C
any(circumflexsomevowels) + [SHIFT K_BKSLASH] > index(ogonekvowels,1) U+0302

c if low line combined with t/d, shortcut to underlined ts/dz
'd' + [SHIFT K_BKQUOTE] > 'd̲z̲'
'D' + [SHIFT K_BKQUOTE] > 'D̲z̲'
't' + [SHIFT K_BKQUOTE] > 't̲s̲'
'T' + [SHIFT K_BKQUOTE] > 'T̲s̲'

