store(&VERSION) "10.0"
store(&KEYBOARDVERSION) '9.5.1'
c store(&ETHNOLOGUECODE) "hei"
store(&COPYRIGHT) '(c) 2015-2024 FirstVoices, SIL International, 2015 First Peoples' U+0027 ' Cultural Foundation'
store(&NAME) 'Háiɫzaqvḷa'
store(&TARGETS) 'any'

store(&LAYOUTFILE) 'fv_hailzaqvla.keyman-touch-layout'
store(&VISUALKEYBOARD) 'fv_hailzaqvla.kvks'

begin Unicode > use(main)

group(main) using keys

c special letter keys also behave correctly with capslock
store(&CasedKeys) [K_BKQUOTE] [K_BKSLASH] [K_RBRKT] [K_LBRKT]

+ [K_BKQUOTE] > 'ħ'
+ [K_BKSLASH] > 'ɫ'
+ [K_RBRKT] > 'ƛ'
+ [K_LBRKT] > 'λ'

+ [SHIFT K_BKQUOTE] > 'Ħ'
+ [SHIFT K_BKSLASH] > 'Ɫ'
+ [SHIFT K_LBRKT] > '⅄'
+ [SHIFT K_RBRKT] > 'ƛ'
+ [SHIFT K_COMMA] > U+0313
+ [SHIFT K_PERIOD] > nul
+ [SHIFT K_6] > U+030C
+ [SHIFT K_HYPHEN] > U+0301

+ [RALT K_SLASH] > 'ʔ'
+ [RALT K_G] > 'ǧ'
+ [RALT K_X] > 'x̌'
+ [RALT K_N] > 'ṇ'
+ [RALT K_L] > 'ḷ'
+ [RALT K_M] > 'ṃ'
+ [RALT K_BKQUOTE] > U+0300

+ [RALT K_6] > '^'
+ [RALT K_BKSLASH] > '\'
+ [RALT K_PERIOD] > '>'
+ [RALT K_COMMA] > '<'
+ [RALT K_RBRKT] > ']'
+ [RALT K_LBRKT] > '['

+ [SHIFT RALT K_X] > 'X̌'
+ [SHIFT RALT K_G] > 'Ǧ'
+ [SHIFT RALT K_N] > 'Ṇ'
+ [SHIFT RALT K_M] > 'Ṃ'
+ [SHIFT RALT K_L] > 'Ḷ'
+ [SHIFT RALT K_SLASH] > 'ʔ'

+ [SHIFT RALT K_EQUAL] > '`'
+ [SHIFT RALT K_BKSLASH] > '|'
+ [SHIFT RALT K_HYPHEN] > '_'
+ [SHIFT RALT K_PERIOD] > '>'
+ [SHIFT RALT K_COMMA] > '<'
+ [SHIFT RALT K_RBRKT] > '}'
+ [SHIFT RALT K_LBRKT] > '{'
+ [SHIFT RALT K_BKQUOTE] > '~'


c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

+ [SHIFT T_Q_0] > 'Q̓' layer('default')
+ [SHIFT T_Q] > 'Q' layer('default')
+ [SHIFT T_W_0] > 'W̓' layer('default')
+ [SHIFT T_W] > 'W' layer('default')
+ [SHIFT T_E_0] > U+00C9 layer('default')
+ [SHIFT T_E_1] > 'E̓' layer('default')
+ [SHIFT T_E] > 'E' layer('default')
+ [SHIFT T_T_0] > 'T̓' layer('default')
+ [SHIFT T_T] > 'T' layer('default')
+ [SHIFT T_Y_0] > 'Y̓' layer('default')
+ [SHIFT T_Y] > 'Y' layer('default')
+ [SHIFT T_U_0] > U+00DA layer('default')
+ [SHIFT T_U_1] > 'U̓' layer('default')
+ [SHIFT T_U] > 'U' layer('default')
+ [SHIFT T_I_0] > U+00CD layer('default')
+ [SHIFT T_I_1] > 'I̓' layer('default')
+ [SHIFT T_I] > 'I' layer('default')
+ [SHIFT T_O] > 'Ɫ' layer('default')
+ [SHIFT T_P_0] > 'P̓' layer('default')
+ [SHIFT T_P] > 'P' layer('default')
+ [SHIFT T_A_0] > U+00C1 layer('default')
+ [SHIFT T_A_1] > 'A̓' layer('default')
+ [SHIFT T_A_2] > U+01CD layer('default')
+ [SHIFT T_A_3] > "'" U+01CD layer('default')
+ [SHIFT T_A] > 'A' layer('default')
+ [SHIFT T_S] > 'S' layer('default')
+ [SHIFT T_D] > 'D' layer('default')
+ [SHIFT T_F] > '⅄' layer('default')
+ [SHIFT T_G_0] > U+01E6 layer('default')
+ [SHIFT T_G] > 'G' layer('default')
+ [SHIFT T_H_0] > 'H̓' layer('default')
+ [SHIFT T_H_1] > U+0126 layer('default')
+ [SHIFT T_H] > 'H' layer('default')
+ [SHIFT T_J_0] > 'ƛ̓' layer('default')
+ [SHIFT T_J] > 'ƛ' layer('default')
+ [SHIFT T_K_0] > 'K̓' layer('default')
+ [SHIFT T_K] > 'K' layer('default')
+ [SHIFT T_L_0] > 'L̓' layer('default')
+ [SHIFT T_L_1] > U+1E36 layer('default')
+ [SHIFT T_L_2] > U+1E36 U+0301 layer('default')
+ [SHIFT T_L_3] > U+1E36 U+0313 layer('default')
+ [SHIFT T_L] > 'L' layer('default')
+ [SHIFT T_COLON] > "'" layer('default')
+ [SHIFT T_Z] > 'Z' layer('default')
+ [SHIFT T_X_0] > 'X̌' layer('default')
+ [SHIFT T_X] > 'X' layer('default')
+ [SHIFT T_C_0] > 'C̓' layer('default')
+ [SHIFT T_C] > 'C' layer('default')
+ [SHIFT T_V] > 'V' layer('default')
+ [SHIFT T_B] > 'B' layer('default')
+ [SHIFT T_N_0] > 'N̓' layer('default')
+ [SHIFT T_N_1] > U+1E46 layer('default')
+ [SHIFT T_N_2] > U+1E46 U+0301 layer('default')
+ [SHIFT T_N_3] > U+1E46 U+0313 layer('default')
+ [SHIFT T_N] > 'N' layer('default')
+ [SHIFT T_M_0] > 'M̓' layer('default')
+ [SHIFT T_M_1] > U+1E42 layer('default')
+ [SHIFT T_M_2] > U+1E42 U+0301 layer('default')
+ [SHIFT T_M_3] > U+1E42 U+0313 layer('default')
+ [SHIFT T_M] > 'M' layer('default')
+ [SHIFT T_GLOT] > U+0294 layer('default')
+ [T_Q_0] > 'q̓'
+ [T_Q] > 'q'
+ [T_W_0] > 'w̓'
+ [T_W] > 'w'
+ [T_E_0] > U+00E9
+ [T_E_1] > 'e̓'
+ [T_E] > 'e'
+ [T_T_0] > 't̓'
+ [T_T] > 't'
+ [T_Y_0] > 'y̓'
+ [T_Y] > 'y'
+ [T_U_0] > U+00FA
+ [T_U_1] > 'u̓'
+ [T_U] > 'u'
+ [T_I_0] > U+00ED
+ [T_I_1] > 'i̓'
+ [T_I] > 'i'
+ [T_O] > 'ɫ'
+ [T_P_0] > 'p̓'
+ [T_P] > 'p'
+ [T_A_0] > U+00E1
+ [T_A_1] > 'a̓'
+ [T_A_2] > U+01CE
+ [T_A_3] > "'" U+01CE
+ [T_A] > 'a'
+ [T_S] > 's'
+ [T_D] > 'd'
+ [T_F] > 'λ'
+ [T_G_0] > U+01E7
+ [T_G] > 'g'
+ [T_H_0] > 'h̓'
+ [T_H_1] > U+0127
+ [T_H] > 'h'
+ [T_J_0] > 'ƛ̓'
+ [T_J] > 'ƛ'
+ [T_K_0] > 'k̓'
+ [T_K] > 'k'
+ [T_L_0] > 'l̓'
+ [T_L_1] > U+1E37
+ [T_L_2] > U+1E37 U+0301
+ [T_L_3] > U+1E37 U+0313
+ [T_L] > 'l'
+ [T_COLON] > "'"
+ [T_Z] > 'z'
+ [T_X_0] > 'x̌'
+ [T_X] > 'x'
+ [T_C_0] > 'c̓'
+ [T_C] > 'c'
+ [T_V] > 'v'
+ [T_B] > 'b'
+ [T_N_0] > 'n̓'
+ [T_N_1] > U+1E47
+ [T_N_2] > U+1E47 U+0301
+ [T_N_3] > U+1E47 U+0313
+ [T_N] > 'n'
+ [T_M_0] > 'm̓'
+ [T_M_1] > U+1E43
+ [T_M_2] > U+1E43 U+0301
+ [T_M_3] > U+1E43 U+0313
+ [T_M] > 'm'
+ [T_GLOT] > U+0294

store(NeedsComApos) 'acehiklḷmṃnṇpqtuwyƛACEHIKLḶMṂNṆPQTUWYƛ'
store(ComApos) [T_COMMA]
any(NeedsComApos) + any(ComApos) > index(NeedsComApos,1) U+0313

c --- END OF TOUCH LAYOUT RULES ---

c --- DIACRITIC MANAGEMENT RULES ---

c diacritics
c combining apostr [SHIFT K_COMMA] U+0313
c combining caron [SHIFT K_6] U+030C
c combining acute [SHIFT K_HYPHEN] U+0301
c combining grave [RALT K_BKQUOTE] U+0300

c Accents on vowels should create single character version
store(vowels) 'aeiouAEIOU'
store(acutevowels) U+00E1 U+00E9 U+00ED U+00F3 U+00FA U+00C1 U+00C9 U+00CD U+00D3 U+00DA
store(gravevowels) U+00E0 U+00E8 U+00EC U+00F2 U+00F9 U+00C0 U+00C8 U+00CC U+00D2 U+00D9
store(caronvowels) U+01CE U+011B U+01D0 U+01D2 U+01D4 U+01CD U+011A U+01CF U+01D1 U+01D3
any(vowels) + [SHIFT K_HYPHEN] > index(acutevowels,1)
any(vowels) + [RALT K_BKQUOTE] > index(gravevowels,1)
any(vowels) + [SHIFT K_6] > index(caronvowels,1)
c caron + g should create single character version
store(plaing) 'gG'
store(carong) 'ǧǦ'
any(plaing) + [SHIFT K_6] > index(carong, 1)
c If a diacritic is added after a diacritic, replace it instead of stacking
store(diacritics) U+0313 U+0301 U+0300 U+030C
any(diacritics) + [SHIFT K_COMMA] > U+0313
any(diacritics) + [SHIFT K_6] > U+030C
any(diacritics) + [SHIFT K_HYPHEN] > U+0301
any(diacritics) + [RALT K_BKQUOTE] > U+0300
c if a diacritic added to a combined character, replace with the right new diacritic instead of stacking
c adding acute
any(acutevowels) + [SHIFT K_HYPHEN] > index(acutevowels,1)
any(gravevowels) + [SHIFT K_HYPHEN] > index(acutevowels,1)
any(caronvowels) + [SHIFT K_HYPHEN] > index(acutevowels,1)
any(vowels) any(diacritics) + [SHIFT K_HYPHEN] > index(acutevowels,1)
any(carong) + [SHIFT K_HYPHEN] > index(plaing,1) U+0301
c adding grave
any(acutevowels) + [RALT K_BKQUOTE] > index(gravevowels,1)
any(gravevowels) + [RALT K_BKQUOTE] > index(gravevowels,1)
any(caronvowels) + [RALT K_BKQUOTE] > index(gravevowels,1)
any(vowels) any(diacritics) + [RALT K_BKQUOTE] > index(gravevowels,1)
any(carong) + [RALT K_BKQUOTE] > index(plaing,1) U+0300
c adding caron
any(acutevowels) + [SHIFT K_6] > index(caronvowels,1)
any(gravevowels) + [SHIFT K_6] > index(caronvowels,1)
any(caronvowels) + [SHIFT K_6] > index(caronvowels,1)
any(plaing) any(diacritics) + [SHIFT K_6] > index(carong,1)
any(carong) + [SHIFT K_6] > index(carong,1)
c adding apostrophe/glottal
any(acutevowels) + [SHIFT K_COMMA] > index(vowels,1) U+0313
any(gravevowels) + [SHIFT K_COMMA] > index(vowels,1) U+0313
any(caronvowels) + [SHIFT K_COMMA] > index(vowels,1) U+0313
any(carong) + [SHIFT K_COMMA] > index(plaing,1) U+0313

c shift-period deadkey should make sonorants with dot
store(basesonorants) 'lmnLMN'
store(dotsonorants) U+1E37 U+1E43 U+1E47 U+1E36 U+1E42 U+1E46
any(basesonorants) + [SHIFT K_PERIOD] > index(dotsonorants,1)
any(basesonorants) any(diacritics) + [SHIFT K_PERIOD] > index(dotsonorants,1) index(diacritics,2)
