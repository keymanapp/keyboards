store(&VERSION) "10.0"
store(&KEYBOARDVERSION) '10.0'
store(&TARGETS) 'any'
c store(&ETHNOLOGUECODE) "clm"
store(&COPYRIGHT) '(c) 2015-2024 FirstVoices, SIL International, 2015 First Peoples' U+0027 ' Cultural Foundation'
store(&NAME) 'Nəxʷsƛ̓ay̓əmúcən'
store(&VISUALKEYBOARD) 'fv_nexwslayemucen.kvks'

begin Unicode > use(main)

group(main) using keys


c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

store(&LAYOUTFILE) "fv_nexwslayemucen.keyman-touch-layout"

C MOBILE - shift layer
+ [SHIFT T_Q_0] > 'Q̕' layer('default')
+ [SHIFT T_Q] > 'Q' layer('default')
+ [SHIFT T_W_0] > 'Q̕ʷ' layer('default')
+ [SHIFT T_W] > 'Qʷ' layer('default')
+ [SHIFT T_E_0] > 'W̕' layer('default')
+ [SHIFT T_E] > 'W' layer('default')
+ [SHIFT T_R] > 'ʷ' layer('default')
+ [SHIFT T_T_0] > 'Ə́' layer('default')
+ [SHIFT T_T_1] > 'E' layer('default')
+ [SHIFT T_T_2] > 'É' layer('default')
+ [SHIFT T_T] > 'Ə' layer('default')
+ [SHIFT T_Y_0] > 'T̕' layer('default')
+ [SHIFT T_Y] > 'T' layer('default')
+ [SHIFT T_U_0] > 'Y̕' layer('default')
+ [SHIFT T_U] > 'Y' layer('default')
+ [SHIFT T_I_0] > 'Ú' layer('default')
+ [SHIFT T_I] > 'U' layer('default')
+ [SHIFT T_O_0] > 'Í' layer('default')
+ [SHIFT T_O] > 'I' layer('default')
+ [SHIFT T_P_0] > 'P̕' layer('default')
+ [SHIFT T_P] > 'P' layer('default')

+ [SHIFT T_A_0] > 'Á' layer('default')
+ [SHIFT T_A] > 'A' layer('default')
+ [SHIFT T_S] > 'S' layer('default')
+ [SHIFT T_D] > 'Š' layer('default')
+ [SHIFT T_H] > 'H' layer('default')
+ [SHIFT T_J] > 'ʔ' layer('default')
+ [SHIFT T_K_1] > 'K' layer('default')
+ [SHIFT T_K_0] > 'K̕ʷ' layer('default')
+ [SHIFT T_K] > 'Kʷ' layer('default')
+ [SHIFT T_L_0] > 'L' layer('default')
+ [SHIFT T_L] > 'ɬ' layer('default')
+ [SHIFT T_COLON] > 'ƛ̕' layer('default')

+ [SHIFT T_Z_0] > 'X̣ʷ' layer('default')
+ [SHIFT T_Z] > 'X̣' layer('default')
+ [SHIFT T_X] > 'Xʷ' layer('default')
+ [SHIFT T_C_0] > 'C̕' layer('default')
+ [SHIFT T_C] > 'C' layer('default')
+ [SHIFT T_V_0] > 'Č̕' layer('default')
+ [SHIFT T_V] > 'Č' layer('default')
+ [SHIFT T_B_0] > 'Ŋ̕' layer('default')
+ [SHIFT T_B] > 'Ŋ' layer('default')
+ [SHIFT T_N_0] > 'N̕' layer('default')
+ [SHIFT T_N] > 'N' layer('default')
+ [SHIFT T_M_0] > 'M̕' layer('default')
+ [SHIFT T_M] > 'M' layer('default')

C MOBILE - default layer
+ [T_Q_0] > 'q̕'
+ [T_Q] > 'q'
+ [T_W_0] > 'q̕ʷ'
+ [T_W] > 'qʷ'
+ [T_E_0] > 'w̕'
+ [T_E] > 'w'
+ [T_R] > 'ʷ'
+ [T_T_0] > 'ə́'
+ [T_T_1] > 'e'
+ [T_T_2] > 'é'
+ [T_T] > 'ə'
+ [T_Y_0] > 't̕'
+ [T_Y] > 't'
+ [T_U_0] > 'y̕'
+ [T_U] > 'y'
+ [T_I_0] > 'ú'
+ [T_I] > 'u'
+ [T_O_0] > 'í'
+ [T_O] > 'i'
+ [T_P_0] > 'p̕'
+ [T_P] > 'p'

+ [T_A_0] > 'á'
+ [T_A] > 'a'
+ [T_S] > 's'
+ [T_D] > 'š'
+ [T_H] > 'h'
+ [T_J] > 'ʔ'
+ [T_K_1] > 'k'
+ [T_K_0] > 'k̕ʷ'
+ [T_K] > 'kʷ'
+ [T_L_0] > 'l'
+ [T_L] > 'ɬ'
+ [T_COLON] > 'ƛ̕'

+ [T_Z_0] > 'x̣ʷ'
+ [T_Z] > 'x̣'
+ [T_X] > 'xʷ'
+ [T_C_0] > 'c̕'
+ [T_C] > 'c'
+ [T_V_0] > 'č̕'
+ [T_V] > 'č'
+ [T_B_0] > 'ŋ̕'
+ [T_B] > 'ŋ'
+ [T_N_0] > 'n̕'
+ [T_N] > 'n'
+ [T_M_0] > 'm̕'
+ [T_M] > 'm'

c --- END OF TOUCH LAYOUT RULES ---


C CONSTRAINTS AND RULES
c Notes:
c desktop and mobile glottal accent share rules
c there are seperate rules for desktop and mobile acute accent
c desktop acute/grave accent is typed BEFORE the letter
c mobile does not have grave accent

c basic stores
store(plainVowels) "aeiuAEIU" c no letter "o"
store(acuteVowels) "áéíúÁÉÍÚ"
store(graveVowels) "àèìùÀÈÌÙ"
store(needsGlottal) "čcmlnŋwyptkqČCMLNŊWYPTKQ"

c desktop deadkeys
store(desktopAcuteKey) ";"
store(desktopGraveKey) "]"

c combining glottal above right
store(glottalKeys) [T_F] [SHIFT T_F] [K_D]
any(needsGlottal) + any(glottalKeys) > index(needsGlottal,1) U+0315

c acute accent - MOBILE
store(acuteKeys) [T_ACUTE] [SHIFT T_ACUTE]
any(plainVowels) + any(acuteKeys) > index(acuteVowels,1) c combining acute deadkey
store(schwa) "əƏ"
any(schwa) + any(acuteKeys) > index(schwa,1) U+0301

c acute accent - DESKTOP
c note the acute deadkey has some extra combinations (borrowed from Montler's KlallamU layout)
any(desktopAcuteKey) + any(plainVowels) > index(acuteVowels,2)
any(desktopAcuteKey) + [K_F] > 'ə' U+0301
any(desktopAcuteKey) + [K_Q] > "qʷ" c from Montler's KlallamU layout
any(desktopAcuteKey) + [K_Z] > 'x̣ʷ' c from Montler's KlallamU layout
any(desktopAcuteKey) + [K_X] > 'x̣ʷ' c from Montler's KlallamU layout
any(desktopAcuteKey) + [K_L] > 'l' c from Montler's KlallamU layout
any(desktopAcuteKey) + [K_PERIOD] > 'ˑ' c from Montler's KlalamU layout
any(desktopAcuteKey) + [K_BKSLASH] > '√' c from Montler's KlalamU layout
any(desktopAcuteKey) + [K_EQUAL] > '≠' c from Montler's KlalamU layout

c grave accent - DESKTOP
any(desktopGraveKey) + any(plainVowels) > index(graveVowels,2)
any(desktopGraveKey) + [K_F] > 'ə' U+0300

c letters with hacek - backspace rule
store(noHacek) "csCS"
store(hasHacek) "čšČŠ"
any(hasHacek) + [K_BKSP] > index(noHacek,1) c backspace on čš will leave the plain letter behind

c accented vowels - backspace rules
any(acuteVowels) + [K_BKSP] > index(plainVowels,1) c backspace on acute vowel will leave a plain vowel behind
any(graveVowels) + [K_BKSP] > index(plainVowels,1) c backspace on grave vowel will leave a plain vowel behind

c combining glottal on rounded sounds
'qʷ' + any(glottalKeys) > 'q' U+0315 'ʷ'
'kʷ' + any(glottalKeys) > 'k' U+0315 'ʷ'

c stacking prevention rules
store(NumbersAndPunctuation) '1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"  
any(NumbersAndPunctuation) + any(glottalKeys) > context beep
c any(NumbersAndPunctuation) + any(desktopGlottalKey) > context beep
any(NumbersAndPunctuation) + any(acuteKeys) > context beep
c any(NumbersAndPunctuation) + any(desktopAcuteKey) > context beep
c any(NumbersAndPunctuation) + any(desktopGraveKey) > context beep
U+0315 + any(glottalKeys) > context beep

C ~*~*~*~*~*~*~*~
C DESKTOP LAYOUT
C The desktop layout is based on the original KlallamU keyboard by Tim Montler.
C see also: extra combinations in the "rules" part of this *.kmn file

C language specific keys
+ [K_B] > "č"
+ [K_F] > "ə"
+ [K_G] > "ŋ"
+ [K_J] > "ʔ"
+ [K_K] > "kʷ"
+ [K_L] > "ɬ"
+ [K_O] > "xʷ"
+ [K_R] > "ʷ"
+ [K_V] > "š"
+ [K_Z] > "x̣"
"u" + "i" > "ƛ̕" c "ui" is glottalized barred lambda

c other
c from Montler's KlallamU layout
c see also: the "rules" section of this *.kmn has additional combinations
"." + "." > "…" c ellipsis

c deadkey - original keys by typing the letter twice
any(desktopGraveKey) + "]" > "]"
any(desktopAcuteKey) + ";" > ";"
c any(desktopGlottalKey) + "d" > "d"

C original keys with right-alt
+ [RALT K_COLON] > ";"
+ [RALT K_RBRKT] > "]"
+ [RALT K_D] > "d"

+ [RALT K_B] > "b"
+ [RALT K_F] > "f"
+ [RALT K_G] > "g"
+ [RALT K_J] > "j"
+ [RALT K_K] > "k"
+ [RALT K_L] > "l"
+ [RALT K_O] > "o"
+ [RALT K_R] > "r"
+ [RALT K_V] > "v"
+ [RALT K_Z] > "z"
