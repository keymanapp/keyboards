store(&VERSION) "9.0"
store(&KEYBOARDVERSION) "9.0"
store(&TARGETS) "windows macosx"
store(&ETHNOLOGUECODE) "kwk"
store(&COPYRIGHT) "© 2016 FirstVoices, Tavultesoft. Portions © 2006 Chris Harvey."
store(&NAME) 'Kwak̕wala'
store(&BITMAP) 'fv_kwakwala.bmp'
store(&VISUALKEYBOARD) 'fv_kwakwala.kvk'
begin Unicode > use(constraints)

group(constraints) using keys
                
c Diacritics

store(underaccent) U+0331
store(underaccentkey) ";"

store(overaccent) U+0301 U+0313 U+0315
store(overaccentkey) "^]/"

any(underaccent) + any(underaccentkey) > context beep
any(overaccent) + any(overaccentkey) > context beep  
any(underaccent) any(overaccent) + any(underaccentkey) > context beep

store(kk) \
[CTRL ALT K_PERIOD] [CTRL ALT K_COMMA] [SHIFT CTRL ALT K_BKQUOTE] [CTRL ALT K_BKQUOTE] \
[SHIFT CTRL ALT K_COLON] [CTRL ALT K_COLON] [CTRL ALT K_SLASH] [CTRL ALT K_QUOTE] \
[CTRL ALT K_BKSLASH] [SHIFT CTRL ALT K_BKSLASH] [SHIFT CTRL ALT K_RBRKT] \
[SHIFT CTRL ALT K_LBRKT] [CTRL ALT K_RBRKT] [CTRL ALT K_LBRKT] [CTRL ALT K_EQUAL] \
[CTRL ALT K_HYPHEN] [SHIFT CTRL ALT K_0] [SHIFT CTRL ALT K_9] [CTRL ALT K_0]  [CTRL ALT K_9] 
                                                                                             
+ any(kk) > use(main)
nomatch > use(main)                                       
 
c nomatch(all) > use(main)
group(main) using keys   

c original punctuation
+ [RALT K_PERIOD] > '>'
+ [RALT K_COMMA] > '<'
+ [SHIFT RALT K_BKQUOTE] > '~'
+ [RALT K_BKQUOTE] > '`'
+ [SHIFT RALT K_COLON] > ':'
+ [RALT K_COLON] > ';'
+ [RALT K_SLASH] > '/'
+ [RALT K_QUOTE] > U+0027
+ [RALT K_BKSLASH] > '\'
+ [SHIFT RALT K_BKSLASH] > '|'
+ [SHIFT RALT K_RBRKT] > '}'
+ [SHIFT RALT K_LBRKT] > '{'
+ [RALT K_RBRKT] > ']'
+ [RALT K_LBRKT] > '['
+ [RALT K_EQUAL] > '+'
+ [RALT K_HYPHEN] > '_' 

c Guillemets
+ [SHIFT RALT K_0] > '›'
+ [SHIFT RALT K_9] > '‹'
+ [RALT K_0] > '»'
+ [RALT K_9] > '«'

c Quotes
+ [SHIFT K_equal] > "”"
+ [SHIFT K_hyphen] > "“"
+ [K_BKQUOTE] > "‘"
+ [shift K_rbrkt] > "’"  
       
c Diacritics

 + ";" > "̱"
 + "^" > "́"
 + "]" > "̕"
 + "/" > "̓"
      
c Language

+ "~" > "·"
                                             
+ [caps shift K_LBRKT] > 'ł'   
+ [ncaps shift K_LBRKT] > 'Ł'
+ [caps K_LBRKT] > 'Ł'
+ [ncaps K_LBRKT] > 'ł'    

match > use(normalize)
nomatch > use(x)

group(normalize)

any(overaccent) any(underaccent) > context(2) context(1)


group(x) using keys

store(allkeys) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"
+ any(allkeys) > index(allkeys, 1)

