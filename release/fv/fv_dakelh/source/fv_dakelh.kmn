store(&VERSION) "9.0"
store(&KEYBOARDVERSION) "9.0"
store(&TARGETS) "windows macosx"
store(&ETHNOLOGUECODE) "caf"
store(&COPYRIGHT) "© 2016 FirstVoices, Tavultesoft. Portions © 2006 Chris Harvey."
store(&NAME) 'Dakelh'
store(&BITMAP) 'fv_dakelh.bmp'
store(&VISUALKEYBOARD) 'fv_dakelh.kvk'
begin Unicode > use(main)


group(main) using keys   
                                 
c ************
c constraints
c ***********

store(underaccent)  "̲" 
store(underaccentkey) [K_Bkquote]

store(overaccent)  '́'
store(overaccentkey) [K_Slash]
                                                                                                             
any(underaccent) + any(underaccentkey) > context beep
any(overaccent) + any(overaccentkey) > context beep  
any(underaccent) any(overaccent) + any(underaccentkey) > context beep

store(accentkey) [K_Slash] [K_Bkquote]                
store(NumbersAndPunctuation) '1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?‘’“”‹›«»' "'"  c preventing accents on non-letters
any(NumbersAndPunctuation) + any(accentkey) > context beep                   
                                                                                                                  
c Original Punctuation

+ [RALT K_PERIOD] > '>'
+ [RALT K_COMMA] > '<'
+ [SHIFT RALT K_BKQUOTE] > '~'
+ [RALT K_BKQUOTE] > '`'
+ [SHIFT RALT K_COLON] > ':'
+ [RALT K_COLON] > ';'
+ [RALT K_SLASH] > '/'
+ [RALT K_QUOTE] > U+0027
+ [RALT K_BKSLASH] > '\'
+ [SHIFT RALT K_BKSLASH] > '|'
+ [SHIFT RALT K_RBRKT] > '}'
+ [SHIFT RALT K_LBRKT] > '{'
+ [RALT K_RBRKT] > ']'
+ [RALT K_LBRKT] > '['
+ [RALT K_EQUAL] > '+'
+ [RALT K_HYPHEN] > '_' 

c Guillemets
+ [SHIFT RALT K_0] > '›'
+ [SHIFT RALT K_9] > '‹'
+ [RALT K_0] > '»'
+ [RALT K_9] > '«'

c Quotes
+ [SHIFT K_equal] > "”"
+ [SHIFT K_hyphen] > "“"
+ [shift K_BKQUOTE] > "‘"
+ [K_QUOTE] > "’"      

c Language Specific

+ [K_BKQUOTE] > "̲"  
+ [shift K_PERIOD] > ':'
+ [shift K_COMMA] > ';'
+ [K_slash] > '́'

+ [caps shift k_colon] > 's̲'   
+ [ncaps shift k_colon] > 'S̲'
+ [caps k_colon] > 'S̲'
+ [ncaps k_colon] > 's̲'
                                             
+ [caps shift K_LBRKT] > 'z̲'   
+ [ncaps shift K_LBRKT] > 'Z̲'
+ [caps K_LBRKT] > 'Z̲'
+ [ncaps K_LBRKT] > 'z̲'

+ [caps shift K_RBRKT] > 'd̲z̲'   
+ [ncaps shift K_RBRKT] > 'D̲z̲'
+ [caps K_RBRKT] > 'D̲Z̲'
+ [ncaps K_RBRKT] > 'd̲z̲'

+ [caps shift K_BKSLASH] > 't̲s̲'   
+ [ncaps shift K_BKSLASH] > 'T̲s̲'
+ [caps K_BKSLASH] > 'T̲S̲'
+ [ncaps K_BKSLASH] > 't̲s̲'
                                    
match > use(normalize)
nomatch > use(x)

group(normalize)

any(overaccent) any(underaccent) > context(2) context(1)


group(x) using keys

store(allkeys) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"
+ any(allkeys) > index(allkeys, 1)


