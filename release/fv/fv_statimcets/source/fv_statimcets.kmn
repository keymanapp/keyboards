store(&VERSION) "9.0"
store(&KEYBOARDVERSION) "9.0"
store(&TARGETS) "windows macosx"
store(&ETHNOLOGUECODE) "lil"
store(&COPYRIGHT) "© 2016 FirstVoices, Tavultesoft. Portions © 2006 Chris Harvey."
store(&NAME) 'St̓át̓imcets'
store(&BITMAP) 'fv_statimcets.bmp'
store(&VISUALKEYBOARD) 'fv_statimcets.kvk'
 
begin Unicode > use(main)                      
group(main) using keys 

c non-caps modified keys
  
store(Mod0) "`~_+;'<>/"
store(Mod1) U+0300 "‘“”" U+0301 U+0313 "’·7"
+ any(Mod0) > index(Mod1,1)  

c *************
c constraints
c *************

store(overaccent)  U+0300 U+0301 U+0313
store(overaccentkey) "`;'"          

any(overaccent) + any(overaccentkey) > context beep 
                               
store(NumbersAndPunctuation) '1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?‘’“”‹›«»' "'"  c preventing accents on non-letters
any(NumbersAndPunctuation) + any(overaccentkey) > context beep

c original values
store(orig0)  [RALT K_7] [RALT K_8] [RALT K_9] [RALT K_0] [RALT K_hyphen] [RALT K_equal] [RALT K_LBRKT] [RALT K_RBRKT] [RALT K_QUOTE] [RALT K_COLON] [RALT K_COMMA] [RALT K_PERIOD]
store(orig1) "‘’«»_+[]';<>"            
store(punc0) [SHIFT RALT K_LBRKT] [SHIFT RALT K_RBRKT] [SHIFT RALT K_9] [SHIFT RALT K_0] [RALT K_BKSLASH] [SHIFT RALT K_BKSLASH]
store(punc1) "{}‹›\|"
+ any(orig0) > index(orig1,1)
+ any(punc0) > index(punc1,1) 

c other characters
store(s0) [CAPS SHIFT K_LBRKT] [CAPS K_LBRKT] [ncaps SHIFT K_LBRKT]  [ncaps K_LBRKT] 
store(s1)  "sSSs"         
+ any(s0) > index(s1,1) "̲"  

store(l0) [CAPS SHIFT K_RBRKT] [CAPS K_RBRKT] [ncaps SHIFT K_RBRKT]  [ncaps K_RBRKT] 
store(l1)  "lLLl"         
+ any(l0) > index(l1,1) "̲"        

+ [CAPS SHIFT K_BKSLASH] > "t̲s̲"
+ [CAPS K_BKSLASH]             > "T̲S̲"
+ [ncaps SHIFT K_BKSLASH]     > "T̲s̲"
+ [ncaps K_BKSLASH]                 > "t̲s̲"
     
 store(t0) [RALT CAPS SHIFT K_T] [RALT CAPS K_T] [RALT ncaps SHIFT K_T]  [RALT ncaps K_T] 
store(t1)  "tTTt"         
+ any(t0) > index(t1,1) "̲"        


nomatch > use(x)                            

group(x) using keys

store(allkeys) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"
+ any(allkeys) > index(allkeys, 1)
                                                        
