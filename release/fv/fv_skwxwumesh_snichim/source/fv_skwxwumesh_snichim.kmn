store(&VERSION) "10.0"
store(&KEYBOARDVERSION) '10.0'
c store(&ETHNOLOGUECODE) "sqa"
store(&COPYRIGHT) '(c) 2024 FirstVoices, SIL International, 2015 First Peoples' U+0027 ' Cultural Foundation'
store(&NAME) 'Sḵwx̱wú7mesh sníchim'

store(&VISUALKEYBOARD) 'fv_skwxwumesh_snichim.kvks'
store(&TARGETS) 'any'

begin Unicode > use(main)

group(main) using keys


c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

store(&LAYOUTFILE) "fv_skwxwumesh_snichim.keyman-touch-layout"

C mobile shift layer
c + [SHIFT T_Q_0] > '!' layer('default')
c + [SHIFT T_Q] > '?' layer('default')
+ [SHIFT T_W_0] > 'W̓' layer('default')
+ [SHIFT T_W] > 'W' layer('default')
+ [SHIFT T_E_0] > 'É' layer('default')
+ [SHIFT T_E] > 'E' layer('default')
+ [SHIFT T_R] > '7' layer('default')
+ [SHIFT T_T_1] > 'TL’' layer('default')
+ [SHIFT T_T_0] > 'T’' layer('default')
+ [SHIFT T_T] > 'T' layer('default')
+ [SHIFT T_TS_0] > 'TS’' layer('default')
+ [SHIFT T_TS] > 'TS' layer('default')
+ [SHIFT T_Y_0] > 'Y̓' layer('default')
+ [SHIFT T_Y] > 'Y' layer('default')
+ [SHIFT T_U_0] > 'Ú' layer('default')
+ [SHIFT T_U] > 'U' layer('default')
+ [SHIFT T_I_0] > 'Í' layer('default')
+ [SHIFT T_I] > 'I' layer('default')
+ [SHIFT T_O_0] > 'P’' layer('default')
+ [SHIFT T_O] > 'P' layer('default')
c + [SHIFT T_A_0] > 'Á' layer('default')
c + [SHIFT T_A] > 'A' layer('default')
+ [SHIFT T_S_0] > 'Á' layer('default')
+ [SHIFT T_S] > 'A' layer('default')
+ [SHIFT T_D_0] > 'SH' layer('default')
+ [SHIFT T_D] > 'S' layer('default')
+ [SHIFT T_H] > 'H' layer('default')
+ [SHIFT T_J_0] > 'Ḵ’' layer('default')
+ [SHIFT T_J] > 'Ḵ' layer('default')
+ [SHIFT T_KK_0] > 'ḴW’' layer('default')
+ [SHIFT T_KK] > 'ḴW' layer('default')
+ [SHIFT T_K_0] > 'K’' layer('default')
+ [SHIFT T_K] > 'K' layer('default')
+ [SHIFT T_KW_0] > 'KW’' layer('default')
+ [SHIFT T_KW] > 'KW' layer('default')
+ [SHIFT T_L_0] > 'L̓' layer('default')
+ [SHIFT T_L] > 'L' layer('default')
+ [SHIFT T_LH] > 'LH' layer('default')
C + [SHIFT T_COLON_0] > '"' layer('default')
+ [SHIFT T_COLON] > "’" layer('default')
+ [SHIFT T_Z] > 'X̱' layer('default')
+ [SHIFT T_XX] > 'X̱W' layer('default')
+ [SHIFT T_X] > 'X' layer('default')
+ [SHIFT T_XW] > 'XW' layer('default')
+ [SHIFT T_C_0] > 'CH’' layer('default')
+ [SHIFT T_C] > 'CH' layer('default')
c + [SHIFT T_V] > ',' layer('default')
+ [SHIFT T_B] > '.' layer('default')
+ [SHIFT T_N_0] > 'N̓' layer('default')
+ [SHIFT T_N] > 'N' layer('default')
+ [SHIFT T_M_0] > 'M̓' layer('default')
+ [SHIFT T_M] > 'M' layer('default')


C mobile unshifted layer
c + [T_Q_0] > '!'
c + [T_Q] > '?'
+ [T_W_0] > 'w̓'
+ [T_W] > 'w'
+ [T_E_0] > 'é'
+ [T_E] > 'e'
+ [T_R] > '7'
+ [T_T_1] > 'tl’'
+ [T_T_0] > 't’'
+ [T_T] > 't'
+ [T_TS_0] > 'ts’'
+ [T_TS] > 'ts'
+ [T_Y_0] > 'y̓'
+ [T_Y] > 'y'
+ [T_U_0] > 'ú'
+ [T_U] > 'u'
+ [T_I_0] > 'í'
+ [T_I] > 'i'
+ [T_O_0] > 'p’'
+ [T_O] > 'p'
c + [T_A_0] > 'á'
c + [T_A] > 'a'
+ [T_S_0] > 'á'
+ [T_S] > 'a'
+ [T_D_0] > 'sh'
+ [T_D] > 's'
+ [T_H] > 'h'
+ [T_J_0] > 'ḵ’'
+ [T_J] > 'ḵ'
+ [T_KK_0] > 'ḵw’'
+ [T_KK] > 'ḵw'
+ [T_K_0] > 'k’'
+ [T_K] > 'k'
+ [T_KW_0] > 'kw’'
+ [T_KW] > 'kw'
+ [T_L_0] > 'l̓'
+ [T_L] > 'l'
+ [T_LH] > 'lh'
C + [T_COLON_0] > '"' c double quote
+ [T_COLON] > "’"   c single quote
+ [T_Z] > 'x̱'
+ [T_XX] > 'x̱w'
+ [T_X] > 'x'
+ [T_XW] > 'xw'
+ [T_C_0] > 'ch’'
+ [T_C] > 'ch'
c + [T_V] > ','
+ [T_B] > '.'
+ [T_N_0] > 'n̓'
+ [T_N] > 'n'
+ [T_M_0] > 'm̓'
+ [T_M] > 'm'

c --- END OF TOUCH LAYOUT RULES ---


C CONSTRAINTS AND RULES

c combining deadkey for glottal apostrophe diacritic on resonants
store(plainResonants) "mlnwyMLNWY"
store(glottalKeys) [T_Q] [SHIFT T_Q] [K_BKQUOTE]
any(plainResonants) + any(glottalKeys) > index(plainResonants,1) U+0313

c Rules - vowels with acute accent
store(plainVowels) "aeiouAEIOU"
store(acuteVowels) "áéíóúÁÉÍÓÚ"
store(acuteKeys) [T_ACUTE] [SHIFT T_ACUTE] [K_COLON]
any(plainVowels) + any(acuteKeys) > index(acuteVowels,1) c combining acute deadkey
any(acuteVowels) + [K_BKSP] > index(plainVowels,1) c backspace on acute vowel will leave a plain vowel behind

c rules - k, x with macron below
store(plain_kx) "kKxX"
store(macronKeys) [T_V] [SHIFT T_V] [K_HYPHEN]
any(plain_kx) + any(macronKeys) > index(plain_kx,1) U+0331

c rules - stacking prevention
store(NumbersAndPunctuation) '1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"  c preventing accents on non-letters
any(NumbersAndPunctuation) + any(glottalKeys) > context beep                   
any(NumbersAndPunctuation) + any(acuteKeys) > context beep
any(NumbersAndPunctuation) + any(macronKeys) > context beep
U+0313 + any(glottalKeys) > context beep


C DESKTOP LAYOUT

C combining keys
+ [K_BKQUOTE] > U+0313 c glottal comma above
+ [K_COLON] > U+0301 c acute accent
+ [K_HYPHEN] > U+0331 c macron below

C other special keys
+ [K_QUOTE] > "’"
+ [K_LBRKT] > "ḵ"
+ [K_RBRKT] > "x̱"
+ [SHIFT K_LBRKT] > "Ḵ"
+ [SHIFT K_RBRKT] > "X̱"

C original keys with right-alt
+ [RALT K_BKQUOTE] > "`"
+ [RALT K_COLON] > ";"
+ [RALT K_HYPHEN] > "-"

+ [RALT K_QUOTE] > "'"
+ [RALT K_LBRKT] > "["
+ [RALT K_RBRKT] > "]"
+ [RALT SHIFT K_LBRKT] > "{"
+ [RALT SHIFT K_RBRKT] > "}"
