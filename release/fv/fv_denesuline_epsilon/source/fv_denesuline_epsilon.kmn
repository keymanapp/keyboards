store(&VERSION) "9.0"
store(&KEYBOARDVERSION) "9.0"
store(&TARGETS) "windows macosx"
store(&ETHNOLOGUECODE) "chp"
store(&COPYRIGHT) "© 2016 FirstVoices, Tavultesoft. Portions © 2006 Chris Harvey."
store(&NAME) 'Dënesųłıné'
store(&BITMAP) 'fv_Denesuline_Epsilon.bmp'
store(&VISUALKEYBOARD) 'fv_Denesuline_Epsilon.kvk'
begin Unicode > use(main)


group(main) using keys   
   
c constraints

store(underaccent)  U+0328
store(underaccentkey) [K_RBRKT]

store(overaccent)  U+0301
store(overaccentkey) [K_bkquote]

any(underaccent) + any(underaccentkey) > context beep
any(overaccent) + any(overaccentkey) > context beep  
any(underaccent) any(overaccent) + any(underaccentkey) > context beep

U+0328 U+0301 + [K_equal] > context beep
any(underaccent) + [K_equal] > context U+0301
any(overaccent) + [K_equal] > U+0328 context                                                                    
                               
store(accentkey) [K_equal] [K_Bkquote]  [K_RBRKT]
store(NumbersAndPunctuation) '1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?‘’“”‹›«»' "'"  c preventing accents on non-letters
any(NumbersAndPunctuation) + any(accentkey) > context beep

c original punctuation
+ [RALT K_PERIOD] > '>'
+ [RALT K_COMMA] > '<'
+ [SHIFT RALT K_BKQUOTE] > '~'
+ [RALT K_BKQUOTE] > '`'
+ [SHIFT RALT K_COLON] > ':'
+ [RALT K_COLON] > ';'
+ [RALT K_SLASH] > '/'
+ [RALT K_QUOTE] > U+0027
+ [RALT K_BKSLASH] > '\'
+ [SHIFT RALT K_BKSLASH] > '|'
+ [SHIFT RALT K_RBRKT] > '}'
+ [SHIFT RALT K_LBRKT] > '{'
+ [RALT K_RBRKT] > ']'
+ [RALT K_LBRKT] > '['
+ [RALT K_EQUAL] > '='    
+ [RALT shift K_EQUAL] > '+'
+ [RALT K_HYPHEN] > '_' 
+ "<" > ";"
+ ">" > ":"

c Guillemets
+ [SHIFT RALT K_0] > '›'
+ [SHIFT RALT K_9] > '‹'
+ [RALT K_0] > '»'
+ [RALT K_9] > '«'

c Quotes
+ [SHIFT K_RBRKT] > "”"
+ [SHIFT K_equal] > "“"
+ [shift K_BKQUOTE] > "‘"
+ [K_QUOTE] > "’"      

c language

+ [caps shift k_colon] > 'ł'   
+ [ncaps shift k_colon] > 'Ł'
+ [caps k_colon] > 'Ł'
+ [ncaps k_colon] > 'ł'
                                             
+ [caps shift K_LBRKT] > 'ɛ'   
+ [ncaps shift K_LBRKT] > 'Ɛ'
+ [caps K_LBRKT] > 'Ɛ'
+ [ncaps K_LBRKT] > 'ɛ'

+ [K_bkquote] > "́"     c cdmacute
+ [k_equal] >  "̨́"    c cdmogo cdmacu
+ [k_rbrkt] > "̨"      c cdmogo

+ "/" > "ʔ"

match > use(normalize)
nomatch > use(x)

group(normalize)

any(overaccent) any(underaccent) > context(2) context(1)


group(x) using keys

store(allkeys) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"
+ any(allkeys) > index(allkeys, 1)


