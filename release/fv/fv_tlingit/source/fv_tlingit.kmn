store(&VERSION) "10.0"
store(&TARGETS) 'any'
c store(&ETHNOLOGUECODE) "tli"
store(&COPYRIGHT) '(c) 2015-2024 FirstVoices, SIL International, 2015 First Peoples' U+0027 ' Cultural Foundation, parts 2007 Chris Harvey'
store(&NAME) 'Łingít'
c store(&BITMAP) 'fv_tlingit.ico'
store(&KEYBOARDVERSION) '10.1'
store(&VISUALKEYBOARD) 'fv_tlingit.kvks'

begin Unicode > use(main)

group(main) using keys

c --- START OF DESKTOP LAYOUT RULES ---

store(&CasedKeys) [K_RBRKT]
+ [SHIFT K_RBRKT] > 'Ł'
+ [K_RBRKT] > 'ł'

c combining characters
+ [K_SLASH] > context beep c macron U+0331
+ [K_BKSLASH] > context beep c ogonek U+0328
+ [K_COLON] > context beep c acute U+0301
+ [K_LBRKT] > context beep c circumflex U+0302
+ [K_BKQUOTE] > context beep c grave U+0300
c shifted combining characters
+ [SHIFT K_BKSLASH] > context beep c ogonek U+0328
+ [SHIFT K_COLON] > context beep c acute U+0301
+ [SHIFT K_LBRKT] > context beep c circumflex U+0302
+ [SHIFT K_BKQUOTE] > context beep c grave U+0300

c move punctuation off semicolon
+ [SHIFT K_PERIOD] > ':'
+ [SHIFT K_COMMA] > ';'

c original punctuation available if you press alt
+ [SHIFT ALT K_BKSLASH] > '|'
+ [SHIFT ALT K_BKQUOTE] > '~'
+ [SHIFT ALT K_RBRKT] > '}'
+ [SHIFT ALT K_LBRKT] > '{'
+ [SHIFT ALT K_COLON] > ':'
+ [SHIFT ALT K_PERIOD] > '>'
+ [SHIFT ALT K_COMMA] > '<'
+ [ALT K_BKSLASH] > '\'
+ [ALT K_COLON] > ';'
+ [ALT K_SLASH] > '/'
+ [SHIFT ALT K_SLASH] > '/' c alternate way to type slash for Google Worspace
+ [ALT K_BKQUOTE] > '`'
+ [ALT K_RBRKT] > ']'
+ [ALT K_LBRKT] > '['
c guillemets
+ [SHIFT ALT K_9] > '«'
+ [SHIFT ALT K_0] > '»'
+ [ALT K_0] > '›'
+ [ALT K_9] > '‹'

c --- END DESKTOP LAYOUT RULES ---

c --- START OF TOUCH LAYOUT RULES ---

c Touch layout rules originally imported from KeyMap.plist on Tue Nov 17 2015

store(&LAYOUTFILE) 'fv_tlingit.keyman-touch-layout'

+ [SHIFT T_W] > 'W' layer('default')
+ [SHIFT T_E_0] > 'É' layer('default')
+ [SHIFT T_E_1] > 'È' layer('default')
+ [SHIFT T_E_2] > 'Ê' layer('default')
+ [SHIFT T_E] > 'E' layer('default')
+ [SHIFT T_T] > 'T' layer('default')
+ [SHIFT T_Y] > 'Y' layer('default')
+ [SHIFT T_U_0] > 'Ú' layer('default')
+ [SHIFT T_U_1] > 'Ù' layer('default')
+ [SHIFT T_U_2] > 'Û' layer('default')
+ [SHIFT T_U] > 'U' layer('default')
+ [SHIFT T_I_0] > 'Í' layer('default')
+ [SHIFT T_I_1] > 'Ì' layer('default')
+ [SHIFT T_I_2] > 'Î' layer('default')
+ [SHIFT T_I] > 'I' layer('default')
+ [SHIFT T_O_0] > 'Ó' layer('default')
+ [SHIFT T_O_1] > 'Ò' layer('default')
+ [SHIFT T_O_2] > 'Ô' layer('default')
+ [SHIFT T_O] > 'O' layer('default')
+ [SHIFT T_P] > 'Ł' layer('default')
+ [SHIFT T_A_0] > 'Á' layer('default')
+ [SHIFT T_A_1] > 'À' layer('default')
+ [SHIFT T_A_2] > 'Â' layer('default')
+ [SHIFT T_A] > 'A' layer('default')
+ [SHIFT T_S] > 'S' layer('default')
+ [SHIFT T_D] > 'D' layer('default')
+ [SHIFT T_G] > 'G' layer('default')
+ [SHIFT T_G_0] > 'G̱' layer('default')
+ [SHIFT T_H] > 'H' layer('default')
+ [SHIFT T_J] > 'J' layer('default')
+ [SHIFT T_K] > 'K' layer('default')
+ [SHIFT T_K_0] > 'Ḵ' layer('default')
+ [SHIFT T_L] > 'L' layer('default')
+ [SHIFT T_COLON_0] > '"' layer('default')
+ [SHIFT T_COLON] > "'" layer('default')
+ [SHIFT T_Z] > 'Z' layer('default')
+ [SHIFT T_X] > 'X' layer('default')
+ [SHIFT T_X_0] > 'X̱' layer('default')
+ [SHIFT T_C] > 'C' layer('default')
+ [SHIFT T_N] > 'N' layer('default')
+ [SHIFT T_M] > 'M' layer('default')
+ [T_W] > 'w'
+ [T_E_0] > 'é'
+ [T_E_1] > 'è'
+ [T_E_2] > 'ê'
+ [T_E] > 'e'
+ [T_T] > 't'
+ [T_Y] > 'y'
+ [T_U_0] > 'ú'
+ [T_U_1] > 'ù'
+ [T_U_2] > 'û'
+ [T_U] > 'u'
+ [T_I_0] > 'í'
+ [T_I_1] > 'ì'
+ [T_I_2] > 'î'
+ [T_I] > 'i'
+ [T_O_0] > 'ó'
+ [T_O_1] > 'ò'
+ [T_O_2] > 'ô'
+ [T_O] > 'o'
+ [T_P] > 'ł'
+ [T_A_0] > 'á'
+ [T_A_1] > 'à'
+ [T_A_2] > 'â'
+ [T_A] > 'a'
+ [T_S] > 's'
+ [T_D] > 'd'
+ [T_G] > 'g'
+ [T_G_0] > 'g̱'
+ [T_H] > 'h'
+ [T_J] > 'j'
+ [T_K] > 'k'
+ [T_K_0] > 'ḵ'
+ [T_L] > 'l'
+ [T_COLON_0] > '"'
+ [T_COLON] > "'"
+ [T_Z] > 'z'
+ [T_X] > 'x'
+ [T_X_0] > 'x̱'
+ [T_C] > 'c'
+ [T_N] > 'n'
+ [T_M] > 'm'

c --- END OF TOUCH LAYOUT RULES ---
 

c combining character rules

c --MACRON--
c if macron comes after kgx, it is macron
store(macronkey) [K_SLASH] [T_B]
store(needsmacron) 'gkxGKX'
any(needsmacron) + any(macronkey) > index(needsmacron,1) U+0331
U+0331 + any(macronkey) > U+0331

c --ABOVE ACCENTS--
store(AcuteKeys) [K_COLON] [T_R]
store(CircumflexKeys) [K_LBRKT] [T_V]
store(GraveKeys) [K_BKQUOTE] [T_F]
store(OgonekKeys) [K_BKSLASH] [SHIFT K_BKSLASH] [T_Q]
store(OgoAcuKeys) [SHIFT K_COLON]
store(OgoCircKeys) [SHIFT K_LBRKT]
store(OgoGraKeys) [SHIFT K_BKQUOTE]
store(shiftedaccentkey) [SHIFT K_COLON] [SHIFT K_LBRKT] [SHIFT K_BKQUOTE] c desktop only 
store(Vowels) 'aeiouAEIOUaeiouAEIOU'
store(HasAcute) 'áéíóúÁÉÍÓÚáéíóúÁÉÍÓÚ'
store(HasGrave) 'àèìòùÀÈÌÒÙàèìòùÀÈÌÒÙ'
store(HasCircumflex) 'âêîôûÂÊÎÔÛâêîôûÂÊÎÔÛ'
store(HasOgonek) 'ąęįǫųĄĘĮǪŲąęįǫųĄĘĮǪŲ'

store(AccentKeys) [K_COLON] [K_LBRKT] [K_BKQUOTE]
store(MobileAccentKeys) [T_R] [T_V] [T_F]
store(combiningaccent) U+0301 U+0302 U+0300

any(Vowels) + any(AcuteKeys) > index(HasAcute,1)
any(Vowels) + any(CircumflexKeys) > index(HasCircumflex,1)
any(Vowels) + any(GraveKeys) > index(HasGrave,1)
any(Vowels) + any(OgonekKeys) > index(HasOgonek,1)
any(Vowels) + any(OgoAcuKeys) > index(HasOgonek,1) U+0301
any(Vowels) + any(OgoCircKeys) > index(HasOgonek,1) U+0302
any(Vowels) + any(OgoGraKeys) > index(HasOgonek,1) U+0300

any(HasAcute) + any(OgonekKeys) > index(HasOgonek,1) U+0301
any(HasCircumflex) + any(OgonekKeys) > index(HasOgonek,1) U+0302
any(HasGrave) + any(OgonekKeys) > index(HasOgonek,1) U+0300
any(HasOgonek) + any(AcuteKeys) > index(HasOgonek,1) U+0301
any(HasOgonek) + any(CircumflexKeys) > index(HasOgonek,1) U+0302
any(HasOgonek) + any(GraveKeys) > index(HasOgonek,1) U+0300


c if accent key pressed after accent, replace the accent
any(HasAcute) + any(CircumflexKeys) > index(HasCircumflex,1)
any(HasAcute) + any(GraveKeys) > index(HasGrave,1)
any(HasCircumflex) + any(AcuteKeys) > index(HasAcute,1)
any(HasCircumflex) + any(GraveKeys) > index(HasGrave,1)
any(HasGrave) + any(AcuteKeys) > index(HasAcute,1)
any(HasGrave) + any(CircumflexKeys) > index(HasCircumflex,1)

c --OGONEK--
store(ogonekvowels) 'ąęįǫųĄĘĮǪŲąęįǫųĄĘĮǪŲ'

c if ogonek key pressed after vowel (optional accent), change to ogonek vowel
any(vowels) any(combiningaccent) + any(OgonekKeys) > index(HasOgonek,1) index(combiningaccent,2)
any(HasAcute) + any(shiftedaccentkey) > index(HasOgonek,1) index(combiningaccent,2)
any(HasCircumflex) + any(shiftedaccentkey) > index(HasOgonek,1) index(combiningaccent,2)
any(HasGrave) + any(shiftedaccentkey) > index(HasOgonek,1) index(combiningaccent,2)
any(HasOgonek) + any(shiftedaccentkey) > index(HasOgonek,1) index(combiningaccent,2)

c if accent key pressed after vowel with ogo+accent, replace the accent
any(combiningaccent) + any(AccentKeys) > index(combiningaccent,2)
any(combiningaccent) + any(MobileAccentKeys) > index(combiningaccent,2)
any(combiningaccent) + any(shiftedaccentkey) > index(combiningaccent,2)

c backspace only accent so keyboard behaves as it was before normalizing
any(HasAcute) + [K_BKSP] > index(Vowels,1)
any(HasCircumflex) + [K_BKSP] > index(Vowels,1)
any(HasGrave) + [K_BKSP] > index(Vowels,1)
any(HasOgonek) + [K_BKSP] > index(Vowels,1)
