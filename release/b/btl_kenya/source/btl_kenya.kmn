c btl_kenya generated from template at 2019-11-19 10:31:45
c with name "Kenya (BTL)"
store(&VERSION) '10.0'
store(&NAME) 'Kenya (BTL)'
store(&COPYRIGHT) '© 1994-2019 Bible Translation & Literacy, SIL International'
store(&KEYBOARDVERSION) '1.1'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'btl_kenya.kvks'
store(&LAYOUTFILE) 'btl_kenya.keyman-touch-layout'
store(&BITMAP) 'btl_kenya.ico'

begin Unicode > use(main)

c store(&mnemoniclayout) "1"

c modifier letter
store(mod)          ";"

store(a_K)          "a"    "A" 
store(a_B)          U+0251 U+2C6D 

c below
store(dotU_K)       "."
store(dotU)         U+0323
store(dotU_B)       "bBdD"
store(dotComp)      U+1E05 U+1E04 U+1E0D U+1E0C

store(invBreveU_K)  "]"
store(invBreveU)    U+032F
store(invBreveU_B)  "dDtT"

store(macronU_K)    "_"
store(macronU)      U+0331
store(macronU_B)    "vV"

c above
store(graveA_K)     "`"
store(graveA)       U+0300
store(graveA_B)     "aeiouAEIOU"
store(graveComp)    U+00E0 U+00E8 U+00EC U+00F2 U+00F9 U+00C0 U+00C8 U+00CC U+00D2 U+00D9

store(acuteA_K)     "'"
store(acuteA)       U+0301
store(acuteA_B)     "aeiouAEIOU"
store(acuteComp)    U+00E1 U+00E9 U+00ED U+00F3 U+00FA U+00C1 U+00C9 U+00CD U+00D3 U+00DA

store(circA_K)      "^"
store(circA)        U+0302
store(circA_B)      "iuIU"
store(circComp)     U+00EE U+00FB U+00CE U+00DB

store(macronA_K)    "="
store(macronA)      U+0304
store(macronA_B)    "aeiouAEIOU"
store(macronComp)   U+0101 U+0113 U+012B U+014D U+016B U+0100 U+0112 U+012A U+014C U+016A


store(diaeresisA_K) ":"
store(diaeresisA)   U+0308
store(diaeresisA_B) "aeiouAEIOU"
store(diaeresisComp) U+00E4 U+00EB U+00EF U+00F6 U+00FC U+00C4 U+00CB U+00CF U+00D6 U+00DC

group(main) using keys

c Literacy a
any(mod)                    + any(a_K)           > index(a_B,2)
any(macronA_K) any(mod)     + any(a_K) > index(a_B,3) outs(macronA)
any(acuteA_K) any(mod)      + any(a_K) > index(a_B,3) outs(acuteA)
any(graveA_K) any(mod)      + any(a_K) > index(a_B,3) outs(graveA)
any(diaeresisA_K) any(mod)  + any(a_K) > index(a_B,3) outs(diaeresisA)


c Sabaot, Endo, Orma: Barred characters (using umlaut loc)
c Pokot: Umlaut characters (using regular Windows font)
any(macronA_K) + any(macronA_B)      > index(macronComp,2)
any(diaeresisA_K) + any(diaeresisA_B) > index(diaeresisComp,2)

c Rendille: High tone characters
any(acuteA_K) + any(acuteA_B)         > index(acuteComp,2)

c Rendille: Low tone characters
any(graveA_K) + any(graveA_B)         > index(graveComp,2) 

c Pokomo
any(dotU_K) + any(dotU_B)             > index(dotComp,2) 
any(invBreveU_K) + any(invBreveU_B)       > index(invBreveU_B,2) outs(invBreveU) 

c Giryama
any(macronU_K) + any(macronU_B)      > index(macronU_B,2) outs(macronU) 

c Tharaka
any(circA_K) + any(circA_B)           > index(circComp,2)

c cleanup
any(circComp)      + [K_BKSP] > index(circA_B,1)
any(dotComp)       + [K_BKSP] > index(dotU_B,1)
any(graveComp)     + [K_BKSP] > index(graveA_B,1)
any(acuteComp)     + [K_BKSP] > index(acuteA_B,1)
any(diaeresisComp) + [K_BKSP] > index(diaeresisA_B,1)
any(macronComp)    + [K_BKSP] > index(macronA_B,1)


if(&platform = 'touch') + [T_V_Macron] > U+0076 U+0331
if(&platform = 'touch') + [SHIFT T_V_Macron] > U+0056 U+0331

if(&platform = 'touch') + [T_B_Dot] > U+0062 U+0323
if(&platform = 'touch') + [SHIFT T_B_Dot] > U+0042 U+0323
if(&platform = 'touch') + [T_D_Dot] > U+0064 U+0323
if(&platform = 'touch') + [SHIFT T_D_Dot] > U+0044 U+0323

if(&platform = 'touch') + [T_D_Breve] > U+0064 U+032F
if(&platform = 'touch') + [SHIFT T_D_Breve] > U+0044 U+032F
if(&platform = 'touch') + [T_T_Breve] > U+0074 U+032F
if(&platform = 'touch') + [SHIFT T_T_Breve] > U+0054 U+032F

if(&platform = 'touch') + [SHIFT T_2C6D_Grave] > U+2C6D U+0300
if(&platform = 'touch') + [SHIFT T_2C6D_Acute] > U+2C6D U+0301
if(&platform = 'touch') + [SHIFT T_2C6D_Diaeresis] > U+2C6D U+0308
if(&platform = 'touch') + [SHIFT T_2C6D_Macron] > U+2C6D U+0304

if(&platform = 'touch') + [T_0251_Grave] > U+0251 U+0300
if(&platform = 'touch') + [T_0251_Acute] > U+0251 U+0301
if(&platform = 'touch') + [T_0251_Diaeresis] > U+0251 U+0308
if(&platform = 'touch') + [T_0251_Macron] > U+0251 U+0304
