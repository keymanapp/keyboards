c =====================Begin Identity Section===================================================
c 
c The Geʾez Frontier Foundation's mnemonic input method for the Ethiopic orthographies of Eritrea
c and Ethiopia on US-QWERTY keyboards for SIL Keyman, compliant with Unicode 14 and later.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_ethiopic/1.0/gff_ethiopic.php
c Source        :  https://github.com/keymanapp/keyboards/gff_ethiopic
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
c 
store(&Version) '15.0'
store(&KEYBOARDVERSION) '1.0.1'
store(&Name) 'Ethiopic (Modern Geʾez)'    
store(&COPYRIGHT) '© Geʾez Frontier Foundation'
store(&MESSAGE) 'This is an Ethiopic (Modern Geʾez) mnemonic input method.  It requires a font supporting Ethiopic script under the Unicode 4.1 standard.'
store(&CapsAlwaysOff) '1'
store(&HotKey) '[CTRL ALT K_E]'
store(&BITMAP) 'gff_ethiopic.ico'
store(&VISUALKEYBOARD) 'gff_ethiopic.kvks'
store(&TARGETS) 'any'
store(&LAYOUTFILE) 'gff_ethiopic.keyman-touch-layout'
store(&KMW_EMBEDCSS) 'gff_ethiopic.css'
$keymanonly:  store(&mnemonicLayout) "1"
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================
c The remainder of this document is formatted with a fixed width font.

c Define the set of Ethiopic characters to support contest based input:
c
store(Ethiopic) ' ' 'ሀ' .. '፼' .. 'ᎀ' .. '᎙ⶀ' .. 'ⷞꬁ' .. 'ꬮ𞟠' .. '𞟾'

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
  store(ግዕዝ)  'ሀለሐ𞟠መሠረሰሸⶠቀቐⷀበቨተቸⶨኀነኘከⷈኸⷐወዐዘዠⶰየደዸጀገጘⷘጠጨⶸꬠጰጸꬨፀፈፐ'        
  store(ካዕብ)  'ሁሉሑ𞟡ሙሡሩሱሹⶡቁቑⷁቡቩቱቹⶩኁኑኙኩⷉኹⷑዉዑዙዡⶱዩዱዹጁጉጙⷙጡጩⶹꬡጱጹꬩፁፉፑ'
  store(ሣልስ)  'ሂሊሒ𞟢ሚሢሪሲሺⶢቂቒⷂቢቪቲቺⶪኂኒኚኪⷊኺⷒዊዒዚዢⶲዪዲዺጂጊጚⷚጢጪⶺꬢጲጺꬪፂፊፒ'
  store(ራብዕ)  'ሃላሓ𞟣ማሣራሳሻⶣቃቓⷃባቫታቻⶫኃናኛካⷋኻⷓዋዓዛዣⶳያዳዻጃጋጛⷛጣጫⶻꬣጳጻꬫፃፋፓ'
  store(ኃምስ)  'ሄሌሔ𞟤ሜሤሬሴሼⶤቄቔⷄቤቬቴቼⶬኄኔኜኬⷌኼⷔዌዔዜዤⶴዬዴዼጄጌጜⷜጤጬⶼꬤጴጼꬬፄፌፔ'
  store(ሳድስ)  'ህልሕ𞟥ምሥርስሽⶥቅቕⷅብቭትችⶭኅንኝክⷍኽⷕውዐዝዥⶵይድዽጅግጝⷝጥጭⶽꬥጵጽꬭፅፍፕ'
  store(ሳብዕ)  'ሆሎሖ𞟦ሞሦሮሶሾⶦቆቖⷆቦቮቶቾⶮኅኖኞኮⷎኾⷖዎዖዞዦⶶዮዶዾጆጎጞⷞጦጮⶾꬦጶጾꬮፆፎፖ'

c QWERTY keymappings
  store(enKeys)  'hlHmrsxqQbvtcnNkKwzZydDjgGTCPSfp'
  store(ሳድስKeys) 'ህልሕምርስሽቅቕብቭትችንኝክኽውዝዥይድዽጅግጝጥጭጵጽፍፕ'

c Mappings for unused uppercase keys
  store(ሌሎች፡keys) 'LMRXBVWYJF'
  store(ሌሎች፡ሳድስ) 'ልምርሽብቭውይጅፍ'

c Me’en, Mursi, Suri “open o” syllables
store(መዕን፡ሳድስ) 'ህልምርስሽቅብትችኅንኝአክውዝይድዽጅግጥጭጵፅፕ'
store(መዕን፡ሳብዕ) 'ሆሎሞሮሶሾቆቦቶቾኆኖኞኦኮዎዞዮዶዾጆጎጦጮጶፆፖ'
store(መዕን፡ሳምን) 'ሇⶀⶁⶂⶃⶄቇⶅⶆⶇኇⶈⶉⶊኯዏⶋዯⶌⶍⶎጏⶏⶐⶑፇⶒ'

c Gamo, Gofa, Dawro, and Basketo (for the ꬁ and ꬉ)
store (GGDB፡ካዕብ) 'ꬁꬉꬑ' 
store (GGDB፡ሣልስ) 'ꬂꬊꬒ'
store (GGDB፡ራብዕ) 'ꬃꬋꬓ'
store (GGDB፡ኃምስ) 'ꬄꬌꬔ'
store (GGDB፡ሳድስ) 'ꬅꬍꬕ'
store (GGDB፡ሳብዕ) 'ꬆꬎꬖ'

store(ራብዕ፡Gurage) 'ቃካጋ'
store(ዘመደ፡ካዕብ፡LegacyGurage) 'ቍኵጕ'
store(ዘመደ፡ሣልስ፡LegacyGurage) 'ቊኲጒᎁᎅᎉᎍ'
store(ዘመደ፡ኃምስ፡LegacyGurage) 'ቌኴጔᎂᎆᎊᎎ'

store(ዘመደ፡ካዕብ፡ModernGurage) '𞟲𞟷𞟺'
store(ዘመደ፡ሣልስ፡ModernGurage) '𞟰𞟵𞟸𞟭𞟳𞟻𞟽'
store(ዘመደ፡ኃምስ፡ModernGurage) '𞟱𞟶𞟹𞟮𞟴𞟼𞟾'

c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Begin Maps of Archaic -iYa Forms-----------------------------------------------
store(archaicYaBase) 'ምርፍ'
store(archaicYaSyllables)  'ፙፘፚ'
store(archaicYa) 'Y'
c ---------------------End Maps of Archaic -iYa Forms-----------------------------------------------


c ---------------------Maps for Vowels----------------------------------------------------------
  store(loneVowelKeys)  [T_ግዕዝ]'aAuUiIeEoO'[T_ኃምስ]
  store(loneVowelEthio) 'እአዐኡዑኢዒእዕኦዖኤ'
c  store(loneVowelKeys)   'aAuUiIeEoO'
c  store(loneVowelEthio)  'ኣዐኡኡኢኢእእኦኦ'
  store(composableVowel) 'አኢዐዒ'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(ሳድስ፡መደበኛ)     'ልሕምሥርስሽብቭትችንኝዝዥድዽጅጥጭጵጽፍፕ'
  store(ካዕብ፡መደበኛ)     'ሉሑሙሡሩሱሹቡቩቱቹኑኙዙዡዱዹጁጡጩጱጹፉፑ'
  store(ዘመደ፡ራብዕ፡መደበኛ) 'ሏሗሟሧሯሷሿቧቯቷቿኗኟዟዧዷዿጇጧጯጷጿፏፗ'

c 'hhHmqQbkKgGfp'
  store(ካዕብ፡ልዩ)  'ሁኁሑሙቁቑቡኩኹጉጙፉፑ'
  store(ራብዕ፡ልዩ)  'ሃኃሓማቃቓባካኻጋጛፋፓ'
  store(ዘመደ፡ግዕዝ) 'ኈኈ𞟨ᎀቈቘᎄኰዀጐⶓᎈᎌ'
  store(ዘመደ፡ካዕብ) 'ኍኍ𞟫ᎃቍቝᎇኵዅጕⶖᎋᎏ'
  store(ዘመደ፡ሣልስ) 'ኊኊ𞟩𞟭ቊቚ𞟳ኲዂጒⶔ𞟻𞟽'
  store(ዘመደ፡ራብዕ) 'ኋኋሗሟቋቛቧኳዃጓጟፏፗ'
  store(ዘመደ፡ኃምስ) 'ኌኌ𞟪𞟮ቌቜ𞟴ኴዄጔⶕ𞟼𞟾'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(ArabOnes) '23456789'
  store(ones)     '፪፫፬፭፮፯፰፱'
  store(tens)     '፳፴፵፶፷፸፹፺'
  store(arabNumbers) '123456789'
  store(ethNumbers)  '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
  store(arabNumbersWithZero) '0123456789'
  store(ColonOrComma) ':,'
  store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
$keymanonly: store(ግዕዝ-key) 'eEäÄéÉèÈêÊëË'
$keymanweb:  store(ግዕዝ-key) 'eE'
$keymanonly: store(ካዕብ-key) 'uUùÙúÚûÛüÜ'
$keymanweb:  store(ካዕብ-key) 'uU'
$keymanonly: store(ሣልስ-key) 'iIìÌíÍîÎïÏ'
$keymanweb:  store(ሣልስ-key) 'iI'
$keymanonly: store(ራብዕ-key) 'aAàÀáÁâÂãÃåÅ'
$keymanweb:  store(ራብዕ-key) 'aA'
$keymanonly: store(ኃምስ-key) 'æÆ'
c $keymanweb:  store(ኃምስ-key) [T_ኃምስ]
$keymanonly: store(ሳብዕ-key) 'oOòÒóÓôÔõÕöÖøØ'
$keymanweb:  store(ሳብዕ-key) 'oO'
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

store(touchVowels)  [T_ግዕዝ][T_ካዕብ][T_ሣልስ][T_ራብዕ][T_ኃምስ][T_ሳብዕ][T_ሳምን][T_WWA]
store(ሀ_ቤተሰብ) 'ሀሁሂሃሄሆሇኋ'
store(ለ_ቤተሰብ) 'ለሉሊላሌሎⶀሏ'
store(ሐ_ቤተሰብ) 'ሐሑሒሓሔሖ!ሗ'
store(𞟠_ቤተሰብ) '𞟠𞟡𞟢𞟣𞟤𞟥𞟦!!'
store(መ_ቤተሰብ) 'መሙሚማሜሞⶁሟ'
store(ሠ_ቤተሰብ) 'ሠሡሢሣሤሦ!ሧ'
store(ረ_ቤተሰብ) 'ረሩሪራሬሮⶂሯ'
store(ሰ_ቤተሰብ) 'ሰሱሲሳሴሶⶃሷ'
store(ꬁ_ቤተሰብ) '!ꬁꬂꬃꬄꬆ!!'
store(ሸ_ቤተሰብ) 'ሸሹሺሻሼሾⶄሿ'
store(ⶠ_ቤተሰብ) 'ⶠⶡⶢⶣⶤⶦ!!'
store(ቀ_ቤተሰብ) 'ቀቁቂቃቄቆቇ!'
store(ቐ_ቤተሰብ) 'ቐቑቒቓቔቖ!!'
store(ⷀ_ቤተሰብ) 'ⷀⷁⷂⷃⷄⷆ!!'
store(በ_ቤተሰብ) 'በቡቢባቤቦⶅቧ'
store(ቨ_ቤተሰብ) 'ቨቩቪቫቬቮ!ቯ'
store(ተ_ቤተሰብ) 'ተቱቲታቴቶⶆቷ'
store(ቸ_ቤተሰብ) 'ቸቹቺቻቼቾⶇቿ'
store(ⶨ_ቤተሰብ) 'ⶨⶩⶪⶫⶬⶮ!!'
store(ኀ_ቤተሰብ) 'ኀኁኂኃኄኆኇ!'
store(ነ_ቤተሰብ) 'ነኑኒናኔኖⶈኗ'
store(ኘ_ቤተሰብ) 'ኘኙኚኛኜኞⶉኟ'
store(አ_ቤተሰብ) 'አኡኢኣኤኦⶊኧ'
store(ከ_ቤተሰብ) 'ከኩኪካኬኮኯ!'
store(ⷈ_ቤተሰብ) 'ⷈⷉⷊⷋⷌⷍⷎ!!'
store(ኸ_ቤተሰብ) 'ኸኹኺኻኼኾ!!'
store(ⷐ_ቤተሰብ) 'ⷐⷑⷒⷓⷔⷕⷖ!!'
store(ወ_ቤተሰብ) 'ወዉዊዋዌዎዏ!'
store(ዐ_ቤተሰብ) 'ዐዑዒዓዔዖ!!'
store(ዘ_ቤተሰብ) 'ዘዙዚዛዜዞⶋዟ'
store(ꬑ_ቤተሰብ) '!ꬑꬒꬓꬔꬖ!!'
store(ዠ_ቤተሰብ) 'ዠዡዢዣዤዦ!ዧ'
store(ⶰ_ቤተሰብ) 'ⶰⶱⶲⶳⶴⶶ!!'
store(የ_ቤተሰብ) 'የዩዪያዬዮዯⶌ!'
store(ደ_ቤተሰብ) 'ደዱዲዳዴዶⶌዷ'
store(ዸ_ቤተሰብ) 'ዸዹዺዻዼዾⶍዿ'
store(ꬉ_ቤተሰብ) '!ꬉꬊꬋꬌꬎ!!'
store(ጀ_ቤተሰብ) 'ጀጁጂጃጄጆⶎጇ'
store(ገ_ቤተሰብ) 'ገጉጊጋጌጎጏ!'
store(ጘ_ቤተሰብ) 'ጘጙጚጛጜጞ!!'
store(ⷘ_ቤተሰብ) 'ⷘⷙⷚⷛⷜⷞ!!'
store(ጠ_ቤተሰብ) 'ጠጡጢጣጤጦⶏጧ'
store(ጨ_ቤተሰብ) 'ጨጩጪጫጬጮⶐጯ'
store(ⶸ_ቤተሰብ) 'ⶸⶹⶺⶻⶼⶾ!!'
store(ꬠ_ቤተሰብ) 'ꬠꬡꬢꬣꬤꬦ!!'
store(ጰ_ቤተሰብ) 'ጰጱጲጳጴጶⶑጷ'
store(ጸ_ቤተሰብ) 'ጸጹጺጻጼጾ!ጿ'
store(ꬨ_ቤተሰብ) 'ꬨꬩꬪꬫꬬꬮ!!'
store(ፀ_ቤተሰብ) 'ፀፁፂፃፄፆፇ!'
store(ፈ_ቤተሰብ) 'ፈፉፊፋፌፎ!ፏ'
store(ፐ_ቤተሰብ) 'ፐፑፒፓፔፖⶒፗ'

store(touchVowelsLabialvelar) [T_ዘመደ_ግዕዝ][T_ዘመደ_ካዕብ][T_ዘመደ_ሣልስ][T_ዘመደ_ራብዕ][T_ዘመደ_ኃምስ]
store(𞟨_ቤተሰብ) '𞟨𞟫𞟩ሗ𞟪'
store(ᎀ_ቤተሰብ) 'ᎀᎃ𞟭ሟ𞟮'
store(ቈ_ቤተሰብ) 'ቈቍቊቋቌ'
store(ቘ_ቤተሰብ) 'ቘቝቚቛቜ'
store(ᎄ_ቤተሰብ) 'ᎄᎇ𞟳ቧ𞟴'
store(ኈ_ቤተሰብ) 'ኈኍኊኋኌ'
store(ኰ_ቤተሰብ) 'ኰኵኲኳኴ'
store(ዀ_ቤተሰብ) 'ዀዅዂዃዄ'
store(ጐ_ቤተሰብ) 'ጐጕጒጓጔ'
store(ⶓ_ቤተሰብ) 'ⶓⶖⶔጟⶕ'
store(ᎈ_ቤተሰብ) 'ᎈᎋ𞟻ፏ𞟼'
store(ᎌ_ቤተሰብ) 'ᎌᎏ𞟽ፗ𞟾'

store(touchVowelsLabialvelarModernGurage) [T_ዘመደ_ካዕብ_ጉራጌ][T_ዘመደ_ሣልስ_ጉራጌ][T_ዘመደ_ኃምስ_ጉራጌ]
store(ቈ_ቤተሰብ_ጉራጌ) '𞟲𞟰𞟱'
store(ኰ_ቤተሰብ_ጉራጌ) '𞟷𞟵𞟶'
store(ጐ_ቤተሰብ_ጉራጌ) '𞟺𞟸𞟹'

store(touchVowelsLabialvelarLegacyGurage) [T_ዘመደ_ሣልስ_ጉራጌ_ታሪካዊ][T_ዘመደ_ኃምስ_ጉራጌ_ታሪካዊ]
store(ᎀ_ቤተሰብ_ጉራጌ_ታሪካዊ) 'ᎁᎂ'
store(ቡ_ቤተሰብ_ጉራጌ_ታሪካዊ) 'ᎅᎆ'
store(ᎈ_ቤተሰብ_ጉራጌ_ታሪካዊ) 'ᎉᎊ'
store(ᎌ_ቤተሰብ_ጉራጌ_ታሪካዊ) 'ᎍᎎ'

c ---------------------Maps of Yared ዜማ Notation---------------------------------------------
  store(ዜማ) '᎐᎓᎒᎙᎑᎔᎖᎕᎗᎘_'
  store(ዜማ፡menu) '[1᎐2᎓3᎒4᎙5᎑6᎔7᎖8᎕9᎗0᎘]'
  store(ዜማ፡keys) '1234567890_'

c ---------------------Fudge - needed for KMW bug work-around ---------------------------------
  store(anyKey) '`1234567890-=~!@#$%^&*()_+' \
    'qwertyuiop[]\QWERTYUIOP{}|' \
    "asdfghjkl;'" 'ASDFGHJKL:"' \
    'zxcvbnm,./ZXCVBNM<>?'
c ---------------------End Maps of Yared ዜማ Notation----------------------------------------

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 
  begin Unicode > use(main)
  group(main) using keys

c ---------------------Input of Special Symbols-------------------------------------------------
  + '_' > outs(ዜማ፡menu)
  outs(ዜማ፡menu) + any(anyKey) > use(ዜማ፡group)

$keymanonly: + [CTRL '.'] > U+135E
$keymanonly: U+135E + [CTRL '.'] > U+135F
$keymanonly: U+135F + [CTRL '.'] > U+135D
$keymanonly: U+135D + [CTRL '.'] > U+135D beep

c
c Ethiopic combining marks for vowel and consonant stress.
c
c The following can happen from the touch keyboards፡
c
U+135E + [U_135E] > U+135F c 1 + 1 > 2
U+135E + [U_135F] > U+135D c 1 + 2 > 3
U+135F + [U_135E] > U+135D c 2 + 1 > 3
c
c warn but don't combine:
c
U+135E + [U_135D] > U+135E beep  c 1 + 3
U+135F + [U_135F] > U+135F beep  c 2 + 2
U+135F + [U_135D] > U+135F beep  c 2 + 3
U+135D + [U_135E] > U+135D beep  c 3 + 1
U+135D + [U_135F] > U+135D beep  c 3 + 2
U+135D + [U_135D] > U+135D beep  c 3 + 3

  + any(ሌሎች፡keys) > index(ሌሎች፡ሳድስ,1)
  + any(enKeys)   > index(ሳድስKeys,1)
c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Begin Mobile Input ------------------------------------------------------
c
c Mobile input is separated from Desktop in v3.1 for ease of maintenance
c
  any(ሳድስ) + [T_ግዕዝ] > index(ግዕዝ,1)
  any(ሳድስ) + [T_ካዕብ] > index(ካዕብ,1)
  any(ሳድስ) + [T_ሣልስ] > index(ሣልስ,1)
  any(ሳድስ) + [T_ራብዕ] > index(ራብዕ,1)
  any(ሳድስ) + [T_ኃምስ] > index(ኃምስ,1) 
  any(ሳድስ) + [T_ሳብዕ] > index(ሳብዕ,1)
  any(ሳድስ፡መደበኛ) + [T_WWA] > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  any(መዕን፡ሳድስ)  + [T_ሳምን] > index(መዕን፡ሳምን,1)

  any(GGDB፡ሳድስ) + [T_ካዕብ] > index(GGDB፡ካዕብ,1)
  any(GGDB፡ሳድስ) + [T_ሣልስ] > index(GGDB፡ሣልስ,1)
  any(GGDB፡ሳድስ) + [T_ራብዕ] > index(GGDB፡ራብዕ,1)
  any(GGDB፡ሳድስ) + [T_ኃምስ] > index(GGDB፡ኃምስ,1) 
  any(GGDB፡ሳድስ) + [T_ሳብዕ] > index(GGDB፡ሳብዕ,1)
  
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ግዕዝ] > index(ዘመደ፡ግዕዝ,1)
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ሣልስ] > index(ዘመደ፡ሣልስ,1)
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ራብዕ] > index(ዘመደ፡ራብዕ,1)
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ኃምስ] > index(ዘመደ፡ኃምስ,1)
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ካዕብ] > index(ዘመደ፡ካዕብ,1)

  any(ራብዕ፡Gurage) + [T_ዘመደ_ካዕብ_ጉራጌ] > index(ዘመደ፡ካዕብ፡ModernGurage,1)
  any(ራብዕ፡Gurage) + [T_ዘመደ_ሣልስ_ጉራጌ] > index(ዘመደ፡ሣልስ፡ModernGurage,1)
  any(ራብዕ፡Gurage) + [T_ዘመደ_ኃምስ_ጉራጌ] > index(ዘመደ፡ኃምስ፡ModernGurage,1)

  
  if( &layer = "default" ) + any(touchVowels) > index(አ_ቤተሰብ,2)
  if( &layer = "ሀ-layer" ) + any(touchVowels) > index(ሀ_ቤተሰብ,2)
  if( &layer = "ሀ-layer" ) + 'h' > 'ህ'   c avoid double-strike composition from touch layouts.
  if( &layer = "ለ-layer" ) + any(touchVowels) > index(ለ_ቤተሰብ,2)
  if( &layer = "ሐ-layer" ) + any(touchVowels) > index(ሐ_ቤተሰብ,2)
  if( &layer = "ሐ-layer" ) + 'H' > 'ሕ'
  if( &layer = "መ-layer" ) + any(touchVowels) > index(መ_ቤተሰብ,2)
  if( &layer = "ሠ-layer" ) + any(touchVowels) > index(ሠ_ቤተሰብ,2)
  if( &layer = "ረ-layer" ) + any(touchVowels) > index(ረ_ቤተሰብ,2)
  if( &layer = "ሰ-layer" ) + any(touchVowels) > index(ሰ_ቤተሰብ,2)
  if( &layer = "ሰ-layer" ) + 's' > 'ስ'   c avoid double-strike composition from touch layouts.
  if( &layer = "legacy-ꬁ-extra" ) + any(touchVowels) > index(ꬁ_ቤተሰብ,2)
  if( &layer = "ሸ-layer" ) + any(touchVowels) > index(ሸ_ቤተሰብ,2)
  if( &layer = "ሸ-layer" ) + 'x' > 'ሽ'
  if( &layer = "legacy-ⶠ-extra" ) + any(touchVowels) > index(ⶠ_ቤተሰብ,2)
  if( &layer = "ቀ-layer" ) + any(touchVowels) > index(ቀ_ቤተሰብ,2)
  if( &layer = "ቀ-layer" ) + 'q' > 'ቅ'
  if( &layer = "ቐ-layer" ) + any(touchVowels) > index(ቐ_ቤተሰብ,2)
  if( &layer = "legacy-ⷀ-extra" ) + any(touchVowels) > index(ⷀ_ቤተሰብ,2)
  if( &layer = "በ-layer" ) + any(touchVowels) > index(በ_ቤተሰብ,2)
  if( &layer = "ቨ-layer" ) + any(touchVowels) > index(ቨ_ቤተሰብ,2)
  if( &layer = "ተ-layer" ) + any(touchVowels) > index(ተ_ቤተሰብ,2)
  if( &layer = "ቸ-layer" ) + any(touchVowels) > index(ቸ_ቤተሰብ,2)
  if( &layer = "ቸ-layer" ) + 'c' > 'ች'   c avoid double-strike composition from touch layouts.
  if( &layer = "legacy-ⶨ-extra" ) + any(touchVowels) > index(ⶨ_ቤተሰብ,2)
  if( &layer = "ኀ-layer" ) + any(touchVowels) > index(ኀ_ቤተሰብ,2)
  if( &layer = "legacy-ኀ-extra" ) + any(touchVowels) > index(ኀ_ቤተሰብ,2)
  if( &layer = "ነ-layer" ) + any(touchVowels) > index(ነ_ቤተሰብ,2) 
  if( &layer = "ኘ-layer" ) + any(touchVowels) > index(ኘ_ቤተሰብ,2)
  if( &layer = "አ-layer" ) 'እ' + any(touchVowels) > index(አ_ቤተሰብ,3)
  if( &layer = "አ-layer" ) + any(touchVowels) > index(አ_ቤተሰብ,2)
  if( &layer = "ከ-layer" ) + any(touchVowels) > index(ከ_ቤተሰብ,2)
  if( &layer = "ከ-layer" ) + 'k' > 'ክ'
  if( &layer = "legacy-ⷈ-extra" ) + any(touchVowels) > index(ⷈ_ቤተሰብ,2)
  if( &layer = "ኸ-layer" ) + any(touchVowels) > index(ኸ_ቤተሰብ,2)
  if( &layer = "ኸ-layer" ) + 'K' > 'ኽ'
  if( &layer = "legacy-ⷐ-extra" ) + any(touchVowels) > index(ⷐ_ቤተሰብ,2)
  if( &layer = "ዐ-layer" ) 'ዕ' + any(touchVowels) > index(ዐ_ቤተሰብ,3)
  if( &layer = "ዐ-layer" ) 'ዐ' + any(touchVowels) > context(2) index(ዐ_ቤተሰብ,3)
  if( &layer = "ዐ-layer" ) + any(touchVowels) > index(ዐ_ቤተሰብ,2)
  if( &layer = "ወ-layer" ) + any(touchVowels) > index(ወ_ቤተሰብ,2)
  if( &layer = "ዘ-layer" ) + any(touchVowels) > index(ዘ_ቤተሰብ,2)
  if( &layer = "ዘ-layer" ) + 'z' > 'ዝ'
  if( &layer = "legacy-ꬑ-extra" ) + any(touchVowels) > index(ꬑ_ቤተሰብ,2)
  if( &layer = "ዠ-layer" ) + any(touchVowels) > index(ዠ_ቤተሰብ,2)
  if( &layer = "ዠ-layer" ) + 'Z' > 'ዥ'
  if( &layer = "legacy-ⶰ-extra" ) + any(touchVowels) > index(ⶰ_ቤተሰብ,2)
  if( &layer = "የ-layer" ) + any(touchVowels) > index(የ_ቤተሰብ,2)
  if( &layer = "ደ-layer" ) + any(touchVowels) > index(ደ_ቤተሰብ,2)
  if( &layer = "legacy-ዸ-extra" ) + any(touchVowels) > index(ዸ_ቤተሰብ,2)
  if( &layer = "legacy-ዸ-extra" ) + 'D' > 'ዽ'
  if( &layer = "legacy-ꬉ-extra" ) + any(touchVowels) > index(ꬉ_ቤተሰብ,2)
  if( &layer = "ጀ-layer" ) + any(touchVowels) > index(ጀ_ቤተሰብ,2)
  if( &layer = "ገ-layer" ) + any(touchVowels) > index(ገ_ቤተሰብ,2)
  if( &layer = "ገ-layer" ) + 'g' > 'ግ'
  if( &layer = "ጘ-layer" ) + any(touchVowels) > index(ጘ_ቤተሰብ,2)
  if( &layer = "legacy-ⷘ-extra" ) + any(touchVowels) > index(ⷘ_ቤተሰብ,2)
  if( &layer = "ጠ-layer" ) + any(touchVowels) > index(ጠ_ቤተሰብ,2) 
  if( &layer = "ጨ-layer" ) + any(touchVowels) > index(ጨ_ቤተሰብ,2)
  if( &layer = "ጨ-layer" ) + 'C' > 'ጭ'
  if( &layer = "legacy-ⶸ-extra" ) + any(touchVowels) > index(ⶸ_ቤተሰብ,2)
  if( &layer = "legacy-ꬠ-extra" ) + any(touchVowels) > index(ꬠ_ቤተሰብ,2)
  if( &layer = "ጰ-layer" ) + any(touchVowels) > index(ጰ_ቤተሰብ,2) 
  if( &layer = "ጸ-layer" ) + any(touchVowels) > index(ጸ_ቤተሰብ,2)
  if( &layer = "ጸ-layer" ) + 'S' > 'ጽ'   c avoid double-strike composition from touch layouts.
  if( &layer = "legacy-ꬨ-extra" ) + any(touchVowels) > index(ꬨ_ቤተሰብ,2)
  if( &layer = "ፀ-layer" ) + any(touchVowels) > index(ፀ_ቤተሰብ,2)
  if( &layer = "ፈ-layer" ) + any(touchVowels) > index(ፈ_ቤተሰብ,2)
  if( &layer = "ፐ-layer" ) + any(touchVowels) > index(ፐ_ቤተሰብ,2)
  if( &layer = "𞟠-layer" ) + any(touchVowels) > index(𞟠_ቤተሰብ,2)

  if( &layer = "𞟨-layer" ) + any(touchVowelsLabialvelar) > index(𞟨_ቤተሰብ,2)
  if( &layer = "ᎀ-layer" ) + any(touchVowelsLabialvelar) > index(ᎀ_ቤተሰብ,2)
  if( &layer = "ቈ-layer" ) + any(touchVowelsLabialvelar) > index(ቈ_ቤተሰብ,2)
  if( &layer = "ቘ-layer" ) + any(touchVowelsLabialvelar) > index(ቘ_ቤተሰብ,2)
  if( &layer = "ᎄ-layer" ) + any(touchVowelsLabialvelar) > index(ᎄ_ቤተሰብ,2)
  if( &layer = "ኈ-layer" ) + any(touchVowelsLabialvelar) > index(ኈ_ቤተሰብ,2)
  if( &layer = "ኰ-layer" ) + any(touchVowelsLabialvelar) > index(ኰ_ቤተሰብ,2)
  if( &layer = "ዀ-layer" ) + any(touchVowelsLabialvelar) > index(ዀ_ቤተሰብ,2)
  if( &layer = "ጐ-layer" ) + any(touchVowelsLabialvelar) > index(ጐ_ቤተሰብ,2)
  if( &layer = "ⶓ-layer" ) + any(touchVowelsLabialvelar) > index(ⶓ_ቤተሰብ,2)
  if( &layer = "ᎈ-layer" ) + any(touchVowelsLabialvelar) > index(ᎈ_ቤተሰብ,2)
  if( &layer = "ᎌ-layer" ) + any(touchVowelsLabialvelar) > index(ᎌ_ቤተሰብ,2)

  if( &layer = "ቈ-layer" ) + any(touchVowelsLabialvelarModernGurage) > index(ቈ_ቤተሰብ_ጉራጌ,2)
  if( &layer = "ኰ-layer" ) + any(touchVowelsLabialvelarModernGurage) > index(ኰ_ቤተሰብ_ጉራጌ,2)
  if( &layer = "ጐ-layer" ) + any(touchVowelsLabialvelarModernGurage) > index(ጐ_ቤተሰብ_ጉራጌ,2)

  if( &layer = "ᎀ-layer" ) + any(touchVowelsLabialvelarLegacyGurage) > index(ᎀ_ቤተሰብ_ጉራጌ_ታሪካዊ,2)
  if( &layer = "ᎄ-layer" ) + any(touchVowelsLabialvelarLegacyGurage) > index(ቡ_ቤተሰብ_ጉራጌ_ታሪካዊ,2)
  if( &layer = "ᎈ-layer" ) + any(touchVowelsLabialvelarLegacyGurage) > index(ᎈ_ቤተሰብ_ጉራጌ_ታሪካዊ,2)
  if( &layer = "ᎌ-layer" ) + any(touchVowelsLabialvelarLegacyGurage) > index(ᎌ_ቤተሰብ_ጉራጌ_ታሪካዊ,2)
c
c ---------------------End of Mobile Input ------------------------------------------------------
c
c Note: Given the new touch-specific logic above, much or all of the following that
c       address letter composition may no longer be relevant.  That is, it may all effectively
c       be $keymanonly statements.
c
c ---------------------Input of Double Strike Letters-------------------------------------------
  'ስ' + 's' > 'ሥ'
  'ህ' + 'h' > 'ኅ'
  'ጽ' + 'S' > 'ፅ'
  'ሽ' + 'x' > 'ⶥ'
  'ሽ' + 'X' > 'ⶥ'
  'ች' + 'c' > 'ⶭ' c Bench
  'ጭ' + 'C' > 'ⶽ' c Bench
  'ⶽ' + 'C' > 'ꬥ' c Gumuz
  'ዝ' + 'z' > 'ꬕ' c Basketo
  'ዥ' + 'Z' > 'ⶵ' c Gamo-Gofa-Dawro
  'ሕ' + 'H' > '𞟥' c Gurage
  'ሥ' + 's' > 'ꬅ' c Gamo-Gofa-Dawro, Basketo
  'ፅ' + 'S' > 'ꬭ' c Gumuz
  'ዽ' + 'D' > 'ꬍ' c Gamo-Gofa-Dawro, Basketo
  
  'ክ' + 'k' > 'ⷍ' c Legacy Gurage
  'ኽ' + 'K' > 'ⷕ' c Legacy Gurage
  'ቅ' + 'q' > 'ⷅ' c Legacy Gurage
  'ግ' + 'g' > 'ⷝ' c Legacy Gurage

c ---------------------End Input of Double Strike Letters---------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c 
c Alef-A Vowels as Vowels
  + any(loneVowelKeys) > index(loneVowelEthio,1)

  'እ' + any(ግዕዝ-key) > 'ኧ'
  'ኢ' + any(ግዕዝ-key) > 'ኤ'
  'አ' + any(ራብዕ-key) > 'ኣ'

  'ዐ' + any(ግዕዝ-key) > 'ዕ'
  'ዐ' + any(ካዕብ-key) > 'ዑ'
  'ዐ' + any(ሣልስ-key) > 'ዒ'
  'ዐ' + any(ራብዕ-key) > 'ዓ'
  'ዐ' + any(ሳብዕ-key) > 'ዖ'

  any(composableVowel) + "'" > index(composableVowel,1) deadkey(2)
c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(ሳድስ) + any(ግዕዝ-key) > index(ግዕዝ,1)
  any(ሳድስ) + any(ካዕብ-key) > index(ካዕብ,1)
  any(ሳድስ) + any(ሣልስ-key) > index(ሣልስ,1)
  any(ሳድስ) + any(ራብዕ-key) > index(ራብዕ,1)
$keymanonly: any(ሳድስ) + any(ኃምስ-key) > index(ኃምስ,1)
  any(ሣልስ) + any(ግዕዝ-key) > index(ኃምስ,1)  
  any(ሳድስ) + any(ሳብዕ-key) > index(ሳብዕ,1)

  any(archaicYaBase) + 'Y' > index(archaicYaSyllables,1)

  any(መዕን፡ሳብዕ) + any(ሳብዕ-key) > index(መዕን፡ሳምን,1)

  any(GGDB፡ሳድስ) + any(ግዕዝ-key) > context(1) beep
  any(GGDB፡ሳድስ) + any(ካዕብ-key) > index(GGDB፡ካዕብ,1)
  any(GGDB፡ሳድስ) + any(ሣልስ-key) > index(GGDB፡ሣልስ,1)
  any(GGDB፡ሳድስ) + any(ራብዕ-key) > index(GGDB፡ራብዕ,1)
  any(GGDB፡ሣልስ) + any(ግዕዝ-key) > index(GGDB፡ኃምስ,1) 
  any(GGDB፡ሳድስ) + any(ሳብዕ-key) > index(GGDB፡ሳብዕ,1)

  any(ዘመደ፡ካዕብ፡LegacyGurage) + any(ካዕብ-key) > index(ዘመደ፡ካዕብ፡ModernGurage,1)
  any(ዘመደ፡ሣልስ፡LegacyGurage) + any(ሣልስ-key) > index(ዘመደ፡ሣልስ፡ModernGurage,1)
  any(ዘመደ፡ኃምስ፡LegacyGurage) + any(ግዕዝ-key) > index(ዘመደ፡ኃምስ፡ModernGurage,1)

  any(ዘመደ፡ሣልስ፡ModernGurage) + any(ግዕዝ-key) > index(ዘመደ፡ኃምስ፡ModernGurage,1)

  any(ዘመደ፡ሣልስ፡ModernGurage) + any(ሣልስ-key) > index(ዘመደ፡ሣልስ፡LegacyGurage,1) c Needed for composing ᎁ,ᎅ,ᎉ, and ᎍ. Redundant, but consistent, for ቊ, ኲ, and ጒ.
  any(ዘመደ፡ኃምስ፡ModernGurage) + any(ግዕዝ-key) > index(ዘመደ፡ኃምስ፡LegacyGurage,1) c Needed for composing ᎂ, ᎆ, ᎊ, and ᎎ. Redundant, but consistent, for ቌ, ኴ, and ጔ.

c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(ካዕብ፡መደበኛ) + any(ራብዕ-key) > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  any(ካዕብ፡ልዩ) + any(ግዕዝ-key) > index(ዘመደ፡ግዕዝ,1)
  any(ካዕብ፡ልዩ) + any(ካዕብ-key) > index(ዘመደ፡ካዕብ,1)
  any(ካዕብ፡ልዩ) + any(ሣልስ-key) > index(ዘመደ፡ሣልስ,1)
  any(ካዕብ፡ልዩ) + any(ራብዕ-key) > index(ዘመደ፡ራብዕ,1)
$keymanonly: any(ካዕብ፡ልዩ) + any(ኃምስ-key) > index(ዘመደ፡ኃምስ,1)
  any(ዘመደ፡ሣልስ) + any(ግዕዝ-key)  > index(ዘመደ፡ኃምስ,1)
c ---------------------End Input of Labiovelars-------------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c 
c The only dead key for entry of special characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c 
  any(ሳድስ)      + "'" > context(1) deadkey(1)
c $keymanonly: any(ሳድስ) + [CTRL ' '] > index(ሳድስ,1) deadkey(1)
  any(ካዕብ፡መደበኛ) + "'" > context(1) deadkey(1)
  any(ካዕብ፡ልዩ)    + "'" > context(1) deadkey(1)
  any(ዘመደ፡ሣልስ)  + "'" > context(1) deadkey(1)             
  any(ሣልስ)      + "'" > context(1) deadkey(1)


  any(arabNumbersWithZero) + ':' > context(1) ':'
  any(arabNumbersWithZero) ':' + ' ' > context(1) '፡' ' ' 

      + ':' > '፡'
  '፡' + ':' > '።'
  '።' + ':' > ':'
  ':' + ':' > '፡'  c makes circular
  '፡' + '+' > '፠'
  '፡' + '#' > '፨'
  
  '።' + '.' > '.'
  '።' + any(arabNumbersWithZero) > '.' index(arabNumbersWithZero,2)
  any(Ethiopic) + '.' > index(Ethiopic,1) '።'
  any(arabNumbersWithZero) '.' + ' ' > index(arabNumbersWithZero,1) '።' ' ' 
   
   
  any(arabNumbersWithZero) + ',' > index(arabNumbersWithZero,1) ','
      + ',' > '፣'
  '፣' + ',' > '፥'
  '፥' + ',' > ','
  ',' + ',' > '፣'  c makes circular
  notany(arabNumbersWithZero) '፣' + any(arabNumbersWithZero) > context(1) '፣' index(arabNumbersWithZero,3)
  '፣' + any(arabNumbersWithZero) > ',' index(arabNumbersWithZero,2)
  any(arabNumbersWithZero) ',' + ' ' > index(arabNumbersWithZero,1) '፣' ' ' 

      + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፤'  c makes circular
      
  '፡' + '-' > '፦' 
  
  '?' + '?' > '፧'  c Eritrean use

      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
$keymanonly:  store(euro)    'ÇçÐðÝýÿßÑñ'
$keymanonly:  store(euroMap) 'ጭችድድይይይጽኝኝ'
$keymanonly:  + any(euro) > index(euroMap,1)

$keymanonly:  'ጽ' + 'ß' > 'ፅ'
$keymanonly:  + '¨' > U+135F

$keymanonly:  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c  this mapping has an Amharic bias
$keymanonly:  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
$keymanonly:  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Arabic Numerals
c 
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c 
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  '#' + '1' > '፩'
  '#' + any(ArabOnes) > index(ones,2)
  '#' + '#' > '#'

c special cases for multiples of one
    '፩' + '0' >   '፲'
    '፲' + '0' >   '፻'
    '፻' + '0' >  '፲፻'
   '፲፻' + '0' >   '፼'
    '፼' + '0' >  '፲፼'    
   '፲፼' + '0' >  '፻፼' 
   '፻፼' + '0' > '፲፻፼'
  '፲፻፼' + '0' >  '፼፼'
   '፼፼' + '0' > context beep  c do not go any higher, we could beep here

c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1)  '፻' c Hundreds
  any(ones)  '፻' + '0' > index(tens,1)  '፻' c Thousands
  any(tens)  '፻' + '0' > index(ones,1)  '፼' c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1)  '፼' c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions

c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)
c ---------------------End Input of Numbers-----------------------------------------------------

+ [T_THOUSAND] > '፲፻'
                                            
+ [T_BACK] > dk(1)

+ [T_BEEP] > beep
c ---------------------Begin ዜማ Group--------------------------------------------------------
group(ዜማ፡group) using keys
+ any(ዜማ፡keys) > index(ዜማ, 1)
nomatch        > use(main)      
c ---------------------End ዜማ Group----------------------------------------------------------
c =====================End Functional Section===================================================
