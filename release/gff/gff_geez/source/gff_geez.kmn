c =====================Begin Identity Section===================================================
c 
c The Geʾez Frontier Foundation's mnemonic input method for Ge'ez script on US-QWERTY
c keyboards for SIL Keyman, compliant with Unicode 4.1 and later.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_geez/1.2/gff_geez.php
c Source        :  https://github.com/keymanapp/keyboards/gff_geez
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
c 
store(&Version) '15.0'
store(&KEYBOARDVERSION) '2.0.1'
store(&Name) 'ግዕዝ (Geʾez)'    
store(&COPYRIGHT) '© 2009-2023 Geʾez Frontier Foundation'
store(&Message) 'This is a Geʾez language mnemonic input method. It requires a font supporting Ethiopic script under the Unicode 4.1 standard.'
store(&CapsAlwaysOff) '1'
store(&HotKey) '[CTRL ALT K_G]'
store(&BITMAP) 'gff_geez.ico'
store(&VISUALKEYBOARD) 'gff_geez.kvks'
$keymanonly:  store(&mnemonicLayout) "1"
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================

c The remainder of this document is formatted with a fixed width font.

c Define the set of Ethiopic characters to support contest based input:
c
store(Ethiopic) ' ' 'ሀ' .. '፼' .. 'ᎀ' .. '᎙ⶀ' .. 'ⷞꬁ' .. 'ꬮ𞟠' .. '𞟾'

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
  store(ግዕዝ) 'ሀለሐመሠረሰቀበተኀነከወዐዘየደገጠጰጸፀፈፐ'
  store(ካዕብ) 'ሁሉሑሙሡሩሱቁቡቱኁኑኩዉዑዙዩዱጉጡጱጹፁፉፑ'
  store(ሣልስ) 'ሂሊሒሚሢሪሲቂቢቲኂኒኪዊዒዚዪዲጊጢጲጺፂፊፒ'
  store(ራብዕ) 'ሃላሓማሣራሳቃባታኃናካዋዓዛያዳጋጣጳጻፃፋፓ'
  store(ኃምስ) 'ሄሌሔሜሤሬሴቄቤቴኄኔኬዌዔዜዬዴጌጤጴጼፄፌፔ'
  store(ሳድስ) 'ህልሕምሥርስቅብትኅንክውዕዝይድግጥጵጽፅፍፕ'
  store(ሳብዕ) 'ሆሎሖሞሦሮሶቆቦቶኆኖኮዎዖዞዮዶጎጦጶጾፆፎፖ'

c QWERTY keymappings
  store(enKeys) 'hlHmrsqbtxnkwzydgTPSDfp'
  store(ሳድስKeys) 'ህልሕምርስቅብትኅንክውዝይድግጥጵጽፅፍፕ'

c the following is Ge'ez specific
  store(ሌሎች፡keys) 'LMRXBvVcQNKWYjJZGCF'
  store(ሌሎች፡ሳድስ)  'ልምርኅብብብትቅንክውይግግዝግጥፍ'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------


c ---------------------Maps for Vowels----------------------------------------------------------
  store(composableVowel) 'አኢዐዒ'

$keymanonly: store(አሌፍ፡ግዕዝ፡key) 'aàáâãå'
$keymanweb:  store(አሌፍ፡ግዕዝ፡key) 'a'
$keymanonly: store(አሌፍ፡ካዕብ፡key) 'uùúûü'
$keymanweb:  store(አሌፍ፡ካዕብ፡key) 'u'
$keymanonly: store(አሌፍ፡ሣልስ፡key) 'iìíîï'
$keymanweb:  store(አሌፍ፡ሣልስ፡key) 'i'
$keymanonly: store(አሌፍ፡ኃምስ፡key) 'æ'
$keymanonly: store(አሌፍ፡ሳድስ፡key) 'eäéèêë'
$keymanweb:  store(አሌፍ፡ሳድስ፡key) 'e'
$keymanonly: store(አሌፍ፡ሳብዕ፡key) 'oòóôõöø'
$keymanweb:  store(አሌፍ፡ሳብዕ፡key) 'o'

$keymanonly: store(ዓይን፡ግዕዝ፡key) 'AÀÁÂÃÅ'
$keymanweb:  store(ዓይን፡ግዕዝ፡key) 'A'
$keymanonly: store(ዓይን፡ካዕብ፡key) 'UÙÚÛÜ'
$keymanweb:  store(ዓይን፡ካዕብ፡key) 'U'
$keymanonly: store(ዓይን፡ሣልስ፡key) 'IÌÍÎÏ'
$keymanweb:  store(ዓይን፡ሣልስ፡key) 'I'
$keymanonly: store(ዓይን፡ሳድስ፡key) 'EÄÉÈÊË'
$keymanonly: store(ዓይን፡ኃምስ፡key) 'Æ'
$keymanweb:  store(ዓይን፡ሳድስ፡key) 'E'
$keymanonly: store(ዓይን፡ሳብዕ፡key) 'OÒÓÔÕÖØ'
$keymanweb:  store(ዓይን፡ሳብዕ፡key) 'O'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(ሳድስ፡መደበኛ) 'ልሕምሥርስሽብትንኝዝድጥጵጽፅፍፕ'
  store(ካዕብ፡መደበኛ) 'ሉሑሙሡሩሱሹቡቱኑኙዙዱጡጱጹፁፉፑ'
  store(ዘመደ፡ራብዕ፡መደበኛ) 'ሏሗሟሧሯሷሿቧቷኗኟዟዷጧጷጿጿፏፗ'

c 'hhkqg'
  store(ካዕብ፡ልዩ)  'ሁኁኩቁጉ'
  store(ዘመደ፡ግዕዝ) 'ኈኈኰቈጐ'
  store(ዘመደ፡ካዕብ) 'ኍኍኵቍጕ'
  store(ዘመደ፡ሣልስ) 'ኊኊኲቊጒ'
  store(ዘመደ፡ራብዕ) 'ኋኋኳቋጓ'
  store(ዘመደ፡ኃምስ) 'ኌኌኴቌጔ'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(ArabOnes) '23456789'
  store(ones)     '፪፫፬፭፮፯፰፱'
  store(tens)     '፳፴፵፶፷፸፹፺'
  store(arabNumbers) '123456789'
  store(ethNumbers)  '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
  store(arabNumbersWithZero) '0123456789'
  store(arabNumbersCharacters) '0123456789.,:'
  store(ColonOrComma) ':,'
  store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
$keymanonly: store(ግዕዝ፡key) 'eäéèêë'
$keymanweb:  store(ግዕዝ፡key) 'e'
$keymanonly: store(ካዕብ፡key) 'uùúûü'
$keymanweb:  store(ካዕብ፡key) 'u'
$keymanonly: store(ሣልስ፡key) 'iìíîï'
$keymanweb:  store(ሣልስ፡key) 'i'
$keymanonly: store(ራብዕ፡key) 'aàáâãå'
$keymanweb:  store(ራብዕ፡key) 'a'
$keymanonly: store(ኃምስ፡key) 'æ'
$keymanonly: store(ሳብዕ፡key) 'oòóôõöø'
$keymanweb:  store(ሳብዕ፡key) 'o'

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

c ---------------------Maps of Yared ዜማ Notation---------------------------------------------
  store(ጥብቅ) U+135F
  store(ዜማ) '᎐᎓᎒᎙᎑᎔᎖᎕᎗᎘_'
  store(ዜማ፡menu) '[1᎐2᎓3᎒4᎙5᎑6᎔7᎖8᎕9᎗0᎘]'
  store(ዜማ፡keys) '1234567890_'
c ---------------------End Maps of Yared ዜማ Notation----------------------------------------

c ---------------------Fudge - needed for KMW bug work-around ---------------------------------
  store(anyKey) '`1234567890-=~!@#$%^&*()_+' \
    'qwertyuiop[]\QWERTYUIOP{}|' \
    "asdfghjkl;'" 'ASDFGHJKL:"' \
    'zxcvbnm,./ZXCVBNM<>?'
c =====================End Data Section=========================================================
                  
c =====================Begin Functional Section=================================================
c 
store(&TARGETS) 'web desktop'

begin Unicode > use(main)
   
c The main group
  group(main) using keys

c ---------------------Input of ልዩ Symbols-------------------------------------------------
c 
c The input section here is for rules that must precede other rules
c
$keymanonly: + [CTRL '.'] > U+135E
$keymanonly: U+135E + [CTRL '.'] > U+135F
$keymanonly: U+135F + [CTRL '.'] > U+135D
$keymanonly: U+135D + [CTRL '.'] > U+135D beep

  + '_' > outs(ዜማ፡menu)
  outs(ዜማ፡menu) + any(anyKey) > use(ዜማ፡group)

  deadkey(1) + "'" > "'"

  + any(ሌሎች፡keys) > index(ሌሎች፡ሳድስ,1)
  + any(enKeys)   > index(ሳድስKeys,1)
c ---------------------End Input of ልዩ Symbols---------------------------------------------

c ---------------------Input of Double Strike Letters-------------------------------------------
  'ስ' + 's' > 'ሥ'
  'ህ' + 'h' > 'ኅ'
  'ጽ' + 'S' > 'ፅ'
c ---------------------End Input of Double Strike Letters---------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c 
 + any(አሌፍ፡ግዕዝ፡key) > 'አ'
 + any(አሌፍ፡ካዕብ፡key) > 'ኡ'
 + any(አሌፍ፡ሣልስ፡key) > 'ኢ'
$keymanonly:  + any(አሌፍ፡ኃምስ፡key) > 'ኤ'
 + any(አሌፍ፡ሳድስ፡key) > 'እ'
 + any(አሌፍ፡ሳብዕ፡key) > 'ኦ'

  'ኢ' + any(ግዕዝ፡key) > 'ኤ'
  'አ' + any(ራብዕ፡key) > 'ኣ'

 + any(ዓይን፡ግዕዝ፡key) > 'ዐ'
 + any(ዓይን፡ካዕብ፡key) > 'ዑ'
 + any(ዓይን፡ሣልስ፡key) > 'ዒ'
$keymanonly:  + any(ዓይን፡ኃምስ፡key) > 'ዔ'
 + any(ዓይን፡ሳድስ፡key) > 'ዕ'
 + any(ዓይን፡ሳብዕ፡key) > 'ዖ'


  'ዐ' + any(ግዕዝ፡key) > 'ዕ'
  'ዐ' + any(ካዕብ፡key) > 'ዑ'
  'ዐ' + any(ሣልስ፡key) > 'ዒ'
  'ዐ' + any(ራብዕ፡key) > 'ዓ'
  'ዐ' + any(ሳብዕ፡key) > 'ዖ'
  'ዒ' + 'E' > 'ዔ'

  any(composableVowel) + "'" > index(composableVowel,1) deadkey(2)
c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(ሳድስ) + any(ግዕዝ፡key) > index(ግዕዝ,1)
  any(ሳድስ) + any(ካዕብ፡key) > index(ካዕብ,1)
  any(ሳድስ) + any(ሣልስ፡key) > index(ሣልስ,1)
  any(ሳድስ) + any(ራብዕ፡key) > index(ራብዕ,1)
$keymanonly:   any(ሳድስ) + any(ኃምስ፡key) > index(ኃምስ,1)
  any(ሣልስ) + any(ግዕዝ፡key) > index(ኃምስ,1)  
  any(ሳድስ) + any(ሳብዕ፡key) > index(ሳብዕ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(ካዕብ፡መደበኛ) + any(ራብዕ፡key) > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  any(ካዕብ፡ልዩ)   + any(ግዕዝ፡key) > index(ዘመደ፡ግዕዝ,1)
  any(ካዕብ፡ልዩ)   + any(ካዕብ፡key) > index(ዘመደ፡ካዕብ,1) 
  any(ካዕብ፡ልዩ)   + any(ሣልስ፡key) > index(ዘመደ፡ሣልስ,1)
  any(ካዕብ፡ልዩ)   + any(ራብዕ፡key) > index(ዘመደ፡ራብዕ,1)  
  any(ዘመደ፡ሣልስ)  + any(ግዕዝ፡key) > index(ዘመደ፡ኃምስ,1)

c Alternative input to support confusion around the 'o' glypheme:
  'ኮ' + 'o' > 'ኰ'
  'ጎ' + 'o' > 'ጐ'

  'ኰ' + 'o' > 'ኮ'
  'ጐ' + 'o' > 'ጎ'
c ---------------------End Input of Labiovelars-------------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c 
c The only dead key for entry of ልዩ characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c 
  any(ሳድስ)      + "'" > context(1) deadkey(1)
  any(ካዕብ፡መደበኛ) + "'" > context(1) deadkey(1)
  any(ካዕብ፡ልዩ)   + "'" > context(1) deadkey(1)
  any(ዘመደ፡ሣልስ)  + "'" > context(1) deadkey(1)             
  any(ሣልስ)      + "'" > context(1) deadkey(1)
  

  any(arabNumbersWithZero) + ':' > context(1) ':'
  any(arabNumbersWithZero) ':' + ' ' > context(1) '፡' ' ' 


  + ':'     > '፡'
$keymanonly: + [SHIFT ' '] > '፡'
  '፡' + ':' > '።'
  '።' + ':' > '፨'
  '፨' + ':' > '፠'  c makes circular
  '፠' + ':' > ':'  c makes circular

  
  '።' + '.' > '.'
  '።' + any(arabNumbersWithZero) > '.' index(arabNumbersWithZero,2)
  any(Ethiopic) + '.' > context(1) '።'
  any(arabNumbersWithZero) '.' + ' ' > context(1) '።' ' ' 
  
  
 any(arabNumbersWithZero) + ',' > context(1) ','
      + ',' > '፥'
  '፥' + ',' > '፣'
  '፣' + ',' > ','
  ',' + ',' > '፥'  c makes circular

  notany(arabNumbersWithZero) '፥' + any(arabNumbersWithZero) > context(1) '፥' index(arabNumbersWithZero,3)
  '፥' + any(arabNumbersWithZero) > ',' index(arabNumbersWithZero,2)
  any(arabNumbersWithZero) ',' + ' ' > context(1) '፥' ' ' 

      + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፤'  c makes circular

  '፡' + '-' > '፦'     
  
  + '?' > '፧'       
  '፧' + '?' > '?'

c The following group is here for compatibility with proposed ISO transliteration for Ethiopic:
  '፡' + '#' > '፨'
  '፡' + '+' > '፠'  
  '፡' + ',' > '፣'  
  '፡' + '?' > '፧'    

      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
$keymanonly:  store(euro) 'ÇçÐðÝýÿßÑñ'
$keymanonly:  store(euroMap) 'ጭችድድይይይጽኝኝ'
$keymanonly:  + any(euro) > index(euroMap,1)

$keymanonly:  'ጽ' + 'ß' > 'ፅ'
$keymanonly:  + '¨' > U+135F

c $keymanonly:  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c this mapping has an Amharic bias
c $keymanonly:  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
c $keymanonly:  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Araicc Numerals
c 
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c 
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  '#' + any(arabNumbersWithZero) > index(arabNumbersWithZero,2)
  '#' + '#' > '#'
  any(arabNumbersCharacters) + any(arabNumbersCharacters)  >  index(arabNumbersCharacters,1) index(arabNumbersCharacters,2)
  '$' + any(arabNumbersWithZero) > '$' index(arabNumbersWithZero,2)

  + '1' > '፩'
  + any(ArabOnes) > index(ones,1)

c ልዩ cases for multiples of one
    '፩' + '0' >   '፲'
    '፲' + '0' >   '፻'
    '፻' + '0' >  '፲፻'
   '፲፻' + '0' >   '፼'
    '፼' + '0' >  '፲፼'    
   '፲፼' + '0' >  '፻፼' 
   '፻፼' + '0' > '፲፻፼'
  '፲፻፼' + '0' >  '፼፼'
   '፼፼' + '0' > context beep  c do not go any higher, we could beep here

c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1) '፻'  c Hundreds
  any(ones)  '፻' + '0' > index(tens,1) '፻'  c Thousands
  any(tens)  '፻' + '0' > index(ones,1) '፼'  c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1) '፼'  c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions 
  any(ones) '፼፼' + '0' > context beep  c do not go any higher, alert the user with a beep 

c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)

c ---------------------End Input of Numbers-----------------------------------------------------

c ---------------------Begin of EMFUI Symbols-------------------------------------------------
c
c These entries are added to input the EMUFI symbols included in the Abba Garima font.
c One additional symbol, the very frequent Rubricated Ethiopic Fullstop, is added for 
c convenience and is also a trial to support the 500+ EMUFI symbols more fully.
  store(emufi፡keys) '፡።‹›'  
  store(emufi፡symbols) ''
  'ቆመ' + '@' > ''
  'ፕ' + '@' > '☧'
  any(emufi፡keys) + '@' > index(emufi፡symbols,1) 
c
c ---------------------End of EMUFI Symbols---------------------------------------------------

c ---------------------Begin ዜማ Group--------------------------------------------------------
group(ዜማ፡group) using keys
+ any(ዜማ፡keys) > index(ዜማ, 1)
nomatch        > use(main)      
c ---------------------End ዜማ Group----------------------------------------------------------

       

c =====================End Functional Section===================================================
