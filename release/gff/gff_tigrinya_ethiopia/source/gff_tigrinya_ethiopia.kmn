c =====================Begin Identity Section===================================================
c 
c The Ge'ez Frontier Foundation's mnemonic input method for Tigrinya script under Ethiopian writing conventions on US-QWERTY
c keyboards for SIL Keyman, compliant with Unicode 3.0 and later.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_tigrinya_ethiopia/1.2/gff_tigrinya_ethiopia.php
c Source        :  https://github.com/keymanapp/keyboards/gff_tigrinya_ethiopia
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
c 
  store(&Version) '9.0'
  store(&Name) 'ትግርኛ (Tigrinya)'
  store(&EthnologueCode) 'tir'
store(&LANGUAGE) 'x0473'
store(&WINDOWSLANGUAGES) 'x0473'
store(&COPYRIGHT) '© 2009-2019 Ge' U+0027 'ez Frontier Foundation'
  store(&Message) 'This is a Tigrinya language mnemonic input method that applies Ethiopian writing conventions.  It requires a font supporting Ethiopic script under the Unicode 3.0 standard.'
store(&KEYBOARDVERSION) '1.2'
  store(&CapsAlwaysOff) '1'
  store(&HotKey) '[CTRL ALT K_T]'
store(&BITMAP) 'gff_tigrinya_et.ico'
store(&VISUALKEYBOARD) 'gff_tigrinya_ethiopia.kvks'
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================

c The remainder of this document is formatted with a fixed width font.

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
  store(ግዕዝ)  'ሀለሐመሠረሰሸቀቐበቨተቸኀነኘአከኸወዐዘዠየደጀገጠጨጰጸፀፈፐ'
  store(ካዕብ)  'ሁሉሑሙሡሩሱሹቁቑቡቩቱቹኁኑኙኡኩኹዉዑዙዡዩዱጁጉጡጩጱጹፁፉፑ'
  store(ሣልስ)  'ሂሊሒሚሢሪሲሺቂቒቢቪቲቺኂኒኚኢኪኺዊዒዚዢዪዲጂጊጢጪጲጺፂፊፒ'
  store(ራብዕ)  'ሃላሓማሣራሳሻቃቓባቫታቻኃናኛኣካኻዋዓዛዣያዳጃጋጣጫጳጻፃፋፓ'
  store(ኃምስ)  'ሄሌሔሜሤሬሴሼቄቔቤቬቴቼኄኔኜኤኬኼዌዔዜዤዬዴጄጌጤጬጴጼፄፌፔ'
  store(ሳድስ)  'ህልሕምሥርስሽቅቕብቭትችኅንኝእክኽውዐዝዥይድጅግጥጭጵጽፅፍፕ'
  store(ሳብዕ)  'ሆሎሖሞሦሮሶሾቆቖቦቮቶቾኆኖኞኦኮኾዎዖዞዦዮዶጆጎጦጮጶጾፆፎፖ'

c QWERTY keymappings
  store(keys)  'hlHm_rsxqQbvtc_nN_kKw_zZydjgTCPS_fp'

c the following is Tigrinya specific
  store(ሌሎች፡keys) 'LMRXBVWYDJGF'
  store(ሌሎች፡ሳድስ)  'ልምርሽብቭውይድጅግፍ'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Maps for Vowels----------------------------------------------------------
  store(loneVowelKeys)   'aAuUiIeEoO'
  store(loneVowelEthio)  'ኣዓኡዑኢዒእዕኦዖ'
  store(composableVowel) 'ኢእዒዕ'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(ሳድስ፡መደበኛ)     'ልሕምሥርስሽብቭትችንኝዝዥድጅጥጭጵጽፅፍፕ'
  store(ካዕብ፡መደበኛ)     'ሉሑሙሡሩሱሹቡቩቱቹኑኙዙዡዱጁጡጩጱጹፁፉፑ'
  store(ዘመደ፡ራብዕ፡መደበኛ) 'ሏሗሟሧሯሷሿቧቯቷቿኗኟዟዧዷጇጧጯጷጿጿፏፗ'

c 'hhqQkg'
  store(ካዕብ፡ልዩ)  'ሁኁቁቑኩኹጉ'
  store(ዘመደ፡ግዕዝ) 'ኈኈቈቘኰዀጐ'
  store(ዘመደ፡ካዕብ) 'ኍኍቍቝኵዅጕ'
  store(ዘመደ፡ሣልስ) 'ኊኊቊቚኲዂጒ'
  store(ዘመደ፡ራብዕ) 'ኋኋቋቛኳዃጓ'
  store(ዘመደ፡ኃምስ) 'ኌኌቌቜኴዄጔ'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(ArabOnes) '23456789'
  store(ones)     '፪፫፬፭፮፯፰፱'
  store(tens)     '፳፴፵፶፷፸፹፺'
  store(arabNumbers) '123456789'
  store(ethNumbers)  '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
  store(arabNumbersWithZero) '0123456789'
  store(ColonOrComma) ':,'
  store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
  store(ግዕዝ-key) 'eEäÄéÉèÈêÊëË'
  store(ካዕብ-key) 'uUùÙúÚûÛüÜ'
  store(ሣልስ-key) 'iIìÌíÍîÎïÏ'
  store(ራብዕ-key) 'aAàÀáÁâÂãÃåÅ'
  store(ኃምስ-key) 'æÆ'
  store(ሳብዕ-key) 'oOòÒóÓôÔõÕöÖøØ'
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 
store(&TARGETS) 'windows macosx linux'
  begin Unicode > use(main)
  group(main) using keys

c ---------------------Input of Special Symbols-------------------------------------------------
c 
c The input section here is for rules that must precede other rules
c 
  + '_' > U+135F c gemination
  U+135F + '_' > '_'

  + any(ሌሎች፡keys) > index(ሌሎች፡ሳድስ,1)
  + any(keys)     > index(ሳድስ,1)
c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Input of Double Strike Letters-------------------------------------------
  'ስ' + 's' > 'ሥ'
  'ህ' + 'h' > 'ኅ'
  'ጽ' + 'S' > 'ፅ'
c ---------------------End Input of Double Strike Letters---------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c 
  + any(loneVowelKeys) > index(loneVowelEthio,1)
  any(composableVowel) + "'" > index(composableVowel,1) deadkey(2)

c Alef-A Vowels as Vowels
c
c Diable multistrike vowel composition for now, re-enable with a keyboard
c configuration option (working again as per: https://github.com/keymanapp/keyman/issues/792)
c

c Alef-A Vowels as Regular Syllables
  c 'እ' + any(ካዕብ-key) > 'ኡ'
  c 'እ' + any(ሣልስ-key) > 'ኢ'
  c 'እ' + any(ራብዕ-key) > 'ኣ'
  c 'ኢ' + any(ግዕዝ-key) > 'ኤ'
  c 'እ' + any(ሳብዕ-key) > 'ኦ'

c Aynu-A Vowels as Regular Sylalbles
  c 'እ' + any(ግዕዝ-key) > 'ዕ'
  c 'ዕ' + any(ግዕዝ-key) > 'ዐ'
  c 'ዕ' + any(ካዕብ-key) > 'ዑ'
  c 'ዕ' + any(ሣልስ-key) > 'ዒ'
  c 'ዕ' + any(ራብዕ-key) > 'ዓ'
  c 'ዒ' + any(ግዕዝ-key) > 'ዔ'
  c 'ዕ' + any(ሳብዕ-key) > 'ዖ'

c Aynu-A Vowels as Vowels
  c 'ኡ' + any(ካዕብ-key) > 'ዑ'
  c 'ኢ' + any(ሣልስ-key) > 'ዒ'
  c 'ኦ' + any(ሳብዕ-key) > 'ዖ'

c Completing the Amharic "a" chain:
  c 'አ' + any(ራብዕ-key) > 'ዓ'
  c 'ዓ' + any(ራብዕ-key) > 'ዐ'
  c 'ዐ' + any(ራብዕ-key) > 'ኣ'

  c 'አ' + any(ግዕዝ-key) > 'ኧ'  c I3559 mcd 16-11-2012, as per ET documentation 'ae'
c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(ሳድስ) + any(ግዕዝ-key) > index(ግዕዝ,1)
  any(ሳድስ) + any(ካዕብ-key) > index(ካዕብ,1)
  any(ሳድስ) + any(ሣልስ-key) > index(ሣልስ,1)
  any(ሳድስ) + any(ራብዕ-key) > index(ራብዕ,1)
  any(ሳድስ) + any(ኃምስ-key) > index(ኃምስ,1)
  any(ሣልስ) + any(ግዕዝ-key) > index(ኃምስ,1)  
  any(ሳድስ) + any(ሳብዕ-key) > index(ሳብዕ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(ካዕብ፡መደበኛ) + any(ራብዕ-key) > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  any(ካዕብ፡ልዩ)   + any(ግዕዝ-key) > index(ዘመደ፡ግዕዝ,1)
  any(ካዕብ፡ልዩ)   + any(ካዕብ-key) > index(ዘመደ፡ካዕብ,1)
  any(ካዕብ፡ልዩ)   + any(ሣልስ-key) > index(ዘመደ፡ሣልስ,1)
  any(ካዕብ፡ልዩ)   + any(ራብዕ-key) > index(ዘመደ፡ራብዕ,1)
  any(ዘመደ፡ሣልስ)  + any(ግዕዝ-key) > index(ዘመደ፡ኃምስ,1)

c Alternative input to support confusion around the 'o' glypheme:
  'ኮ' + 'o' > 'ኰ'
  'ጎ' + 'o' > 'ጐ'

  'ኰ' + 'o' > 'ኮ'
  'ጐ' + 'o' > 'ጎ'
c ---------------------End Input of Labiovelars-------------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c 
c The only dead key for entry of special characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c 
  any(ሳድስ)      + "'" > index(ሳድስ,1) deadkey(1)
  any(ሳድስ)      + [CTRL K_SPACE] > index(ሳድስ,1) deadkey(1)
  any(ካዕብ፡መደበኛ) + "'" > index(ካዕብ፡መደበኛ,1) deadkey(1)
  any(ካዕብ፡ልዩ)   + "'" > index(ካዕብ፡ልዩ,1) deadkey(1)
  any(ዘመደ፡ሣልስ)  + "'" > index(ዘመደ፡ሣልስ,1) deadkey(1)             
  any(ሣልስ)      + "'" > index(ሣልስ,1) deadkey(1)


      + ':'     > '፡'
      + [SHIFT K_SPACE] > '፡'
  '፡' + ':' > '።'
  '፡' + '-' > '፦'
  '፡' + '+' > '፠'
  '፡' + '#' > '፨'  
  '።' + ':' > ':'
  ':' + ':' > '፡'  c makes circular  
  
      + ',' > '፣'
  '፣' + ',' > '፥'
  '፥' + ',' > ','
  ',' + ',' > '፣'  c makes circular

      + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፤'  c makes circular

      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
  store(euro)    'ÇçÐðÝýÿßÑñ'
  store(euroMap) 'ጭችድድይይይጽኝኝ'
  + any(euro) > index(euroMap,1)

  'ጽ' + 'ß' > 'ፅ'
  + '¨' > U+135F

  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c this mapping has an Amharic bias
  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Arabic Numerals
c 
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c 
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  '#' + '1' > '፩'
  '#' + any(ArabOnes) > index(ones,2)

c special cases for multiples of one
    '፩' + '0' >   '፲'
    '፲' + '0' >   '፻'
    '፻' + '0' >  '፲፻'
   '፲፻' + '0' >   '፼'
    '፼' + '0' >  '፲፼'    
   '፲፼' + '0' >  '፻፼' 
   '፻፼' + '0' > '፲፻፼'
  '፲፻፼' + '0' >  '፼፼'
   '፼፼' + '0' > context beep  c do not go any higher, we could beep here

c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1)  '፻' c Hundreds
  any(ones)  '፻' + '0' > index(tens,1)  '፻' c Thousands
  any(tens)  '፻' + '0' > index(ones,1)  '፼' c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1)  '፼' c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions

c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)
c ---------------------End Input of Numbers-----------------------------------------------------
                                            
c =====================End Functional Section===================================================
