c =====================Begin Identity Section===================================================
c 
c The Ge'ez Frontier Foundation's mnemonic input method for Tigrinya script under Eritrean conventions on US-QWERTY
c keyboards for SIL Keyman, compliant with Unicode 3.0 and later.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_tigrinya_eritrea/1.1/gff_tigrinya_eritrea.php
c Source        :  https://github.com/keymanapp/keyboards/gff_tigrinya_eritrea
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
c 
  store(&Version) '9.0'
  store(&Name) 'ትግርኛ (Tigrinya)'
  store(&EthnologueCode) 'tir'
store(&LANGUAGE) 'x0873'
store(&WINDOWSLANGUAGES) 'x0873'
  store(&Copyright) '© 2009-2017 Ge' U+0027 'ez Frontier Foundation'
  store(&Message) 'This is a Tigrinya language mnemonic input method that applies Eritrean writing conventions.  It requires a font supporting Ethiopic script under the Unicode 3.0 standard.'
  store(&KeyboardVersion) '1.1'
  store(&CapsAlwaysOff) '1'
  store(&HotKey) '[CTRL ALT K_T]'
store(&BITMAP) 'gff_tigrinya.ico'
store(&VISUALKEYBOARD) 'gff_tigrinya_eritrea.kvk'
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================

c The remainder of this document is formatted with a fixed width font.

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
  store(gIz)   'ሀለሐመረሰሸቀቐበቨተቸነኘከኸወዐዘዠየደጀገጠጨጰጸፈፐ'
  store(kaIb)  'ሁሉሑሙሩሱሹቁቑቡቩቱቹኑኙኩኹዉዑዙዡዩዱጁጉጡጩጱጹፉፑ'
  store(sals)  'ሂሊሒሚሪሲሺቂቒቢቪቲቺኒኚኪኺዊዒዚዢዪዲጂጊጢጪጲጺፊፒ'
  store(rabI)  'ሃላሓማራሳሻቃቓባቫታቻናኛካኻዋዓዛዣያዳጃጋጣጫጳጻፋፓ'
  store(hams)  'ሄሌሔሜሬሴሼቄቔቤቬቴቼኔኜኬኼዌዔዜዤዬዴጄጌጤጬጴጼፌፔ'
  store(sads)  'ህልሕምርስሽቅቕብቭትችንኝክኽውዐዝዥይድጅግጥጭጵጽፍፕ'
  store(sabI)  'ሆሎሖሞሮሶሾቆቖቦቮቶቾኖኞኮኾዎዖዞዦዮዶጆጎጦጮጶጾፎፖ'

c QWERTY keymappings
  store(keys)  'hlHmrsxqQbvtcnNkKw_zZydjgTCPSfp'

c the following is Tigrinya specific
  store(extraKeys) 'LMRXBVWYDJFG'
  store(extraSads) 'ልምርሽብቭውይድጅፍግ'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Maps for Vowels----------------------------------------------------------
  store(loneVowelKeys)  'aAuUiIeEoO'
  store(loneVowelEthio) 'ኣዐኡኡኢኢእእኦኦ'
  store(composableVowel) 'አኡኢእኦዐዒዓዕ'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(zemedeKeys)      'ልሕምርስሽብቭትችኅንኝዝዥድጅጥጭጵጽፍፕ'
  store(zemedeKeysKaIb)  'ሉሑሙሩሱሹቡቩቱቹኁኑኙዙዡዱጁጡጩጱጹፉፑ'
  store(zemedeEthio)     'ሏሗሟሯሷሿቧቯቷቿኋኗኟዟዧዷጇጧጯጷጿፏፗ'

c 'hhqQkg'
  store(zemedeKeysKaIbExtended)  'ሁኁቁቑኩኹጉ'
  store(zemedeWe)  'ኈኈቈቘኰዀጐ'
  store(zemedeWu)  'ኍኍቍቝኵዅጕ'
  store(zemedeWi)  'ኊኊቊቚኲዂጒ'
  store(zemedeWa)  'ኋኋቋቛኳዃጓ'
  store(zemedeWEE) 'ኌኌቌቜኴዄጔ'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(ArabOnes) '23456789'
  store(ones)     '፪፫፬፭፮፯፰፱'
  store(tens)     '፳፴፵፶፷፸፹፺'
  store(arabNumbers) '123456789'
  store(ethNumbers) '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
  store(arabNumbersWithZero) '0123456789'
  store(ColonOrComma) ':,'
  store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
  store(gIz-key)  'eEäÄéÉèÈêÊëË'
  store(kaIb-key) 'uUùÙúÚûÛüÜ'
  store(sals-key) 'iIìÌíÍîÎïÏ'
  store(rabI-key) 'aAàÀáÁâÂãÃåÅ'
  store(hams-key) 'æÆ'
  store(sabI-key) 'oOòÒóÓôÔõÕöÖøØ'
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 

  begin Unicode > use(main)
  group(main) using keys

c ---------------------Input of Special Symbols-------------------------------------------------
c 
c The input section here is for rules that must precede other rules
c 
  + '_' > U+135F c gemination
  U+135F + '_' > '_'

  + any(extraKeys) > index(extraSads,1)
  + any(keys)     > index(sads,1)
c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Input of Double Strike Letters-------------------------------------------
c None in Eritrean Tigrinya
c ---------------------End Input of Double Strike Letters---------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c 
c Alef-A Vowels as Vowels
  + any(loneVowelKeys) > index(loneVowelEthio,1)

  'አ' + any(gIz-key)  > 'እ'
  'አ' + any(kaIb-key) > 'ኡ'
  'አ' + any(sals-key) > 'ኢ'
c 'አ' + any(rabI-key) > 'ኣ'
  'ኢ' + any(gIz-key)  > 'ኤ'
  'አ' + any(sabI-key) > 'ኦ'

c Aynu-A Vowels as Regular Sylalbles
  'እ' + any(gIz-key)  > 'ዕ'

c Aynu-A Vowels as Vowels
  'ኡ' + any(kaIb-key) > 'ዑ'
  'ኢ' + any(sals-key) > 'ዒ'
  'ኤ' + any(gIz-key)  > 'ዔ'
  'ኦ' + any(sabI-key) > 'ዖ'

c Completing the Tigrinya-ER "a" chain:
c Tigrinya-ER: ኣ -> ዓ -> ዐ -> አ
  'ኣ' + any(rabI-key) > 'ዓ'
  'ዓ' + any(rabI-key) > 'ዐ' deadkey(1)
  'ዐ' deadkey(1) + any(rabI-key) > 'አ'

  'ዐ' + any(gIz-key)  > 'ዕ'
  'ዐ' + any(kaIb-key) > 'ዑ'
  'ዐ' + any(sals-key) > 'ዒ'
  'ዐ' + any(rabI-key) > 'ዓ'
  'ዐ' + any(sabI-key) > 'ዖ'

  any(composableVowel) + "'" > index(composableVowel,1) deadkey(2)
c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(sads) + any(gIz-key)  > index(gIz,1)
  any(sads) + any(kaIb-key) > index(kaIb,1)
  any(sads) + any(sals-key) > index(sals,1)
  any(sads) + any(rabI-key) > index(rabI,1)
  any(sads) + any(hams-key) > index(hams,1)
  any(sals) + any(gIz-key)  > index(hams,1)  
  any(sads) + any(sabI-key) > index(sabI,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(zemedeKeysKaIb)         + any(rabI-key) > index(zemedeEthio,1)
  any(zemedeKeysKaIbExtended) + any(gIz-key)  > index(zemedeWe,1)
  any(zemedeKeysKaIbExtended) + any(kaIb-key) > index(zemedeWu,1)
  any(zemedeKeysKaIbExtended) + any(sals-key) > index(zemedeWi,1)
  any(zemedeKeysKaIbExtended) + any(rabI-key) > index(zemedeWa,1)
  any(zemedeWi)               + any(gIz-key)  > index(zemedeWEE,1)

c Alternative input to support confusion around the 'o' glypheme:
  'ኮ' + 'o' > 'ኰ'
  'ጎ' + 'o' > 'ጐ'

  'ኰ' + 'o' > 'ኮ'
  'ጐ' + 'o' > 'ጎ'
c ---------------------End Input of Labiovelars-------------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c 
c The only dead key for entry of special characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c 
  any(sads)  + "'" > index(sads,1) deadkey(1)
  deadkey(1) + "'" > "'"


      + ':' > '፡'
  '፡' + ':' > '።'
  '።' + ':' > ':'
  ':' + ':' > '፡'  c makes circular 
  '፡' + '+' > '፠'
  '፡' + '#' > '፨'
      + ',' > '፡'
  '፡' + ',' > '፥' 
  '፥' + ',' > ','
  ',' + ',' > '፡'  c makes circular

      + ';' > '፣'
  '፣' + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፣'  c makes circular

  '፡' + '-' > '፦'

  '?' + '?' > '፧'  c Eritrean use
  
      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
  store(euro) 'ÇçÐðÝýÿßÑñ'
  store(euroMap) 'ጭችድድይይይጽኝኝ'
  + any(euro) > index(euroMap,1)

  + '¨' > U+135F

  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c this mapping has an Amharic bias
  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Arabic Numerals
c 
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c 
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  '#' + '1' > '፩'
  '#' + any(ArabOnes) > index(ones,2)
  '#' + '#' > '#'

c special cases for multiples of one
    '፩' + '0' >   '፲'
    '፲' + '0' >   '፻'
    '፻' + '0' >  '፲፻'
   '፲፻' + '0' >   '፼'
    '፼' + '0' >  '፲፼'    
   '፲፼' + '0' >  '፻፼' 
   '፻፼' + '0' > '፲፻፼'
  '፲፻፼' + '0' >  '፼፼'
   '፼፼' + '0' > context beep  c do not go any higher, we could beep here

c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1)  '፻' c Hundreds
  any(ones)  '፻' + '0' > index(tens,1)  '፻' c Thousands
  any(tens)  '፻' + '0' > index(ones,1)  '፼' c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1)  '፼' c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions

c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)
c ---------------------End Input of Numbers-----------------------------------------------------
                                            
c =====================End Functional Section===================================================
