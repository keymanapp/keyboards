c =====================Begin Identity Section===================================================
c 
c The Ge'ez Frontier Foundation's mnemonic input method for Amharic script on US-QWERTY
c keyboards for SIL Keyman, compliant with Unicode 4.1 and later.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_amharic/1.9/gff_amharic.php
c Source        :  https://github.com/keymanapp/keyboards/tree/master/release/gff/gff_amharic
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
c 
  store(&VERSION) '9.0'
store(&NAME) 'አማርኛ (Amharic)'
c $keymanonly:store(&MnemonicLayout) "1"
  store(&CapsAlwaysOff) "0"
store(&COPYRIGHT) '© 1997-2020 Ge' U+0027 'ez Frontier Foundation, SIL International'
  store(&Message) "This is an Amharic language mnemonic input method for Ethiopic script that requires Unicode 4.1 support."
store(&WINDOWSLANGUAGES) 'x045E'
store(&LANGUAGE) 'x045E'
  store(&EthnologueCode) "amh"
store(&VISUALKEYBOARD) 'gff_amharic.kvks'
  store(&HOTKEY) "[CTRL ALT K_A]"
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
  store(ግዕዝ)  'ሀለሐመሠረሰሸቀበቨተቸኀነኘከኸወዐዘዠየደጀገጠጨጰጸፀፈፐ'
  store(ካዕብ)  'ሁሉሑሙሡሩሱሹቁቡቩቱቹኁኑኙኩኹዉዑዙዡዩዱጁጉጡጩጱጹፁፉፑ'
  store(ሣልስ)  'ሂሊሒሚሢሪሲሺቂቢቪቲቺኂኒኚኪኺዊዒዚዢዪዲጂጊጢጪጲጺፂፊፒ'
  store(ራብዕ)  'ሃላሓማሣራሳሻቃባቫታቻኃናኛካኻዋዓዛዣያዳጃጋጣጫጳጻፃፋፓ'
  store(ኃምስ)  'ሄሌሔሜሤሬሴሼቄቤቬቴቼኄኔኜኬኼዌዔዜዤዬዴጄጌጤጬጴጼፄፌፔ'
  store(ሳድስ)  'ህልሕምሥርስሽቅብቭትችኅንኝክኽውዐዝዥይድጅግጥጭጵጽፅፍፕ'
  store(ሳብዕ)  'ሆሎሖሞሦሮሶሾቆቦቮቶቾኆኖኞኮኾዎዖዞዦዮዶጆጎጦጮጶጾፆፎፖ'

  store(keys)  'hlHm_rsxqbvtc_nNkKw_zZydjgTCPS_fp'
c the following is Amharic specific
  store(ሌሎች፡keys) 'LMRXBVQWYDJGF'
  store(ሌሎች፡ሳድስ)  'ልምርሽብቭቅውይድጅግፍ'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Maps for Vowels----------------------------------------------------------
  store(alefKeys)  'aAuUiIeEoO'
  store(alefEthio) 'አአኡኡኢኢእእኦኦ'
  
c store(ሳድስTerminator) [K_SHIFT_LEFT] [K_SHIFT_RIGHT]
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(ሳድስ፡መደበኛ)     'ህልሕምሥርስሽቅብቭትችኅንኝክዝዥድጅግጥጭጵጽፅፍፕ'
  store(ካዕብ፡መደበኛ)     'ሁሉሑሙሡሩሱሹቁቡቩቱቹኁኑኙኩዙዡዱጁጉጡጩጱጹፁፉፑ'
  store(ዘመደ፡ራብዕ፡መደበኛ) 'ኋሏሗሟሧሯሷሿቋቧቯቷቿኋኗኟኳዟዧዷጇጓጧጯጷጿጿፏፗ'

c 'hhkqg'
  store(ካዕብ፡ልዩ)  'ሁኁኩቁጉ'
  store(ዘመደ፡ግዕዝ) 'ኈኈኰቈጐ'
  store(ዘመደ፡ካዕብ) 'ኍኍኵቍጕ'
  store(ዘመደ፡ሣልስ) 'ኊኊኲቊጒ'
  store(ዘመደ፡ኃምስ) 'ኌኌኴቌጔ'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(ArabOnes) '23456789'
  store(ones)     '፪፫፬፭፮፯፰፱'
  store(tens)     '፳፴፵፶፷፸፹፺'
  store(arabNumbers) '123456789'
  store(ethNumbers)  '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
  store(arabNumbersWithZero) '0123456789'
  store(ColonOrComma) ':,'
  store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
$keymanonly: store(ግዕዝ-key)  'eEäÄéÉèÈêÊëË'
$keymanweb:  store(ግዕዝ-key)  'eE'
$keymanonly: store(ካዕብ-key) 'uUùÙúÚûÛüÜ'
$keymanweb:  store(ካዕብ-key) 'uU'
$keymanonly: store(ሣልስ-key) 'iIìÌíÍîÎïÏ'
$keymanweb:  store(ሣልስ-key) 'iI'
$keymanonly: store(ራብዕ-key) 'aAàÀáÁâÂãÃåÅ'
$keymanweb:  store(ራብዕ-key) 'aA'
$keymanonly: store(ኃምስ-key) 'æÆ'
$keymanonly: store(ሳብዕ-key) 'oOòÒóÓôÔõÕöÖøØ'
$keymanweb:  store(ሳብዕ-key) 'oO'
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 
store(&LAYOUTFILE) 'gff_amharic.keyman-touch-layout'
store(&KEYBOARDVERSION) '1.9'
store(&TARGETS) 'any windows macosx'
store(&BITMAP) 'gff_amharic.ico'

begin Unicode > use(main)
group(main) using keys    

c ---------------------Extra keys for touch screen input devices ------------------------
c 
 + [T_GLOTTAL_E] > 'እ'
 + [T_PHARYNGEAL_E] > 'ዕ'
 + [T_TE]  > 'ት' 
 + [T_THE] > 'ጥ' 
 + [T_GLOTTAL_U] > 'ኡ'
 + [T_PHARYNGEAL_U] > 'ዑ'
 + [T_GLOTTAL_I] > 'ኢ'
 + [T_PHARYNGEAL_I] > 'ዒ'
 + [T_GLOTTAL_O] > 'ኦ'
 + [T_PHARYNGEAL_O] > 'ዖ'
 + [T_PE]  > 'ፕ' 
 + [T_PHE] > 'ጵ' 
 
 + [T_GLOTTAL_A] > 'አ'
 + [T_PHARYNGEAL_A] > 'ዓ'  
 + [T_SE] > 'ስ'
 + [T_SZE] > 'ሥ'
 + [T_TSE] > 'ጽ'
 + [T_TZE] > 'ፅ'
 + [T_HE] > 'ህ'
 + [T_XE] > 'ኅ'
 + [T_HHE] > 'ሕ'
 
 + [T_ZE] > 'ዝ'
 + [T_ZHE] > 'ዥ'
 + [T_CE] > 'ች'
 + [T_CHE] > 'ጭ'
 + [T_NE] > 'ን'
 + [T_NYE] > 'ኝ'
 
 + [T_KE] > 'ክ'
 + [T_KXE] > 'ኽ'             
 
 + [T_WORDSPACE] > "፡"  
 
 + [T_LCHEVRON] > "«"
 + [T_RCHEVRON] > "»" 
"«" + [T_LCHEVRON] > "("
"»" + [T_RCHEVRON] > ")"
"(" + [T_LCHEVRON] > "["
")" + [T_RCHEVRON] > "]"
"[" + [T_LCHEVRON] > "<"
"]" + [T_RCHEVRON] > ">"
"<" + [T_LCHEVRON] > "«"
">" + [T_RCHEVRON] > "»"

".." + "." > "…"
"…"  + "." > "..."

 + [T_LPARENTHESIS] > "("
 + [T_LBRKT] > "["
 + [T_LBRACE] > "{"
 + [T_LANGLE] > "<"
 + [T_RPARENTHESIS] > ")"
 + [T_RBRKT] > "]"
 + [T_RBRACE] > "}"
 + [T_RANGLE] > ">"

 + [T_COMMA_ENG] > ","
 + [T_COLON_ENG] > ":"
 + [T_SEMICOLON_ENG] > ";"
 + [T_QUESTION_ENG] > "?"
 + [T_EXCLAMATION] > "!"
 
 + [T_ALPHA] > nul  	c Layer switch only
 + [T_new_2514] > nul   c dummy (spacer) keys
 + [T_new_2515] > nul
 + [T_new_2516] > nul
 
c Ethiopic characters from pop-up keys
 + [T_COMMA] > "፣"   	
 + [T_FULL_STOP] > "።" 	
 + [T_PARAGRAPH_SEPARATOR] > "፨"
 + [T_SECTION_MARK] > "፠"   	
 + [T_QUESTION_MARK] > "፧"   
 + [T_SEMICOLON] > "፤"
 + [T_COLON] > "፥"   	
 + [T_PREFACE_COLON] > "፦" 
 + [T_GEMINATION_MARK] > U+135F
 
c Ethiopic numerals
 + [T_ONE] > "፩"   
 + [T_TWO] > "፪"   
 + [T_THREE] > "፫"   
 + [T_FOUR] > "፬"   
 + [T_FIVE] > "፭"   
 + [T_SIX] > "፮"   
 + [T_SEVEN] > "፯"   
 + [T_EIGHT] > "፰"   
 + [T_NINE] > "፱"   
 + [T_TEN] > "፲"   
 + [T_TWENTY] > "፳"   
 + [T_THIRTY] > "፴"   
 + [T_FORTY] > "፵"   
 + [T_FIFTY] > "፶"   
 + [T_SIXTY] > "፷"   
 + [T_SEVENTY] > "፸"   
 + [T_EIGHTY] > "፹"   
 + [T_NINETY] > "፺"   
 + [T_HUNDRED] > "፻"   
 + [T_TEN_THOUSAND] > "፼"
 
 
c ---------------------Input of Special Symbols-------------------------------------------------
c 
c The input section here is for rules that must precede other rules
c 
  + '_' > U+135F c gemination
  U+135F + '_' > '_'

c The following two rules had to be added here (not later) to fix the deadkey issue
  deadkey(1) + "'" > "'"
  deadkey(1) + any(alefKeys) > index(alefEthio,2)
 
  + any(ሌሎች፡keys) > index(ሌሎች፡ሳድስ,1)
  + any(keys)     > index(ሳድስ,1)
c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Input of Double Strike Letters-------------------------------------------
  'ስ' + 's' > 'ሥ'
  'ህ' + 'h' > 'ኅ'
  'ጽ' + 'S' > 'ፅ'
c ---------------------End Input of Double Strike Letters---------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c 
c Alef-A Vowels as Vowels
  + any(alefKeys) > index(alefEthio,1)
  any(alefEthio) + "'" > index(alefEthio,1) deadkey(1)  
  'አ' + any(ግዕዝ-key) > 'ኧ'

c Alef-A Vowels as Regular Syllables
  'እ' + any(ካዕብ-key) > 'ኡ'
  'እ' + any(ሣልስ-key) > 'ኢ'
  'እ' + any(ራብዕ-key) > 'ኣ'
  'ኢ' + any(ግዕዝ-key) > 'ኤ'
  'እ' + any(ሳብዕ-key) > 'ኦ'

c Aynu-A Vowels as Regular Syllables
  'እ' + any(ግዕዝ-key) > 'ዕ'
  'ዕ' + any(ግዕዝ-key) > 'ዐ'
  'ዕ' + any(ካዕብ-key) > 'ዑ'
  'ዕ' + any(ሣልስ-key) > 'ዒ'
  'ዕ' + any(ራብዕ-key) > 'ዓ'
  'ዒ' + any(ግዕዝ-key) > 'ዔ'
  'ዕ' + any(ሳብዕ-key) > 'ዖ'

c Aynu-A Vowels as Vowels
  'ኡ' + any(ካዕብ-key) > 'ዑ'
  'ኢ' + any(ሣልስ-key) > 'ዒ'
  'ኦ' + any(ሳብዕ-key) > 'ዖ'

c Completing the Amharic "a" chain:
  'አ' + any(ራብዕ-key) > 'ዓ'
  'ዓ' + any(ራብዕ-key) > 'ዐ'
  'ዐ' + any(ራብዕ-key) > 'ኣ'

c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(ሳድስ) + any(ግዕዝ-key) > index(ግዕዝ,1)
  any(ሳድስ) + any(ካዕብ-key) > index(ካዕብ,1)
  any(ሳድስ) + any(ሣልስ-key) > index(ሣልስ,1)
  any(ሳድስ) + any(ራብዕ-key) > index(ራብዕ,1)
$keymanonly: any(ሳድስ) + any(ኃምስ-key) > index(ኃምስ,1)
  any(ሣልስ) + any(ግዕዝ-key) > index(ኃምስ,1)  
  any(ሳድስ) + any(ሳብዕ-key) > index(ሳብዕ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(ካዕብ፡መደበኛ)              + any(ራብዕ-key) > index(ዘመደ፡ራብዕ፡መደበኛ,1)  
  any(ካዕብ፡ልዩ) + any(ግዕዝ-key) > index(ዘመደ፡ግዕዝ,1)
  any(ካዕብ፡ልዩ) + any(ካዕብ-key) > index(ዘመደ፡ካዕብ,1)
  any(ካዕብ፡ልዩ) + any(ሣልስ-key) > index(ዘመደ፡ሣልስ,1)
  any(ዘመደ፡ሣልስ)               + any(ግዕዝ-key)  > index(ዘመደ፡ኃምስ,1)

  'ኮ' + 'o' > 'ኰ'
  'ጎ' + 'o' > 'ጐ'

  'ኰ' + 'o' > 'ኮ'
  'ጐ' + 'o' > 'ጎ'
c ---------------------End Input of Labiovelars-------------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c 
c The only dead key for entry of special characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c 
  any(ሳድስ)      + "'" > index(ሳድስ,1) deadkey(1)
  any(ሳድስ)      + [CTRL K_SPACE] > index(ሳድስ,1) deadkey(1) 
  any(ካዕብ፡መደበኛ) + "'" > index(ካዕብ፡መደበኛ,1) deadkey(1)
  any(ካዕብ፡ልዩ)   + "'" > index(ካዕብ፡ልዩ,1) deadkey(1)
  any(ዘመደ፡ሣልስ)  + "'" > index(ዘመደ፡ሣልስ,1) deadkey(1)  
  any(ሣልስ)      + "'" > index(ሣልስ,1) deadkey(1)
   
 
c deadkey(1) + "'" > "'"
c deadkey(1) + any(alefKeys) > index(alefEthio,2)

  + ':'     > '፡'
  + [SHIFT K_SPACE] > '፡'
  '፡' + ':' > '።'
  '።' + ':' > ':'
  ':' + ':' > '፡'  c makes circular
      + ',' > '፣'
  '፣' + ',' > '፥'
  '፥' + ',' > ','
  ',' + ',' > '፣'  c makes circular

      + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፤'  c makes circular

  '፡' + '-' > '፦'

      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
  store(euro) 'ÇçÐðÝýÿßÑñ'
  store(euroMap) 'ጭችድድይይይጽኝኝ'
$keymanonly: + any(euro) > index(euroMap,1)

$keymanonly: 'ጽ' + 'ß' > 'ፅ'
$keymanonly: + '¨' > U+135F

  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c this mapping has an Amharic bias
  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
$keymanonly:  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Arabic Numerals
c 
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c 
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  '#' + '1' > '፩'
  '#' + any(ArabOnes) > index(ones,2)
  '#' + '#' > '#' 

c special cases for multiples of one
   '፩' + '0' > '፲'
   '፲' + '0' > '፻'
   '፻' + '0' > '፲፻'
  '፲፻' + '0' > '፼'
   '፼' + '0' > '፲፼'
  '፲፼' + '0' > '፻፼'
  '፻፼' + '0' > '፲፻፼'
 '፲፻፼' + '0' > '፼፼'
  '፼፼' + '0' > context beep  c do not go any higher, alert the user with a beep
 
 
c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1) '፻'  c Hundreds
  any(ones)  '፻' + '0' > index(tens,1) '፻'  c Thousands
  any(tens)  '፻' + '0' > index(ones,1) '፼'  c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1) '፼'  c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions 
  any(ones) '፼፼' + '0' > context beep  c do not go any higher, alert the user with a beep 


c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)
c ---------------------End Input of Numbers-----------------------------------------------------                             
                                            
c =====================End Functional Section===================================================
