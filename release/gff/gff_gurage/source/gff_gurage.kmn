c =====================Begin Identity Section===================================================
c 
c The Geʾez Frontier Foundation's mnemonic input method for the experimental Gurage orthography
c devised by Dr. Fekede Menuta and adopted in the Unicode 14 Ethiopic Extended-B block.
c 
c Keyman        :  http://www.keyman.com/
c Documentation :  https://help.keyman.com/keyboard/gff_gurage/0.6/gff_gurage.php
c Source        :  https://github.com/keymanapp/keyboards/gff_gurage
c License       :  https://opensource.org/licenses/MIT
c Bugs          :  https://github.com/keymanapp/keyboards/issues
c 
c Specification :  http://keyboards.ethiopic.org/specification/
c Other Info    :  http://keyboards.ethiopic.org/ , http://unicode.org/charts/
  store(&Version) '15.0'
store(&NAME) 'ጉራጌ (Gurage)'
c store(&ETHNOLOGUECODE) 'sgw'
store(&COPYRIGHT) '© 2019-2023 Geʾez Frontier Foundation'
store(&MESSAGE) 'This is a Gurage (ጉራጊና) language mnemonic input method.  It requires a font supporting the Ethiopic Extended-B block defined in the Unicode 14 standard.'
  store(&CapsAlwaysOff) '1'
  store(&HotKey) '[CTRL ALT K_G]'
store(&BITMAP) 'gff_gurage.ico'
store(&VISUALKEYBOARD) 'gff_gurage.kvks'
store(&LAYOUTFILE) 'gff_gurage.keyman-touch-layout'
store(&KMW_EMBEDCSS) 'gff_gurage.css'
$keymanonly:  store(&mnemonicLayout) "1"
c 
c =====================End Identity Section=====================================================

c =====================Begin Data Section=======================================================
c
c Define the set of Ethiopic characters to support contest based input:
c
store(Ethiopic) ' ' 'ሀ' .. '፼' .. 'ᎀ' .. '᎙ⶀ' .. 'ⷞꬁ' .. 'ꬮ𞟠' .. '𞟾'

c The remainder of this document is formatted with a fixed width font.

c ---------------------Maps of Syllabic Forms---------------------------------------------------
c the following is universal (language neutral) these mappings can be overridden by
c language specific mappings
store(ግዕዝ) 'ሐ𞟠ለመረሰሸቀቐበተቸነኘከኸወዐዘዠየደጀገጘጠጨፈፐ'
store(ካዕብ) 'ሑ𞟡ሉሙሩሱሹቁቑቡቱቹኑኙኩኹዉዑዙዡዩዱጁጉጙጡጩፉፑ'
store(ሣልስ) 'ሒ𞟢ሊሚሪሲሺቂቒቢቲቺኒኚኪኺዊዒዚዢዪዲጂጊጚጢጪፊፒ'
store(ራብዕ) 'ሓ𞟣ላማራሳሻቃቓባታቻናኛካኻዋዓዛዣያዳጃጋጛጣጫፋፓ'
store(ኃምስ) 'ሔ𞟤ሌሜሬሴሼቄቔቤቴቼኔኜኬኼዌዔዜዤዬዴጄጌጜጤጬፌፔ'
store(ሳድስ) 'ሕ𞟥ልምርስሽቅቕብትችንኝክኽውዕዝዥይድጅግጝጥጭፍፕ'
store(ሳብዕ) 'ሖ𞟦ሎሞሮሶሾቆቖቦቶቾኖኞኮኾዎዖዞዦዮዶጆጎጞጦጮፎፖ'

store(keys) 'hHlmrsxqQbtcnNkKw_zZydjgGTCfp'


c the following is Sebatbeit specific
store(ሌሎች፡keys) 'LMRXBVvWYDJSFP'
store(ሌሎች፡ሳድስ)  'ልምርሽብብብውይድጅስፍፕ'
c ---------------------End Maps of Syllabic Forms-----------------------------------------------

c ---------------------Begin Maps of Sebatbeit Forms-----------------------------------------------
c Deprecating experimentally.
c
c store(potentialSebatbeit) 'ሕቅክግ'
c store(sebatbeit)  '𞟥ቕኽጝ'
c store(sebatbeitY) 'yY'
c ---------------------End Maps of Sebatbeit Forms-----------------------------------------------

c ---------------------Maps for Vowels----------------------------------------------------------
  store(loneVowelKeys)   [T_ግዕዝ]'aAuUiIeEoO'[T_ኃምስ]
  store(loneVowelEthio) 'እኣዐኡዑኢዒእዕኦዖኤ'
  store(composableVowel) 'ኣኢዐዒ'
c ---------------------End Vowel Maps-----------------------------------------------------------

c --------------------Maps for Labiovelars------------------------------------------------------
  store(ሳድስ፡መደበኛ)     'ምብጵጽፍፕ'
  store(ካዕብ፡መደበኛ)     'ሙቡጱጹፉፑ'
  store(ዘመደ፡ራብዕ፡መደበኛ) 'ሟቧጷጿፏፗ'

c 'hmbqkKgfp'
  store(ካዕብ፡ልዩ)  'ሑሙቁቡኩጉፉፑ'
  store(ራብዕ፡ልዩ)  'ሓማቃባካጋፋፓ'
  store(ዘመደ፡ግዕዝ) '𞟨ᎀቈᎄኰጐᎈᎌ'
  store(ዘመደ፡ካዕብ) '𞟫ᎃ𞟲ᎇ𞟷𞟺ᎋᎏ'
  store(ዘመደ፡ሣልስ) '𞟩𞟭𞟰𞟳𞟵𞟸𞟻𞟽'
  store(ዘመደ፡ራብዕ) 'ሗሟቋቧኳጓፏፗ'
  store(ዘመደ፡ኃምስ) '𞟪𞟮𞟱𞟴𞟶𞟹𞟼𞟾'
c ---------------------End Labiovelars----------------------------------------------------------

c ---------------------Maps for Numbers---------------------------------------------------------
  store(ArabOnes) '23456789'
  store(ones)     '፪፫፬፭፮፯፰፱'
  store(tens)     '፳፴፵፶፷፸፹፺'
  store(arabNumbers) '123456789'
  store(ethNumbers)  '፩፪፫፬፭፮፯፰፱፲፳፴፵፶፷፸፹፺፻፼'
  store(arabNumbersWithZero) '0123456789'
  store(ColonOrComma)        ':,'
  store(ethWordspaceOrComma) '፡፣'
c ---------------------End Numbers--------------------------------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------
$keymanonly: store(ግዕዝ-key) 'eEäÄéÉèÈêÊëË'
$keymanweb:  store(ግዕዝ-key) 'eE'
$keymanonly: store(ካዕብ-key) 'uUùÙúÚûÛüÜ'
$keymanweb:  store(ካዕብ-key) 'uU'
$keymanonly: store(ሣልስ-key) 'iIìÌíÍîÎïÏ'
$keymanweb:  store(ሣልስ-key) 'iI'
$keymanonly: store(ራብዕ-key) 'aAàÀáÁâÂãÃåÅ'
$keymanweb:  store(ራብዕ-key) 'aA'
$keymanonly: store(ኃምስ-key) 'æÆ'
c $keymanweb:  store(ኃምስ-key) [T_ኃምስ]
$keymanonly: store(ሳብዕ-key) 'oOòÒóÓôÔõÕöÖøØ'
$keymanweb:  store(ሳብዕ-key) 'oO'
c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

c ---------------------Maps of Keystrokes of Syllabic Forms-------------------------------------

store(touchVowels)  [T_ግዕዝ][T_ካዕብ][T_ሣልስ][T_ራብዕ][T_ኃምስ][T_ሳብዕ][T_WWA]
store(ሀ_ቤተሰብ) 'ሀሁሂሃሄሆኋ'
store(ለ_ቤተሰብ) 'ለሉሊላሌሎሏ'
store(ሐ_ቤተሰብ) 'ሐሑሒሓሔሖሗ'
store(𞟠_ቤተሰብ) '𞟠𞟡𞟢𞟣𞟤𞟥𞟦!'
store(መ_ቤተሰብ) 'መሙሚማሜሞሟ'
store(ሠ_ቤተሰብ) 'ሠሡሢሣሤሦሧ'
store(ረ_ቤተሰብ) 'ረሩሪራሬሮሯ'
store(ሰ_ቤተሰብ) 'ሰሱሲሳሴሶሷ'
store(ሸ_ቤተሰብ) 'ሸሹሺሻሼሾሿ'
store(ቀ_ቤተሰብ) 'ቀቁቂቃቄቆ!'
store(ቐ_ቤተሰብ) 'ቐቑቒቓቔቖ!'
store(በ_ቤተሰብ) 'በቡቢባቤቦቧ'
store(ቨ_ቤተሰብ) 'ቨቩቪቫቬቮቯ'
store(ተ_ቤተሰብ) 'ተቱቲታቴቶቷ'
store(ቸ_ቤተሰብ) 'ቸቹቺቻቼቾቿ'
store(ኀ_ቤተሰብ) 'ኀኁኂኃኄኆኋ'
store(ነ_ቤተሰብ) 'ነኑኒናኔኖኗ'
store(ኘ_ቤተሰብ) 'ኘኙኚኛኜኞኟ'
store(አ_ቤተሰብ) 'አኡኢኣኤኦኧ'
store(ከ_ቤተሰብ) 'ከኩኪካኬኮ!'
store(ኸ_ቤተሰብ) 'ኸኹኺኻኼኾ!'
store(ወ_ቤተሰብ) 'ወዉዊዋዌዎ!'
store(ዐ_ቤተሰብ) 'ዐዑዒዓዔዖ!'
store(ዘ_ቤተሰብ) 'ዘዙዚዛዜዞዟ'
store(ዠ_ቤተሰብ) 'ዠዡዢዣዤዦዧ'
store(የ_ቤተሰብ) 'የዩዪያዬዮዯ!'
store(ደ_ቤተሰብ) 'ደዱዲዳዴዶዷ'
store(ዸ_ቤተሰብ) 'ዸዹዺዻዼዾዿ'
store(ጀ_ቤተሰብ) 'ጀጁጂጃጄጆጇ'
store(ገ_ቤተሰብ) 'ገጉጊጋጌጎ!'
store(ጘ_ቤተሰብ) 'ጘጙጚጛጜጞ!'
store(ጠ_ቤተሰብ) 'ጠጡጢጣጤጦጧ'
store(ጨ_ቤተሰብ) 'ጨጩጪጫጬጮጯ'
store(ጰ_ቤተሰብ) 'ጰጱጲጳጴጶጷ'
store(ጸ_ቤተሰብ) 'ጸጹጺጻጼጾጿ'
store(ፀ_ቤተሰብ) 'ፀፁፂፃፄፆ!'
store(ፈ_ቤተሰብ) 'ፈፉፊፋፌፎፏ'
store(ፐ_ቤተሰብ) 'ፐፑፒፓፔፖፗ'

store(touchVowelsLabialvelar) [T_ዘመደ_ግዕዝ][T_ዘመደ_ካዕብ][T_ዘመደ_ሣልስ][T_ዘመደ_ራብዕ][T_ዘመደ_ኃምስ]
store(𞟨_ቤተሰብ) '𞟨𞟫𞟩ሗ𞟪'
store(ᎀ_ቤተሰብ) 'ᎀᎃ𞟭ሟ𞟮'
store(ቈ_ቤተሰብ) 'ቈቍቊቋቌ'
store(ቘ_ቤተሰብ) 'ቘቝቚቛቜ'
store(ᎄ_ቤተሰብ) 'ᎄᎇ𞟳ቧ𞟴'
store(ኈ_ቤተሰብ) 'ኈኍኊኋኌ'
store(ኰ_ቤተሰብ) 'ኰኵኲኳኴ'
store(ዀ_ቤተሰብ) 'ዀዅዂዃዄ'
store(ጐ_ቤተሰብ) 'ጐጕጒጓጔ'
store(ⶓ_ቤተሰብ) 'ⶓⶖⶔጟⶕ'
store(ᎈ_ቤተሰብ) 'ᎈᎋ𞟻ፏ𞟼'
store(ᎌ_ቤተሰብ) 'ᎌᎏ𞟽ፗ𞟾'

store(touchVowelsLabialvelarModernGurage) [T_ዘመደ_ካዕብ_ጉራጌ][T_ዘመደ_ሣልስ_ጉራጌ][T_ዘመደ_ኃምስ_ጉራጌ]
store(ቈ_ቤተሰብ_ጉራጌ) '𞟲𞟰𞟱'
store(ኰ_ቤተሰብ_ጉራጌ) '𞟷𞟵𞟶'
store(ጐ_ቤተሰብ_ጉራጌ) '𞟺𞟸𞟹'

store(non-gurage፡ሳድስ)  'ህሥቭኅዽፅጸጵ'
store(non-gurage፡ግዕዝ)  'ሀሠቨኀዸፀጸጰ'  						
store(non-gurage፡ካዕብ)  'ሁሡቩኁዹፁጹጱ'
store(non-gurage፡ሣልስ)  'ሂሢቪኂዺፂጺጲ'
store(non-gurage፡ራብዕ)  'ሃሣቫኃዻፃጻጳ'
store(non-gurage፡ኃምስ)  'ሄሤቬኄዼፄጼጴ'
store(non-gurage፡ሳብዕ)  'ሆሦቮኆዾፆጾጶ'

c =====================End Data Section=========================================================

c =====================Begin Functional Section=================================================
c 
store(&TARGETS) 'any'
store(&KEYBOARDVERSION) '1.0'
  begin Unicode > use(main)
  group(main) using keys

c ---------------------Begin Mobile Input ------------------------------------------------------
c
c Mobile input is separated from Desktop in v3.1 for ease of maintenance
c
  any(ሳድስ) + [T_ግዕዝ] > index(ግዕዝ,1)
  any(ሳድስ) + [T_ካዕብ] > index(ካዕብ,1)
  any(ሳድስ) + [T_ሣልስ] > index(ሣልስ,1)
  any(ሳድስ) + [T_ራብዕ] > index(ራብዕ,1)
  any(ሳድስ) + [T_ኃምስ] > index(ኃምስ,1) 
  any(ሳድስ) + [T_ሳብዕ] > index(ሳብዕ,1)
  any(ሳድስ፡መደበኛ) + [T_WWA] > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ግዕዝ] > index(ዘመደ፡ግዕዝ,1)
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ሣልስ] > index(ዘመደ፡ሣልስ,1)
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ራብዕ] > index(ዘመደ፡ራብዕ,1)
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ኃምስ] > index(ዘመደ፡ኃምስ,1)
  any(ራብዕ፡ልዩ) +  [T_ዘመደ_ካዕብ] > index(ዘመደ፡ካዕብ,1)

  any(non-gurage፡ሳድስ) + [T_ግዕዝ] > index(non-gurage፡ግዕዝ,1)
  any(non-gurage፡ሳድስ) + [T_ካዕብ] > index(non-gurage፡ካዕብ,1)
  any(non-gurage፡ሳድስ) + [T_ሣልስ] > index(non-gurage፡ሣልስ,1)
  any(non-gurage፡ሳድስ) + [T_ራብዕ] > index(non-gurage፡ራብዕ,1)
  any(non-gurage፡ሳድስ) + [T_ኃምስ] > index(non-gurage፡ኃምስ,1) 
  any(non-gurage፡ሳድስ) + [T_ሳብዕ] > index(non-gurage፡ሳብዕ,1)

  
  if( &layer = "default" ) + any(touchVowels) > index(አ_ቤተሰብ,2)
  if( &layer = "ሀ-extra" ) + any(touchVowels) > index(ሀ_ቤተሰብ,2)
  if( &layer = "ለ-layer" ) + any(touchVowels) > index(ለ_ቤተሰብ,2)
  if( &layer = "ሐ-layer" ) + any(touchVowels) > index(ሐ_ቤተሰብ,2)
  if( &layer = "መ-layer" ) + any(touchVowels) > index(መ_ቤተሰብ,2)
  if( &layer = "ሠ-extra" ) + any(touchVowels) > index(ሠ_ቤተሰብ,2)
  if( &layer = "ረ-layer" ) + any(touchVowels) > index(ረ_ቤተሰብ,2)
  if( &layer = "ሰ-layer" ) + any(touchVowels) > index(ሰ_ቤተሰብ,2)
  if( &layer = "ሸ-layer" ) + any(touchVowels) > index(ሸ_ቤተሰብ,2)
  if( &layer = "ቀ-layer" ) + any(touchVowels) > index(ቀ_ቤተሰብ,2)
  if( &layer = "ቐ-layer" ) + any(touchVowels) > index(ቐ_ቤተሰብ,2)
  if( &layer = "በ-layer" ) + any(touchVowels) > index(በ_ቤተሰብ,2)
  if( &layer = "ቨ-layer" ) + any(touchVowels) > index(ቨ_ቤተሰብ,2)
  if( &layer = "ቨ-extra" ) + any(touchVowels) > index(ቨ_ቤተሰብ,2)
  if( &layer = "ተ-layer" ) + any(touchVowels) > index(ተ_ቤተሰብ,2)
  if( &layer = "ቸ-layer" ) + any(touchVowels) > index(ቸ_ቤተሰብ,2)
  if( &layer = "ኀ-extra" ) + any(touchVowels) > index(ኀ_ቤተሰብ,2)
  if( &layer = "ነ-layer" ) + any(touchVowels) > index(ነ_ቤተሰብ,2) 
  if( &layer = "ኘ-layer" ) + any(touchVowels) > index(ኘ_ቤተሰብ,2)
  if( &layer = "ኘ-extra" ) + any(touchVowels) > index(ኘ_ቤተሰብ,2)
  if( &layer = "አ-layer" ) 'እ' + any(touchVowels) > index(አ_ቤተሰብ,3)
  if( &layer = "አ-layer" ) + any(touchVowels) > index(አ_ቤተሰብ,2)
  if( &layer = "ከ-layer" ) + any(touchVowels) > index(ከ_ቤተሰብ,2)
  if( &layer = "ኸ-layer" ) + any(touchVowels) > index(ኸ_ቤተሰብ,2)
  if( &layer = "ኸ-extra" ) + any(touchVowels) > index(ኸ_ቤተሰብ,2)
  if( &layer = "ዐ-extra" ) 'ዕ' + any(touchVowels) > index(ዐ_ቤተሰብ,3)
  if( &layer = "ዐ-extra" ) 'ዐ' + any(touchVowels) > context(2) index(ዐ_ቤተሰብ,3)
  if( &layer = "ዐ-extra" ) + any(touchVowels) > index(ዐ_ቤተሰብ,2)
  if( &layer = "ወ-layer" ) + any(touchVowels) > index(ወ_ቤተሰብ,2)
  if( &layer = "ዘ-layer" ) + any(touchVowels) > index(ዘ_ቤተሰብ,2)
  if( &layer = "ዠ-layer" ) + any(touchVowels) > index(ዠ_ቤተሰብ,2)
  if( &layer = "ዠ-extra" ) + any(touchVowels) > index(ዠ_ቤተሰብ,2)
  if( &layer = "የ-layer" ) + any(touchVowels) > index(የ_ቤተሰብ,2)
  if( &layer = "ደ-layer" ) + any(touchVowels) > index(ደ_ቤተሰብ,2)
  if( &layer = "ዸ-extra" ) + any(touchVowels) > index(ዸ_ቤተሰብ,2)
  if( &layer = "ጀ-layer" ) + any(touchVowels) > index(ጀ_ቤተሰብ,2)
  if( &layer = "ገ-layer" ) + any(touchVowels) > index(ገ_ቤተሰብ,2)
  if( &layer = "ጘ-layer" ) + any(touchVowels) > index(ጘ_ቤተሰብ,2)
  if( &layer = "ጠ-layer" ) + any(touchVowels) > index(ጠ_ቤተሰብ,2) 
  if( &layer = "ጨ-layer" ) + any(touchVowels) > index(ጨ_ቤተሰብ,2)
  if( &layer = "ጰ-layer" ) + any(touchVowels) > index(ጰ_ቤተሰብ,2)
  if( &layer = "ጰ-extra" ) + any(touchVowels) > index(ጰ_ቤተሰብ,2)  
  if( &layer = "ጸ-extra" ) + any(touchVowels) > index(ጸ_ቤተሰብ,2)
  if( &layer = "ፀ-extra" ) + any(touchVowels) > index(ፀ_ቤተሰብ,2)
  if( &layer = "ፈ-layer" ) + any(touchVowels) > index(ፈ_ቤተሰብ,2)
  if( &layer = "ፐ-layer" ) + any(touchVowels) > index(ፐ_ቤተሰብ,2)
  if( &layer = "𞟠-layer" ) + any(touchVowels) > index(𞟠_ቤተሰብ,2)

  if( &layer = "𞟨-layer" ) + any(touchVowelsLabialvelar) > index(𞟨_ቤተሰብ,2)
  if( &layer = "ᎀ-layer" ) + any(touchVowelsLabialvelar) > index(ᎀ_ቤተሰብ,2)
  if( &layer = "ቈ-layer" ) + any(touchVowelsLabialvelar) > index(ቈ_ቤተሰብ,2)
  if( &layer = "ᎄ-layer" ) + any(touchVowelsLabialvelar) > index(ᎄ_ቤተሰብ,2)
  if( &layer = "ኈ-layer" ) + any(touchVowelsLabialvelar) > index(ኈ_ቤተሰብ,2)
  if( &layer = "ኰ-layer" ) + any(touchVowelsLabialvelar) > index(ኰ_ቤተሰብ,2)
  if( &layer = "ጐ-layer" ) + any(touchVowelsLabialvelar) > index(ጐ_ቤተሰብ,2)
  if( &layer = "ᎈ-layer" ) + any(touchVowelsLabialvelar) > index(ᎈ_ቤተሰብ,2)
  if( &layer = "ᎌ-layer" ) + any(touchVowelsLabialvelar) > index(ᎌ_ቤተሰብ,2)

  if( &layer = "ቈ-layer" ) + any(touchVowelsLabialvelarModernGurage) > index(ቈ_ቤተሰብ_ጉራጌ,2)
  if( &layer = "ኰ-layer" ) + any(touchVowelsLabialvelarModernGurage) > index(ኰ_ቤተሰብ_ጉራጌ,2)
  if( &layer = "ጐ-layer" ) + any(touchVowelsLabialvelarModernGurage) > index(ጐ_ቤተሰብ_ጉራጌ,2)

c Addition for tablet layout ("extras" are no longer "extra")
  if( &layer = "ሀ-layer" ) + any(touchVowels) > index(ሀ_ቤተሰብ,2)
  if( &layer = "ሠ-layer" ) + any(touchVowels) > index(ሠ_ቤተሰብ,2)
  if( &layer = "ኀ-layer" ) + any(touchVowels) > index(ኀ_ቤተሰብ,2)
  if( &layer = "ዐ-layer" ) 'ዕ' + any(touchVowels) > index(ዐ_ቤተሰብ,3)
  if( &layer = "ዐ-layer" ) 'ዐ' + any(touchVowels) > context(2) index(ዐ_ቤተሰብ,3)
  if( &layer = "ዐ-layer" ) + any(touchVowels) > index(ዐ_ቤተሰብ,2)
  if( &layer = "ዠ-layer" ) + any(touchVowels) > index(ዠ_ቤተሰብ,2)
  if( &layer = "ጰ-layer" ) + any(touchVowels) > index(ጰ_ቤተሰብ,2)  
  if( &layer = "ጸ-layer" ) + any(touchVowels) > index(ጸ_ቤተሰብ,2)
  if( &layer = "ፀ-layer" ) + any(touchVowels) > index(ፀ_ቤተሰብ,2)
c
c ---------------------End of Mobile Input ------------------------------------------------------

c ---------------------Input of Special Symbols--------------------------------------------------
c 
c The input section here is for rules that must precede other rules
c 

$keymanonly: + [CTRL '.'] > U+135E
$keymanonly: U+135E + [CTRL '.'] > U+135F
$keymanonly: U+135F + [CTRL '.'] > U+135D
$keymanonly: U+135D + [CTRL '.'] > U+135D beep
c
c Ethiopic combining marks for vowel and consonant stress.
c
c The following can happen from the touch keyboards፡
c
U+135E + [U_135E] > U+135F c 1 + 1 > 2
U+135E + [U_135F] > U+135D c 1 + 2 > 3
U+135F + [U_135E] > U+135D c 2 + 1 > 3
c
c warn but don't combine:
c
U+135E + [U_135D] > U+135E beep  c 1 + 3
U+135F + [U_135F] > U+135F beep  c 2 + 2
U+135F + [U_135D] > U+135F beep  c 2 + 3
U+135D + [U_135E] > U+135D beep  c 3 + 1
U+135D + [U_135F] > U+135D beep  c 3 + 2
U+135D + [U_135D] > U+135D beep  c 3 + 3


  + any(ሌሎች፡keys) > index(ሌሎች፡ሳድስ,1)
  + any(keys)     > index(ሳድስ,1)
c ---------------------End Input of Special Symbols---------------------------------------------

c ---------------------Input of Lone Vowels-----------------------------------------------------
c 
c Alef-A Vowels as Vowels
  + any(loneVowelKeys) > index(loneVowelEthio,1)

  'ኢ' + any(ግዕዝ-key) > 'ኤ'
  'ኣ' + any(ራብዕ-key) > 'አ'

  'ዐ' + any(ግዕዝ-key) > 'ዕ'
  'ዐ' + any(ካዕብ-key) > 'ዑ'
  'ዐ' + any(ሣልስ-key) > 'ዒ'
  'ዐ' + any(ራብዕ-key) > 'ዓ'
  'ዐ' + any(ሳብዕ-key) > 'ዖ'

  any(composableVowel) + "'" > index(composableVowel,1) deadkey(2)
c ---------------------End Input of Lone Vowels-------------------------------------------------

c ---------------------Input of Basic Syllables-------------------------------------------------
  any(ሳድስ) + any(ግዕዝ-key) > index(ግዕዝ,1)
  any(ሳድስ) + any(ካዕብ-key) > index(ካዕብ,1)
  any(ሳድስ) + any(ሣልስ-key) > index(ሣልስ,1)
  any(ሳድስ) + any(ራብዕ-key) > index(ራብዕ,1)
$keymanonly: any(ሳድስ) + any(ኃምስ-key) > index(ኃምስ,1)
  any(ሣልስ) + any(ግዕዝ-key) > index(ኃምስ,1)  
  any(ሳድስ) + any(ሳብዕ-key) > index(ሳብዕ,1)
c ---------------------End Input of Basic Syllables---------------------------------------------

c ---------------------Input of Sebatbeit Syllables-------------------------------------------------
c  any(potentialSebatbeit) + any(sebatbeitY) > index(sebatbeit,1)
c ---------------------End Input of Sebatbeit Syllables---------------------------------------------

c ---------------------Input of Labiovelars-----------------------------------------------------
  any(ካዕብ፡መደበኛ) + any(ራብዕ-key) > index(ዘመደ፡ራብዕ፡መደበኛ,1)
  any(ካዕብ፡ልዩ) + any(ግዕዝ-key) > index(ዘመደ፡ግዕዝ,1)
  any(ካዕብ፡ልዩ) + any(ካዕብ-key) > index(ዘመደ፡ካዕብ,1)
  any(ካዕብ፡ልዩ) + any(ሣልስ-key) > index(ዘመደ፡ሣልስ,1)
  any(ካዕብ፡ልዩ) + any(ራብዕ-key) > index(ዘመደ፡ራብዕ,1)
$keymanonly: any(ካዕብ፡ልዩ) + any(ኃምስ-key) > index(ዘመደ፡ኃምስ,1)
  any(ዘመደ፡ሣልስ) + any(ግዕዝ-key)  > index(ዘመደ፡ኃምስ,1)
c ---------------------End Input of Labiovelars-------------------------------------------------

c ---------------------Input of Punctuation-----------------------------------------------------
c 
c The only dead key for entry of special characters and
c to handle the ge'ez + lone vowel problem:  ግእዝ and not ገዝ
c 
  any(ሳድስ)      + "'" > context(1) deadkey(1)
$keymanonly: any(ሳድስ) + [CTRL ' '] > context(1) deadkey(1)
  any(ካዕብ፡መደበኛ) + "'" > context(1) deadkey(1)
  any(ራብዕ፡ልዩ)   + "'" > context(1) deadkey(1)
  any(ዘመደ፡ሣልስ)  + "'" > context(1) deadkey(1)             
  any(ሣልስ)      + "'" > context(1) deadkey(1)

  any(arabNumbersWithZero) + ':' > context(1) ':'
  any(arabNumbersWithZero) ':' + ' ' > context(1) '፡' ' ' 
  
+ ':'      > '፡'
$keymanonly: + [SHIFT ' '] > '፡'
  '፡' + ':' > '።'
  '፡' + '-' > '፦'
  '፡' + '+' > '፠'
  '፡' + '#' > '፨'  
  '።' + ':' > ':'
  ':' + ':' > '፡'  c makes circular
  
  '።' + '.' > '.'
  '።' + any(arabNumbersWithZero) > '.' index(arabNumbersWithZero,2)
  any(Ethiopic) + '.' > context(1) '።'
  any(arabNumbersWithZero) '.' + ' ' > context(1) '።' ' ' 
   
   
  any(arabNumbersWithZero) + ',' > context(1) ','
      + ',' > '፣'
  '፣' + ',' > '፥'
  '፥' + ',' > ','
  ',' + ',' > '፣'  c makes circular
  notany(arabNumbersWithZero) '፣' + any(arabNumbersWithZero) > context(1) '፣' index(arabNumbersWithZero,3)
  '፣' + any(arabNumbersWithZero) > ',' index(arabNumbersWithZero,2)
  any(arabNumbersWithZero) ',' + ' ' > context(1) '፣' ' ' 


      + ';' > '፤'
  '፤' + ';' > ';'
  ';' + ';' > '፤'  c makes circular

      + '<' > '‹'
      + '>' > '›'
  '‹' + '<' > '«'
  '›' + '>' > '»'
  '«' + '<' > '<'
  '»' + '>' > '>'
c ---------------------End Input of Punctuation-------------------------------------------------

c ---------------------Input of European Characters---------------------------------------------
c  store(euro)    'ÇçÐðÝýÿßÑñ'
c  store(euroMap) 'ጭችድድይይይጽኝኝ'
c  + any(euro )   > index(euroMap,1)

c  + '¨' > U+135F

c  store(euroVowels) 'àÀáÁâÂãÃåÅùÙúÚûÛüÜìÌíÍîÎïÏæÆéÉèÈêÊëËòÒóÓôÔõÕöÖøØ'
c this mapping has an Amharic bias
c  store(euroVowelsMap) 'አአአአአአአአአአኡኡኡኡኡኡኡኡኢኢኢኢኢኢኢኢኤኤእእእእእእእእኦኦኦኦኦኦኦኦኦኦኦኦ'
c  + any(euroVowels) > index(euroVowelsMap,1)
c ---------------------End Input of European Characters-----------------------------------------

c ---------------------Input of Numbers---------------------------------------------------------

c Special Rule for Arabic Numerals
c 
c The following attempts to auto-correct the use of Ethiopic wordspace and
c Ethiopic comma within an Arabic numeral context.  Ethiopic wordspace gets
c used erroneously in time formats and Ethiopic commas as an order of thousands
c delimiter. The correction context is not known until numerals appear on _both_
c sides of the punctuation.
c 
  any(arabNumbersWithZero) any(ethWordspaceOrComma) + any(arabNumbers) > index(arabNumbersWithZero,1) index(ColonOrComma,2) index(arabNumbers,3)

c Ethiopic Numerals

  '#' + '1' > '፩'
  '#' + any(ArabOnes) > index(ones,2)
  '#' + '#' > '#'

c special cases for multiples of one
    '፩' + '0' >   '፲'
    '፲' + '0' >   '፻'
    '፻' + '0' >  '፲፻'
   '፲፻' + '0' >   '፼'
    '፼' + '0' >  '፲፼'    
   '፲፼' + '0' >  '፻፼' 
   '፻፼' + '0' > '፲፻፼'
  '፲፻፼' + '0' >  '፼፼'
   '፼፼' + '0' > context beep  c do not go any higher, we could beep here

c upto the order of 100 million
  any(ones)      + '0' > index(tens,1)
  any(tens)      + '0' > index(ones,1)  '፻' c Hundreds
  any(ones)  '፻' + '0' > index(tens,1)  '፻' c Thousands
  any(tens)  '፻' + '0' > index(ones,1)  '፼' c Ten Thousands
  any(ones)  '፼' + '0' > index(tens,1)  '፼' c Hundred Thousands
  any(tens)  '፼' + '0' > index(ones,1) '፻፼' c Millions
  any(ones) '፻፼' + '0' > index(tens,1) '፻፼' c Ten Millions
  any(tens) '፻፼' + '0' > index(ones,1) '፼፼' c Hundred Millions

c enhance this later, look for something that can copy a match over
  any(ethNumbers) + any(arabNumbers) > index(ethNumbers,1)  index(ethNumbers,2)

+ [T_THOUSAND] > '፲፻'
c ---------------------End Input of Numbers-----------------------------------------------------

+ [T_SPACER] > beep c this should be impossible, this line needed to to avoid "Warning: 2092" warnings for unknown key T_SPACER
                                            
c =====================End Functional Section===================================================
