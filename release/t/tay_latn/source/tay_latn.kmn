c Vietnamese Unikey layout NFC
c M. Hosken
c Apr 2014

store(&VERSION) '10.0'
store(&NAME) 'Tày Latin'
store(&COPYRIGHT) 'Copyright © SIL Global'
store(&KEYBOARDVERSION) '1.1'
store(&TARGETS) 'any'
store(&BITMAP) 'tay_latn.ico'
store(&VISUALKEYBOARD) 'tay_latn.kvks'
store(&LAYOUTFILE) 'tay_latn.keyman-touch-layout'

begin unicode > use(main)

store(extratoneK)   "q"     "k"
store(extratone)    U+0331  U+030F

store(combtoneK)    "f"     "s"     "r"     "j"     "x"
store(combtone)     U+0300  U+0301  U+0309  U+0323  U+0303
store(atone)        U+00E0  U+00E1  U+1EA3  U+1EA1  U+00E3  c U+0061
store(actone)       U+00C0  U+00C1  U+1EA2  U+1EA0  U+00C3  c U+0041
store(abrevetone)   U+1EB1  U+1EAF  U+1EB3  U+1EB7  U+1EB5  c U+0103
store(acbrevetone)  U+1EB0  U+1EAE  U+1EB2  U+1EB6  U+1EB4  c U+0102
store(acirctone)    U+1EA7  U+1EA5  U+1EA9  U+1EAD  U+1EAB  c U+00E2
store(accirctone)   U+1EA6  U+1EA4  U+1EA8  U+1EAC  U+1EAA  c U+00C2
store(etone)        U+00E8  U+00E9  U+1EBB  U+1EB9  U+1EBD  c U+0065
store(ectone)       U+00C8  U+00C9  U+1EBA  U+1EB8  U+1EBC  c U+0045
store(ecirctone)    U+1EC1  U+1EBF  U+1EC3  U+1EC7  U+1EC5  c U+00EA
store(eccirctone)   U+1EC0  U+1EBE  U+1EC4  U+1EC6  U+1EC4  c U+00CA
store(itone)        U+00EC  U+00ED  U+1EC9  U+1ECB  U+0129  c U+0069
store(ictone)       U+00CC  U+00CD  U+1EC8  U+1ECA  U+0128  c U+0049
store(otone)        U+00F2  U+00F3  U+1ECF  U+1ECD  U+00F5  c U+006F
store(octone)       U+00D2  U+00D3  U+1ECE  U+1ECC  U+00d5  c U+004F
store(ocirctone)    U+1ED3  U+1ED1  U+1ED5  U+1ED9  U+1ED7  c U+00F4
store(occirctone)   U+1ED2  U+1ED0  U+1ED4  U+1ED8  U+1ED6  c U+00D4
store(ohooktone)    U+1EDD  U+1EDB  U+1EDF  U+1EE3  U+1EE1  c U+01A1
store(ochooktone)   U+1EDC  U+1EDA  U+1EDE  U+1EE2  U+1EE0  c U+01A0
store(utone)        U+00F9  U+00FA  U+1EE7  U+1EE5  U+0169  c U+0075
store(uctone)       U+00D9  U+00DA  U+1EE6  U+1EE4  U+0168  c U+0055
store(uhooktone)    U+1EEB  U+1EE9  U+1EED  U+1EF1  U+1EEF  c U+01B0
store(uchooktone)   U+1EEA  U+1EE8  U+1EEC  U+1EF0  U+1EEE  c U+01AF

store(vowels)       "aAeEiIoOuUyY"
store(evowels)      U+0103 U+0102 U+00E2 U+00C2 U+00EA U+00CA U+00F4 U+00D4 U+01A1 U+01A0 U+01B0 U+01AF

store(akeys)        "w"     "a"     "W"     "A"
store(amods)        U+0103  U+00E2  U+0103  U+00E2
store(acmods)       U+0102  U+00C2  U+0102  U+00C2

store(ekeys)        "e"     "E"
store(emods)        U+00EA  U+00EA
store(ecmods)       U+00CA  U+00CA

store(okeys)        "o"     "O"     "w"     "W"     "["
store(omods)        U+00F4  U+00F4  U+01A1  U+01A1  U+01A1
store(ocmods)       U+00D4  U+00D4  U+01A0  U+01A0  U+01A0

store(ukeys)        "w"     "W"     "]"
store(umods)        U+01B0  U+01B0  U+01B0
store(ucmods)       U+01AF  U+01AF  U+01AF

store(dkeys)        "d"     "D"
store(dmods)        U+0111  U+0111
store(dcmods)       U+0110  U+0110

group(main) using keys

c specials
U+00F4 + "o" > "oo"
U+00F4 + "O" > "oO"
U+00D4 + "o" > "Oo"
U+00D4 + "O" > "OO"

"a" + any(akeys) > index(amods, 2)
"A" + any(akeys) > index(acmods, 2)
"e" + any(ekeys) > index(emods, 2)
"E" + any(ekeys) > index(ecmods, 2)
"o" + any(okeys) > index(omods, 2)
"O" + any(okeys) > index(ocmods, 2)
"u" + any(ukeys) > index(umods, 2)
"U" + any(ukeys) > index(ucmods, 2)
"d" + any(dkeys) > index(dmods, 2)
"D" + any(dkeys) > index(dcmods, 2)

U+0061 + any(combtoneK) > index(atone, 2)
U+0041 + any(combtoneK) > index(actone, 2)
U+0103 + any(combtoneK) > index(abrevetone, 2)
U+0102 + any(combtoneK) > index(acbrevetone, 2)
U+00E2 + any(combtoneK) > index(acirctone, 2)
U+00C2 + any(combtoneK) > index(accirctone, 2)
U+0065 + any(combtoneK) > index(etone, 2)
U+0045 + any(combtoneK) > index(ectone, 2)
U+00EA + any(combtoneK) > index(ecirctone, 2)
U+00CA + any(combtoneK) > index(eccirctone, 2)
U+0069 + any(combtoneK) > index(itone, 2)
U+0049 + any(combtoneK) > index(ictone, 2)
U+006F + any(combtoneK) > index(otone, 2)
U+004F + any(combtoneK) > index(octone, 2)
U+00F4 + any(combtoneK) > index(ocirctone, 2)
U+00D4 + any(combtoneK) > index(occirctone, 2)
U+01A1 + any(combtoneK) > index(ohooktone, 2)
U+01A0 + any(combtoneK) > index(ochooktone, 2)
U+0075 + any(combtoneK) > index(utone, 2)
U+0055 + any(combtoneK) > index(uctone, 2)
U+01B0 + any(combtoneK) > index(uhooktone, 2)
U+01AF + any(combtoneK) > index(uchooktone, 2)

any(vowels) + any(combtoneK) > context index(combtone, 2)
any(vowels) + any(extratoneK) > context index(extratone, 2)
any(evowels) + any(extratoneK) > context index(extratone, 2)




