c *** Tamil99 variant Keyboard for use with e-Kalappai Keyman in Windows
c *** Incorporates all rules of Tamil99 (with some variations in the manner
c *** of conformance to some of the rules) as well as additional features

c *** This keymap is a modification and extension from the original file written for
c e-Kalappai for Windows by Umar.

c *** This modification and extension is by Mugunth and K.Sethu (Oct 2008)

c *** References for tamil99 layout and keystroke rules:
c *** layout:                 http://www.tamilvu.org/Tamilnet99/annex1.htm
c *** keystroke rules:    http://www.tamilvu.org/Tamilnet99/annex2.htm

store(&VERSION) "10.0"
store(&KEYBOARDVERSION) "2.1"
store(&Name) "த99-விரிவு | ta99 Extended"
store(&Copyright)"© 2008-2020 thamizha.com and SIL International"
store(&MESSAGE) "ta99-ext Unicode Keyboard - Contributed by Umar(csd_one@yahoo.com), Mugunth (mugunth@gmail.com) and K. Sethu (skhome@gmail.com)"
store(&BITMAP) 'thamizha_tamil99_ext.ico'
c store(&WINDOWSLANGUAGES) 'x0449'
c store(&LANGUAGE) 'x0449'
c store(&ETHNOLOGUECODE) 'tam'
store(&VISUALKEYBOARD) 'thamizha_tamil99_ext.kvks'
store(&TARGETS) 'web desktop'

begin Unicode > use(Main)

group( Main ) using keys

c ============================================
c Assignments for bases of uyir, akarameis, grantha akarmeis and aytham
c - as per layout and Rule 1

store( uyirE )            "aqswdegtrcxz"          
store( uyirE1 )           "qswdegtrcxz"           
store( akara_meyE )     "hb[]opl;jk'mnv/yui"       

store( uyirT )             U+0B85 U+0B86 U+0B87 U+0B88 U+0B89 U+0B8A \
                                 U+0B8E U+0B8F U+0B90 U+0B92 U+0B93 U+0B94

store( akara_meyT )   U+0B95 U+0B99 U+0B9A U+0B9E U+0B9F U+0BA3 \
                                 U+0BA4 U+0BA8 U+0BAA U+0BAE U+0BAF U+0BB0 \
                                 U+0BB2 U+0BB5 U+0BB4 U+0BB3 U+0BB1 U+0BA9

store( vow )               U+0BBE U+0BBF U+0BC0 U+0BC1 U+0BC2 U+0BC6 \
                                 U+0BC7 U+0BC8 U+0BCA U+0BCB U+0BCC

store(sri)                   U+0BB8 U+0BCD U+0BB0 U+0BC0
store(sri2)                  U+0BB6 U+0BCD U+0BB0 U+0BC0

store(ksha)                U+0B95 U+0BCD U+0BB7

c Grantha AkaraMeys - including key "U" for ஶ (SHA - U+0BB6)
store(grandhaE)        "QWERU"
store(grandhaT)         U+0BB8 U+0BB7 U+0B9C U+0BB9 U+0BB6

store(nul) "GHJN"

+ any(nul) > nul
 
 + "F" > U+0B83
 
+ any( uyirE ) > index( uyirT, 1 )
+ any( akara_meyE ) > index( akara_meyT, 1 )
+ any( grandhaE ) > index( grandhaT, 1 )

c ஸ்ரீ மற்றும் க்ஷ (Sri and ksha)
 + "T" > outs(sri)
 + "Y" > outs(ksha)

c ஸ்ரீ = ஶ + புள்ளி + ரீ  வரையரைக்கு விசை "U"
c Assignment of I for new Sri =  U+0BB6 U+0BCD U+0BB0 U+0BC0
 + "I" > outs(sri2)

c க்+ஷ--> க்‌ஷ (Non Joining to ksha ligature when க் & ஷ் typed in)
U+0B95 U+0BCD + "W" > U+0B95 U+0BCD U+200C U+0BB7 

c தமிழ் குறியீடுகள் - Tamil Symbols
+ "A"  > U+0bF9         c (௹ - ரூபாய் - Rupee)
+ "S"  > U+0bfa         c (௺ - இலக்கம் - Number)
+ "D"  > U+0bf8		c (௸ - மேற்படி - as above)
+ "Z"  > U+0bf3 	c (௳ - தினம் - day)
+ "X"  > U+0bf4		c (௴ - மாதம் - month)
+ "C"  > U+0bf5		c (௵ - வருடம் - year)
+ "V"  > U+0bf6		c (௶ - செலவு (பற்று) - debit)
+ "B"  > U+0bf7   	c (௷ - வரவு - credit)

c Reassigned standard keys
c [ ] ; ' : " /
+ "O" > U+005b
+ "P" > U+005d
+ ":" > U+003b
+ '"' > U+0027
+ "K" > U+0022
+ "L" > U+003a
+ "M" > U+002f

c ============================================
c Rule 2:  அகர-மெய் + புள்ளி = மெய் (base consonant plus virama to get voiceless consonant)
any( akara_meyT ) + "f" > index( akara_meyT, 1 ) U+0BCD
any( grandhaT ) + "f" > index( grandhaT, 1 ) U+0BCD

c Rule 3 : மற்றைய உயிர்மெய்கள் (other inflexions consonants )
any( akara_meyT ) + any( uyirE1 ) > index( akara_meyT, 1 ) index( vow,2)
any( grandhaT ) + any( uyirE1 ) > index( grandhaT, 1 ) index( vow,2)
c =============================================
c Rules 4 and 5
c உடனிலை மெய்மயக்கம்  (Auto Pulli Feature)
c 1). Rules 4 & 5 for 18 akara-meis:
c ---------------------------------------------------
U+0B95 + "h" > U+0B95 U+0BCD U+0B95
U+0B95 U+0BCD U+0B95 + "h" > context U+0B95

U+0B99 + "b" > U+0B99 U+0BCD U+0B99
U+0B99 U+0BCD U+0B99 + "b" > context U+0B99

U+0B9A + "[" > U+0B9A U+0BCD U+0B9A
U+0B9A U+0BCD U+0B9A + "[" > context U+0B9A

U+0B9E + "]" > U+0B9E U+0BCD U+0B9E
U+0B9E U+0BCD U+0B9E + "]" > context U+0B9E

U+0B9F + "o" > U+0B9F U+0BCD U+0B9F
U+0B9F U+0BCD U+0B9F + "o" > context U+0B9F

U+0BA3 + "p" > U+0BA3 U+0BCD U+0BA3
U+0BA3 U+0BCD U+0BA3 + "p" > context U+0BA3

U+0BA4 + "l" > U+0BA4 U+0BCD U+0BA4 
U+0BA4 U+0BCD U+0BA4 + "l" > context U+0BA4

U+0BA8 + ";" > U+0BA8 U+0BCD U+0BA8 
U+0BA8 U+0BCD U+0BA8 + ";" > context U+0BA8

U+0BAA + "j" > U+0BAA U+0BCD U+0BAA 
U+0BAA U+0BCD U+0BAA + "j" > context U+0BAA
  
U+0BAE + "k" > U+0BAE U+0BCD U+0BAE 
U+0BAE U+0BCD U+0BAE + "k" > context U+0BAE

U+0BAF + "'" > U+0BAF U+0BCD U+0BAF 
U+0BAF U+0BCD U+0BAF + "'" > context U+0BAF

U+0BB0 + "m" > U+0BB0 U+0BCD U+0BB0 
U+0BB0 U+0BCD U+0BB0 + "m" > context U+0BB0

U+0BB2 + "n" > U+0BB2 U+0BCD U+0BB2 
U+0BB0 U+0BCD U+0BB2 + "n" > context U+0BB2

U+0BB5 + "v" > U+0BB5 U+0BCD U+0BB5 
U+0BB5 U+0BCD U+0BB5 + "v" > context U+0BB5

U+0BB4 + "/" > U+0BB4 U+0BCD U+0BB4 
U+0BB4 U+0BCD U+0BB4 + "/" > context U+0BB4

U+0BB3 + "y" > U+0BB3 U+0BCD U+0BB3 
U+0BB3 U+0BCD U+0BB3 + "y" > context U+0BB3

U+0BB1 + "u" > U+0BB1 U+0BCD U+0BB1 
U+0BB1 U+0BCD U+0BB1 + "u" > context U+0BB1

U+0BA9 + "i" > U+0BA9 U+0BCD U+0BA9
U+0BA9 U+0BCD U+0BA9 + "i" > context U+0BA9

c 
c 2). Rules 4 & 5 for six grantha akara-meis:
c -------------------------------------------------------------
U+0BB8 + "Q" > U+0BB8 U+0BCD U+0BB8
U+0BB8 U+0BCD U+0BB8 + "Q" > context U+0BB8

U+0BB7 + "W" > U+0BB7 U+0BCD U+0BB7
U+0BB7 U+0BCD U+0BB7 + "W" > context U+0BB7

U+0B9C + "E" > U+0B9C U+0BCD U+0B9C
U+0B9C U+0BCD U+0B9C + "E" > context U+0B9C

U+0BB9 + "R" > U+0BB9 U+0BCD U+0BB9
U+0BB9 U+0BCD U+0BB9 + "R" > context U+0BB9

U+0b95 U+0bcd U+0bb7 + "Y" > U+0b95 U+0bcd U+0bb7 U+0bcd U+0b95 U+0bcd U+0bb7
U+0b95 U+0bcd U+0bb7 U+0bcd U+0b95 U+0bcd U+0bb7 + "Y" > context U+0b95 U+0bcd U+0bb7

U+0BB6 + "I" > U+0BB6 U+0BCD U+0BB6
U+0BB6 U+0BCD U+0BB6 + "I" > context U+0BB6

c =============================================

c Rule 6
c தமிழ் / கிரந்த அகரமெய் அடுத்து அ வரின் மாற்றமில்லை
c (When a tamil or grandha base consonant is follweed by அ no change to base consonant - to facilitate rules 6 )
c any( akara_meyT ) + "a" > index( akara_meyT, 1) [K_LEFT][K_RIGHT]
c any( grandhaT ) + "a" > index( grandhaT, 1 ) [K_LEFT][K_RIGHT]

any( akara_meyT ) + "a" > context U+200D    c Corrected by Tavultesoft, Dec08
any( grandhaT ) + "a" > context U+200D      c Corrected by Tavultesoft, Dec08

c ============================================
c Rule 7
c வேற்றுனிலை மெய்மயக்கம்  (Auto Pulli Feature for Soft-Hard consonant pairs)
c Rule 7 for (ங க), (ஞ ச), (ந த), (ண ட), (ம ப), (ன ற)
c -------------------------------------------------------------------------------
 U+0B99 + "h" > U+0B99 U+0BCD U+0B95
 U+0B99 U+0BCD U+0B95 + "h" > context U+0B95
 U+0BA8 +  "l" > U+0BA8 U+0BCD U+0BA4
 U+0BA8 U+0BCD U+0BA4 + "l" > context U+0BA4
 U+0BAE + "j" > U+0BAE U+0BCD U+0BAA
 U+0BAE U+0BCD U+0BAA + "j" > context U+0BAA
 U+0B9E + "[" > U+0B9E U+0BCD U+0B9A
 U+0B9E U+0BCD U+0B9A + "[" > context U+0B9A
 U+0BA3 + "o" > U+0BA3 U+0BCD U+0B9F
 U+0BA3 U+0BCD U+0B9F + "o" > context U+0B9F
 U+0BA9 + "u" > U+0BA9 U+0BCD U+0BB1
 U+0BA9 U+0BCD U+0BB1 + "u" > context U+0BB1

c ================================= ===========
c Rule 8 conformed to without having to add any special assignments
c =============================================
c Rule: 9
c Dead Key (for caret) :
 + "^" > dk(0)
c To get Caret (type in caret twice to get caret - Rule 9)
dk(0) + '^'  > U+005E

c உயிரொலிகளைப் பார்க்க
c Rule 9: To reveal Vowel Modifer Glyphs (ா, ி, ீ, ு, ூ, ெ, ே, ை) using Caret
dk(0) + "q" > U+0BBE
dk(0) + "s" > U+0BBF
dk(0) + "w" > U+0BC0
dk(0) + "d" > U+0BC1
dk(0) + "e" > U+0BC2
dk(0) + "g" > U+0BC6
dk(0) + "t" > U+0BC7
dk(0) + "r" > U+0BC8
c =============================================

c Rule 10: - Assign for Bullet symbol (For Copyright see under extneded feature)

dk(0) + '.' > U+2022
c =============================================
c Rule 11: left and right single / double quotes and non breaking space
dk(0) + '7' > U+2018
dk(0) + '8' > U+2019
dk(0) + '9' > U+201C
dk(0) + '0' > U+201D
dk(0) + 'S' > U+00A0
c =============================================

c Extended features
c --------------------------------

c Copyright Symbol using caret + Capital "C" (and not caret + lower case "c":)
dk(0) + "C" > U+00A9

c Reveal Unicode Vowel Modifiers for okara, Okaara and Aukaara Meis:
dk(0) + "c" > U+0BCA
dk(0) + "x" > U+0BCB
dk(0) + "z" > U+0BCC

c If a Grandha akara-mei if followed by a space transform the akara-mei to mei:
any( grandhaT) + [K_SPACE] > index( grandhaT, 1 ) U+0BCD U+0020

c any( grandhaT) U+0BCD index( grandhaT, 1 ) U+0BCD > index( grandhaT, 1 ) index( grandhaT, 1 )

c Assign f also with aytham (when f follows any character other than an akara-mei
c mapping it to aytham)
+ "f" >  U+0B83
c End

