c gautami_thamizh_tamil generated from template at 2024-08-19 12:19:38
c with name "Gautami Thamizh/Tamil"
store(&VERSION) '10.0'
store(&NAME) 'Gautami Thamizh/Tamil'
store(&COPYRIGHT) 'Copyright © Gautam Sengupta'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any'
store(&BITMAP) 'gautami_thamizh_tamil.ico'
store(&VISUALKEYBOARD) 'gautami_thamizh_tamil.kvks'
store(&LAYOUTFILE) 'gautami_thamizh_tamil.keyman-touch-layout'
store(&Hotkey) [CTRL SHIFT K_T]

begin Unicode > use(main)

group(main) using keys

store(vowel) "ஆஇஈஉஊஎஏஐஒஓஔ"
store(matra) U+0bbe U+0bbf U+0bc0 U+0bc1 U+0bc2 U+0bc6 U+0bc7 U+0bc8 U+0bca U+0bcb U+0bcc
store(m_bearer) "கஙசஜஞடணதநனபமஙயஎரறலழளவஸஶஷஹ"
store(currency_rota_1) "₹$€£௹௶௷"
store(currency_rota_2)  "$€£௹௶௷₹"
store(num_rota_1) "௺௰௱௲"
store(num_rota_2) "௰௱௲௺"

c NUMERALS
+[RALT SHIFT K_3] > "#"
+[SHIFT K_3] > "௺"
any(num_rota_1) + [SHIFT K_3] > index(num_rota_2, 1)

c CURRENCY
+[SHIFT K_4] > "₹"
+[RALT SHIFT K_4] > "$"
any(currency_rota_1) + [SHIFT K_4] > index(currency_rota_2, 1)

c DEAD KEYS
+ [RALT K_X] > 'x'
+ [K_X] > dk(x)
dk(x) +[K_X] > dk(x)

c DITTO
+[K_BKQUOTE] > "௸"
+[RALT K_BKQUOTE] > "'"

c Tamil symbols for DAY MONTH YEAR
+[SHIFT K_2] > "௳"             c DAY SIGN
U+0bf3 +[SHIFT K_2] > U+0bf4    c MONTH SIG
U+0bf4 +[SHIFT K_2] > U+0bf5    c YEAR SIGN
U+0bf5 +[SHIFT K_2] > "௳"      c DAY SIGN 
+[RALT SHIFT K_2] > "@"

+ "|" > "।"
"।" + "|" > U+0965
U+0965 + "|" > "।"
+ [RALT SHIFT K_BKSLASH] > "|"

+ [RALT K_PERIOD] > "."
+ [K_PERIOD] > U+0bcd
U+0bcd + [K_PERIOD] > U+0b82
U+0b82 + [K_PERIOD] > U+0bcd
 
+ [RALT K_BKSLASH] > '\'
+ [K_BKSLASH] > U+0bcd

+ [K_NPPLUS] > U+200d       c    Keypad +    >   ZWJ
+ [K_NPMINUS] > U+200c      c    Keypad -    >   ZWNJ

c Joining and Splitting with {/}
+[RALT K_SLASH] > "/"
c Joining
any(vowel) + "/" > index(matra,1)
any(m_bearer) 'அ' +[K_SLASH] > context(1)
c SPLITTING
any(matra) + [K_SLASH] > index(vowel, 1)
any(m_bearer) + [K_SLASH] > context(1) 'அ'

+ [RALT K_A] > "a"
+ [K_A] > "அ"
dk(x) + [K_A] > "அ"
"அ" + [K_A] > "ஆ"
"ஆ" + [K_A] > "அ"
any(m_bearer) +[K_A] > context "ா"
+ [SHIFT K_A] > "ஆ"          use(transform)
+ [RALT SHIFT K_A] > "A"

+ [K_I] > "இ" use(transform)
+ [RALT K_I] > "i"
+ [SHIFT K_I] > "ஈ" use(transform)
+ [RALT SHIFT K_I] > "I"
"இ" + [K_I] > "ஈ"
"ஈ" + [K_I] > "இ"
"ி" + [K_I] > "ீ"
"ீ" + [K_I] > "ி"

+ [K_U] > "உ"               use(transform)
+ [SHIFT K_U] > "ஊ"         use(transform)
+ [RALT K_U] > "u"
+ [RALT SHIFT K_U] > "U"
"உ" + [K_U] > "ஊ"
"ஊ" + [K_U] > "உ"
"ு" + [K_U] > "ூ"
"ூ" + [K_U] > "ு"

+ [RALT K_E] > "e"
+ [K_E] > "எ"               use(transform)
"எ" + [K_E] > "ஏ"
"ஏ" + [K_E] > "எ"
U+0bc6 + [K_E] > U+0bc7
U+0bc7 + [K_E] > U+0bc6

+ [RALT SHIFT K_E] > "E"
+ [SHIFT K_E] > "ஐ"         use(transform)

+ [RALT K_O] > "o"
+ [K_O] > "ஒ"               use(transform)
"ஒ" + [K_O] > "ஓ"
"ஓ" + [K_O] > "ஒ"
"ொ" + [K_O] > "ோ"
"ோ" + [K_O] > "ொ"

+ [RALT SHIFT K_O] > "O"
+ [SHIFT K_O] > 'ஔ'       use(transform)
"ௌ" + [SHIFT K_O] > 'ௗ'
'ௗ' + [SHIFT K_O] > 'ௌ'

+ [RALT K_K] > "k"
+ [RALT SHIFT K_K] > "K"
+ [K_K] > "க"               use(transform)
+ [RALT K_G] > "g"
+ [RALT SHIFT K_G] > "G"
+ [K_G] > "க"               use(transform)
+ [RALT K_C] > "c"
+ [RALT SHIFT K_C] > "C"
+ [K_C] > "ச"               use(transform)
+ [RALT SHIFT K_T] > "T"
+ [SHIFT K_T] > "ட"         use(transform)
+ [RALT SHIFT K_D] > "D"    use(transform)
+ [SHIFT K_D] > "ட"         use(transform)
+ [RALT K_T] > "t"
+ [K_T] > "த"               use(transform)
+ [RALT K_D] > "d"
+ [K_D] > "த"               use(transform)
+ [RALT K_P] > "p"
+ [RALT SHIFT K_P] > "P"
+ [K_P] > "ப"               use(transform)
+ [RALT K_B] > "b"
+ [RALT SHIFT K_B] > "B"
+ [K_B] > "ப"               use(transform)

+ [RALT K_F] > "f"
+ [RALT SHIFT K_F] > "F"
+ [RALT K_Q] > "q"
+ [RALT SHIFT K_Q] > "Q"
+ [RALT K_W] > "w"
+ [RALT SHIFT K_W] > "W"
+ [RALT K_Z] > "z"
+ [RALT SHIFT K_Z] > "Z"

+ [RALT K_J] > "j"
+ [RALT SHIFT K_J] > "J"
+ [K_J] > 'ஜ'              use(transform)

+ [RALT K_N] > "n"
+ [K_N] > "ந"              use(transform)
"ந" + [K_N] > 'ன'
'ன' + [K_N] > 'ஞ'        use(transform)
'ஞ' + [K_N] > "ந"
+ [RALT SHIFT K_N] > "N"
+ [SHIFT K_N] > "ண"       use(transform)  

+ [RALT K_M] > "m"
+ [K_M] > "ம"              use(transform)
'ம' + [K_M] > "ங"         use(transform)
'ங' + [K_M] > "ம"         use(transform)

+ [RALT SHIFT K_M] > "M"
+ [SHIFT K_M] > U+0bd0


+ [RALT K_S] > "s"
+ [K_S] > "ஸ"             use(transform)
'ஸ' + [K_S] > 'ஶ'        use(transform)
'ஶ' + [K_S] > 'ஸ'        use(transform)
+ [RALT SHIFT K_S] > "S"
+ [SHIFT K_S] > "ஷ"       use(transform)

+[RALT K_H] > "h"
+ [K_H] > "ஹ"             use(transform)

+[RALT SHIFT K_H] > "H"
+[SHIFT K_H] > "ஃ"          use(transform)

+ [RALT K_Y] > "y"
+ [K_Y] > "ய"              use(transform)

+ [RALT K_R] > "r"
+ [K_R] > "ர"               use(transform)

+[RALT SHIFT K_R] > "R"
+[SHIFT K_R] > "ற"          use(transform)

+ [RALT K_L] > "l"
+ [K_L] > "ல"           use(transform)
'ல' +[K_L] > "ழ"      use(transform)
'ழ' +[K_L] > "ல"      use(transform)
+ [RALT SHIFT K_L] > "L"
+ [SHIFT K_L] > 'ள'     use(transform)

+ [RALT K_V] > "v"
+ [RALT SHIFT K_V] > "V"
+ [K_V] > 'வ'            use(transform)

c NUMERALS
+ [K_1] > U+0be7
+ [K_2] > U+0be8
+ [K_3] > U+0be9
+ [K_4] > U+0bea
+ [K_5] > U+0beb
+ [K_6] > U+0bec
+ [K_7] > U+0bed
+ [K_8] > U+0bee
+ [K_9] > U+0bef
+ [K_0] > U+0be6

+ [RALT K_1] > "1"
+ [RALT K_2] > "2"
+ [RALT K_3] > "3"
+ [RALT K_4] > "4"
+ [RALT K_5] > "5"
+ [RALT K_6] > "6"
+ [RALT K_7] > "7"
+ [RALT K_8] > "8"
+ [RALT K_9] > "9"
+ [RALT K_0] > "0"

group(transform) 
any(m_bearer) any(vowel) > context(1) index(matra,2)
dk(x) any(vowel) > context(2)
dk(x) any(m_bearer) > context(2)

c EOF
