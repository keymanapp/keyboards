store(&VERSION) '10.0'
store(&NAME) 'Fulfulde Latin (QWERTY)'
store(&COPYRIGHT) '© SIL International'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any'
store(&VISUALKEYBOARD) 'fulfulde_latin_qwerty.kvks'
store(&LAYOUTFILE) 'fulfulde_latin_qwerty.keyman-touch-layout'

begin Unicode > use(main)

c store(Hooks) U+0253 U+0181 U+0257 U+018A U+0272 U+019D U+014B U+014A U+01B4 U+01B3

store(LCalph) "abcdefghijklmnoprstuwy" c qzxv
store(UCalph) "ABCDEFGHIJKLMNOPRSTUWY" c QZXV
store(digits) "0123456789"

store(Khook) "qQzZxXvV"
store(Uhook) U+01b4 U+01b3 U+014b U+014a U+0257 U+018A U+0253 U+0181

store(punct) "`~!@#$%^&*()-_[]\{}|;':,./<>?" '"' c =+

store(Kextras) [K_EQUAL]  [SHIFT K_EQUAL]  [RALT K_EQUAL] [SHIFT RALT K_EQUAL] \
               [RALT K_Q] [SHIFT RALT K_Q] [RALT K_X]     [SHIFT RALT K_X]     \
               [RALT K_V] [SHIFT RALT K_V] [RALT K_N]     [SHIFT RALT K_N] \
               [RALT K_K] [SHIFT RALT K_K] [RALT K_M]     [SHIFT RALT K_M] [RALT K_QUOTE]

store(Uextras) U+007A     U+005A           U+003D         U+002B \
               U+0071     U+0051           U+0078         U+0058 \
               U+0076     U+0056           U+0272         U+019D \
               U+0199     U+0198           U+00F1         U+00D1            U+02BC

group(main) using keys

+ any(LCalph)   > index(LCalph,1)
+ any(UCalph)   > index(UCalph,1)
+ any(digits)   > index(digits,1)
+ any(Khook)    > index(Uhook,1)
+ any(punct)    > index(punct,1)
+ any(Kextras)  > index(Uextras,1)
