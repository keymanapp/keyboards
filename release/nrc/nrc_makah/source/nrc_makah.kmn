c Mobile (Android, iPhone) keyboard for Qʷi·qʷi·diččaq (Makah).
store(&VERSION) '10.0'
store(&NAME) 'Makah'
store(&COPYRIGHT) '2021 © National Research Council Canada'
store(&TARGETS) 'mobile'
store(&LAYOUTFILE) 'nrc_makah.keyman-touch-layout'
store(&MESSAGE) 'On behalf of the Makah Cultural & Research Center'
store(&KEYBOARDVERSION) '1.0'

store(x) 'xX'
store(ejective) 'cčkptƛqCČKPTQ'

begin Unicode > use(main)

group(main) using keys
+ [T_XSH]         > 'x' U+030C
+ [SHIFT T_XSH]   > 'X' U+030C

c Add ejective diacritic to certain consonants:
+ [T_CEJ]         > 'c' U+0313
+ [SHIFT T_CEJ]   > 'C' U+0313
+ [T_CHEJ]        > 'č' U+0313
+ [SHIFT T_CHEJ]  > 'Č' U+0313
+ [T_KEJ]         > 'k' U+0313
+ [SHIFT T_KEJ]   > 'K' U+0313
+ [T_LSHEJ]       > 'ƛ' U+0313
+ [SHIFT T_LSHEJ] > 'ƛ' U+0313  c There is no capital ƛ!
+ [T_PEJ]         > 'p' U+0313
+ [SHIFT T_PEJ]   > 'P' U+0313
+ [T_QEJ]         > 'q' U+0313
+ [SHIFT T_QEJ]   > 'Q' U+0313
+ [T_TEJ]         > 't' U+0313
+ [SHIFT T_TEJ]   > 'T' U+0313

c There is no capital ŋ!
+ [T_ENG]         > 'ŋ' 
+ [SHIFT T_ENG]   > 'ŋ' 

c Backspace deletes both diacritic AND base character:
any(ejective) U+0313 + [K_BKSP] > nul
any(ejective) U+0313 + [SHIFT K_BKSP] > nul
any(x)        U+030c + [K_BKSP] > nul
any(x)        U+030c + [SHIFT K_BKSP] > nul
