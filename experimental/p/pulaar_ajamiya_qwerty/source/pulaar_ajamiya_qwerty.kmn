store(&VERSION) '10.0'
store(&NAME) 'Pulaar Ajamiya (Qwerty)'
store(&COPYRIGHT) 'Copyright © SIL Global'
store(&KEYBOARDVERSION) '1.0'
store(&TARGETS) 'any'
store(&BITMAP) 'pulaar_ajamiya_qwerty.ico'
store(&VISUALKEYBOARD) 'pulaar_ajamiya_qwerty.kvks'
store(&LAYOUTFILE) 'pulaar_ajamiya_qwerty.keyman-touch-layout'
store(&KMW_RTL) '1'

begin Unicode > use(main)

store(charsK) [K_A]       [K_B]  [K_T]  [SHIFT K_T] [K_J]       [K_H]       [K_X]       [K_D] \
              [SHIFT K_X] [K_R]  [K_Z]  [K_S]       [SHIFT K_C] [SHIFT K_S] [SHIFT K_D] [SHIFT K_Z] [K_QUOTE] [SHIFT K_G] \
              [K_F]       [K_Q]  [K_L]  [K_M]       [K_N]       [SHIFT K_H] [K_W]       [K_Y]  \
              [SHIFT K_Y] [K_K]  [K_G] \
              [K_P]       [K_C]  [K_V] [SHIFT K_N] \
              [SHIFT K_B]
store(charsU) U+0627      U+0628 U+062A U+062B      U+062C      U+062D      U+062E      U+062F \
              U+0630      U+0631 U+0632 U+0633      U+0634      U+0635      U+0637      U+0638      U+0639 U+063A \
              U+0641      U+0642 U+0644 U+0645      U+0646      U+0647      U+0648      U+064A \
              U+0683      U+06A9 U+06AF \
              U+0752      U+0756 U+075D U+0767 \
              U+08A0 

store(diaK) [SHIFT K_A] [K_U]  [K_I]  [K_O]  [K_E]  [SHIFT K_W] [SHIFT K_Q] 
store(diaU) U+064E      U+064F U+0650 U+08F7 U+08F9 U+0651      U+0652  

store(PuncK) [K_PERIOD] [K_COMMA] [K_COLON] [RALT K_PERIOD] [SHIFT K_9]  [SHIFT K_0] [RALT K_9] [RALT K_0] [SHIFT K_SLASH] [RALT K_QUOTE] [SHIFT K_5] [SHIFT K_7] [RALT K_7]
store(PuncU) U+061E     U+060C    U+061B    U+002E          U+FD3E       U+FD3F      U+0028     U+0029     U+061F         U+0027         U+066A       U+0640      U+0026

store(nul) "ERUIOPFJKVM"

group(main) using keys
+ any(nul) > nul
+ any(charsK) > index(charsU,1)
+ any(diaK) > index(diaU,1)
+ any(PuncK) > index(PuncU,1)

c quotes

U+003C + [SHIFT K_COMMA] > U+2039
U+2039 + [SHIFT K_COMMA] > U+00AB
U+003E + [SHIFT K_PERIOD] > U+203A
U+203A + [SHIFT K_PERIOD] > U+00BB

+ [SHIFT K_L] > U+0644 U+064E U+0627

+ [RALT K_Q] > U+200C c ZWNJ
+ [RALT K_W] > U+200D c ZWJ
+ [RALT K_E] > U+200E c LRM
+ [RALT K_R] > U+200F c RLM
+ [RALT K_T] > U+2067 c LRI
+ [RALT K_Y] > U+2068 c FSI
+ [RALT K_U] > U+2069 c POP

+ [RALT K_5] > U+0025 c %


