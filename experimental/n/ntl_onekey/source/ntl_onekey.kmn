c Title:        ntl_onekey.kmn
c Author:       A. Cheuk and E. Ding
c Description:  New Tai Lue Unicode keyboard
c Remarks:      NTL now visually encoded per resolution of UTC Meeting 141; originally adapted from xidai legacy encoding
c 
c AYWC	1.000	22-MAY-2015     Original
c EJD	1.001	18-DEC-2017	Added touch layout
c EJD	1.002	13-FEB-2018	Fix up numeric layer issues
c DDW   1.003   21-JAN-2019     Add language tag to keyboard package
c EJD   1.004   10-OCT-2019     Add documentation (help, welcome, readme)
store(&VERSION) '9.0'
store(&NAME) '新傣文一键'
store(&TARGETS) 'windows macosx iphone androidphone mobile'
store(&ETHNOLOGUECODE) 'khb'
store(&COPYRIGHT) 'Copyright © 2015-2019 SIL International'
store(&MESSAGE) 'This NTL OneKey (新傣文一键) keyboard is distributed under the MIT License.'
store(&LAYOUTFILE) 'ntl_onekey-layout.js'
store(&KEYBOARDVERSION) '1.004'
store(&BITMAP) 'ntl_onekey.ico'

begin Unicode > use(main)

store(baseK)	"~!@#$%^&*()_+" "`1234567890-=" "QWERTYUIOP{}|" "qwertyuiop[]\" \
                "ASDFGHJKL:" '"' "asdfghjkl;'" "ZXCVBNM<>?" "zxcvbnm,./ "

store(baseU)   \ 
    U+2026 U+19DA U+19BA U+19BB U+19BC U+19BD U+19BE U+19BF U+19C0 U+19C1 U+19C2 U+19C4 U+19C6 \
    U+19D0 U+19D1 U+19D2 U+19D3 U+19D4 U+19D5 U+19D6 U+19D7 U+19D8 U+19D9 U+19C3 U+19C5 U+19C7 \
    U+19B9 U+19B7 U+19B4 U+19B2 U+1991 U+1992 U+1998 U+19A8 U+19A9 U+1997 U+19AB U+19DF U+FF01 \
    U+19B8 U+19B5 U+19B3 U+19B1 U+198E U+198F U+1995 U+19A6 U+19A7 U+1994 U+19AA U+19DE U+3001 \
    U+1981 U+198C U+19A4 U+199D U+1987 U+19A3 U+198D U+1985 U+199F U+FF1A U+201C \
    U+1980 U+1989 U+19A1 U+199A U+1984 U+19A0 U+198A U+1982 U+199C U+FF1B U+2018 \
    U+198B U+1986 U+19C9 U+199E U+19A5 U+1993 U+1999 U+FF08 U+FF09 U+FF1F \
    U+1988 U+1983 U+19C8 U+199B U+19A2 U+1990 U+1996 U+FF0C U+3002 U+19B0 U+0020
    
c not sure if U+3000 is needed.

group(main) using keys

+ any(baseK) > index(baseU, 1)
U+19B5 + 'w' > U+19B6   c Press 'w' twice to get U+19B6

U+2018 + "'" > U+2019   c Press "'" twice to get right single quotation mark
U+201C + '"' > U+201D   c Press '"' twice to get right double quotation mark

U+FF08 + '<' > U+300A   c Press '<' twice to get left double angle bracket
U+FF09 + '>' > U+300B   c Press '>' twice to get right double angle bracket

U+3002 + '.' > U+FF0E   c Press '.' twice to get full width full stop
U+2026 + '~' > U+2014   c Press '~' twice to get em dash
