store(&VERSION) "9.0"
store(&KEYBOARDVERSION) "9.0"
store(&TARGETS) "windows macosx"
store(&ETHNOLOGUECODE) "thp"
store(&COPYRIGHT) "© 2015 FirstVoices, Tavultesoft. Portions © 2006 Chris Harvey."
store(&NAME) 'Nłeʔkepmxcin'
store(&BITMAP) 'fv_nlekepmxcin.bmp'
store(&VISUALKEYBOARD) 'fv_nlekepmxcin.kvk'
 
begin Unicode > use(main)                      
group(main) using keys   


c *************
c constraints
c *************

store(topaccent)  U+0300 U+0301 U+0313
store(topaccentkey) "`;'"          

any(topaccent) + any(topaccentkey) > context beep

store(lowaccent) U+0323 
any(lowaccent) + "1" > context beep
                  
store(NumbersAndPunctuation) '1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"  c preventing accents on non-letters
any(NumbersAndPunctuation) + any(topaccentkey) > context beep                   
any(NumbersAndPunctuation) + "1" > context beep

c original values
store(orig0) [RALT K_1] [RALT K_2] [RALT K_3] [RALT K_4] [RALT K_5] [RALT K_6] [RALT K_7] [RALT K_8] [RALT K_9] [RALT K_0] [RALT K_LBRKT] [RALT K_RBRKT] [RALT K_QUOTE] [RALT K_COLON] [RALT K_COMMA] [RALT K_PERIOD]
store(orig1) "1234567890[]';<>"            
store(punc0) [SHIFT RALT K_1] [SHIFT RALT K_2] [SHIFT RALT K_3] [SHIFT RALT K_4] [SHIFT RALT K_5] [SHIFT RALT K_6] [SHIFT RALT K_7] [SHIFT RALT K_8] [SHIFT RALT K_9] [SHIFT RALT K_0] [SHIFT RALT K_LBRKT] [SHIFT RALT K_RBRKT] 
store(punc1) "!@#$%^&*(){}"
+ any(orig0) > index(orig1,1)
+ any(punc0) > index(punc1,1)

c non-caps modified keys
  
store(Mod0) "`~140)[]{}';<>" [SHIFT RALT K_HYPHEN] [SHIFT RALT K_EQUAL] [RALT K_HYPHEN] [RALT K_EQUAL] 
store(Mod1) U+0300 "·" U+0323 "ʔʕ¢‘’“”" U+0313 U+0301 "()‹›«»"      
+ any(Mod0) > index(Mod1,1)   

c glottaled
store(MiniGlot0) [CAPS SHIFT K_8] [NCAPS K_8]  
store(MiniGlot1)  "ƛƛ"
store(MajGlot0)  [NCAPS SHIFT K_8] [CAPS K_8] 
store(MajGlot1) U+F899 U+F899
+ any(MiniGlot0) > index(MiniGlot1,1) "̓"
+ any(MajGlot0) > index(MajGlot1,1) "̓"           

c dotted
store(MiniDot0) [CAPS SHIFT K_7] [NCAPS K_7]  
store(MiniDot1)  "xx"
store(MajDot0)   [NCAPS SHIFT K_7]  [CAPS K_7] 
store(MajDot1) "XX"             
+ any(MiniDot0) > index(MiniDot1,1) U+0323        
+ any(MajDot0) > index(MajDot1,1) U+0323   

c other characters
store(MiniOth0) [CAPS SHIFT K_2] [CAPS SHIFT K_3] [CAPS SHIFT K_5]  [CAPS SHIFT K_6] [CAPS SHIFT K_9] [NCAPS K_2]  [NCAPS K_3] [NCAPS K_5]  [NCAPS K_6]  [NCAPS K_9] 
store(MiniOth1)  "ʷəɣθłʷəɣθł"
store(MajOth0)  [NCAPS SHIFT K_2] [NCAPS SHIFT K_3] [NCAPS SHIFT K_5] [NCAPS SHIFT K_6] [NCAPS SHIFT K_9] [CAPS K_2] [CAPS K_3]  [CAPS K_5]  [CAPS K_6]  [CAPS K_9] 
store(MajOth1) "ᵂƏƔΘŁᵂƏƔΘŁ"             
+ any(MiniOth0) > index(MiniOth1,1)       
+ any(MajOth0) > index(MajOth1,1)   

match > use(normalize)
nomatch > use(x)

group(normalize)

any(topaccent) any(lowaccent) > context(2) context(1)


group(x) using keys

store(allkeys) 'abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ1234567890-=[]\;,./`~!@#$%^&*()_+{}|:"<>?' "'"
+ any(allkeys) > index(allkeys, 1)                                                              
