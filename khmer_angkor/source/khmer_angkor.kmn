store(&VERSION) '9.0'
store(&NAME) "Khmer Angkor" c TODO need a new name
c BITMAP "khmer10.bmp"
store(&COPYRIGHT) '© 2017 Keyman'
store(&MESSAGE) "More than just a Khmer Unicode keyboard."
store(&VISUALKEYBOARD) 'khmer_angkor.kvk'
store(&TARGETS) 'any windows'
store(&LAYOUTFILE) 'khmer_angkor-layout.js'
store(&KEYBOARDVERSION) '1.3'
c store(&BITMAP) 'khmer10.ico'
store(&ETHNOLOGUECODE) 'khm'
store(&LANGUAGE) 'x0453'
store(&WINDOWSLANGUAGES) 'x0453'
store(&BITMAP) 'khmer_angkor.ico'

begin Unicode > use(main)



c ==============================================STORES==============================================



c 33 consonants and two deprecated consonants

store(c_key)           [K_K] [K_X] [SHIFT K_K] [SHIFT K_X] [K_G] \
                       [K_C] [K_Q] [SHIFT K_C] [SHIFT K_Q] [SHIFT K_J] \
                       [K_D] [K_Z] [SHIFT K_D] [SHIFT K_Z] [SHIFT K_N] \
                       [K_T] [K_F] [SHIFT K_T] [SHIFT K_F] [K_N] \
                       [K_B] [K_P] [SHIFT K_B] [SHIFT K_P] [K_M] \
                       [K_Y] [K_R] [K_L] [K_V] [K_S] [K_H] [SHIFT K_L] [SHIFT K_G] \
                       [RALT K_S] [RALT K_B] 
store(c_touch_key)     [K_K] [K_X] [SHIFT K_K] [SHIFT K_X] [K_G] \
                       [K_C] [K_Q] [SHIFT K_C] [SHIFT K_Q] [SHIFT K_J] \
                       [K_D] [K_Z] [SHIFT K_D] [SHIFT K_Z] [SHIFT K_N] \
                       [K_T] [K_F] [SHIFT K_T] [SHIFT K_F] [K_N] \
                       [K_B] [K_P] [SHIFT K_B] [SHIFT K_P] [K_M] \
                       [K_Y] [K_R] [K_L] [K_V] [K_S] [K_H] [SHIFT K_L] [SHIFT K_G] \
                       [CTRL ALT K_S] [CTRL ALT K_B]                      
store(c_out)           U+1780 U+1781 U+1782 U+1783 U+1784 \
    	   	   	       U+1785 U+1786 U+1787 U+1788 U+1789 \
    	   	   	       U+178A U+178B U+178C U+178D U+178E \
    	   	   	       U+178F U+1790 U+1791 U+1792 U+1793 \
    	   	   	       U+1794 U+1795 U+1796 U+1797 U+1798 \
    	   	   	       U+1799 U+179A U+179B U+179C U+179F U+17A0 U+17A1 U+17A2 \
    	   	   	       U+179D U+179E c deprecated, but they are used in minority languages	

c All genuine dependent vowels

store(v_gen_key)       [K_A] [K_I] [SHIFT K_I] [K_W] [SHIFT K_W] [K_U] [SHIFT K_U] [SHIFT K_Y] \
                       [K_COLON] [SHIFT K_LBRKT] [K_LBRKT] [K_E] [SHIFT K_E] [SHIFT K_S] [K_O] [SHIFT K_O] 
store(v_gen)           U+17B6 U+17B7 U+17B8 U+17B9 U+17BA U+17BB U+17BC U+17BD \
                       U+17BE U+17BF U+17C0 U+17C1 U+17C2 U+17C3 U+17C4 U+17C5 
                       
c All pseudo dependent vowels

store(v_pseudo_key)    [SHIFT K_M] [SHIFT K_H] [RALT K_QUOTE]
store(v_pseudo_touch_key)    [SHIFT K_M] [SHIFT K_H] [CTRL ALT K_QUOTE]
store(v_pseudo)        U+17C6 U+17C7 U+17C8 

c Both genuine and pseudo vowels

store(v_key)           outs(v_gen_key) outs(v_pseudo_key) 
store(v_touch_key)     outs(v_gen_key) outs(v_pseudo_touch_key)     
store(v_out)           outs(v_gen)     outs(v_pseudo)
store(v_any)           outs(v_out)

c These seven vowels can be concatenated with Reahmuk (U+17C7) to make េះ  ោះ  ុះ  ិះ  ីះ  ឹះ  and ែះ.

store(v_combo_R)       U+17C1 U+17C4 U+17BB U+17B7 U+17B8 U+17B9 U+17C2 

c these two vowels can be concatenated with Nikahit (U+17C6) to make ាំ and ុំ.

store(v_combo_N)       U+17B6 U+17BB 
store(v_combo)         outs(v_combo_R) outs(v_combo_N)

c Independent vowels

store(ind_v_key)       [K_HYPHEN] [RALT K_I] [SHIFT K_RBRKT] [RALT K_T] [RALT K_LBRKT] [RALT K_RBRKT] [RALT K_R] [SHIFT K_R] \
                       [SHIFT K_BKSLASH] [K_BKSLASH] [RALT K_E] [RALT K_P] [RALT K_O] [K_EQUAL] [K_RBRKT]   
store(ind_v_touch_key)       [K_HYPHEN] [CTRL ALT K_I] [SHIFT K_RBRKT] [CTRL ALT K_T] [CTRL ALT K_LBRKT] [CTRL ALT K_RBRKT] [CTRL ALT K_R] [SHIFT K_R] \
                       [SHIFT K_BKSLASH] [K_BKSLASH] [CTRL ALT K_E] [CTRL ALT K_P] [CTRL ALT K_O] [K_EQUAL] [K_RBRKT]                         
store(ind_v_out)       U+17A5 U+17A6 U+17A7 U+17A8 U+17A9 U+17AA U+17AB U+17AC \
                       U+17AD U+17AE U+17AF U+17B0 U+17B1 U+17B2 U+17B3 

c Diacritics

store(diacritic_key)   [K_QUOTE] [SHIFT K_7] [SHIFT K_HYPHEN] [SHIFT K_8] [SHIFT K_6] [RALT K_QUOTE] \  
                       [RALT K_EQUAL] [RALT K_3] [RALT K_W] [RALT K_Q] [K_J] 
store(diacritic_touch_key)   [K_QUOTE] [SHIFT K_7] [SHIFT K_HYPHEN] [SHIFT K_8] [SHIFT K_6] [CTRL ALT K_QUOTE] \  
                       [CTRL ALT K_EQUAL] [CTRL ALT K_3] [CTRL ALT K_W] [CTRL ALT K_Q] [K_J] 
store(diacritic_out)   U+17CB U+17D0 U+17CC U+17CF U+17CD U+17C8 \
                       U+17CE U+17D1 U+17DD U+17DC U+17D2  

c Consonant shifter--Muusikatoan(U+17C9) and Triisap(U+17CA)

store(c_shifter_key)   [SHIFT K_QUOTE] [K_SLASH]
store(c_shifter)       U+17C9          U+17CA  

c punctuations

store(punct_key)       [K_PERIOD] [SHIFT K_PERIOD] [RALT K_COLON] [SHIFT K_2] [RALT K_L] [RALT K_6] [RALT K_7]   
store(punct_touch_key)       [K_PERIOD] [SHIFT K_PERIOD] [CTRL ALT K_COLON] [SHIFT K_2] [CTRL ALT K_L] [CTRL ALT K_6] [CTRL ALT K_7]   
store(punct_out)       U+17D4     U+17D5           U+17D6         U+17D7      U+17D8     U+17D9     U+17DA

store(latin_punct_key) [K_BKQUOTE] [SHIFT K_BKQUOTE] [SHIFT K_9] [SHIFT K_0] [SHIFT K_1] [SHIFT K_3] \
                       [SHIFT K_5] [SHIFT K_EQUAL] [SHIFT K_SLASH] [RALT K_9] [RALT K_0] [RALT K_BKSLASH] \
                       [SHIFT K_2] [SHIFT K_8] [RALT K_COMMA] [RALT K_HYPHEN] [RALT K_PERIOD] [RALT K_SLASH] \
                       [RALT K_BKQUOTE] [RALT K_1]
store(latin_punct_touch_key) [K_BKQUOTE] [SHIFT K_BKQUOTE] [SHIFT K_9] [SHIFT K_0] [SHIFT K_1] [SHIFT K_3] \
                       [SHIFT K_5] [SHIFT K_EQUAL] [SHIFT K_SLASH] [CTRL ALT K_9] [CTRL ALT K_0] [CTRL ALT K_BKSLASH] \
                       [SHIFT K_2] [SHIFT K_8] [CTRL ALT K_COMMA] [CTRL ALT K_HYPHEN] [CTRL ALT K_PERIOD] [CTRL ALT K_SLASH] \
                       [CTRL ALT K_BKQUOTE] [CTRL ALT K_1]
store(latin_punct_out) U+00AB U+00BB U+0028 U+0029 U+0021 U+0022 \
                       U+0025 U+003D U+003F U+007B U+007D U+005C \
                       U+0040 U+002A U+002C U+0078 U+002E U+002F \
    	               U+200D U+200C

c Currency: Riel, Dollar and Euro

store(currency_key)    [SHIFT K_4] [RALT K_4] [RALT K_5]  
store(currency_touch_key)    [SHIFT K_4] [CTRL ALT K_4] [CTRL ALT K_5]
store(currency_out)    U+17DB      U+0024     U+20AC

c Khmer digits 0 through 9

store(digit_key)       [K_0]  [K_1]  [K_2]  [K_3]  [K_4]  [K_5]  [K_6]  [K_7]  [K_8]  [K_9]  
store(digit_out)       U+17E0 U+17E1 U+17E2 U+17E3 U+17E4 U+17E5 U+17E6 U+17E7 U+17E8 U+17E9

c Khmer Lek Attak (a.k.a. divination lore)

store(lek_attak_key)   [SHIFT CTRL ALT K_0] [SHIFT CTRL ALT K_1] [SHIFT CTRL ALT K_2] [SHIFT CTRL ALT K_3] [SHIFT CTRL ALT K_4] \
                       [SHIFT CTRL ALT K_5] [SHIFT CTRL ALT K_6] [SHIFT CTRL ALT K_7] [SHIFT CTRL ALT K_8] [SHIFT CTRL ALT K_9]                    
store(lek_attak_out)   U+17F0 U+17F1 U+17F2 U+17F3 U+17F4 \
                       U+17F5 U+17F6 U+17F7 U+17F8 U+17F9     

c Lunar date format (Khmer style)

store(lunar_date_key)  [SHIFT CTRL ALT K_A] [SHIFT CTRL ALT K_B] [SHIFT CTRL ALT K_C] [SHIFT CTRL ALT K_D] [SHIFT CTRL ALT K_E] \
                       [SHIFT CTRL ALT K_F] [SHIFT CTRL ALT K_G] [SHIFT CTRL ALT K_H] [SHIFT CTRL ALT K_I] [SHIFT CTRL ALT K_J] \
                       [SHIFT CTRL ALT K_K] [SHIFT CTRL ALT K_L] [SHIFT CTRL ALT K_M] [SHIFT CTRL ALT K_N] [SHIFT CTRL ALT K_O] \
    	               [SHIFT CTRL ALT K_P] [SHIFT CTRL ALT K_Q] [SHIFT CTRL ALT K_R] [SHIFT CTRL ALT K_S] [SHIFT CTRL ALT K_T] \
    	               [SHIFT CTRL ALT K_U] [SHIFT CTRL ALT K_V] [SHIFT CTRL ALT K_W] [SHIFT CTRL ALT K_X] [SHIFT CTRL ALT K_Y] \
    	               [SHIFT CTRL ALT K_Z] [SHIFT CTRL ALT K_COLON] [SHIFT CTRL ALT K_COMMA] [SHIFT CTRL ALT K_PERIOD] [SHIFT CTRL ALT K_LBRKT] \
    	               [SHIFT CTRL ALT K_RBRKT] [SHIFT CTRL ALT K_QUOTE]
store(lunar_date_out)  U+19EC U+19FB U+19F9 U+19EE U+19E2 \
                       U+19EF U+19F0 U+19F1 U+19E7 U+19F2 \
                       U+19F3 U+19F4 U+19FD U+19FC U+19E8 \
    	               U+19E9 U+19E0 U+19E3 U+19ED U+19E4 \
    	               U+19E6 U+19FA U+19E1 U+19F8 U+19E5 \
    	               U+19F7 U+19F5 U+19FE U+19FF U+19EA \
    	               U+19EB U+19F6 

c Subscripts​ for mobile/table layouts

store(input_subcons) \
                       [T_17D2_1780] [T_17D2_1781] [T_17D2_1782] [T_17D2_1783] [T_17D2_1784] [T_17D2_1785] [T_17D2_1786] [T_17D2_1787] \
                       [T_17D2_1788] [T_17D2_1789] [T_17D2_178A] [T_17D2_178B] [T_17D2_178C] [T_17D2_178D] [T_17D2_178E] [T_17D2_178F] \
                       [T_17D2_1790] [T_17D2_1791] [T_17D2_1792] [T_17D2_1793] [T_17D2_1794] [T_17D2_1795] [T_17D2_1796] [T_17D2_1797] \
                       [T_17D2_1798] [T_17D2_1799] [T_17D2_179A] [T_17D2_179B] [T_17D2_179C] [T_17D2_179D] [T_17D2_179E] [T_17D2_179F] \
                       [T_17D2_17A0] [T_17D2_17A1] [T_17D2_17A2]

store(subcons) \
                       U+1780 U+1781 U+1782 U+1783 U+1784 U+1785 U+1786 U+1787 \
                       U+1788 U+1789 U+178A U+178B U+178C U+178D U+178E U+178F \
                       U+1790 U+1791 U+1792 U+1793 U+1794 U+1795 U+1796 U+1797 \
                       U+1798 U+1799 U+179A U+179B U+179C U+179D U+179E U+179F \
                       U+17A0 U+17A1 U+17A2                         
                       
c Arabic digits 0 through 9

store(arabic_digit_key) [U_0030] [U_0031] [U_0032] [U_0033] [U_0034] [U_0035] [U_0036] [U_0037] [U_0038] [U_0039]
store(arabic_digit_out) U+0030   U+0031   U+0032   U+0033   U+0034   U+0035   U+0036   U+0037   U+0038   U+0039                        
                    
c These stores are for consonant (cluster) and consonant shifter configuration

store(v_above)         U+17B7 U+17B8 U+17B9 U+17BA \
                       U+17BE                      c These vowels are rendered above the base consonant.
store(shiftable_c_1st) U+179F U+17A0 U+17A2        c These consonants shift to the 2nd series when adding U+17CA to them.        
store(shiftable_c_2nd) U+1784 U+1789 U+1798 U+1799 \
                       U+179A U+179C U+1794        c These consonants shift to the 1st series when adding U+17C9 to them. 
store(c_2nd_combo_LO)  U+1799 U+1798 U+1784 U+1794 \
                       U+179C                      c These can be combined with U+179B to make a 2nd series consonant cluster.
store(c_2nd_combo_MO)  U+1799 U+179B U+1784 U+179A c These can be combined with U+1798 to make a 2nd series consonant cluster. 
store(c_1st_combo_LO)  U+1794 U+17A0 U+17A2        c they can be combined with U+179B to make a 1st series consonant cluster.  
store(c_1st_combo_MO)  U+17A0 U+179F U+17A2        c they can be combined with U+1798 to make a 1st series consonant cluster.  
store(c_combo_SA)      U+1794 U+1799 U+179B U+1798 \
                       U+1793 U+1789 U+1784 U+179A \
                       U+179C U+17A2               c they can be combined with U+179F to make a 1st series consonant cluster.
store(c_combo_QA)      U+1786 U+1788 U+1794 U+1795 \
                       U+178F U+1791               c they can be combined with U+17A2 to make a 1st series consonant cluster.
store(c_combo_HA)      U+179C U+17A3               c they can be combined with U+17A0 to make a 1st series consonant cluster.  

c stores for the constraints on the consonant shifter and consonant clusters.

c store(c_combo_KA)        U+178A U+1793 U+1798 U+179A U+179C
c store(c_combo_KHA)       U+1785 U+1784 U+178F U+1793 U+1798 U+179B U+179C
c store(c_combo_CA)        U+1793 U+179A
c store(c_combo_CHA)       U+1780 U+1784 U+1793 U+1798 U+179B U+179C
c store(c_combo_THA)       U+1780 U+1784 U+1793 U+1798 U+179B U+179C
c store(c_combo_BA)        U+179A U+179B
c store(c_combo_PHA)       U+1780 U+1784 U+1791 U+1789 U+178F U+1793 U+179B U+179F
c store(c_combo_SA_prime)  U+1780 U+178F U+178A
c store(c_combo_KHA_prime) U+1787 U+1791
c store(c_combo_KO)        U+1793 U+1798 U+179A U+179C
c store(c_combo_KHO)       U+1789 U+1793 U+1798 U+179B U+179C
c store(c_combo_CO)        U+179A U+179C
c store(c_combo_CHO)       U+1784 U+1793 U+1798 U+179B U+179C
c store(c_combo_THO)       U+1784 U+1793 U+1798 U+179B U+179C
c store(c_combo_PHA_prime) U+1782 U+1791
c store(c_combo_PO)        U+1793 U+179A U+179B
c store(c_combo_PHO)       U+1784 U+1787 U+1789 U+1793 U+179B U+179F



c ==============================================BASIC RULES & CONSTRAINTS/ROTATIONS==============================================
    


group(main) using keys

c Temporary Basic rules for Touch layout

platform('desktop') + any(c_key)           > index(c_out,2) 
platform('mobile') + any(c_touch_key)           > index(c_out,2)
platform('tablet') + any(c_touch_key)           > index(c_out,2)

platform('desktop') + any(v_key)                > index(v_out,2) 
platform('mobile') + any(v_touch_key)           > index(v_out,2)
platform('tablet') + any(v_touch_key)           > index(v_out,2)

platform('desktop') + any(ind_v_key)            > index(ind_v_out,2) 
platform('mobile') + any(ind_v_touch_key)       > index(ind_v_out,2)
platform('tablet') + any(ind_v_touch_key)       > index(ind_v_out,2) 

platform('desktop') + any(diacritic_key)        > index(diacritic_out,2)  
platform('mobile') + any(diacritic_touch_key)   > index(diacritic_out,2)
platform('tablet') + any(diacritic_touch_key)   > index(diacritic_out,2)

+ any(c_shifter_key)   > index(c_shifter,1)

platform('desktop') + any(punct_key)            > index(punct_out,2)
platform('mobile') + any(punct_touch_key)       > index(punct_out,2) 
platform('tablet') + any(punct_touch_key)       > index(punct_out,2)

platform('desktop') + any(latin_punct_key)      > index(latin_punct_out,2) 
platform('mobile') + any(latin_punct_touch_key) > index(latin_punct_out,2)
platform('tablet') + any(latin_punct_touch_key) > index(latin_punct_out,2)

platform('desktop') + any(currency_key)         > index(currency_out,2)
platform('mobile') + any(currency_touch_key)    > index(currency_out,2)  
platform('tablet') + any(currency_touch_key)    > index(currency_out,2)

+ any(digit_key)       > index(digit_out,1) 
platform('desktop')+ any(arabic_digit_key) > index(arabic_digit_out,2)
platform('mobile')+ any(arabic_digit_key) > index(arabic_digit_out,2)
platform('tablet')+ any(arabic_digit_key) > index(arabic_digit_out,2)

platform('desktop') + any(lek_attak_key)   > index(lek_attak_out,2)
platform('mobile') + any(lek_attak_key)   > index(lek_attak_out,2)
platform('tablet') + any(lek_attak_key)   > index(lek_attak_out,2)   

+ any(lunar_date_key)  > index(lunar_date_out,1)

c Basic rules with RALT key

c + any(c_key)           > index(c_out,1)
c + any(v_key)           > index(v_out,1)
c + any(ind_v_key)       > index(ind_v_out,1)
c + any(diacritic_key)   > index(diacritic_out,1)
c + any(c_shifter_key)   > index(c_shifter,1)
c + any(punct_key)       > index(punct_out,1)
c + any(latin_punct_key) > index(latin_punct_out,1)
c + any(currency_key)    > index(currency_out,1)
c + any(digit_key)       > index(digit_out,1)
c + any(lek_attak_key)   > index(lek_attak_out,1)
c + any(lunar_date_key)  > index(lunar_date_out,1)
+ [K_SPACE]            > U+200B
+ [SHIFT K_SPACE]      > U+0020
+ [CTRL ALT K_SPACE]   > U+00A0   
+ [SHIFT K_A]          > U+17B6 U+17C6
+ [SHIFT K_V]          > U+17C1 U+17C7
+ [SHIFT K_COLON]      > U+17C4 U+17C7
+ [K_COMMA]            > U+17BB U+17C6
+ [SHIFT K_COMMA]      > U+17BB U+17C7 

c Constraints

any(v_gen) + [K_QUOTE]              > context beep c no Bantoc after any genuine vowel  
any(v_pseudo) + [K_QUOTE]           > context beep c no Bantoc after any psuedo vowel
any(c_shifter) + [K_QUOTE]          > context beep c no Bantoc after Triisap/Muusikatoan 
U+17D2 any(c_out) + [K_QUOTE]       > context beep c no Bantoc on a subscript 
any(c_shifter) + any(c_shifter_key) > context beep c no two Triisap/Muusikatoan

c Rotation

U+17C7 + [SHIFT K_H]            > U+17C8       	   c U+17C7 is changed to U+17C8 when pressing [SHIFT K_H].
U+17C8 + [SHIFT K_H]            > U+17C7       	   c U+17C8 is changed to U+17C7 when K_H is pressed.
any(v_gen) U+17C7 + [SHIFT K_H] > context beep     c Don't rotate between U+17C7 and U+17C8. 

c When [BB] or [B6] are placed after [C6], the order should be reversed. (Case #5)

U+17C6 + [K_U] > U+17BB U+17C6
U+17C6 + [K_A] > U+17B6 U+17C6

c One backspace deletes a two-part vowels (i.e. psuedoVowel + U+17C7)

any(v_combo_N) U+17C6 + [K_BKSP] > nul
any(v_combo_R) U+17C7 + [K_BKSP] > nul 

c When pressing [K_U] after ាំ and ុំ, the U+17BB is converted to a respective consonant shifter placing before them (ាំ and ុំ).

any(shiftable_c_1st) U+17B6 U+17C6 + [K_U] > context(1) U+17CA context(2) context(3) c fix ស ាំ ុ > ស៊ាំ
any(shiftable_c_2nd) U+17B6 U+17C6 + [K_U] > context(1) U+17C9 context(2) context(3) c fix ម ាំ  ុ > ម៉ាំ   

c Consonant shifter constraints (beep when the consonant shifter is used in inappropriate environment)

any(shiftable_c_1st) + [SHIFT K_QUOTE] > context U+17C9 beep 
any(shiftable_c_2nd) + [K_SLASH]       > context U+17CA beep

c 1st series consonant clusters

any(c_combo_QA) U+17D2 U+17A2 + [SHIFT K_QUOTE]     > context U+17C9 beep  
U+179B U+17D2 any(c_1st_combo_LO) + [SHIFT K_QUOTE] > context U+17C9 beep
U+1798 U+17D2 any(c_1st_combo_MO) + [SHIFT K_QUOTE] > context U+17C9 beep
U+179F U+17D2 any(c_combo_SA) + [SHIFT K_QUOTE]     > context U+17C9 beep
any(c_combo_HA) U+17D2 U+17A0 + [SHIFT K_QUOTE]     > context U+17C9 beep
U+17A2 U+17D2 U+1784 + [SHIFT K_QUOTE]  	   	    > context U+17C9 beep
U+17A2 U+17D2 U+179C + [SHIFT K_QUOTE]              > context U+17C9 beep

c 2nd series consonant clusters --they should never be used with [K_SLASH], but if it

U+179B U+17D2 any(c_2nd_combo_LO) + [K_SLASH] > context U+17CA beep
U+1798 U+17D2 any(c_2nd_combo_MO) + [K_SLASH] > context U+17CA beep
   
c no consonant shifter is allowed in the following consonant clusters

c 1st series consonant clusters --never accept any consonant shifter

c U+1780 U+17D2 any(c_combo_KA) + any(c_shifter_key)       > context beep
c U+1781 U+17D2 any(c_combo_KHA) + any(c_shifter_key)      > context beep
c U+1785 U+17D2 any(c_combo_CA) + any(c_shifter_key)       > context beep
c U+1786 U+17D2 any(c_combo_CHA) + any(c_shifter_key)      > context beep
c U+178F U+17D2 U+179A + any(c_shifter_key)                > context beep
c U+1790 U+17D2 any(c_combo_THA) + any(c_shifter_key)      > context beep
c U+1794 U+17D2 any(c_combo_BA) + any(c_shifter_key)       > context beep
c U+1795 U+17D2 any(c_combo_PHA) + any(c_shifter_key)      > context beep
c U+1798 U+17D2 U+1791 + any(c_shifter_key)                > context beep
c U+179B U+17D2 U+1780 + any(c_shifter_key)                > context beep
c U+179F U+17D2 any(c_combo_SA_prime) + any(c_shifter_key) > context beep

c 2nd series consonant clusters --never accept any consonant shifter

c U+1780 U+17D2 U+178C + any(c_shifter_key)                 > context beep
c U+1781 U+17D2 any(c_combo_KHA_prime) + any(c_shifter_key) > context beep
c U+1782 U+17D2 any(c_combo_KO) + any(c_shifter_key)        > context beep
c U+1783 U+17D2 any(c_combo_KHO) + any(c_shifter_key)       > context beep
c U+1786 U+17D2 U+1782 + any(c_shifter_key)                 > context beep
c U+1787 U+17D2 any(c_combo_CO) + any(c_shifter_key)        > context beep
c U+1788 U+17D2 any(c_combo_CHO) + any(c_shifter_key)       > context beep
c U+1790 U+17D2 U+1782 + any(c_shifter_key)                 > context beep
c U+1791 U+17D2 U+179A + any(c_shifter_key)                 > context beep
c U+1792 U+17D2 any(c_combo_THO) + any(c_shifter_key)       > context beep
c U+1795 U+17D2 any(c_combo_PHA_prime) + any(c_shifter_key) > context beep
c U+1796 U+17D2 any(c_combo_PO) + any(c_shifter_key)        > context beep
c U+1797 U+17D2 any(c_combo_PHO) + any(c_shifter_key)       > context beep



c ==============================================MOBILE/TABLET TOUCH LAYOUT==============================================



+ any(input_subcons) > U+17D2 index(subcons, 1)

platform('tablet') U+17D2 any(subcons) + [K_BKSP] > nul
platform('mobile') U+17D2 any(subcons) + [K_BKSP] > nul

c + [K_NPSTAR]        > U+002A
c + [SHIFT K_NPSTAR]  > U+002A
c + [K_NPPLUS]        > U+002B
c + [SHIFT K_NPPLUS]  > U+002B
c + [K_NPMINUS]       > U+002D
c + [SHIFT K_NPMINUS] > U+002D
 


c ==============================================NORMALIZATION==============================================

           

match > use(normalise)

group(normalise)

c Illegitimate vowel combinations should be transformed to the legitimate ones upon the last key stroke (Case #6 and #7).

U+17C1 U+17B6 > U+17C4   c when type េ and ា, tranform them to ោ
U+17B6 U+17C1 > U+17C4   c when type ា and េ, tranform them to ោ
U+17C1 U+17B8 > U+17BE   c when type េ and ី , tranform them to ើ
U+17B8 U+17C1 > U+17BE   c when type ី  and េ, tranform them to ើ

c Reordering - Triisap and Muusikatoan store before a vowel and a vowelcomboR and psuedo vowel.

any(v_any) any(c_shifter)                              > context(2) context(1)            c place either a enguine or pseudo vowel after a consonant shifter
any(v_gen) any(v_pseudo) any(c_shifter)                > context(3) context(1) context(2) c place a two-part vowel after a consonant shifter(i.e. មោះ៉ )
any(c_shifter) any(v_any) any(c_shifter)               > context(3) context(2)            c when either a genuine or pseudo vowel is in between two consonant shifters, replace the one on the left with the one on the right.
any(c_shifter) any(v_gen) any(v_pseudo) any(c_shifter) > context(2) context(3) context(4) c when a two-part vowel is in between two consonants shifter, replace the one on the left with the one the right.

c Rotate vowels, diacritics and punctuation

any(v_gen) any(v_gen)                     > context(2) c the last vowel stroke replaces the previous one.    
any(diacritic_out) any(diacritic_out)     > context(2) c the last diacritic stroke replaces the previuos one.
any(punct_out) any(punct_out)             > context(2) c the last punctuation stroke replaces the previous one.
any(latin_punct_out) any(latin_punct_out) > context(2) c the last latin punctuation stroke replaces the previous one.


c Rotation of double vowels

any(v_gen) any(v_pseudo) any(v_gen)               > context(3)            c a two-part vowel rotates to a genuine vowel.
any(v_gen) any(v_pseudo) any(v_pseudo)            > context(3)            c a two-part vowel rotates to a pseudo vowel.
any(v_gen) any(v_gen) any(v_pseudo)               > context(2) context(3) c a genuine vowel rotates to a two-part vowel.
any(v_pseudo) any(v_gen) any(v_pseudo)            > context(2) context(3) c a pseudo vowel roates to a two-part vowel.
any(v_gen) any(v_pseudo) any(v_gen) any(v_pseudo) > context(3) context(4) c a two-part vowel rotates to another two-part vowel.
any(v_pseudo) any(v_pseudo)                       > context(2)            c a pseudo vowel rotates to another pseudo vowel.
any(v_pseudo) any(v_gen)                          > context(2)            c a pseudo vowel roates to a genuine vowel.
                
c Delete extraneous coeng markers

U+17D2 U+17D2                   > context(1)            c no two coengs are allowed.
U+17D2 any(v_any)               > context(2)            c The coeng is deleted when a vowel is put after it. 
U+17D2 any(v_gen) any(v_pseudo) > context(2) context(3) c The coeng is deleted when a pseudo vowel is put after it.

c Case #1: when a subscript is typed after a vowel, the subscript should be automatically moved to before the vowel.

any(v_any) U+17D2 any(subcons)            > context(2) context(3) context(1)            c a vowel has to come after a subscript.
any(v_combo_N) U+17C6 U+17D2 any(subcons) > context(3) context(4) context(1) context(2) c these two-part vowels have to come after a subscript.
any(v_combo_R) U+17C7 U+17D2 any(subcons) > context(3) context(4) context(1) context(2) c these two-part vowels have to come after a subscript.

c a subscript placed after a shifter and a vowel (a genuine/pseudo vowel or a two-part vowel) has to before them.

any(c_shifter) any(v_any) U+17D2 any(subcons)            > context(3) context(4) context(1) context(2)            c after a genuine/pseudo vowel
any(c_shifter) any(v_combo_N) U+17C6 U+17D2 any(subcons) > context(4) context(5) context(1) context(2) context(3) c after a two-part vowel
any(c_shifter) any(v_combo_R) U+17C7 U+17D2 any(subcons) > context(4) context(5) context(1) context(2) context(3) c after a two-part vowel

c Case #2: when [D2+DA] is typed before a subscript, it should be moved after it.

U+17D2 U+178A U+17D2 U+179A                             > context(1) U+178F context(3) context(4)                           c change subscript ្ដ to ្ត after ្រ
U+17D2 U+179A U+17D2 U+178A                             > context(3) U+178F context(1) context(2)                           c  ្ដ placed after ្រ has to be changed to ្ត  and placed before ្រ 

U+17D2 U+178A any(v_any) U+17D2 U+179A                  > context(1) U+178F context(4) context(5) context(3)                c fixed 6e
U+17D2 U+178A any(v_combo_N) U+17C6 U+17D2 U+179A       > context(1) U+178F context(5) context(6) context(3) context(4)
U+17D2 U+178A any(v_combo_R) U+17C7 U+17D2 U+179A       > context(1) U+178F context(5) context(6) context(3) context(4)

U+17D2 U+179A any(v_any) U+17D2 U+178A                  > context(1) U+178F context(4) context(2) context(3)                c fixed 6g
U+17D2 U+179A any(v_combo_N) U+17C6 U+17D2 U+178A       > context(1) U+178F context(5) context(2) context(3) context(4)
U+17D2 U+179A any(v_combo_R) U+17C7 U+17D2 U+178A       > context(1) U+178F context(5) context(2) context(3) context(4)

U+17D2 U+179A any(c_shifter) U+17D2 any(subcons)        > context(4) context(5) context(1) context(2) context(3)            c when a shifter intervenes, move it to the end.
U+17D2 U+179A U+17D2 any(subcons)                       > context(3) context(4) context(1) context(2)                       c move any subscript placed after [D2+DA] to before them  
U+17D2 U+179A any(v_any) U+17D2 any(subcons)            > context(4) context(5) context(1) context(2) context(3)            c when a vowel intervenes, move it to the end 
U+17D2 U+179A any(v_combo_N) U+17C6 U+17D2 any(subcons) > context(5) context(6) context(1) context(2) context(3) context(4) c when a two-part vowel intervenes, move it to the end too.
U+17D2 U+179A any(v_combo_R) U+17C7 U+17D2 any(subcons) > context(5) context(6) context(1) context(2) context(3) context(4) c (same as stated right above)

c when a subscript is placed after a consonant shifter, they should be reversed.  (Case #3)

any(c_shifter) U+17D2 any(subcons) > context(2) context(3) context(1)  

c +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
c + when [BB] is place either before or after an above vowel (ិ ី ឹ ឺ ើ and ាំ) (Case #4)   +
c + the [BB] should transform to a respective consonant shifter place before the vowel.  +
c + the consonant shifter is a Trisap if the base consonant is ស ហ អ                                       +
c + the consonant shifter is a Muusikatoan if the base consonant is ង ញ ម យ រ វ ប                 +
c +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

c consonant clusters --1st > 2nd series (with U+17BB)

any(c_combo_QA) U+17D2 U+17A2 U+17BB any(v_above)             > context(1) context(2) context(3) U+17CA context(5)                  
any(c_combo_QA) U+17D2 U+17A2 U+17BB U+17B6 U+17C6            > context(1) context(2) context(3) U+17CA context(5) context(6)       
any(c_combo_QA) U+17D2 U+17A2 any(v_above) U+17BB             > context(1) context(2) context(3) U+17CA context(4)                                         
any(c_combo_QA) U+17D2 U+17A2 U+17BB U+17C6 U+17B6 U+17C6     > context(1) context(2) context(3) U+17CA context(6) context(7)
U+179B U+17D2 any(c_1st_combo_LO) U+17BB any(v_above)         > context(1) context(2) context(3) U+17CA context(5)                  
U+179B U+17D2 any(c_1st_combo_LO) U+17BB U+17B6 U+17C6        > context(1) context(2) context(3) U+17CA context(5) context(6)       
U+179B U+17D2 any(c_1st_combo_LO) any(v_above) U+17BB         > context(1) context(2) context(3) U+17CA context(4)                                   
U+179B U+17D2 any(c_1st_combo_LO) U+17BB U+17C6 U+17B6 U+17C6 > context(1) context(2) context(3) U+17CA context(6) context(7)
U+1798 U+17D2 any(c_1st_combo_MO) U+17BB any(v_above)         > context(1) context(2) context(3) U+17CA context(5)           
U+1798 U+17D2 any(c_1st_combo_MO) U+17BB U+17B6 U+17C6        > context(1) context(2) context(3) U+17CA context(5) context(6)
U+1798 U+17D2 any(c_1st_combo_MO) any(v_above) U+17BB         > context(1) context(2) context(3) U+17CA context(4)                      
U+1798 U+17D2 any(c_1st_combo_MO) U+17BB U+17C6 U+17B6 U+17C6 > context(1) context(2) context(3) U+17CA context(6) context(7)
U+179F U+17D2 any(c_combo_SA) U+17BB any(v_above)             > context(1) context(2) context(3) U+17CA context(5)           
U+179F U+17D2 any(c_combo_SA) U+17BB U+17B6 U+17C6            > context(1) context(2) context(3) U+17CA context(5) context(6)
U+179F U+17D2 any(c_combo_SA) any(v_above) U+17BB             > context(1) context(2) context(3) U+17CA context(4)                      
U+179F U+17D2 any(c_combo_SA) U+17BB U+17C6 U+17B6 U+17C6     > context(1) context(2) context(3) U+17CA context(6) context(7)
any(c_combo_HA) U+17D2 U+17A0 U+17BB any(v_above)             > context(1) context(2) context(3) U+17CA context(5)           
any(c_combo_HA) U+17D2 U+17A0 U+17BB U+17B6 U+17C6            > context(1) context(2) context(3) U+17CA context(5) context(6)
any(c_combo_HA) U+17D2 U+17A0 any(v_above) U+17BB             > context(1) context(2) context(3) U+17CA context(4)                     
any(c_combo_HA) U+17D2 U+17A0 U+17BB U+17C6 U+17B6 U+17C6     > context(1) context(2) context(3) U+17CA context(6) context(7)
U+17A2 U+17D2 U+1784 U+17BB any(v_above)                      > context(1) context(2) context(3) U+17CA context(5)           
U+17A2 U+17D2 U+1784 U+17BB U+17B6 U+17C6                     > context(1) context(2) context(3) U+17CA context(5) context(6)
U+17A2 U+17D2 U+1784 any(v_above) U+17BB                      > context(1) context(2) context(3) U+17CA context(4)                      
U+17A2 U+17D2 U+1784 U+17BB U+17C6 U+17B6 U+17C6              > context(1) context(2) context(3) U+17CA context(6) context(7)
U+17A2 U+17D2 U+179C U+17BB any(v_above)                      > context(1) context(2) context(3) U+17CA context(5)           
U+17A2 U+17D2 U+179C U+17BB U+17B6 U+17C6                     > context(1) context(2) context(3) U+17CA context(5) context(6)
U+17A2 U+17D2 U+179C any(v_above) U+17BB                      > context(1) context(2) context(3) U+17CA context(4)                      
U+17A2 U+17D2 U+179C U+17BB U+17C6 U+17B6 U+17C6              > context(1) context(2) context(3) U+17CA context(6) context(7)

c consonant clusters --2nd > 1st series (with U+17BB)

U+179B U+17D2 any(c_2nd_combo_LO) U+17BB any(v_above)         > context(1) context(2) context(3) U+17C9 context(5)
U+179B U+17D2 any(c_2nd_combo_LO) U+17BB U+17B6 U+17C6        > context(1) context(2) context(3) U+17C9 context(5) context(6)
U+179B U+17D2 any(c_2nd_combo_LO) any(v_above) U+17BB         > context(1) context(2) context(3) U+17C9 context(4)  
U+179B U+17D2 any(c_2nd_combo_LO) U+17BB U+17C6 U+17B6 U+17C6 > context(1) context(2) context(3) U+17C9 context(6) context(7)
U+1798 U+17D2 any(c_2nd_combo_MO) U+17BB any(v_above)         > context(1) context(2) context(3) U+17C9 context(5)
U+1798 U+17D2 any(c_2nd_combo_MO) U+17BB U+17B6 U+17C6        > context(1) context(2) context(3) U+17C9 context(5) context(6)
U+1798 U+17D2 any(c_2nd_combo_MO) any(v_above) U+17BB         > context(1) context(2) context(3) U+17C9 context(4) 
U+1798 U+17D2 any(c_2nd_combo_MO) U+17BB U+17C6 U+17B6 U+17C6 > context(1) context(2) context(3) U+17C9 context(6) context(7)

c single consonants --1st > 2nd series (with U+17BB)

any(shiftable_c_1st) U+17BB any(v_above)         > context(1) U+17CA context(3)                   c change U+17BB to U+17CA before the above vowel 
any(shiftable_c_1st) U+17BB U+17B6 U+17C6        > context(1) U+17CA context(3) context(4)        c change U+17BB to U+17CA before ាំ 
any(shiftable_c_1st) any(v_above) U+17BB         > context(1) U+17CA context(2)                   c change U+17BB to U+17CA and place it before the above vowel
any(shiftable_c_1st) U+17BB U+17C6 U+17B6 U+17C6 > context(1) U+17CA context(4) context(5) c change U+17BB U+17C6 to U+17CA 

c single consonants --2nd > 1st series (with U+17BB)

any(shiftable_c_2nd) U+17BB any(v_above)         > context(1) U+17C9 context(3)                   c change U+17BB to U+17C9 before the above vowel
any(shiftable_c_2nd) U+17BB U+17B6 U+17C6        > context(1) U+17C9 context(3) context(4)        c change U+17BB to U+17C9 before ាំ 
any(shiftable_c_2nd) any(v_above) U+17BB         > context(1) U+17C9 context(2)                   c change U+17BB to U+17C9 before the above vowel  
any(shiftable_c_2nd) U+17BB U+17C6 U+17B6 U+17C6 > context(1) U+17C9 context(4) context(5) c change U+17BB U+17C6 to U+17C9 
                                                                                                       
c prevent incorrect use of consonant shifter with 2nd series consonant clusters (without U+17BB)

U+179B U+17D2 any(c_2nd_combo_LO) U+17CA any(v_above)             > context(1) context(2) context(3) U+17C9 context(5)
U+1798 U+17D2 any(c_2nd_combo_MO) U+17CA any(v_above)             > context(1) context(2) context(3) U+17C9 context(5)
U+179B U+17D2 any(c_2nd_combo_LO) U+17CA any(v_gen) any(v_pseudo) > context(1) context(2) context(3) U+17C9 context(5) context(6)
U+1798 U+17D2 any(c_2nd_combo_MO) U+17CA any(v_gen) any(v_pseudo) > context(1) context(2) context(3) U+17C9 context(5) context(6)

c single consonants --2nd > 1st series (without U+17BB)

any(shiftable_c_2nd) U+17CA any(v_above)             > context(1) U+17C9 context(3)                   c change U+17CA to U+17C9 before the above vowel
any(shiftable_c_2nd) U+17CA any(v_gen) any(v_pseudo) > context(1) U+17C9 context(3) context(4)        c change U+17CA to U+17C9 before ាំ

U+17D2 any(shiftable_c_2nd) U+17CA any(v_above)             > context   c stop ស្រ៊ី  from becoming ស្រ៉ី
U+17D2 any(shiftable_c_2nd) U+17CA any(v_gen) any(v_pseudo) > context

c SPECIAL CASE -- the following clusters may accept either of the two consonant shifters.

U+1794 U+17D2 U+1799 any(c_shifter) > context
U+179F U+17D2 U+1794 any(c_shifter) > context
U+1786 U+17D2 U+1794 any(c_shifter) > context
U+1794 U+17D2 U+1799 any(c_shifter) > context
U+179F U+17D2 U+1794 any(c_shifter) > context
U+1786 U+17D2 U+1794 any(c_shifter) > context

c single --1st > 2nd series (without U+17BB)

any(shiftable_c_1st) U+17C9 any(v_above)             > context(1) U+17CA context(3)                   c change U+17C9 to U+17CA before the above vowel
any(shiftable_c_1st) U+17C9 any(v_gen) any(v_pseudo) > context(1) U+17CA context(3) context(4)        c change U+17C9 to U+17CA before ាំ  

c consonant clusters --1st > 2nd series (without U+17BB)

any(c_combo_QA) U+17D2 U+17A2 U+17C9 any(v_above)      > context(1) context(2) context(3) U+17CA context(5)  
U+179B U+17D2 any(c_1st_combo_LO) U+17C9 any(v_above)  > context(1) context(2) context(3) U+17CA context(5) 
U+1798 U+17D2 any(c_1st_combo_MO) U+17C9 any(v_above)  > context(1) context(2) context(3) U+17CA context(5)
U+179F U+17D2 any(c_combo_SA) U+17C9 any(v_above)      > context(1) context(2) context(3) U+17CA context(5)  
any(c_combo_HA) U+17D2 U+17A0 U+17C9 any(v_above)      > context(1) context(2) context(3) U+17CA context(5)  
U+17A2 U+17D2 U+1784 U+17C9 any(v_above)               > context(1) context(2) context(3) U+17CA context(5)   	   	  
U+17A2 U+17D2 U+179C U+17C9 any(v_above)               > context(1) context(2) context(3) U+17CA context(5)
any(c_combo_QA) U+17D2 U+17A2 U+17C9 U+17B6 U+17C6     > context(1) context(2) context(3) U+17CA context(5) context(6)
U+179B U+17D2 any(c_1st_combo_LO) U+17C9 U+17B6 U+17C6 > context(1) context(2) context(3) U+17CA context(5) context(6)       
U+1798 U+17D2 any(c_1st_combo_MO) U+17C9 U+17B6 U+17C6 > context(1) context(2) context(3) U+17CA context(5) context(6)
U+179F U+17D2 any(c_combo_SA) U+17C9 U+17B6 U+17C6     > context(1) context(2) context(3) U+17CA context(5) context(6)
any(c_combo_HA) U+17D2 U+17A0 U+17C9 U+17B6 U+17C6     > context(1) context(2) context(3) U+17CA context(5) context(6)
U+17A2 U+17D2 U+1784 U+17C9 U+17B6 U+17C6              > context(1) context(2) context(3) U+17CA context(5) context(6)
U+17A2 U+17D2 U+179C U+17C9 U+17B6 U+17C6              > context(1) context(2) context(3) U+17CA context(5) context(6)

c Use appropriate identfical subscript in two separate contexts. (Case #8)

U+178E U+17D2 U+178F > context(1) context(2) U+178A 
U+1793 U+17D2 U+178A > context(1) context(2) U+178F 

c EOF
